/-
Copyright (c) 2025 Joseph Tooby-Smith. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Zhi Kai Pong, Joseph Tooby-Smith, Lode Vermeulen
-/
import PhysLean.SpaceAndTime.Space.Derivatives.Grad
/-!

# Divergence on Space

## i. Overview

In this module we define the divergence operator on functions and
distributions from `Space d` to `EuclideanSpace â„ (Fin d)`, and prove various basic
properties about it.

## ii. Key results

- `div` : The divergence operator on functions from `Space d` to `EuclideanSpace â„ (Fin d)`.
- `distDiv` : The divergence operator on distributions from `Space d` to `EuclideanSpace â„ (Fin d)`.
- `distDiv_ofFunction` : The divergence of a distribution from a bounded function.

## iii. Table of contents

- A. The divergence on functions
  - A.1. The divergence on the zero function
  - A.2. The divergence on a constant function
  - A.3. The divergence distributes over addition
  - A.4. The divergence distributes over scalar multiplication
  - A.5. The divergence of a linear map is a linear map
- B. Divergence of distributions
  - B.1. Basic equalities
  - B.2. Divergence on distributions from bounded functions

## iv. References

-/

namespace Space

variable {W} [NormedAddCommGroup W] [NormedSpace â„ W]

/-!

## A. The divergence on functions

-/

/-- The vector calculus operator `div`. -/
noncomputable def div {d} (f : Space d â†’ EuclideanSpace â„ (Fin d)) :
    Space d â†’ â„ := fun x =>
  -- get i-th component of `f`
  let fi i x := coord i (f x)
  -- derivative of i-th component in i-th coordinate
  -- âˆ‚fáµ¢/âˆ‚xâ±¼
  let df i x := âˆ‚[i] (fi i) x
  âˆ‘ i, df i x

@[inherit_doc div]
macro (name := divNotation) "âˆ‡" "â¬" f:term:100 : term => `(div $f)

/-!

### A.1. The divergence on the zero function

-/

@[simp]
lemma div_zero : âˆ‡ â¬ (0 : Space d â†’ EuclideanSpace â„ (Fin d)) = 0 := by
  unfold div Space.deriv Finset.sum
  simp only [Pi.ofNat_apply, fderiv_fun_const, ContinuousLinearMap.zero_apply, Multiset.map_const',
    Finset.card_val, Finset.card_univ, Fintype.card_fin, Multiset.sum_replicate, smul_zero]
  rfl

/-!

### A.2. The divergence on a constant function

-/

@[simp]
lemma div_const : âˆ‡ â¬ (fun _ : Space d => v) = 0 := by
  unfold div Space.deriv Finset.sum
  simp only [fderiv_fun_const, Pi.ofNat_apply, ContinuousLinearMap.zero_apply, Multiset.map_const',
    Finset.card_val, Finset.card_univ, Fintype.card_fin, Multiset.sum_replicate, smul_zero]
  rfl

/-!

### A.3. The divergence distributes over addition

-/

lemma div_add (f1 f2 : Space d â†’ EuclideanSpace â„ (Fin d))
    (hf1 : Differentiable â„ f1) (hf2 : Differentiable â„ f2) :
    âˆ‡ â¬ (f1 + f2) = âˆ‡ â¬ f1 + âˆ‡ â¬ f2 := by
  unfold div
  simp only [Pi.add_apply]
  funext x
  simp only [Pi.add_apply]
  rw [â† Finset.sum_add_distrib]
  congr
  funext i
  simp [coord_apply, Space.deriv]
  rw [fderiv_fun_add]
  simp only [ContinuousLinearMap.add_apply]
  Â· fun_prop
  Â· fun_prop

/-!

### A.4. The divergence distributes over scalar multiplication

-/

lemma div_smul (f : Space d â†’ EuclideanSpace â„ (Fin d)) (k : â„)
    (hf : Differentiable â„ f) :
    âˆ‡ â¬ (k â€¢ f) = k â€¢ âˆ‡ â¬ f := by
  unfold div
  simp only [Pi.smul_apply]
  funext x
  simp only [Pi.smul_apply, smul_eq_mul]
  rw [Finset.mul_sum]
  congr
  funext i
  simp [coord_apply]
  simp [Space.deriv]
  rw [fderiv_const_mul]
  simp only [ContinuousLinearMap.coe_smul', Pi.smul_apply, smul_eq_mul]
  Â· fun_prop

/-!

### A.5. The divergence of a linear map is a linear map

-/

lemma div_linear_map (f : W â†’ Space 3 â†’ EuclideanSpace â„ (Fin 3))
    (hf : âˆ€ w, Differentiable â„ (f w))
    (hf' : IsLinearMap â„ f) :
    IsLinearMap â„ (fun w => âˆ‡ â¬ (f w)) := by
  constructor
  Â· intro w w'
    rw [hf'.map_add]
    rw [div_add]
    repeat fun_prop
  Â· intros k w
    rw [hf'.map_smul]
    rw [div_smul]
    fun_prop

/-!

## B. Divergence of distributions

-/

open MeasureTheory SchwartzMap InnerProductSpace Distribution

/-- The divergence of a distribution `(Space d) â†’d[â„] (EuclideanSpace â„ (Fin d))` as a distribution
  `(Space d) â†’d[â„] â„`. -/
noncomputable def distDiv {d} :
    ((Space d) â†’d[â„] (EuclideanSpace â„ (Fin d))) â†’â‚—[â„] (Space d) â†’d[â„] â„ where
  toFun f :=
    let trace : (Space d â†’L[â„] (EuclideanSpace â„ (Fin d))) â†’L[â„] â„ := {
      toFun v := âˆ‘ i, âŸªv (basis i), basis iâŸ«_â„
      map_add' v1 v2 := by
        simp only [ContinuousLinearMap.add_apply, inner_basis, PiLp.add_apply]
        rw [Finset.sum_add_distrib]
      map_smul' a v := by
        simp only [ContinuousLinearMap.coe_smul', Pi.smul_apply, inner_basis, PiLp.smul_apply,
          smul_eq_mul, RingHom.id_apply]
        rw [Finset.mul_sum]
      cont := by fun_prop}
    trace.comp (Distribution.fderivD â„ f)
  map_add' f1 f2 := by
    ext x
    simp
  map_smul' a f := by
    ext x
    simp

/-!

### B.1. Basic equalities

-/

lemma distDiv_apply_eq_sum_fderivD {d}
    (f : (Space d) â†’d[â„] EuclideanSpace â„ (Fin d)) (Î· : ğ“¢(Space d, â„)) :
    distDiv f Î· = âˆ‘ i, fderivD â„ f Î· (basis i) i := by
  simp [distDiv]

lemma distDiv_apply_eq_sum_distDeriv {d}
    (f : (Space d) â†’d[â„] EuclideanSpace â„ (Fin d)) (Î· : ğ“¢(Space d, â„)) :
    distDiv f Î· = âˆ‘ i, distDeriv i f Î· i := by
  rw [distDiv_apply_eq_sum_fderivD]
  rfl

/-!

### B.2. Divergence on distributions from bounded functions

-/

/-- The divergence of a distribution from a bounded function. -/
lemma distDiv_ofFunction {dm1 : â„•} {f : Space dm1.succ â†’ EuclideanSpace â„ (Fin dm1.succ)}
    {hf : IsDistBounded f} (Î· : ğ“¢(Space dm1.succ, â„)) :
    distDiv (distOfFunction f hf) Î· =
    - âˆ« x : Space dm1.succ, âŸªf x, Space.grad Î· xâŸ«_â„ := by
  rw [distDiv_apply_eq_sum_fderivD]
  conv_rhs =>
    enter [1, 2, x]
    rw [grad_eq_sum, inner_sum]
  conv_lhs =>
    enter [2, i]
    rw [fderivD_apply, distOfFunction_apply]
  /- The following lemma could probably be moved out of this result. -/
  have integrable_lemma (i j : Fin (dm1 + 1)) :
      Integrable (fun x =>
        (((SchwartzMap.evalCLM (ğ•œ := â„) (basis i)) ((fderivCLM â„) Î·)) x â€¢ f x) j) volume := by
    simp only [PiLp.smul_apply]
    exact (hf.pi_comp j).integrable_space _
  rw [MeasureTheory.integral_finset_sum]
  Â· simp
    congr
    funext i
    rw [MeasureTheory.eval_integral_piLp]
    Â· congr
      funext x
      simp [inner_smul_right]
      left
      rw [deriv_eq_fderiv_basis]
      rfl
    Â· intro j
      exact integrable_lemma i j
  Â· intro i hi
    simp only [Nat.succ_eq_add_one, inner_smul_right, inner_basis]
    convert integrable_lemma i i using 2
    rename_i x
    simp only [Nat.succ_eq_add_one, PiLp.smul_apply, smul_eq_mul, mul_eq_mul_right_iff]
    left
    rw [deriv_eq_fderiv_basis]
    rfl

end Space
