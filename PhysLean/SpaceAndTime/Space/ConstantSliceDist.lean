/-
Copyright (c) 2025 Joseph Tooby-Smith. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Joseph Tooby-Smith
-/
import PhysLean.SpaceAndTime.Space.Slice
/-!

# Constant slice distributions

## i. Overview

In this moudule we define the lift of distributions on `Space d` to distributions
on `Space d.succ` which are constant between slices in the `i`th direction.

This is used, for example, to define distributions which are translationally invariant
in the `i`th direction.

Examples of distributions which can be constructed in this way include the dirac deltas for
lines and planes, rather then points.

## ii. Key results

- `sliceSchwartz` : The continuous linear map which takes a Schwartz map on
  `Space d.succ` and gives a Schwartz map on `Space d` by integrating over the `i`th direction.
- `constantSliceDist` : The distribution on `Space d.succ` formed by a distribution on `Space d`
  which is translationally invariant in the `i`th direction.

## iii. Table of contents

- A. Schwartz maps
  - A.1. Bounded condition for derivatives of Schwartz maps on slices
  - A.2. Integrability for of Schwartz maps on slices
  - A.3. Continiuity of integrations of slices of Schwartz maps
  - A.4. Derivative of integrations of slices of Schwartz maps
  - A.5. Differentiability as a slices of Schwartz maps
  - A.6. Smoothness as slices of Schwartz maps
  - A.7. Iterated derivatives of integrations of slices of Schwartz maps
  - A.8. The map integrating over one component of a Schwartz map
- B. Constant slice distribution
  - B.1. Derivative of constant slice distributions

## iv. References

-/
open SchwartzMap NNReal
noncomputable section

variable (ğ•œ : Type) {E F F' : Type} [RCLike ğ•œ] [NormedAddCommGroup E] [NormedAddCommGroup F]
  [NormedAddCommGroup F'] [NormedSpace â„ E] [NormedSpace â„ F]

namespace Space

open MeasureTheory Real

/-!

## A. Schwartz maps

-/

/-!

### A.1. Bounded condition for derivatives of Schwartz maps on slices

-/

lemma schwartzMap_slice_bound {n m} {d : â„•} (i : Fin d.succ) :
    âˆƒ rt, âˆ€ (Î· : ğ“¢(Space d.succ, â„)), âˆƒ k,
    Integrable (fun x : â„ => â€–((1 + â€–xâ€–) ^ rt)â»Â¹â€–) volume âˆ§
    (âˆ€ (x : Space d), âˆ€ r, â€–(slice i).symm (r, x)â€– ^ m *
      â€–iteratedFDeriv â„ n â‡‘Î· ((slice i).symm (r, x))â€– â‰¤ k * â€–(1 + â€–râ€–) ^ (rt)â€–â»Â¹)
      âˆ§ k = (2 ^ (rt + m, n).1 * ((Finset.Iic (rt + m, n)).sup
        fun m => SchwartzMap.seminorm â„ m.1 m.2) Î·) := by
  obtain âŸ¨rt, hrtâŸ© : âˆƒ r, Integrable (fun x : â„ => â€–((1 + â€–xâ€–) ^ r)â»Â¹â€–) volume := by
      obtain âŸ¨r, hâŸ© := Measure.HasTemperateGrowth.exists_integrable (Î¼ := volume (Î± := â„))
      use r
      convert h using 1
      funext x
      simp only [norm_inv, norm_pow, Real.norm_eq_abs, Real.rpow_neg_natCast, zpow_neg,
        zpow_natCast, inv_inj]
      rw [abs_of_nonneg (by positivity)]
  use rt
  intro Î·
  have h0 := one_add_le_sup_seminorm_apply (ğ•œ := â„) (m := (rt + m, n))
      (k := rt + m) (n := n) le_rfl (le_rfl) Î·
  simp at h0
  let k := 2 ^ (rt + m, n).1 *
    ((Finset.Iic (rt + m, n)).sup fun m => SchwartzMap.seminorm â„ m.1 m.2) Î·
  refine âŸ¨k, âŸ¨hrt, fun x r => ?_, by rflâŸ©âŸ©
  trans k * â€–(1 + â€–((slice i).symm (r, x))â€–) ^ rtâ€–â»Â¹; swap
  Â· refine mul_le_mul_of_nonneg (by rfl) ?_ (by positivity) (by positivity)
    by_cases rt = 0
    Â· subst rt
      simp
    refine inv_antiâ‚€ ?_ ?_
    Â· simp only [norm_eq_abs, norm_pow]
      rw [abs_of_nonneg (by positivity)]
      positivity
    simp only [norm_pow, Real.norm_eq_abs, Nat.succ_eq_add_one]
    refine pow_le_pow_leftâ‚€ (by positivity) ?_ rt
    rw [abs_of_nonneg (by positivity)]
    conv_rhs => rw [abs_of_nonneg (by positivity)]
    simp only [add_le_add_iff_left]
    exact abs_right_le_norm_slice_symm i r x
  refine (le_mul_inv_iffâ‚€ ?_).mpr (le_trans ?_ (h0 ((slice i).symm (r, x))))
  Â· simp
    by_cases hr : rt = 0
    Â· subst rt
      simp
    positivity
  trans (â€–((slice i).symm (r, x))â€– ^ m * â€–(1 + â€–((slice i).symm (r, x))â€–) ^ rtâ€–) *
    â€–iteratedFDeriv â„ n â‡‘Î· (((slice i).symm (r, x)))â€–
  Â· apply le_of_eq
    simp [mul_assoc]
    left
    ring
  apply mul_le_mul_of_nonneg _ (by rfl) (by positivity) (by positivity)
  trans (1 + â€–((slice i).symm (r, x))â€–) ^ m * (1 + â€–((slice i).symm (r, x))â€–) ^ rt
  Â· refine mul_le_mul_of_nonneg ?_ ?_ (by positivity) (by positivity)
    Â· apply pow_le_pow_leftâ‚€ (by positivity) ?_ m
      simp
    Â· simp
      rw [abs_of_nonneg (by positivity)]
  apply le_of_eq
  ring_nf

/-!

### A.2. Integrability for of Schwartz maps on slices

-/

@[fun_prop]
lemma schwartzMap_mul_iteratedFDeriv_integrable_slice_symm {d : â„•} (n m : â„•)
    (Î· : ğ“¢(Space d.succ, â„))
    (x : Space d) (i : Fin d.succ) :
    Integrable (fun r => â€–(slice i).symm (r, x)â€– ^ m *
    â€–iteratedFDeriv â„ n â‡‘Î· ((slice i).symm (r, x))â€–) volume := by
  obtain âŸ¨rt, hrtâŸ© := schwartzMap_slice_bound (m := m) (n := n) (d := d) i
  obtain âŸ¨k, hrt, hbound, k_eqâŸ© := hrt Î·
  apply Integrable.mono' (g := fun t => k * â€–(1 + â€–tâ€–) ^ (rt)â€–â»Â¹)
  Â· apply Integrable.const_mul
    convert hrt using 1
    simp
  Â· apply Continuous.aestronglyMeasurable
    apply Continuous.mul
    Â· fun_prop
    apply Continuous.norm
    apply Continuous.comp'
    apply ContDiff.continuous_iteratedFDeriv (n := (n + 1 : â„•))
    exact Nat.cast_le.mpr (by omega)
    have hÎ· := Î·.smooth'
    apply hÎ·.of_le (ENat.LEInfty.out)
    fun_prop
  Â· filter_upwards with t
    apply le_trans _ (hbound x t)
    apply le_of_eq
    simp only [Nat.succ_eq_add_one, norm_mul, norm_pow, Real.norm_eq_abs]
    rw [abs_of_nonneg (by positivity)]
    simp

lemma schwartzMap_integrable_slice_symm {d : â„•} (i : Fin d.succ) (Î· : ğ“¢(Space d.succ, â„))
    (x : Space d) : Integrable (fun r => Î· ((slice i).symm (r, x))) volume := by
  apply (schwartzMap_mul_iteratedFDeriv_integrable_slice_symm 0 0 Î· x i).congr'
  Â· fun_prop
  Â· simp

@[fun_prop]
lemma schwartzMap_fderiv_integrable_slice_symm {d : â„•} (Î· : ğ“¢(Space d.succ, â„)) (x : Space d)
    (i : Fin d.succ) :
    Integrable (fun r => fderiv â„ (fun x => Î· (((slice i).symm (r, x)))) x) volume := by
  apply Integrable.mono' (g := fun r =>
    â€–iteratedFDeriv â„ 1 â‡‘Î· ((slice i).symm (r, x))â€– * â€–(slice i).symm.toContinuousLinearMap.comp
          (ContinuousLinearMap.prod (0 : Space d â†’L[â„] â„) (ContinuousLinearMap.id â„ (Space d)))â€–)
  Â· apply Integrable.mul_const
    simpa using (schwartzMap_mul_iteratedFDeriv_integrable_slice_symm 1 0 Î· x i)
  Â· apply Continuous.aestronglyMeasurable
    refine Continuous.fderiv_one ?_ ?_
    Â· exact (Î·.smooth'.of_le (by simp)).comp ((slice i).symm.contDiff)
    Â· fun_prop
  Â· filter_upwards with r
    calc _
        _ â‰¤ â€–(fderiv â„ â‡‘Î· (((slice i).symm (r, x))))â€– *
          â€–fderiv â„ (fun x => (slice i).symm (r, x)) xâ€– := by
          rw [fderiv_comp' _ _ (by fun_prop)]; swap
          Â· apply Differentiable.differentiableAt
            exact Î·.smooth'.differentiable (by simp)
          exact ContinuousLinearMap.opNorm_comp_le (fderiv â„ â‡‘Î· (((slice i).symm (r, x)))) _
        _ â‰¤ â€–iteratedFDeriv â„ 1 (â‡‘Î·) ((((slice i).symm (r, x))))â€– *
            â€–fderiv â„ (fun x => (slice i).symm (r, x)) xâ€– := by
          apply le_of_eq
          congr 1
          simp only [Nat.succ_eq_add_one]
          rw [â† iteratedFDerivWithin_univ, norm_iteratedFDerivWithin_one, fderivWithin_univ]
          exact uniqueDiffWithinAt_univ
        _ â‰¤ â€–iteratedFDeriv â„ 1 (â‡‘Î·) ((((slice i).symm (r, x))))â€–
            * â€–(slice i).symm.toContinuousLinearMap.comp
          (ContinuousLinearMap.prod (0 : Space d â†’L[â„] â„)
          (ContinuousLinearMap.id â„ (Space d)))â€– := by
            apply le_of_eq
            congr
            rw [fderiv_comp', DifferentiableAt.fderiv_prodMk (by fun_prop) (by fun_prop)]
            simp only [Nat.succ_eq_add_one, fderiv_slice_symm, fderiv_fun_const, Pi.zero_apply,
              fderiv_id']
            fun_prop
            fun_prop

@[fun_prop]
lemma schwartzMap_fderiv_left_integrable_slice_symm {d : â„•} (Î· : ğ“¢(Space d.succ, â„)) (x : Space d)
    (i : Fin d.succ) :
    Integrable (fun r => fderiv â„ (fun r => Î· (((slice i).symm (r, x)))) r 1) volume := by
  conv_lhs =>
    enter [r]
    simp only [Nat.succ_eq_add_one, one_mul]
    change fderiv â„ (Î· âˆ˜ fun r => ((slice i).symm (r, x))) r 1
    rw [fderiv_comp _ (by
      apply Differentiable.differentiableAt
      exact Î·.smooth'.differentiable (by simp))
      (by fun_prop)]
    simp only [Nat.succ_eq_add_one, ContinuousLinearMap.coe_comp', Function.comp_apply,
      fderiv_slice_symm_left_apply]
    change SchwartzMap.pderivCLM â„ _ Î· _
  exact schwartzMap_integrable_slice_symm _ _ _

@[fun_prop]
lemma schwartzMap_iteratedFDeriv_norm_slice_symm_integrable {n} {d : â„•} (Î· : ğ“¢(Space d.succ, â„))
    (x : Space d) (i : Fin d.succ) :
    Integrable (fun r => â€–iteratedFDeriv â„ n â‡‘Î· (((slice i).symm (r, x)))â€–) volume := by
  convert schwartzMap_mul_iteratedFDeriv_integrable_slice_symm n 0 Î· x i using 1
  funext t
  simp

@[fun_prop]
lemma schwartzMap_iteratedFDeriv_slice_symm_integrable {n} {d : â„•} (Î· : ğ“¢(Space d.succ, â„))
    (x : Space d) (i : Fin d.succ) :
    Integrable (fun r => iteratedFDeriv â„ n â‡‘Î· (((slice i).symm (r, x)))) volume := by
  rw [â† MeasureTheory.integrable_norm_iff]
  Â· fun_prop
  Â· apply Continuous.aestronglyMeasurable
    apply Continuous.comp'
    apply ContDiff.continuous_iteratedFDeriv (n := (n + 1 : â„•))
    exact Nat.cast_le.mpr (by omega)
    have hÎ· := Î·.smooth'
    apply hÎ·.of_le (ENat.LEInfty.out)
    fun_prop

/-!

### A.3. Continiuity of integrations of slices of Schwartz maps
-/

lemma continuous_schwartzMap_slice_integral {d} (i : Fin d.succ) (Î· : ğ“¢(Space d.succ, â„)) :
    Continuous (fun x : Space d => âˆ« r : â„, Î· ((slice i).symm (r, x))) := by
  obtain âŸ¨rt, hrtâŸ© := schwartzMap_slice_bound (m := 0) (n := 0) (d := d) i
  obtain âŸ¨k, hrt, hbound, k_eqâŸ© := hrt Î·
  apply MeasureTheory.continuous_of_dominated (bound := fun t => k * â€–(1 + â€–tâ€–) ^ (rt)â€–â»Â¹)
  Â· intro x
    fun_prop
  Â· intro x
    filter_upwards with t
    simpa using hbound x t
  Â· apply Integrable.const_mul
    convert hrt using 1
    funext t
    simp
  Â· filter_upwards with t
    fun_prop

/-!

### A.4. Derivative of integrations of slices of Schwartz maps

-/

lemma schwartzMap_slice_integral_hasFDerivAt {d : â„•} (Î· : ğ“¢(Space d.succ, â„)) (i : Fin d.succ)
    (xâ‚€ : Space d) :
    HasFDerivAt (fun x => âˆ« (r : â„), Î· ((slice i).symm (r, x)))
      (âˆ« (r : â„), fderiv â„ (fun x : Space d => Î· ((slice i).symm (r, x))) xâ‚€) xâ‚€ := by
  let F : Space d â†’ â„ â†’ â„ := fun x r => Î· ((slice i).symm (r, x))
  let F' : Space d â†’ â„ â†’ Space d â†’L[â„] â„ :=
    fun xâ‚€ r => fderiv â„ (fun x : Space d => Î· ((slice i).symm (r, x))) xâ‚€
  have hF : âˆ€ t, âˆ€ x, HasFDerivAt (F Â· t) (F' x t) x := by
    intro t x
    dsimp [F, F']
    refine DifferentiableAt.hasFDerivAt ?_
    have hf := Î·.smooth'
    apply Differentiable.differentiableAt
    apply Differentiable.comp
    Â· exact hf.differentiable (by simp)
    Â· fun_prop
  obtain âŸ¨rt, hrtâŸ© := schwartzMap_slice_bound (m := 0) (n := 1) (d := d) i
  obtain âŸ¨k, hrt, hbound, k_eqâŸ© := hrt Î·
  suffices h1 : HasFDerivAt (fun x => âˆ« (a : â„), F x a) (âˆ« (a : â„), F' xâ‚€ a) xâ‚€ by exact h1
  apply hasFDerivAt_integral_of_dominated_of_fderiv_le
    (bound := fun t => (k * â€–(slice i).symm.toContinuousLinearMap.comp
          (ContinuousLinearMap.prod (0 : Space d â†’L[â„] â„) (ContinuousLinearMap.id â„ (Space d)))â€–)
          * â€–(1 + â€–tâ€–) ^ (rt)â€–â»Â¹) (Îµ := 1)
  Â· simp
  Â· filter_upwards with x
    fun_prop
  Â· simp [F]
    exact schwartzMap_integrable_slice_symm i Î· xâ‚€
  Â· simp [F']
    apply Continuous.aestronglyMeasurable
    refine Continuous.fderiv_one ?_ ?_
    Â· apply ContDiff.comp
      change ContDiff â„ 1 Î·
      apply Î·.smooth'.of_le (by simp)
      apply ContDiff.comp
      Â· exact ContinuousLinearEquiv.contDiff (slice i).symm
      Â· fun_prop
    Â· fun_prop
  Â· filter_upwards with r
    intro x _
    calc _
        _ â‰¤ â€–(fderiv â„ â‡‘Î· (((slice i).symm (r, x))))â€– *
          â€–fderiv â„ (fun x => (slice i).symm (r, x)) xâ€– := by
          simp [F']
          rw [fderiv_comp' _ _ (by fun_prop)]; swap
          Â· apply Differentiable.differentiableAt
            exact Î·.smooth'.differentiable (by simp)
          exact ContinuousLinearMap.opNorm_comp_le (fderiv â„ â‡‘Î· (((slice i).symm (r, x)))) _
        _ â‰¤ â€–iteratedFDeriv â„ 1 (â‡‘Î·) ((((slice i).symm (r, x))))â€– *
            â€–fderiv â„ (fun x => (slice i).symm (r, x)) xâ€– := by
          apply le_of_eq
          congr 1
          simp only [Nat.succ_eq_add_one]
          rw [â† iteratedFDerivWithin_univ, norm_iteratedFDerivWithin_one, fderivWithin_univ]
          exact uniqueDiffWithinAt_univ
        _ â‰¤ k * (|1 + â€–râ€–| ^ rt)â»Â¹ * â€–fderiv â„ (fun x => (slice i).symm (r, x)) xâ€– := by
          refine mul_le_mul_of_nonneg_right ?_ (by positivity)
          simpa using hbound x r
        _ â‰¤ k * (|1 + â€–râ€–| ^ rt)â»Â¹ * â€–(slice i).symm.toContinuousLinearMap.comp
          (ContinuousLinearMap.prod (0 : Space d â†’L[â„] â„)
          (ContinuousLinearMap.id â„ (Space d)))â€– := by
          apply le_of_eq
          congr 1
          rw [fderiv_comp', DifferentiableAt.fderiv_prodMk (by fun_prop) (by fun_prop)]
          simp only [Nat.succ_eq_add_one, fderiv_slice_symm, fderiv_fun_const, Pi.zero_apply,
            fderiv_id']
          fun_prop
          fun_prop
    apply le_of_eq
    simp only [norm_eq_abs, Nat.succ_eq_add_one, norm_pow]
    ring
  Â· apply Integrable.const_mul
    convert hrt using 1
    funext t
    simp
  Â· filter_upwards with t
    intro x _
    exact hF t x

/-!

### A.5. Differentiability as a slices of Schwartz maps

-/

lemma schwartzMap_slice_integral_differentiable {d : â„•} (Î· : ğ“¢(Space d.succ, â„))
    (i : Fin d.succ) :
    Differentiable â„ (fun x => âˆ« (r : â„), Î· ((slice i).symm (r, x))) :=
  fun x => (schwartzMap_slice_integral_hasFDerivAt Î· i x).differentiableAt

/-!

### A.6. Smoothness as slices of Schwartz maps

-/

lemma schwartzMap_slice_integral_contDiff {d : â„•} (n : â„•) (Î· : ğ“¢(Space d.succ, â„))
    (i : Fin d.succ) :
    ContDiff â„ n (fun x => âˆ« (r : â„), Î· ((slice i).symm (r, x))) := by
  revert Î·
  induction n with
  | zero =>
    intro Î·
    simp only [Nat.succ_eq_add_one, CharP.cast_eq_zero, contDiff_zero]
    exact continuous_schwartzMap_slice_integral i Î·
  | succ n ih =>
    intro Î·
    simp only [Nat.succ_eq_add_one, Nat.cast_add, Nat.cast_one]
    rw [contDiff_succ_iff_hasFDerivAt]
    use fun xâ‚€ => (âˆ« (r : â„), fderiv â„ (fun x : Space d => Î· ((slice i).symm (r, x))) xâ‚€)
    apply And.intro
    Â· rw [contDiff_clm_apply_iff]
      intro y
      have hl : (fun x => (âˆ« (r : â„), fderiv â„ (fun x => Î· (((slice i).symm (r, x)))) x) y) =
          fun x => (âˆ« (r : â„), fderiv â„ (fun x => Î· (((slice i).symm (r, x)))) x y) := by
        funext x
        simp only [Nat.succ_eq_add_one]
        rw [ContinuousLinearMap.integral_apply]
        fun_prop
      rw [hl]
      have hl2 : (fun x => âˆ« (r : â„), (fderiv â„ (fun x => Î· (((slice i).symm (r, x)))) x) y)=
          fun x => âˆ« (r : â„), SchwartzMap.pderivCLM â„ ((slice i).symm (0, y)) Î·
            (((slice i).symm (r, x))) := by
        funext x
        congr
        funext t
        simp only [Nat.succ_eq_add_one, pderivCLM_apply]
        rw [fderiv_comp']
        simp only [ContinuousLinearMap.coe_comp', Function.comp_apply,
          fderiv_slice_symm_right_apply, Nat.succ_eq_add_one]
        Â· apply Differentiable.differentiableAt
          exact Î·.smooth'.differentiable (by simp)
        fun_prop
      rw [hl2]
      apply ih
    Â· exact fun x => schwartzMap_slice_integral_hasFDerivAt Î· i x
/-!

### A.7. Iterated derivatives of integrations of slices of Schwartz maps

-/

lemma schwartzMap_slice_integral_iteratedFDeriv_apply {d : â„•} (n : â„•) (Î· : ğ“¢(Space d.succ, â„))
    (i : Fin d.succ) :
    âˆ€ x, âˆ€ y, iteratedFDeriv â„ n (fun x => âˆ« (r : â„), Î· ((slice i).symm (r, x))) x y =
      âˆ« (r : â„), (iteratedFDeriv â„ n Î· ((slice i).symm (r, x)))
      (fun j => (slice i).symm (0, y j)) := by
  induction n with
  | zero =>
    simp
  | succ n ih =>
    intro x y
    calc _
      _ = ((fderiv â„ (fun x => iteratedFDeriv â„ n
          (fun x => âˆ« (r : â„), Î· ((slice i).symm (r, x))) x (Fin.tail y)) x) (y 0)) := by
        rw [iteratedFDeriv_succ_apply_left]
        refine Eq.symm (fderiv_continuousMultilinear_apply_const_apply ?_ (Fin.tail y) (y 0))
        apply Differentiable.differentiableAt
        apply (schwartzMap_slice_integral_contDiff (n + 1) Î· i).differentiable_iteratedFDeriv
        refine Nat.cast_lt.mpr ?_
        omega
      _ = (fderiv â„ (fun x => âˆ« (r : â„), (iteratedFDeriv â„ n (â‡‘Î·) ((slice i).symm (r, x)))
          fun j => (slice i).symm (0, Fin.tail y j)) x) (y 0) := by
        conv_lhs =>
          enter [1, 2, x]
          rw [ih]
      _ = (fderiv â„ (fun x => âˆ« (r : â„), (Î·.iteratedPDeriv â„
          (fun j => (slice i).symm (0, Fin.tail y j)) (((slice i).symm (r, x)))))) x (y 0) := by
        congr
        funext x
        congr
        funext t
        rw [iteratedPDeriv_eq_iteratedFDeriv]
      _ = âˆ« (r : â„), (fderiv â„ (fun x => ((iteratedPDeriv â„ fun j =>
          (slice i).symm (0, Fin.tail y j)) Î·)
          ((slice i).symm (r, x))) x) (y 0) := by
        rw [(schwartzMap_slice_integral_hasFDerivAt _ i x).fderiv]
        rw [ContinuousLinearMap.integral_apply]
        fun_prop
    congr
    funext r
    calc _
        _ = (fderiv â„ (fun x => (iteratedFDeriv â„ n (â‡‘Î·) ((slice i).symm (r, x))
            (fun j => (slice i).symm (0, Fin.tail y j)))) x) (y 0) := by
          congr
          funext x
          rw [iteratedPDeriv_eq_iteratedFDeriv]
    rw [iteratedFDeriv_succ_apply_left]
    simp only [Nat.succ_eq_add_one]
    rw [â† fderiv_continuousMultilinear_apply_const_apply]
    rw [â† fderiv_fun_slice_symm_right_apply]
    rfl
    Â· apply Differentiable.differentiableAt
      refine Differentiable.continuousMultilinear_apply_const ?_
          (Fin.tail fun j => (slice i).symm (0, y j))
      refine Differentiable.fun_comp ?_ ?_
      apply ContDiff.differentiable_iteratedFDeriv (n := (n + 1 : â„•))
      refine Nat.cast_lt.mpr ?_
      simp only [lt_add_iff_pos_right, zero_lt_one]
      have hÎ· := Î·.smooth'
      apply ContDiff.comp
      Â· exact hÎ·.of_le (by exact ENat.LEInfty.out)
      Â· fun_prop
      fun_prop
    Â· apply Differentiable.differentiableAt
      refine Differentiable.fun_comp ?_ ?_
      apply ContDiff.differentiable_iteratedFDeriv (n := (n + 1 : â„•))
      refine Nat.cast_lt.mpr ?_
      simp only [lt_add_iff_pos_right, zero_lt_one]
      have hÎ· := Î·.smooth'
      apply ContDiff.comp
      Â· exact hÎ·.of_le (by exact ENat.LEInfty.out)
      Â· fun_prop
      fun_prop

lemma schwartzMap_slice_integral_iteratedFDeriv {d : â„•} (n : â„•) (Î· : ğ“¢(Space d.succ, â„))
    (i : Fin d.succ) (x : Space d) :
    iteratedFDeriv â„ n (fun x => âˆ« (r : â„), Î· ((slice i).symm (r, x))) x
    = (âˆ« (r : â„), iteratedFDeriv â„ n Î· ((slice i).symm (r, x))).compContinuousLinearMap
      (fun _ => (slice i).symm.toContinuousLinearMap.comp
      (ContinuousLinearMap.prod (0 : Space d â†’L[â„] â„) (ContinuousLinearMap.id â„ (Space d)))) := by
  ext y
  rw [schwartzMap_slice_integral_iteratedFDeriv_apply]
  rw [â† ContinuousMultilinearMap.integral_apply]
  rfl
  simp only [Nat.succ_eq_add_one]
  exact schwartzMap_iteratedFDeriv_slice_symm_integrable Î· x i

lemma schwartzMap_slice_integral_iteratedFDeriv_norm_le {d : â„•} (n : â„•) (Î· : ğ“¢(Space d.succ, â„))
    (i : Fin d.succ) (x : Space d) :
    â€–iteratedFDeriv â„ n (fun x => âˆ« (r : â„), Î· ((slice i).symm (r, x))) xâ€– â‰¤
        (âˆ« (r : â„), â€–iteratedFDeriv â„ n Î· ((slice i).symm (r, x))â€–) *
        â€–(slice i).symm.toContinuousLinearMap.comp
          (ContinuousLinearMap.prod (0 : Space d â†’L[â„] â„)
          (ContinuousLinearMap.id â„ (Space d)))â€– ^ n := by
  rw [schwartzMap_slice_integral_iteratedFDeriv]
  apply le_trans (ContinuousMultilinearMap.norm_compContinuousLinearMap_le _ _)
  simp
  refine mul_le_mul ?_ (by rfl) (by positivity) (by positivity)
  exact norm_integral_le_integral_norm fun a => iteratedFDeriv â„ n â‡‘Î· _

lemma schwartzMap_mul_pow_slice_integral_iteratedFDeriv_norm_le {d : â„•} (n m : â„•) (i : Fin d.succ) :
    âˆƒ rt, âˆ€ (Î· : ğ“¢(Space d.succ, â„)),âˆ€ (x : Space d),
    Integrable (fun x : â„ => â€–((1 + â€–xâ€–) ^ rt)â»Â¹â€–) volume âˆ§
    â€–xâ€– ^ m * â€–iteratedFDeriv â„ n (fun x => âˆ« (r : â„), Î· ((slice i).symm (r, x))) xâ€– â‰¤
        ((âˆ« (r : â„), â€–((1 + â€–râ€–) ^ rt)â»Â¹â€–) *
        â€–(slice i).symm.toContinuousLinearMap.comp
          (ContinuousLinearMap.prod (0 : Space d â†’L[â„] â„)
          (ContinuousLinearMap.id â„ (Space d)))â€– ^ n)
        * (2 ^ (rt + m, n).1 * ((Finset.Iic (rt + m, n)).sup
          fun m => SchwartzMap.seminorm â„ m.1 m.2) Î·) := by
  obtain âŸ¨rt, hrtâŸ© := schwartzMap_slice_bound (m := m) (n := n) (d := d) i
  use rt
  intro Î· x
  obtain âŸ¨k, hrt, hbound, k_eqâŸ© := hrt Î·
  refine âŸ¨hrt, ?_âŸ©
  generalize hk : 2 ^ (rt + m, n).1 * ((Finset.Iic (rt + m, n)).sup
    fun m => SchwartzMap.seminorm â„ m.1 m.2) Î· = k' at *
  subst k_eq
  have hk' : 0 â‰¤ k := by rw [â† hk]; positivity
  calc _
      _ â‰¤ â€–xâ€– ^ m * ((âˆ« (r : â„), â€–iteratedFDeriv â„ n Î· ((slice i).symm (r, x))â€–) *
          â€–(slice i).symm.toContinuousLinearMap.comp
          ((0 : Space d â†’L[â„] â„).prod (ContinuousLinearMap.id â„ (Space d)))â€– ^ n) := by
        refine mul_le_mul_of_nonneg (by rfl) ?_ (by positivity) (by positivity)
        exact schwartzMap_slice_integral_iteratedFDeriv_norm_le n Î· i x
      _ â‰¤ (âˆ« (r : â„), â€–xâ€– ^ m * â€–iteratedFDeriv â„ n Î· ((slice i).symm (r, x))â€–) *
          â€–(slice i).symm.toContinuousLinearMap.comp
          ((0 : Space d â†’L[â„] â„).prod (ContinuousLinearMap.id â„ (Space d)))â€– ^ n := by
        apply le_of_eq
        rw [â† mul_assoc, MeasureTheory.integral_const_mul]
      _ â‰¤ (âˆ« (r : â„), â€–((slice i).symm (r, x))â€– ^ m *
          â€–iteratedFDeriv â„ n Î· (((slice i).symm (r, x)))â€–) *
          â€–(slice i).symm.toContinuousLinearMap.comp
          ((0 : Space d â†’L[â„] â„).prod (ContinuousLinearMap.id â„ (Space d)))â€– ^ n := by
        refine mul_le_mul_of_nonneg ?_ (by rfl) (by positivity) (by positivity)
        refine integral_mono ?_ ?_ ?_
        Â· apply Integrable.const_mul
          fun_prop
        Â· fun_prop
        Â· refine Pi.le_def.mpr ?_
          intro t
          apply mul_le_mul_of_nonneg _ (by rfl) (by positivity) (by positivity)
          refine pow_le_pow_leftâ‚€ (by positivity) ?_ m
          simp
      _ â‰¤ ((âˆ« (r : â„), k * â€–((1 + â€–râ€–) ^ rt)â»Â¹â€–)) *
          â€–(slice i).symm.toContinuousLinearMap.comp
          ((0 : Space d â†’L[â„] â„).prod (ContinuousLinearMap.id â„ (Space d)))â€– ^ n := by
        refine mul_le_mul_of_nonneg ?_ (by rfl) (by positivity) (by positivity)
        refine integral_mono ?_ ?_ ?_
        Â· fun_prop
        Â· apply Integrable.const_mul
          exact hrt
        Â· refine Pi.le_def.mpr ?_
          intro t
          convert hbound x t using 1
          simp
  apply le_of_eq
  rw [MeasureTheory.integral_const_mul]
  ring

/-!

### A.8. The map integrating over one component of a Schwartz map

-/

/-- The continuous linear map taking a Schwartz map and integrating over the `i`th component,
  to give a Schwartz map of one dimension lower. -/
def sliceSchwartz {d : â„•} (i : Fin d.succ) :
    ğ“¢(Space d.succ, â„) â†’L[â„] ğ“¢(Space d, â„) := by
  refine SchwartzMap.mkCLM (fun Î· x => âˆ« (r : â„), Î· ((slice i).symm (r, x))) ?_ ?_ ?_ ?_
  Â· intro Î·1 Î·2 x
    simp only [Nat.succ_eq_add_one, add_apply]
    rw [integral_add]
    Â· exact schwartzMap_integrable_slice_symm i Î·1 x
    Â· exact schwartzMap_integrable_slice_symm i Î·2 x
  Â· intro a Î· x
    simp only [Nat.succ_eq_add_one, smul_apply, smul_eq_mul, RingHom.id_apply]
    rw [integral_const_mul]
  Â· intro Î·
    simp only [Nat.succ_eq_add_one]
    refine contDiff_infty.mpr ?_
    intro n
    exact schwartzMap_slice_integral_contDiff n Î· i
  Â· simp
    intro m n
    obtain âŸ¨rt, hrtâŸ© := schwartzMap_mul_pow_slice_integral_iteratedFDeriv_norm_le
      (d := d) (n := n) (m := m) i
    use (Finset.Iic (rt + m, n))
    use 2 ^ (rt + m, n).1 * (âˆ« (r : â„), â€–((1 + â€–râ€–) ^ rt)â»Â¹â€–) *
        â€–(slice i).symm.toContinuousLinearMap.comp
        ((0 : Space d â†’L[â„] â„).prod (ContinuousLinearMap.id â„ (Space d)))â€– ^ n
    apply And.intro
    Â· positivity
    intro Î· x
    specialize hrt Î· x
    obtain âŸ¨hrt1, hboundâŸ© := hrt
    apply le_trans hbound
    apply le_of_eq
    ring_nf
    rfl

lemma sliceSchwartz_apply {d : â„•} (i : Fin d.succ) (Î· : ğ“¢(Space d.succ, â„)) (x : Space d) :
    sliceSchwartz i Î· x = âˆ« (r : â„), Î· ((slice i).symm (r, x)) := by
  rfl
/-!

## B. Constant slice distribution
-/

/-- Distributions on `Space d.succ` from distributions on `Space d` given a
  direction `i`.
  These distributions are constant on slices in the `i` direction.. -/
def constantSliceDist {M : Type} [NormedAddCommGroup M] [NormedSpace â„ M] {d : â„•} (i : Fin d.succ) :
    ((Space d) â†’d[â„] M) â†’â‚—[â„] (Space d.succ) â†’d[â„] M where
  toFun f := f âˆ˜L sliceSchwartz i
  map_add' f g := by
    ext Î·
    simp
  map_smul' c f := by
    ext Î·
    simp

lemma constantSliceDist_apply {M : Type} [NormedAddCommGroup M] [NormedSpace â„ M]
    {d : â„•} (i : Fin d.succ) (f : (Space d) â†’d[â„] M) (Î· : ğ“¢(Space d.succ, â„)) :
    constantSliceDist i f Î· = f (sliceSchwartz i Î·) := by
  rfl

/-!

### B.1. Derivative of constant slice distributions

-/

lemma distDeriv_constantSliceDist_same {M : Type} [NormedAddCommGroup M] [NormedSpace â„ M]
    {d : â„•} (i : Fin d.succ) (f : (Space d) â†’d[â„] M) :
    distDeriv i (constantSliceDist i f) = 0 := by
  ext Î·
  simp [constantSliceDist_apply, Space.distDeriv_apply, Distribution.fderivD_apply]
  trans f 0; swap
  Â· simp
  congr
  ext x
  simp [sliceSchwartz_apply]
  calc _
    _ = âˆ« r, fderiv â„ Î· ((slice i).symm (r, x)) (basis i) := by rfl
    _ = âˆ« r, fderiv â„ (fun r => Î· ((slice i).symm (r, x))) r 1 := by
        congr
        funext r
        rw [basis_self_eq_slice, fderiv_fun_slice_symm_left_apply]
        apply Differentiable.differentiableAt
        exact Î·.differentiable
    _ = âˆ« (r : â„), (fun r => 1) r * fderiv â„ (fun r => Î· ((slice i).symm (r, x))) r 1 := by simp
    _ = - âˆ« (r : â„), fderiv â„ (fun t => 1) r 1 * (fun r => Î· ((slice i).symm (r, x))) r := by
      rw [integral_mul_fderiv_eq_neg_fderiv_mul_of_integrable]
      Â· simp
      Â· simp
        change Integrable (fun r => fderiv â„ (fun r => Î· ((slice i).symm (r, x))) r 1) volume
        fun_prop
      Â· simp
        exact schwartzMap_integrable_slice_symm i Î· x
      Â· fun_prop
      Â· apply Differentiable.comp
        Â· exact Î·.smooth'.differentiable (by simp)
        Â· fun_prop
  simp

lemma distDeriv_constantSliceDist_succAbove {M : Type} [NormedAddCommGroup M] [NormedSpace â„ M]
    {d : â„•} (i : Fin d.succ) (j : Fin d) (f : (Space d) â†’d[â„] M) :
    distDeriv (i.succAbove j) (constantSliceDist i f) =
    constantSliceDist i (distDeriv j f) := by
  ext Î·
  simp [constantSliceDist_apply, Space.distDeriv_apply, Distribution.fderivD_apply]
  congr 1
  ext x
  simp [sliceSchwartz_apply]
  change âˆ« (r : â„), fderiv â„ Î· _ _ = fderiv â„ (fun x => âˆ« (r : â„), Î· _) _ _
  rw [(schwartzMap_slice_integral_hasFDerivAt Î· i x).fderiv]
  rw [ContinuousLinearMap.integral_apply]
  congr
  rw [basis_succAbove_eq_slice]
  funext r
  rw [fderiv_fun_slice_symm_right_apply]
  Â· apply Differentiable.differentiableAt
    exact Î·.differentiable
  Â· fun_prop

end Space
