/-
Copyright (c) 2025 Joseph Tooby-Smith. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Joseph Tooby-Smith
-/
import PhysLean.SpaceAndTime.Space.IsDistBounded
import Mathlib.MeasureTheory.SpecificCodomains.WithLp
/-!

# Distributions from functions on space

## i. Overview

In this module we define distributions on space constructed from functions
`f : Space d â†’ F` satisfying the condition `IsDistBounded f`.

This gives a convenient way to construct distributions from functions, without needing
to reference the underlying Schwartz maps.

## ii. Key results

- `distOfFunction f hf` : The distribution on space constructed from the function
  `f : Space d â†’ F` satisfying the `IsDistBounded f` condition.

## iii. Table of contents

- A. Definition of a distribution from a function
- B. Linarity properties of getting distributions from functions
- C. Properties related to inner products
- D. Components

## iv. References

-/
open SchwartzMap NNReal
noncomputable section

variable (ğ•œ : Type) {E F F' : Type} [RCLike ğ•œ] [NormedAddCommGroup E] [NormedAddCommGroup F]
  [NormedAddCommGroup F'] [NormedSpace â„ E] [NormedSpace â„ F]

namespace Space

open MeasureTheory

/-!

## A. Definition of a distribution from a function

-/

/-- A distribution `Space d â†’d[â„] F` from a function
  `f : Space d â†’ F` which satisfies the `IsDistBounded f` condition. -/
def distOfFunction {d : â„•} (f : Space d â†’ F) (hf : IsDistBounded f) :
    (Space d) â†’d[â„] F := by
  refine mkCLMtoNormedSpace (fun Î· => âˆ« x, Î· x â€¢ f x) ?_ ?_ hf.integral_mul_schwartzMap_bounded
  Â· /- Addition -/
    intro Î· Îº
    simp only [add_apply]
    conv_lhs =>
      enter [2, a]
      rw [add_smul]
    rw [integral_add (by fun_prop) (by fun_prop)]
  Â· /- SMul-/
    intro a Î·
    simp only [smul_apply, smul_eq_mul, RingHom.id_apply]
    conv_lhs =>
      enter [2, a]
      rw [â† smul_smul]
    rw [integral_smul]

lemma distOfFunction_apply {d : â„•} (f : Space d â†’ F)
    (hf : IsDistBounded f) (Î· : ğ“¢(Space d, â„)) :
    distOfFunction f hf Î· = âˆ« x, Î· x â€¢ f x := rfl

/-!

## B. Linarity properties of getting distributions from functions

-/
@[simp]
lemma distOfFunction_zero_eq_zero {d : â„•} :
    distOfFunction (fun _ : Space d => (0 : F)) (by fun_prop) = 0 := by
  ext Î·
  simp [distOfFunction_apply]

lemma distOfFunction_smul {d : â„•} (f : Space d â†’ F)
    (hf : IsDistBounded f) (c : â„) :
    distOfFunction (c â€¢ f) (by fun_prop) = c â€¢ distOfFunction f hf := by
  ext Î·
  change _ = c â€¢ âˆ« x, Î· x â€¢ f x
  rw [distOfFunction_apply]
  simp only [Pi.smul_apply]
  rw [â† integral_smul]
  congr
  funext x
  rw [smul_comm]

lemma distOfFunction_smul_fun {d : â„•} (f : Space d â†’ F)
    (hf : IsDistBounded f) (c : â„) :
    distOfFunction (fun x => c â€¢ f x) (by fun_prop) = c â€¢ distOfFunction f hf := by
  ext Î·
  change _ = c â€¢ âˆ« x, Î· x â€¢ f x
  rw [distOfFunction_apply]
  rw [â† integral_smul]
  congr
  funext x
  rw [smul_comm]

lemma distOfFunction_mul_fun {d : â„•} (f : Space d â†’ â„)
    (hf : IsDistBounded f) (c : â„) :
    distOfFunction (fun x => c * f x) (by fun_prop) = c â€¢ distOfFunction f hf := by
  exact distOfFunction_smul_fun f hf c

lemma distOfFunction_neg {d : â„•} (f : Space d â†’ F)
    (hf : IsDistBounded (fun x => - f x)) :
    distOfFunction (fun x => - f x) hf = - distOfFunction f (by simpa using hf.neg) := by
  convert distOfFunction_smul_fun f (by simpa using hf.neg) (-1) using 1
  Â· simp
  Â· simp

/-!

## C. Properties related to inner products

-/

open InnerProductSpace

lemma distOfFunction_inner {d n : â„•} (f : Space d â†’ EuclideanSpace â„ (Fin n))
    (hf : IsDistBounded f)
    (Î· : ğ“¢(Space d, â„)) (y : EuclideanSpace â„ (Fin n)) :
    âŸªdistOfFunction f hf Î·, yâŸ«_â„ = âˆ« x, Î· x * âŸªf x, yâŸ«_â„ := by
  rw [distOfFunction_apply]
  trans âˆ« x, âŸªy, Î· x â€¢ f xâŸ«_â„; swap
  Â· congr
    funext x
    rw [real_inner_comm]
    simp [inner_smul_left]
  rw [integral_inner, real_inner_comm]
  fun_prop

TODO "LV5RM" "Add a general lemma specifying the derivative of
  functions from distributions."

/-!

## D. Components

-/

lemma distOfFunction_eculid_eval {d n : â„•} (f : Space d â†’ EuclideanSpace â„ (Fin n))
    (hf : IsDistBounded f) (Î· : ğ“¢(Space d, â„)) (i : Fin n) :
    distOfFunction f hf Î· i = distOfFunction (fun x => f x i) (hf.pi_comp i) Î· := by
  simp [distOfFunction_apply]
  rw [MeasureTheory.eval_integral_piLp]
  simp only [PiLp.smul_apply, smul_eq_mul]
  intro i
  simp only [PiLp.smul_apply, smul_eq_mul]
  fun_prop

lemma distOfFunction_vector_eval {d : â„•} (f : Space d â†’ Lorentz.Vector d)
    (hf : IsDistBounded f) (Î· : ğ“¢(Space d, â„)) (i : Fin 1 âŠ• Fin d) :
    distOfFunction f hf Î· i = distOfFunction (fun x => f x i) (hf.vector_component i) Î· := by
  simp [distOfFunction_apply]
  trans âŸªLorentz.Vector.basis i, âˆ« x, Î· x â€¢ f xâŸ«_â„
  Â· rw [Lorentz.Vector.basis_inner]
  rw [â† integral_inner]
  simp [Lorentz.Vector.basis_inner]
  fun_prop

end Space
