/-
Copyright (c) 2025 Joseph Tooby-Smith. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Joseph Tooby-Smith
-/
import PhysLean.SpaceAndTime.Space.DistOfFunction
import PhysLean.SpaceAndTime.Space.Derivatives.Grad
import PhysLean.SpaceAndTime.Space.Derivatives.Div
import Mathlib.MeasureTheory.Measure.Lebesgue.VolumeOfBalls
import Mathlib.Analysis.InnerProductSpace.NormPow
import Mathlib.Analysis.Calculus.FDeriv.Norm
/-!

# The norm on space

## i. Overview

The main content of this file is defining `Space.normPowerSeries`, a power series which is
differentiable everywhere, and which tends to the norm in the limit as `n â†’ âˆ`.

We use properties of this power series to prove various results about distributions involving norms.

## ii. Key results

- `normPowerSeries` : A power series which is differentiable everywhere, and in the limit
  as `n â†’ âˆ` tends to `â€–xâ€–`.
- `normPowerSeries_differentiable` : The power series is differentiable everywhere.
- `normPowerSeries_tendsto` : The power series tends to the norm in the limit as `n â†’ âˆ`.
- `distGrad_distOfFunction_norm_zpow` : The gradient of the distribution defined by a power of the
  norm.
- `distGrad_distOfFunction_log_norm` : The gradient of the distribution defined by the logarithm
  of the norm.
- `distDiv_inv_pow_eq_dim` : The divergence of the distribution defined by the
  inverse power of the norm propotional to the Dirac delta distribution.

## iii. Table of contents

- A. The norm as a power series
  - A.1. Differentiability of the norm power series
  - A.2. The limit of the norm power series
  - A.3. The derivative of the norm power series
  - A.4. Limits of the derivative of the power series
  - A.5. The power series is AEStronglyMeasurable
  - A.6. Bounds on the norm power series
  - A.7. The `IsDistBounded` property of the norm power series
  - A.8. Differentiability of functions
  - A.9. Derivatives of functions
  - A.10. Gradients of distributions based on powers
    - A.10.1. The limits of gradients of distributions based on powers
  - A.11. Gradients of distributions based on logs
    - A.11.1. The limits of gradients of distributions based on logs
- B. Distributions involving norms
  - B.1. The gradient of distributions based on powers
  - B.2. The gradient of distributions based on logs
  - B.3. Divergence equal dirac delta

## iv. References

-/
open SchwartzMap NNReal
noncomputable section

variable (ğ•œ : Type) {E F F' : Type} [RCLike ğ•œ] [NormedAddCommGroup E] [NormedAddCommGroup F]
  [NormedAddCommGroup F'] [NormedSpace â„ E] [NormedSpace â„ F]

namespace Space

open MeasureTheory
/-!

## A. The norm as a power series

-/

/-- A power series which is differentiable everywhere, and in the limit
  as `n â†’ âˆ` tends to `â€–xâ€–`. -/
def normPowerSeries {d} : â„• â†’ Space d â†’ â„ := fun n x =>
  âˆš(â€–xâ€– ^ 2 + 1/(n + 1))

lemma normPowerSeries_eq (n : â„•) :
    normPowerSeries (d := d) n = fun x => âˆš(â€–xâ€– ^ 2 + 1/(n + 1)) := rfl

lemma normPowerSeries_eq_rpow {d} (n : â„•) :
    normPowerSeries (d := d) n = fun x => ((â€–xâ€– ^ 2 + 1/(n + 1))) ^ (1/2 : â„) := by
  rw [normPowerSeries_eq]
  funext x
  rw [â† Real.sqrt_eq_rpow]

/-!

### A.1. Differentiability of the norm power series

-/

@[fun_prop]
lemma normPowerSeries_differentiable {d} (n : â„•) :
    Differentiable â„ (fun (x : Space d) => normPowerSeries n x) := by
  rw [normPowerSeries_eq_rpow]
  refine Differentiable.rpow_const ?_ ?_
  Â· refine (Differentiable.fun_add_iff_right ?_).mpr ?_
    Â· apply Differentiable.norm_sq â„
      fun_prop
    Â· fun_prop
  Â· intro x
    have h1 : 0 < â€–xâ€– ^ 2 + 1 / (â†‘n + 1) := by positivity
    grind

/-!

### A.2. The limit of the norm power series

-/
open InnerProductSpace

open scoped Topology BigOperators FourierTransform

lemma normPowerSeries_tendsto {d} (x : Space d) (hx : x â‰  0) :
    Filter.Tendsto (fun n => normPowerSeries n x) Filter.atTop (ğ“ (â€–xâ€–)) := by
  conv => enter [1, n]; rw [normPowerSeries_eq_rpow]
  simp only [one_div]
  have hx_norm : â€–xâ€– = (â€–xâ€– ^ 2 + 0) ^ (1 / 2 : â„) := by
    rw [â† Real.sqrt_eq_rpow]
    simp
  conv_rhs => rw [hx_norm]
  refine Filter.Tendsto.rpow ?_ ?_ ?_
  Â· apply Filter.Tendsto.add
    Â· exact tendsto_const_nhds
    Â· simpa using tendsto_one_div_add_atTop_nhds_zero_nat
  Â· simp
  Â· left
    simpa using hx

lemma normPowerSeries_inv_tendsto {d} (x : Space d) (hx : x â‰  0) :
    Filter.Tendsto (fun n => (normPowerSeries n x)â»Â¹) Filter.atTop (ğ“ (â€–xâ€–â»Â¹)) := by
  apply Filter.Tendsto.invâ‚€
  Â· exact normPowerSeries_tendsto x hx
  Â· simpa using hx

/-!

### A.3. The derivative of the norm power series

-/
open Space

lemma deriv_normPowerSeries {d} (n : â„•) (x : Space d) (i : Fin d) :
    âˆ‚[i] (normPowerSeries n) x = x i * (normPowerSeries n x)â»Â¹ := by
  rw [deriv_eq_fderiv_basis]
  rw [normPowerSeries_eq]
  rw [fderiv_sqrt]
  simp only [one_div, mul_inv_rev, fderiv_add_const, ContinuousLinearMap.coe_smul', Pi.smul_apply,
    smul_eq_mul]
  rw [â† deriv_eq_fderiv_basis]
  rw [deriv_norm_sq]
  ring
  Â· simp
    apply DifferentiableAt.norm_sq â„
    fun_prop
  Â· positivity

lemma fderiv_normPowerSeries {d} (n : â„•) (x y : Space d) :
    fderiv â„ (fun (x : Space d) => normPowerSeries n x) x y =
      âŸªy, xâŸ«_â„ * (normPowerSeries n x)â»Â¹ := by
  rw [fderiv_eq_sum_deriv, inner_eq_sum, Finset.sum_mul]
  congr
  funext i
  simp [deriv_normPowerSeries]
  ring

/-!

### A.4. Limits of the derivative of the power series

-/

lemma deriv_normPowerSeries_tendsto {d} (x : Space d) (hx : x â‰  0) (i : Fin d) :
    Filter.Tendsto (fun n => âˆ‚[i] (normPowerSeries n) x) Filter.atTop (ğ“ (x i * (â€–xâ€–)â»Â¹)) := by
  conv => enter [1, n]; rw [deriv_normPowerSeries]
  refine Filter.Tendsto.mul ?_ ?_
  Â· exact tendsto_const_nhds
  Â· exact normPowerSeries_inv_tendsto x hx

lemma fderiv_normPowerSeries_tendsto {d} (x y : Space d) (hx : x â‰  0) :
    Filter.Tendsto (fun n => fderiv â„ (fun (x : Space d) => normPowerSeries n x) x y)
      Filter.atTop (ğ“ (âŸªy, xâŸ«_â„ * (â€–xâ€–)â»Â¹)) := by
  conv => enter [1, n]; rw [fderiv_normPowerSeries]
  refine Filter.Tendsto.mul ?_ ?_
  Â· exact tendsto_const_nhds
  Â· exact normPowerSeries_inv_tendsto x hx

/-!

### A.5. The power series is AEStronglyMeasurable

-/

@[fun_prop]
lemma normPowerSeries_aestronglyMeasurable {d} (n : â„•) :
    AEStronglyMeasurable (normPowerSeries n : Space d â†’ â„) volume := by
  rw [normPowerSeries_eq_rpow]
  refine StronglyMeasurable.aestronglyMeasurable ?_
  refine stronglyMeasurable_iff_measurable.mpr ?_
  fun_prop

/-!

### A.6. Bounds on the norm power series

-/

@[simp]
lemma normPowerSeries_nonneg {d} (n : â„•) (x : Space d) :
    0 â‰¤ normPowerSeries n x := by
  rw [normPowerSeries_eq]
  simp

@[simp]
lemma normPowerSeries_pos {d} (n : â„•) (x : Space d) :
    0 < normPowerSeries n x := by
  rw [normPowerSeries_eq]
  simp only [one_div, Real.sqrt_pos]
  positivity

@[simp]
lemma normPowerSeries_ne_zero {d} (n : â„•) (x : Space d) :
    normPowerSeries n x â‰  0 := by
  rw [normPowerSeries_eq]
  simp only [one_div, ne_eq]
  positivity

lemma normPowerSeries_le_norm_sq_add_one {d} (n : â„•) (x : Space d) :
    normPowerSeries n x â‰¤ â€–xâ€– + 1 := by
  trans âˆš(â€–xâ€– ^ 2 + 1)
  Â· rw [normPowerSeries_eq]
    apply Real.sqrt_le_sqrt
    simp only [one_div, add_le_add_iff_left]
    refine inv_le_one_iffâ‚€.mpr ?_
    right
    simp
  Â· refine (Real.sqrt_le_left (by positivity)).mpr ?_
    trans (â€–xâ€– ^ 2 + 1) + (2 * â€–xâ€–)
    Â· simp
    Â· ring_nf
      rfl

@[simp]
lemma norm_lt_normPowerSeries {d} (n : â„•) (x : Space d) :
    â€–xâ€– < normPowerSeries n x := by
  rw [normPowerSeries_eq]
  apply Real.lt_sqrt_of_sq_lt
  simp only [one_div, lt_add_iff_pos_right, inv_pos]
  positivity

lemma norm_le_normPowerSeries {d} (n : â„•) (x : Space d) :
    â€–xâ€– â‰¤ normPowerSeries n x := by
  rw [normPowerSeries_eq]
  apply Real.le_sqrt_of_sq_le
  simp only [one_div, le_add_iff_nonneg_right, inv_nonneg]
  positivity

lemma normPowerSeries_zpow_le_norm_sq_add_one {d} (n : â„•) (m : â„¤) (x : Space d)
    (hx : x â‰  0) :
    (normPowerSeries n x) ^ m â‰¤ (â€–xâ€– + 1) ^ m + â€–xâ€– ^ m := by
  match m with
  | .ofNat m =>
    trans (â€–xâ€– + 1) ^ m
    Â· simp
      refine pow_le_pow_leftâ‚€ (by simp) ?_ m
      exact normPowerSeries_le_norm_sq_add_one n x
    Â· simp
  | .negSucc m =>
    trans (â€–xâ€– ^ (m + 1))â»Â¹; swap
    Â· simp
      positivity
    simp only [zpow_negSucc]
    refine inv_antiâ‚€ ?_ ?_
    Â· positivity
    refine pow_le_pow_leftâ‚€ (by simp) ?_ (m + 1)
    exact norm_le_normPowerSeries n x

lemma normPowerSeries_inv_le {d} (n : â„•) (x : Space d) (hx : x â‰  0) :
    (normPowerSeries n x)â»Â¹ â‰¤ â€–xâ€–â»Â¹ := by
  refine inv_antiâ‚€ ?_ ?_
  Â· positivity
  apply Real.le_sqrt_of_sq_le
  simp only [one_div, le_add_iff_nonneg_right, inv_nonneg]
  positivity

lemma normPowerSeries_log_le_normPowerSeries {d} (n : â„•) (x : Space d) :
    |Real.log (normPowerSeries n x)| â‰¤ (normPowerSeries n x)â»Â¹ + (normPowerSeries n x) := by
  have h1 := Real.neg_inv_le_log (x := (normPowerSeries n x)) (by simp)
  have h2 := Real.log_le_rpow_div (x := (normPowerSeries n x)) (by simp) (Îµ := 1) (by positivity)
  simp_all
  rw [abs_le']
  generalize Real.log â€–xâ€– = r at *
  apply And.intro
  Â· apply h2.trans
    simp
  Â· rw [neg_le]
    apply le_trans _ h1
    simp
lemma normPowerSeries_log_le {d} (n : â„•) (x : Space d) (hx : x â‰  0) :
    |Real.log (normPowerSeries n x)| â‰¤ â€–xâ€–â»Â¹ + (â€–xâ€– + 1) := by
  apply le_trans (normPowerSeries_log_le_normPowerSeries n x) ?_
  apply add_le_add
  Â· exact normPowerSeries_inv_le n x hx
  Â· exact normPowerSeries_le_norm_sq_add_one n x

/-!

### A.7. The `IsDistBounded` property of the norm power series

-/

@[fun_prop]
lemma IsDistBounded.normPowerSeries_zpow {d : â„•} {n : â„•} (m : â„¤) :
    IsDistBounded (d := d) (fun x => (normPowerSeries n x) ^ m) := by
  match m with
  | .ofNat m =>
    simp only [Int.ofNat_eq_coe, zpow_natCast]
    apply IsDistBounded.mono (f := fun (x : Space d) => (â€–xâ€– + 1) ^ m)
    Â· fun_prop
    Â· fun_prop
    intro x
    simp only [norm_pow, Real.norm_eq_abs]
    refine pow_le_pow_leftâ‚€ (by positivity) ?_ m
    rw [abs_of_nonneg (by simp),abs_of_nonneg (by positivity)]
    exact normPowerSeries_le_norm_sq_add_one n x
  | .negSucc m =>
    simp only [zpow_negSucc]
    apply IsDistBounded.mono (f := fun (x : Space d) => ((âˆš(1/(n + 1)) : â„) ^ (m + 1))â»Â¹)
    Â· fun_prop
    Â· rw [normPowerSeries_eq_rpow]
      refine StronglyMeasurable.aestronglyMeasurable ?_
      refine stronglyMeasurable_iff_measurable.mpr ?_
      fun_prop
    Â· intro x
      simp only [norm_inv, norm_pow, Real.norm_eq_abs, one_div]
      refine inv_antiâ‚€ (by positivity) ?_
      refine (pow_le_pow_iff_leftâ‚€ (by positivity) (by positivity) (by simp)).mpr ?_
      rw [abs_of_nonneg (by positivity), abs_of_nonneg (by simp)]
      rw [normPowerSeries_eq]
      simp only [Real.sqrt_inv, one_div]
      rw [â† Real.sqrt_inv]
      apply Real.sqrt_le_sqrt
      simp

@[fun_prop]
lemma IsDistBounded.normPowerSeries_single {d : â„•} {n : â„•} :
    IsDistBounded (d := d) (fun x => (normPowerSeries n x)) := by
  convert IsDistBounded.normPowerSeries_zpow (n := n) (m := 1) using 1
  simp

@[fun_prop]
lemma IsDistBounded.normPowerSeries_inv {d : â„•} {n : â„•} :
    IsDistBounded (d := d) (fun x => (normPowerSeries n x)â»Â¹) := by
  convert normPowerSeries_zpow (n := n) (-1) using 1
  simp

@[fun_prop]
lemma IsDistBounded.normPowerSeries_deriv {d : â„•} (n : â„•) (i : Fin d) :
    IsDistBounded (d := d) (fun x => âˆ‚[i] (normPowerSeries n) x) := by
  conv =>
    enter [1, x];
    rw [deriv_normPowerSeries]
  fun_prop

@[fun_prop]
lemma IsDistBounded.normPowerSeries_fderiv {d : â„•} (n : â„•) (y : Space d) :
    IsDistBounded (d := d) (fun x => fderiv â„ (fun (x : Space d) => normPowerSeries n x) x y) := by
  conv =>
    enter [1, x];
    rw [fderiv_eq_sum_deriv]
  apply IsDistBounded.sum_fun
  fun_prop

@[fun_prop]
lemma IsDistBounded.normPowerSeries_log {d : â„•} (n : â„•) :
    IsDistBounded (d := d) (fun x => Real.log (normPowerSeries n x)) := by
  apply IsDistBounded.mono (f := fun x => (normPowerSeries n x)â»Â¹ + (normPowerSeries n x))
  Â· fun_prop
  Â· apply AEMeasurable.aestronglyMeasurable
    fun_prop
  Â· intro x
    simp only [Real.norm_eq_abs]
    conv_rhs => rw [abs_of_nonneg (by
      apply add_nonneg
      Â· simp
      Â· simp)]
    exact normPowerSeries_log_le_normPowerSeries n x

/-!

### A.8. Differentiability of functions

-/

@[fun_prop]
lemma differentiable_normPowerSeries_zpow {d : â„•} {n : â„•} (m : â„¤) :
    Differentiable â„ (fun x : Space d => (normPowerSeries n x) ^ m) := by
  refine Differentiable.zpow ?_ ?_
  Â· fun_prop
  Â· left
    exact normPowerSeries_ne_zero n

@[fun_prop]
lemma differentiable_normPowerSeries_inv {d : â„•} {n : â„•} :
    Differentiable â„ (fun x : Space d => (normPowerSeries n x)â»Â¹) := by
  convert differentiable_normPowerSeries_zpow (n := n) (m := -1) using 1
  funext x
  simp

@[fun_prop]
lemma differentiable_log_normPowerSeries {d : â„•} {n : â„•} :
    Differentiable â„ (fun x : Space d => Real.log (normPowerSeries n x)) := by
  refine Differentiable.log ?_ ?_
  Â· fun_prop
  Â· intro x
    exact normPowerSeries_ne_zero n x
/-!

### A.9. Derivatives of functions

-/

lemma deriv_normPowerSeries_zpow {d : â„•} {n : â„•} (m : â„¤) (x : Space d) (i : Fin d) :
    âˆ‚[i] (fun x : Space d => (normPowerSeries n x) ^ m) x =
      m * x i * (normPowerSeries n x) ^ (m - 2) := by
  rw [deriv_eq_fderiv_basis]
  change (fderiv â„ ((fun x => x ^ m) âˆ˜ normPowerSeries n) x) (basis i) = _
  rw [fderiv_comp]
  simp only [ContinuousLinearMap.coe_comp', Function.comp_apply, fderiv_eq_smul_deriv, deriv_zpow',
    smul_eq_mul]
  rw [fderiv_normPowerSeries]
  simp only [basis_inner]
  field_simp
  ring_nf
  have h1 : normPowerSeries n x ^ (-1 + m) = normPowerSeries n x ^ ((-2 + m) + 1) := by
    ring_nf
  rw [h1, zpow_addâ‚€]
  simp only [Int.reduceNeg, zpow_one]
  ring
  Â· simp
  Â· refine DifferentiableAt.zpow ?_ ?_
    Â· fun_prop
    Â· left
      exact normPowerSeries_ne_zero n x
  Â· fun_prop

lemma fderiv_normPowerSeries_zpow {d : â„•} {n : â„•} (m : â„¤) (x y : Space d) :
    fderiv â„ (fun x : Space d => (normPowerSeries n x) ^ m) x y =
      m * âŸªy, xâŸ«_â„ * (normPowerSeries n x) ^ (m - 2) := by
  rw [fderiv_eq_sum_deriv, inner_eq_sum, Finset.mul_sum, Finset.sum_mul]
  congr
  funext i
  simp [deriv_normPowerSeries_zpow]
  ring

lemma deriv_log_normPowerSeries {d : â„•} {n : â„•} (x : Space d) (i : Fin d) :
    âˆ‚[i] (fun x : Space d => Real.log (normPowerSeries n x)) x =
      x i * (normPowerSeries n x) ^ (-2 : â„¤) := by
  rw [deriv_eq_fderiv_basis]
  change (fderiv â„ (Real.log âˆ˜ normPowerSeries n) x) (basis i) = _
  rw [fderiv_comp,]
  simp only [ContinuousLinearMap.coe_comp', Function.comp_apply, fderiv_eq_smul_deriv,
    Real.deriv_log', smul_eq_mul, Int.reduceNeg, zpow_neg]
  rw [fderiv_normPowerSeries]
  simp [zpow_ofNat, sq]
  ring
  Â· apply DifferentiableAt.log ?_ ?_
    Â· fun_prop
    exact normPowerSeries_ne_zero n x
  Â· fun_prop

lemma fderiv_log_normPowerSeries {d : â„•} {n : â„•} (x y : Space d) :
    fderiv â„ (fun x : Space d => Real.log (normPowerSeries n x)) x y =
      âŸªy, xâŸ«_â„ * (normPowerSeries n x) ^ (-2 : â„¤) := by
  rw [fderiv_eq_sum_deriv, inner_eq_sum, Finset.sum_mul]
  congr
  funext i
  simp [deriv_log_normPowerSeries]
  ring

/-!

### A.10. Gradients of distributions based on powers

-/

lemma gradient_dist_normPowerSeries_zpow {d : â„•} {n : â„•} (m : â„¤) :
    distGrad (distOfFunction (fun x : Space d => (normPowerSeries n x) ^ m) (by fun_prop)) =
    distOfFunction (fun x : Space d => (m * (normPowerSeries n x) ^ (m - 2)) â€¢ x)
    (by fun_prop) := by
  ext1 Î·
  apply ext_inner_right â„
  intro y
  simp [distGrad_inner_eq]
  rw [Distribution.fderivD_apply, distOfFunction_apply, distOfFunction_inner]
  calc _
    _ = - âˆ« (x : Space d), fderiv â„ Î· x y * normPowerSeries n x ^ m := by
      rfl
    _ = âˆ« (x : Space d), Î· x * fderiv â„ (normPowerSeries n Â· ^ m) x y := by
      rw [integral_mul_fderiv_eq_neg_fderiv_mul_of_integrable]
      Â· fun_prop
      Â· refine IsDistBounded.integrable_space_mul ?_ Î·
        conv => enter [1, x]; rw [fderiv_normPowerSeries_zpow]
        simp [mul_assoc]
        fun_prop
      Â· fun_prop
      Â· exact Î·.differentiable
      Â· fun_prop
    _ = âˆ« (x : Space d), Î· x * (m * âŸªy, xâŸ«_â„ * (normPowerSeries n x) ^ (m - 2)) := by
      congr
      funext x
      rw [fderiv_normPowerSeries_zpow]
  congr
  funext x
  simp [inner_smul_left_eq_smul]
  left
  rw [real_inner_comm]
  ring

/-!

#### A.10.1. The limits of gradients of distributions based on powers

-/

lemma gradient_dist_normPowerSeries_zpow_tendsTo_distGrad_norm {d : â„•} (m : â„¤)
    (hm : - (d.succ - 1 : â„•) â‰¤ m) (Î· : ğ“¢(Space d.succ, â„))
    (y : EuclideanSpace â„ (Fin d.succ)) :
    Filter.Tendsto (fun n =>
    âŸª(distGrad (distOfFunction
    (fun x : Space d.succ => (normPowerSeries n x) ^ m) (by fun_prop))) Î·, yâŸ«_â„)
    Filter.atTop
    (ğ“ (âŸªdistGrad (distOfFunction (fun x : Space d.succ => â€–xâ€– ^ m)
    (IsDistBounded.pow m hm)) Î·, yâŸ«_â„)) := by
  simp [distGrad_inner_eq, Distribution.fderivD_apply, distOfFunction_apply]
  change Filter.Tendsto (fun n => - âˆ« (x : Space d.succ), fderiv â„ Î· x y * normPowerSeries n x ^ m)
    Filter.atTop (ğ“ (- âˆ« (x : Space d.succ), fderiv â„ Î· x y * â€–xâ€– ^ m))
  apply Filter.Tendsto.neg
  apply MeasureTheory.tendsto_integral_of_dominated_convergence
    (bound := fun x => |fderiv â„ Î· x y| * ((â€–xâ€– + 1) ^ m + â€–xâ€– ^ m))
  Â· intro n
    apply IsDistBounded.aeStronglyMeasurable_fderiv_schwartzMap_smul (F := â„) ?_
    fun_prop
  Â· have h1 : Integrable (fun x => (fderiv â„ (â‡‘Î·) x) y * ((â€–xâ€– + 1) ^ m + â€–xâ€– ^ m)) volume := by
      apply IsDistBounded.integrable_space_fderiv ?_
      apply IsDistBounded.add
      Â· refine IsDistBounded.norm_add_pos_nat_zpow m 1 ?_
        simp
      Â· exact IsDistBounded.pow m hm
    rw [â† integrable_norm_iff] at h1
    convert h1 using 1
    funext x
    simp only [Nat.succ_eq_add_one, norm_mul, Real.norm_eq_abs, mul_eq_mul_left_iff, abs_eq_zero]
    left
    rw [abs_of_nonneg (by positivity)]
    fun_prop
  Â· intro n
    rw [Filter.eventually_iff_exists_mem]
    use {0}á¶œ
    constructor
    Â· rw [compl_mem_ae_iff, measure_singleton]
    intro x hx
    simp at hx
    simp
    apply mul_le_mul (by rfl) _ (by positivity) (by positivity)
    rw [abs_of_nonneg (by simp)]
    exact normPowerSeries_zpow_le_norm_sq_add_one n m x hx
  Â· rw [Filter.eventually_iff_exists_mem]
    use {0}á¶œ
    constructor
    Â· rw [compl_mem_ae_iff, measure_singleton]
    intro x hx
    apply Filter.Tendsto.mul
    Â· exact tendsto_const_nhds
    have h1 : Filter.Tendsto (fun x_1 => normPowerSeries x_1 x ^ (m : â„))
      Filter.atTop (ğ“ (â€–xâ€– ^ (m : â„))) := by
      refine Filter.Tendsto.rpow ?_ ?_ ?_
      Â· apply normPowerSeries_tendsto x hx
      Â· simp
      Â· left
        simpa using hx
    simpa using h1

lemma gradient_dist_normPowerSeries_zpow_tendsTo {d : â„•} (m : â„¤) (hm : - (d.succ - 1 : â„•) + 2 â‰¤ m)
    (Î· : ğ“¢(Space d.succ, â„)) (y : EuclideanSpace â„ (Fin d.succ)) :
    Filter.Tendsto (fun n =>
    âŸª(distGrad (distOfFunction (fun x : Space d.succ => (normPowerSeries n x) ^ m)
    (by fun_prop))) Î·, yâŸ«_â„)
    Filter.atTop
    (ğ“ (âŸªdistOfFunction (fun x : Space d.succ => (m * â€–xâ€– ^ (m - 2)) â€¢ x) (by
    simp [â† smul_smul]
    refine IsDistBounded.const_fun_smul ?_ â†‘m
    apply IsDistBounded.zpow_smul_self
    simp_all
    grind) Î·, yâŸ«_â„)) := by
  conv =>
    enter [1, n];
    rw [gradient_dist_normPowerSeries_zpow]
  simp [distOfFunction_inner]
  have h1 (n : â„•) (x : Space d.succ) :
    Î· x * âŸª(â†‘m * normPowerSeries n x ^ (m - 2)) â€¢ x, yâŸ«_â„ =
    Î· x * (m * (âŸªx, yâŸ«_â„ * (normPowerSeries n x) ^ (m - 2))) := by
    simp [inner_smul_left]
    ring_nf
    left
    trivial
  conv =>
    enter [1, n, 2, x];
    rw [h1 n x]
  apply MeasureTheory.tendsto_integral_of_dominated_convergence
    (bound := fun x => |Î· x| * |m| * |âŸªx, yâŸ«_â„| * ((â€–xâ€– + 1) ^ (m - 2) + â€–xâ€– ^ (m - 2)))
  Â· intro n
    apply IsDistBounded.aeStronglyMeasurable_schwartzMap_smul (F := â„) ?_ Î·
    apply IsDistBounded.const_mul_fun
    apply IsDistBounded.isDistBounded_mul_inner'
    fun_prop
  Â· have h1 : Integrable (fun x =>
        Î· x * (m * (âŸªx, yâŸ«_â„ * ((â€–xâ€– + 1) ^ (m - 2) + â€–xâ€– ^ (m - 2))))) volume := by
      apply IsDistBounded.integrable_space_mul ?_ Î·
      apply IsDistBounded.const_mul_fun
      apply IsDistBounded.isDistBounded_mul_inner'
      apply IsDistBounded.add
      Â· refine IsDistBounded.norm_add_pos_nat_zpow (m - 2) 1 ?_
        simp
      Â· apply IsDistBounded.pow (m - 2)
        simp_all
        grind
    rw [â† integrable_norm_iff] at h1
    convert h1 using 1
    funext x
    simp [mul_assoc]
    rw [abs_of_nonneg (by positivity)]
    simp only [true_or]
    fun_prop
  Â· intro n
    rw [Filter.eventually_iff_exists_mem]
    use {0}á¶œ
    constructor
    Â· rw [compl_mem_ae_iff, measure_singleton]
    intro x hx
    simp at hx
    simp [mul_assoc]
    apply mul_le_mul (by rfl) _ (by positivity) (by positivity)
    apply mul_le_mul (by rfl) _ (by positivity) (by positivity)
    apply mul_le_mul (by rfl) _ (by positivity) (by positivity)
    rw [abs_of_nonneg (by simp)]
    exact normPowerSeries_zpow_le_norm_sq_add_one n (m - 2) x hx
  Â· rw [Filter.eventually_iff_exists_mem]
    use {0}á¶œ
    constructor
    Â· rw [compl_mem_ae_iff, measure_singleton]
    intro x hx
    apply Filter.Tendsto.mul
    Â· exact tendsto_const_nhds
    simp [inner_smul_left, mul_assoc]
    apply Filter.Tendsto.mul
    Â· exact tendsto_const_nhds
    ring_nf
    apply Filter.Tendsto.mul
    Â· exact tendsto_const_nhds
    have h1 : Filter.Tendsto (fun x_1 => normPowerSeries x_1 x ^ ((m - 2 : â„¤) : â„))
      Filter.atTop (ğ“ (â€–xâ€– ^ ((m - 2 : â„¤) : â„))) := by
      refine Filter.Tendsto.rpow ?_ ?_ ?_
      Â· apply normPowerSeries_tendsto x hx
      Â· simp
      Â· left
        simpa using hx
    simp [-Int.cast_sub, Real.rpow_intCast] at h1
    convert h1 using 3
    Â· ring
    Â· ring

/-!

### A.11. Gradients of distributions based on logs

-/

lemma gradient_dist_normPowerSeries_log {d : â„•} {n : â„•} :
    distGrad (distOfFunction (fun x : Space d => Real.log (normPowerSeries n x)) (by fun_prop)) =
    distOfFunction (fun x : Space d => ((normPowerSeries n x) ^ (- 2 : â„¤)) â€¢ x)
    (by fun_prop) := by
  ext1 Î·
  apply ext_inner_right â„
  intro y
  simp [distGrad_inner_eq]
  rw [Distribution.fderivD_apply, distOfFunction_apply, distOfFunction_inner]
  calc _
    _ = - âˆ« (x : Space d), fderiv â„ Î· x y * Real.log (normPowerSeries n x) := by
      rfl
    _ = âˆ« (x : Space d), Î· x * fderiv â„ (fun x => Real.log (normPowerSeries n x)) x y := by
      rw [integral_mul_fderiv_eq_neg_fderiv_mul_of_integrable]
      Â· fun_prop
      Â· refine IsDistBounded.integrable_space_mul ?_ Î·
        conv => enter [1, x]; rw [fderiv_log_normPowerSeries]
        fun_prop
      Â· fun_prop
      Â· exact Î·.differentiable
      Â· fun_prop
    _ = âˆ« (x : Space d), Î· x * (âŸªy, xâŸ«_â„ * (normPowerSeries n x) ^ (- 2 : â„¤)) := by
      congr
      funext x
      rw [fderiv_log_normPowerSeries]
  congr
  funext x
  simp [inner_smul_left_eq_smul]
  left
  rw [real_inner_comm]
  ring

/-!

#### A.11.1. The limits of gradients of distributions based on logs

-/

lemma gradient_dist_normPowerSeries_log_tendsTo_distGrad_norm {d : â„•}
    (Î· : ğ“¢(Space d.succ.succ, â„)) (y : EuclideanSpace â„ (Fin d.succ.succ)) :
    Filter.Tendsto (fun n =>
    âŸª(distGrad (distOfFunction
    (fun x : Space d.succ.succ => Real.log (normPowerSeries n x)) (by fun_prop))) Î·, yâŸ«_â„)
    Filter.atTop
    (ğ“ (âŸªdistGrad (distOfFunction (fun x : Space d.succ.succ => Real.log â€–xâ€–)
    (by fun_prop)) Î·, yâŸ«_â„)) := by
  simp [distGrad_inner_eq, Distribution.fderivD_apply, distOfFunction_apply]
  change Filter.Tendsto (fun n => -
    âˆ« (x : Space d.succ.succ), fderiv â„ Î· x y * Real.log (normPowerSeries n x))
    Filter.atTop (ğ“ (- âˆ« (x : Space d.succ.succ), fderiv â„ Î· x y * Real.log â€–xâ€–))
  apply Filter.Tendsto.neg
  apply MeasureTheory.tendsto_integral_of_dominated_convergence
    (bound := fun x => |fderiv â„ Î· x y| * (â€–xâ€–â»Â¹ + (â€–xâ€– + 1)))
  Â· intro n
    apply IsDistBounded.aeStronglyMeasurable_fderiv_schwartzMap_smul (F := â„) ?_
    fun_prop
  Â· have h1 : Integrable (fun x => (fderiv â„ (â‡‘Î·) x) y * (â€–xâ€–â»Â¹ + (â€–xâ€– + 1))) volume := by
      apply IsDistBounded.integrable_space_fderiv ?_
      fun_prop
    rw [â† integrable_norm_iff] at h1
    convert h1 using 1
    funext x
    simp only [Nat.succ_eq_add_one, norm_mul, Real.norm_eq_abs, mul_eq_mul_left_iff, abs_eq_zero]
    left
    rw [abs_of_nonneg (by positivity)]
    fun_prop
  Â· intro n
    rw [Filter.eventually_iff_exists_mem]
    use {0}á¶œ
    constructor
    Â· rw [compl_mem_ae_iff, measure_singleton]
    intro x hx
    simp at hx
    simp
    apply mul_le_mul (by rfl) _ (by positivity) (by positivity)
    exact normPowerSeries_log_le n x hx
  Â· rw [Filter.eventually_iff_exists_mem]
    use {0}á¶œ
    constructor
    Â· rw [compl_mem_ae_iff, measure_singleton]
    intro x hx
    apply Filter.Tendsto.mul
    Â· exact tendsto_const_nhds
    apply Filter.Tendsto.log
    Â· exact normPowerSeries_tendsto x hx
    Â· simpa using hx

lemma gradient_dist_normPowerSeries_log_tendsTo {d : â„•}
    (Î· : ğ“¢(Space d.succ.succ, â„)) (y : EuclideanSpace â„ (Fin d.succ.succ)) :
    Filter.Tendsto (fun n =>
    âŸª(distGrad (distOfFunction (fun x : Space d.succ.succ => Real.log (normPowerSeries n x))
    (by fun_prop))) Î·, yâŸ«_â„)
    Filter.atTop
    (ğ“ (âŸªdistOfFunction (fun x : Space d.succ.succ => (â€–xâ€– ^ (- 2 : â„¤)) â€¢ x) (by
    refine (IsDistBounded.zpow_smul_self _ ?_)
    simp_all) Î·, yâŸ«_â„)) := by
  conv =>
    enter [1, n];
    rw [gradient_dist_normPowerSeries_log]
  simp only [Nat.succ_eq_add_one, Int.reduceNeg, distOfFunction_inner]
  have h1 (n : â„•) (x : Space d.succ.succ) :
    Î· x * âŸª(normPowerSeries n x ^ (- 2 : â„¤)) â€¢ x, yâŸ«_â„ =
    Î· x * ((âŸªx, yâŸ«_â„ * (normPowerSeries n x) ^ (- 2 : â„¤))) := by
    simp [inner_smul_left]
    ring_nf
    left
    trivial
  conv =>
    enter [1, n, 2, x];
    rw [h1 n x]
  apply MeasureTheory.tendsto_integral_of_dominated_convergence
    (bound := fun x => |Î· x| * |âŸªx, yâŸ«_â„| * ((â€–xâ€– + 1) ^ (- 2 : â„¤) + â€–xâ€– ^ (- 2 : â„¤)))
  Â· intro n
    apply IsDistBounded.aeStronglyMeasurable_schwartzMap_smul (F := â„) ?_ Î·
    apply IsDistBounded.isDistBounded_mul_inner'
    fun_prop
  Â· have h1 : Integrable (fun x =>
        Î· x * ((âŸªx, yâŸ«_â„ * ((â€–xâ€– + 1) ^ (- 2 : â„¤) + â€–xâ€– ^ (- 2 : â„¤))))) volume := by
      apply IsDistBounded.integrable_space_mul ?_ Î·
      simp [mul_add]
      apply IsDistBounded.add
      Â· apply IsDistBounded.isDistBounded_mul_inner'

        refine IsDistBounded.norm_add_pos_nat_zpow (- 2) 1 ?_
        simp
      Â· convert IsDistBounded.mul_inner_pow_neg_two y using 1
        funext x
        simp [real_inner_comm]

    rw [â† integrable_norm_iff] at h1
    convert h1 using 1
    funext x
    simp [mul_assoc]
    rw [abs_of_nonneg (by positivity)]
    simp only [true_or]
    fun_prop
  Â· intro n
    rw [Filter.eventually_iff_exists_mem]
    use {0}á¶œ
    constructor
    Â· rw [compl_mem_ae_iff, measure_singleton]
    intro x hx
    simp at hx
    simp [mul_assoc]
    apply mul_le_mul (by rfl) _ (by positivity) (by positivity)
    apply mul_le_mul (by rfl) _ (by positivity) (by positivity)
    rw [abs_of_nonneg (by simp)]
    exact normPowerSeries_zpow_le_norm_sq_add_one n (- 2 : â„¤) x hx
  Â· rw [Filter.eventually_iff_exists_mem]
    use {0}á¶œ
    constructor
    Â· rw [compl_mem_ae_iff, measure_singleton]
    intro x hx
    apply Filter.Tendsto.mul
    Â· exact tendsto_const_nhds
    simp [inner_smul_left, inner_smul_left]
    rw [mul_comm]
    apply Filter.Tendsto.mul
    Â· exact tendsto_const_nhds
    have h1 : Filter.Tendsto (fun x_1 => normPowerSeries x_1 x ^ ((- 2 : â„¤) : â„))
      Filter.atTop (ğ“ (â€–xâ€– ^ ((- 2 : â„¤) : â„))) := by
      refine Filter.Tendsto.rpow ?_ ?_ ?_
      Â· apply normPowerSeries_tendsto x hx
      Â· simp
      Â· left
        simpa using hx
    simpa using h1

/-!

## B. Distributions involving norms

-/

/-!

### B.1. The gradient of distributions based on powers

-/

lemma distGrad_distOfFunction_norm_zpow {d : â„•} (m : â„¤) (hm : - (d.succ - 1 : â„•) + 2 â‰¤ m) :
    distGrad (distOfFunction (fun x : Space d.succ => â€–xâ€– ^ m)
      (IsDistBounded.pow m (by simp_all; omega)))
    = distOfFunction (fun x : Space d.succ => (m * â€–xâ€– ^ (m - 2)) â€¢ x) (by
      simp [â† smul_smul]
      refine IsDistBounded.const_fun_smul ?_ â†‘m
      apply IsDistBounded.zpow_smul_self
      simp_all
      omega) := by
  ext1 Î·
  exact ext_inner_right â„ fun y => tendsto_nhds_unique
    (gradient_dist_normPowerSeries_zpow_tendsTo_distGrad_norm m (by simp_all; omega) Î· y)
    (gradient_dist_normPowerSeries_zpow_tendsTo m hm Î· y)

/-!

### B.2. The gradient of distributions based on logs

-/

lemma distGrad_distOfFunction_log_norm {d : â„•} :
    distGrad (distOfFunction (fun x : Space d.succ.succ => Real.log â€–xâ€–)
      (by fun_prop))
    = distOfFunction (fun x : Space d.succ.succ => (â€–xâ€– ^ (- 2 : â„¤)) â€¢ x) (by
      refine (IsDistBounded.zpow_smul_self _ ?_)
      simp_all) := by
  ext1 Î·
  exact ext_inner_right â„ fun y => tendsto_nhds_unique
    (gradient_dist_normPowerSeries_log_tendsTo_distGrad_norm Î· y)
    (gradient_dist_normPowerSeries_log_tendsTo Î· y)

/-!

### B.3. Divergence equal dirac delta

We show that the divergence of `x â†¦ â€–xâ€– ^ (- d) â€¢ x` is equal to a multiple of the Dirac delta
at `0`.

The proof

-/
open Distribution

lemma distDiv_inv_pow_eq_dim {d : â„•} :
    distDiv (distOfFunction (fun x : Space d.succ => â€–xâ€– ^ (- d.succ : â„¤) â€¢ x)
      (IsDistBounded.zpow_smul_self (- d.succ : â„¤) (by omega))) =
      (d.succ * (volume (Î± := Space d.succ)).real (Metric.ball 0 1)) â€¢ diracDelta â„ 0 := by
  ext Î·
  calc _
      _ = - âˆ« x, âŸªâ€–xâ€–â»Â¹ ^ d.succ â€¢ x, Space.grad Î· xâŸ«_â„ := by
        simp only [Nat.succ_eq_add_one, Nat.cast_add, Nat.cast_one, zpow_neg, distDiv_ofFunction,
          inv_pow]
        rfl
      _ = - âˆ« x, â€–xâ€–â»Â¹ ^ d * âŸªâ€–xâ€–â»Â¹ â€¢ x, Space.grad Î· xâŸ«_â„ := by
        simp only [Nat.succ_eq_add_one, inv_pow, inner_smul_left, map_invâ‚€, conj_trivial, neg_inj]
        ring_nf
      _ = - âˆ« x, â€–xâ€–â»Â¹ ^ d * (_root_.deriv (fun a => Î· (a â€¢ â€–xâ€–â»Â¹ â€¢ x)) â€–xâ€–) := by
        simp only [real_inner_comm,
          â† grad_inner_space_unit_vector _ _ (SchwartzMap.differentiable Î·)]
      _ = - âˆ« r, â€–r.2.1â€–â»Â¹ ^ d * (_root_.deriv (fun a => Î· (a â€¢ r.1)) â€–r.2.1â€–)
        âˆ‚(volume (Î± := Space d.succ).toSphere.prod
        (Measure.volumeIoiPow (Module.finrank â„ (Space d.succ) - 1))) := by
        rw [â† MeasureTheory.MeasurePreserving.integral_comp (f := homeomorphUnitSphereProd _)
          (MeasureTheory.Measure.measurePreserving_homeomorphUnitSphereProd
          (volume (Î± := Space d.succ)))
          (Homeomorph.measurableEmbedding (homeomorphUnitSphereProd (Space d.succ)))]
        congr 1
        simp only [inv_pow, homeomorphUnitSphereProd_apply_snd_coe, norm_norm,
          homeomorphUnitSphereProd_apply_fst_coe]
        let f (x : Space d.succ) : â„ :=
          (â€–â†‘xâ€– ^ d)â»Â¹ * _root_.deriv (fun a => Î· (a â€¢ â€–â†‘xâ€–â»Â¹ â€¢ â†‘x)) â€–â†‘xâ€–
        conv_rhs =>
          enter [2, x]
          change f x.1
        rw [MeasureTheory.integral_subtype_comap (by simp), â† setIntegral_univ]
        change âˆ« x in Set.univ, f x = âˆ« (x : Space d.succ) in _, f x
        refine (setIntegral_congr_set ?_)
        rw [â† MeasureTheory.ae_eq_set_compl]
        trans (âˆ… : Set (Space d.succ))
        Â· apply Filter.EventuallyEq.of_eq
          rw [â† Set.compl_empty]
          exact compl_compl _
        Â· symm
          simp
      _ = - âˆ« n, (âˆ« r, â€–r.1â€–â»Â¹ ^ d *
        (_root_.deriv (fun a => Î· (a â€¢ n)) â€–r.1â€–)
        âˆ‚((Measure.volumeIoiPow (Module.finrank â„ (Space d.succ) - 1))))
        âˆ‚(volume (Î± := Space d.succ).toSphere) := by
        rw [MeasureTheory.integral_prod]
        /- Integrable condition. -/
        convert integrable_isDistBounded_inner_grad_schwartzMap_spherical
          (IsDistBounded.inv_pow_smul_self (d.succ) (by omega)) Î·
        rename_i r
        simp only [Nat.succ_eq_add_one, Real.norm_eq_abs, inv_pow, Function.comp_apply,
          homeomorphUnitSphereProd_symm_apply_coe]
        let x : Space d.succ := r.2.1 â€¢ r.1.1
        have hr := r.2.2
        simp [-Subtype.coe_prop] at hr
        have hr2 : r.2.1 â‰  0 := by exact Ne.symm (ne_of_lt hr)
        rw [abs_of_nonneg (le_of_lt hr)]
        trans (r.2.1 ^ d)â»Â¹ * _root_.deriv (fun a => Î· (a â€¢ â€–â†‘xâ€–â»Â¹ â€¢ â†‘x)) â€–xâ€–
        Â· simp [x, norm_smul]
          left
          congr
          funext a
          congr
          simp [smul_smul]
          rw [abs_of_nonneg (le_of_lt hr)]
          field_simp
          simp only [one_smul]
          rw [abs_of_nonneg (le_of_lt hr)]
        rw [â† grad_inner_space_unit_vector]
        rw [real_inner_comm]
        simp [inner_smul_left, x, norm_smul, abs_of_nonneg (le_of_lt hr)]
        field_simp
        ring
        exact SchwartzMap.differentiable Î·
      _ = - âˆ« n, (âˆ« (r : Set.Ioi (0 : â„)),
        (_root_.deriv (fun a => Î· (a â€¢ n)) r.1) âˆ‚(.comap Subtype.val volume))
        âˆ‚(volume (Î± := Space d.succ).toSphere) := by
        congr
        funext n
        simp [Measure.volumeIoiPow]
        erw [integral_withDensity_eq_integral_smul]
        congr
        funext r
        have hr := r.2
        simp [-Subtype.coe_prop] at hr
        trans ((r.1 ^ d).toNNReal : â„) â€¢ ((r.1 ^ d)â»Â¹ * _root_.deriv (fun a => Î· (a â€¢ â†‘n)) |r.1|)
        Â· rw [NNReal.smul_def]
          simp only [Real.coe_toNNReal', smul_eq_mul, Nat.succ_eq_add_one, mul_eq_mul_left_iff,
            mul_eq_mul_right_iff, inv_inj, sup_eq_right]
          rw [abs_of_nonneg (le_of_lt hr)]
          simp
        trans ((r.1 ^ d) : â„) â€¢ ((r.1 ^ d)â»Â¹ * _root_.deriv (fun a => Î· (a â€¢ â†‘n)) |r.1|)
        Â· congr
          rw [Real.coe_toNNReal']
          rw [max_eq_left]
          apply pow_nonneg
          grind
        have h1 : r.1 â‰  0 := by exact ne_of_gt r.2
        simp only [smul_eq_mul]
        field_simp
        congr
        rw [abs_of_nonneg (le_of_lt hr)]
        fun_prop
      _ = - âˆ« n, (-Î· 0) âˆ‚(volume (Î± := Space d.succ).toSphere) := by
        congr
        funext n
        let Î·' (n : â†‘(Metric.sphere 0 1)) : ğ“¢(â„, â„) := compCLM (g := fun a => a â€¢ n.1) â„ (by
          apply And.intro
          Â· fun_prop
          Â· intro n'
            match n' with
            | 0 =>
              use 1, 1
              simp [norm_smul]
            | 1 =>
              use 0, 1
              intro x
              simp [fderiv_smul_const, iteratedFDeriv_succ_eq_comp_right]
            | n' + 1 + 1 =>
              use 0, 0
              intro x
              simp only [Real.norm_eq_abs, pow_zero, mul_one, norm_le_zero_iff]
              rw [iteratedFDeriv_succ_eq_comp_right]
              simp [fderiv_smul_const]
              rw [iteratedFDeriv_succ_const]
              simp
              rfl) (by use 1, 1; simp [norm_smul]) Î·
        rw [MeasureTheory.integral_subtype_comap (by simp)]
        rw [MeasureTheory.integral_Ioi_of_hasDerivAt_of_tendsto (f := fun a => Î· (a â€¢ n)) (m := 0)]
        Â· simp
        Â· refine ContinuousAt.continuousWithinAt ?_
          fun_prop
        Â· intro x hx
          refine DifferentiableAt.hasDerivAt ?_
          have := Î·.differentiable
          fun_prop
        Â· exact (integrable ((derivCLM â„) (Î·' n))).integrableOn
        Â· exact Filter.Tendsto.mono_left (Î·' n).toZeroAtInfty.zero_at_infty' atTop_le_cocompact
      _ = Î· 0 * (d.succ * (volume (Î± := Space d.succ)).real (Metric.ball 0 1)) := by
        simp only [integral_const, Measure.toSphere_real_apply_univ, finrank_euclideanSpace,
          Fintype.card_fin, smul_eq_mul, mul_neg, neg_neg]
        ring
  simp only [Nat.succ_eq_add_one, Nat.cast_add, Nat.cast_one, ContinuousLinearMap.coe_smul',
    Pi.smul_apply, diracDelta_apply, smul_eq_mul]
  ring

end Space
