/-
Copyright (c) 2025 Joseph Tooby-Smith. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Joseph Tooby-Smith
-/
import PhysLean.SpaceAndTime.Space.Derivatives.Basic
/-!

# Lorentz group actions related to SpaceTime

## i. Overview

We already have a Lorentz group action on `SpaceTime d`, in this module
we define the induced action on Schwartz functions and distributions.

## ii. Key results

- `schwartzAction` : Defines the action of the Lorentz group on Schwartz functions.
- An instance of `DistribMulAction` for the Lorentz group acting on distributions.

## iii. Table of contents

- A. Lorentz group action on Schwartz functions
  - A.1. The definition of the action
  - A.2. Basic properties of the action
  - A.3. Injectivity of the action
  - A.4. Surjectivity of the action
- B. Lorentz group action on distributions
  - B.1. The SMul instance
  - B.2. The DistribMulAction instance
  - B.3. The SMulCommClass instance

## iv. References

-/
noncomputable section

namespace SpaceTime

open Manifold
open Matrix
open Complex
open ComplexConjugate
open TensorSpecies
open SchwartzMap
attribute [-simp] Fintype.sum_sum_type

/-!

## A. Lorentz group action on Schwartz functions

-/

/-!

### A.1. The definition of the action

-/

/-- The Lorentz group action on Schwartz functions taking the Lorentz group to
  continous linear maps. -/
def schwartzAction {d} : LorentzGroup d ‚Üí* ùì¢(SpaceTime d, ‚Ñù) ‚ÜíL[‚Ñù] ùì¢(SpaceTime d, ‚Ñù) where
  toFun Œõ := SchwartzMap.compCLM (ùïú := ‚Ñù)
    (Lorentz.Vector.actionCLM Œõ‚Åª¬π).hasTemperateGrowth <| by
      use 1, ‚ÄñLorentz.Vector.actionCLM Œõ‚Äñ
      simp only [pow_one]
      intro x
      obtain ‚ü®x, rfl‚ü© := Lorentz.Vector.actionCLM_surjective Œõ x
      apply (ContinuousLinearMap.le_opNorm (Lorentz.Vector.actionCLM Œõ) x).trans
      simp [Lorentz.Vector.actionCLM_apply, mul_add]
  map_one' := by
    ext Œ∑ x
    simp [Lorentz.Vector.actionCLM_apply]
  map_mul' Œõ‚ÇÅ Œõ‚ÇÇ := by
    ext Œ∑ x
    simp only [_root_.mul_inv_rev, compCLM_apply, Function.comp_apply,
      Lorentz.Vector.actionCLM_apply, ContinuousLinearMap.coe_mul]
    rw [MulAction.mul_smul]

/-!

### A.2. Basic properties of the action

-/

lemma schwartzAction_mul_apply {d} (Œõ‚ÇÅ Œõ‚ÇÇ : LorentzGroup d)
    (Œ∑ : ùì¢(SpaceTime d, ‚Ñù)) :
    schwartzAction Œõ‚ÇÇ (schwartzAction (Œõ‚ÇÅ) Œ∑) =
    schwartzAction (Œõ‚ÇÇ * Œõ‚ÇÅ) Œ∑ := by
  simp

lemma schwartzAction_apply {d} (Œõ : LorentzGroup d)
    (Œ∑ : ùì¢(SpaceTime d, ‚Ñù)) (x : SpaceTime d) :
    (schwartzAction Œõ Œ∑) x = Œ∑ (Œõ‚Åª¬π ‚Ä¢ x) := rfl

/-!

### A.3. Injectivity of the action

-/

lemma schwartzAction_injective {d} (Œõ : LorentzGroup d) :
    Function.Injective (schwartzAction Œõ) := by
  intro Œ∑1 Œ∑2 h
  ext x
  have h1 : (schwartzAction Œõ‚Åª¬π * schwartzAction Œõ) Œ∑1 =
    (schwartzAction Œõ‚Åª¬π * schwartzAction Œõ) Œ∑2 := by simp [h]
  rw [‚Üê map_mul] at h1
  simp at h1
  rw [h1]

/-!

### A.4. Surjectivity of the action

-/

lemma schwartzAction_surjective {d} (Œõ : LorentzGroup d) :
    Function.Surjective (schwartzAction Œõ) := by
  intro Œ∑
  use (schwartzAction Œõ‚Åª¬π Œ∑)
  change (schwartzAction Œõ * schwartzAction Œõ‚Åª¬π) Œ∑ = _
  rw [‚Üê map_mul]
  simp

/-!

## B. Lorentz group action on distributions

-/
section Distribution

/-!

### B.1. The SMul instance

-/
variable
    {c : Fin n ‚Üí realLorentzTensor.Color} {M : Type} [NormedAddCommGroup M]
      [NormedSpace ‚Ñù M] [Tensorial (realLorentzTensor d) c M] [T2Space M]

open Distribution
instance : SMul (LorentzGroup d) ((SpaceTime d) ‚Üíd[‚Ñù] M) where
  smul Œõ f := (Tensorial.actionCLM (realLorentzTensor d) Œõ) ‚àòL f ‚àòL (schwartzAction Œõ‚Åª¬π)

lemma lorentzGroup_smul_dist_apply (Œõ : LorentzGroup d) (f : (SpaceTime d) ‚Üíd[‚Ñù] M)
    (Œ∑ : ùì¢(SpaceTime d, ‚Ñù)) : (Œõ ‚Ä¢ f) Œ∑ = Œõ ‚Ä¢ (f (schwartzAction Œõ‚Åª¬π Œ∑)) := rfl

/-!

### B.2. The DistribMulAction instance

-/

instance : DistribMulAction (LorentzGroup d) ((SpaceTime d) ‚Üíd[‚Ñù] M) where
  one_smul f := by
    ext Œ∑
    simp [lorentzGroup_smul_dist_apply]
  mul_smul Œõ‚ÇÅ Œõ‚ÇÇ f := by
    ext Œ∑
    simp [lorentzGroup_smul_dist_apply, MulAction.mul_smul]
  smul_zero Œõ := by
    ext Œ∑
    rw [lorentzGroup_smul_dist_apply]
    simp
  smul_add Œõ f1 f2 := by
    ext Œ∑
    simp [ContinuousLinearMap.add_apply, smul_add, lorentzGroup_smul_dist_apply]

/-!

### B.3. The SMulCommClass instance

-/

instance : SMulCommClass ‚Ñù (LorentzGroup d) ((SpaceTime d) ‚Üíd[‚Ñù] M) where
  smul_comm a Œõ f := by
    ext Œ∑
    simp [lorentzGroup_smul_dist_apply]
    rw [SMulCommClass.smul_comm]

end Distribution
end SpaceTime

end
