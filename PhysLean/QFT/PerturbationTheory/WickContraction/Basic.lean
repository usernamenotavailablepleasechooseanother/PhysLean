/-
Copyright (c) 2025 Joseph Tooby-Smith. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Joseph Tooby-Smith
-/
import PhysLean.QFT.PerturbationTheory.FieldSpecification.Basic
/-!

# Wick contractions

-/
open FieldSpecification

variable {ùìï : FieldSpecification}

/--
Given a natural number `n`, which will correspond to the number of fields needing
contracting, a Wick contraction
is a finite set of pairs of `Fin n` (numbers `0`, ..., `n-1`), such that no
element of `Fin n` occurs in more than one pair. The pairs are the positions of fields we
'contract' together.
-/
def WickContraction (n : ‚Ñï) : Type :=
  {f : Finset ((Finset (Fin n))) // (‚àÄ a ‚àà f, a.card = 2) ‚àß
    (‚àÄ a ‚àà f, ‚àÄ b ‚àà f, a = b ‚à® Disjoint a b)}

namespace WickContraction
variable {n : ‚Ñï} (c : WickContraction n)
open PhysLean.List

/-- Wick contractions are decidable. -/
instance : DecidableEq (WickContraction n) := Subtype.instDecidableEq

/-- The contraction consisting of no contracted pairs. -/
def empty : WickContraction n := ‚ü®‚àÖ, by simp, by simp‚ü©

lemma card_zero_iff_empty (c : WickContraction n) : c.1.card = 0 ‚Üî c = empty := by
  rw [Subtype.eq_iff, Finset.card_eq_zero, empty]

lemma exists_pair_of_not_eq_empty (c : WickContraction n) (h : c ‚â† empty) :
    ‚àÉ i j, {i, j} ‚àà c.1 := by
  by_contra hn
  simp only [not_exists] at hn
  have hc : c.1 = ‚àÖ := by
    ext a
    simp only [Finset.notMem_empty, iff_false]
    by_contra hn'
    have hc := c.2.1 a hn'
    rw [@Finset.card_eq_two] at hc
    obtain ‚ü®x, y, hx, rfl‚ü© := hc
    exact hn x y hn'
  apply h
  apply Subtype.eq
  simp [empty, hc]

/-- The equivalence between `WickContraction n` and `WickContraction m`
  derived from a propositional equality of `n` and `m`. -/
def congr : {n m : ‚Ñï} ‚Üí (h : n = m) ‚Üí WickContraction n ‚âÉ WickContraction m
  | n, .(n), rfl => Equiv.refl _

@[simp]
lemma congr_refl : c.congr rfl = c := rfl

@[simp]
lemma card_congr {n m : ‚Ñï} (h : n = m) (c : WickContraction n) :
    (congr h c).1.card = c.1.card := by
  subst h
  simp

lemma congr_contractions {n m : ‚Ñï} (h : n = m) (c : WickContraction n) :
    ((congr h) c).1 = Finset.map (Finset.mapEmbedding (finCongr h)).toEmbedding c.1 := by
  subst h
  simp only [congr_refl, Finset.le_eq_subset, finCongr_refl, Equiv.refl_toEmbedding]
  ext a
  apply Iff.intro <;> intro ha
  ¬∑ simp only [Finset.mem_map, RelEmbedding.coe_toEmbedding]
    use a
    simp only [ha, true_and]
    rw [Finset.mapEmbedding_apply, Finset.map_refl]
  ¬∑ simp only [Finset.mem_map, RelEmbedding.coe_toEmbedding] at ha
    obtain ‚ü®b, hb, hab‚ü© := ha
    rw [Finset.mapEmbedding_apply, Finset.map_refl] at hab
    subst hab
    exact hb

@[simp]
lemma congr_trans {n m o : ‚Ñï} (h1 : n = m) (h2 : m = o) :
    (congr h1).trans (congr h2) = congr (h1.trans h2) := by
  subst h1 h2
  simp [congr]

@[simp]
lemma congr_trans_apply {n m o : ‚Ñï} (h1 : n = m) (h2 : m = o) (c : WickContraction n) :
    (congr h2) ((congr h1) c) = congr (h1.trans h2) c := by
  subst h1 h2
  simp

lemma mem_congr_iff {n m : ‚Ñï} (h : n = m) {c : WickContraction n } {a : Finset (Fin m)} :
    a ‚àà (congr h c).1 ‚Üî Finset.map (finCongr h.symm).toEmbedding a ‚àà c.1 := by
  subst h
  simp

/-- Given a contracted pair in `c : WickContraction n` the contracted pair
  in `congr h c`. -/
def congrLift {n m : ‚Ñï} (h : n = m) {c : WickContraction n} (a : c.1) : (congr h c).1 :=
  ‚ü®a.1.map (finCongr h).toEmbedding, by aesop‚ü©

@[simp]
lemma congrLift_rfl {n : ‚Ñï} {c : WickContraction n} :
    c.congrLift rfl = id := by
  funext a
  simp [congrLift]

lemma congrLift_injective {n m : ‚Ñï} {c : WickContraction n} (h : n = m) :
    Function.Injective (c.congrLift h) := by
  subst h
  simp only [congrLift_rfl]
  exact fun ‚¶Éa‚ÇÅ a‚ÇÇ‚¶Ñ a => a

lemma congrLift_surjective {n m : ‚Ñï} {c : WickContraction n} (h : n = m) :
    Function.Surjective (c.congrLift h) := by
  subst h
  simp [Function.surjective_id]

lemma congrLift_bijective {n m : ‚Ñï} {c : WickContraction n} (h : n = m) :
    Function.Bijective (c.congrLift h) := by
  exact ‚ü®c.congrLift_injective h, c.congrLift_surjective h‚ü©

/-- Given a contracted pair in `c : WickContraction n` the contracted pair
  in `congr h c`. -/
def congrLiftInv {n m : ‚Ñï} (h : n = m) {c : WickContraction n} (a : (congr h c).1) : c.1 :=
  ‚ü®a.1.map (finCongr h.symm).toEmbedding, by aesop‚ü©

lemma congrLiftInv_rfl {n : ‚Ñï} {c : WickContraction n} :
    c.congrLiftInv rfl = id := by
  funext a
  simp [congrLiftInv]

lemma eq_filter_mem_self : c.1 = Finset.filter (fun x => x ‚àà c.1) Finset.univ :=
  (Finset.filter_univ_mem c.1).symm

/-- For a contraction `c : WickContraction n` and `i : Fin n` the `j` such that
  `{i, j}` is a contracted pair in `c`. If such an `j` does not exist, this returns `none`. -/
def getDual? (i : Fin n) : Option (Fin n) := Fin.find (fun j => {i, j} ‚àà c.1)

lemma getDual?_congr {n m : ‚Ñï} (h : n = m) (c : WickContraction n) (i : Fin m) :
    (congr h c).getDual? i = Option.map (finCongr h) (c.getDual? (finCongr h.symm i)) := by
  subst h
  simp

lemma getDual?_congr_get {n m : ‚Ñï} (h : n = m) (c : WickContraction n) (i : Fin m)
    (hg : ((congr h c).getDual? i).isSome) :
    ((congr h c).getDual? i).get hg =
    (finCongr h ((c.getDual? (finCongr h.symm i)).get (by simpa [getDual?_congr] using hg))) := by
  simp only [getDual?_congr, finCongr_apply]
  exact Option.get_map

lemma getDual?_eq_some_iff_mem (i j : Fin n) :
    c.getDual? i = some j ‚Üî {i, j} ‚àà c.1 := by
  simp only [getDual?]
  rw [Fin.find_eq_some_iff]
  apply Iff.intro <;> intro h
  ¬∑ exact h.1
  ¬∑ simp only [h, true_and]
    intro k hk
    have hc := c.2.2 _ h _ hk
    simp only [Finset.disjoint_insert_right, Finset.mem_insert, Finset.mem_singleton, true_or,
      not_true_eq_false, Finset.disjoint_singleton_right, not_or, false_and, or_false] at hc
    have hj : k ‚àà ({i, j} : Finset (Fin n)) := by simp [hc]
    simp only [Finset.mem_insert, Finset.mem_singleton] at hj
    rcases hj with hj | hj
    ¬∑ subst hj
      simp only [Finset.mem_singleton, Finset.insert_eq_of_mem] at hk
      have hc := c.2.1 _ hk
      simp at hc
    ¬∑ subst hj
      simp

@[simp]
lemma getDual?_one_eq_none (c : WickContraction 1) (i : Fin 1) : c.getDual? i = none := by
  by_contra h
  have hn : (c.getDual? i).isSome := by
    rw [‚Üê Option.not_isSome_iff_eq_none] at h
    simpa [- Option.not_isSome, -Option.isNone_iff_eq_none] using h
  rw [@Option.isSome_iff_exists] at hn
  obtain ‚ü®a, hn‚ü© := hn
  rw [getDual?_eq_some_iff_mem] at hn
  have hc := c.2.1 {i, a} hn
  fin_cases i
  fin_cases a
  simp at hc

@[simp]
lemma getDual?_get_self_mem (i : Fin n) (h : (c.getDual? i).isSome) :
    {(c.getDual? i).get h, i} ‚àà c.1 := by
  rw [@Finset.pair_comm, ‚Üê getDual?_eq_some_iff_mem, Option.some_get]

@[simp]
lemma self_getDual?_get_mem (i : Fin n) (h : (c.getDual? i).isSome) :
    {i, (c.getDual? i).get h} ‚àà c.1 := by
  rw [‚Üê getDual?_eq_some_iff_mem, Option.some_get]

lemma getDual?_eq_some_neq (i j : Fin n) (h : c.getDual? i = some j) :
    ¬¨ i = j := by
  rw [getDual?_eq_some_iff_mem] at h
  by_contra hn
  subst hn
  have hc := c.2.1 _ h
  simp at hc

@[simp]
lemma self_neq_getDual?_get (i : Fin n) (h : (c.getDual? i).isSome) :
    ¬¨ i = (c.getDual? i).get h := by
  by_contra hn
  have hx : {i, (c.getDual? i).get h} ‚àà c.1 := by simp
  have hc := c.2.1 _ hx
  nth_rewrite 1 [hn] at hc
  simp at hc

@[simp]
lemma getDual?_get_self_neq (i : Fin n) (h : (c.getDual? i).isSome) :
    ¬¨ (c.getDual? i).get h = i := by
  by_contra hn
  have hx : {i, (c.getDual? i).get h} ‚àà c.1 := by simp
  have hc := c.2.1 _ hx
  nth_rewrite 1 [hn] at hc
  simp at hc

lemma getDual?_isSome_iff (i : Fin n) : (c.getDual? i).isSome ‚Üî ‚àÉ (a : c.1), i ‚àà a.1 := by
  apply Iff.intro <;> intro h
  ¬∑ rw [getDual?, Fin.isSome_find_iff] at h
    obtain ‚ü®a, ha‚ü© := h
    exact ‚ü®‚ü®{i, a}, ha‚ü©, Finset.mem_insert_self ..‚ü©
  ¬∑ obtain ‚ü®a, ha‚ü© := h
    have ha := c.2.1 a a.2
    rw [@Finset.card_eq_two] at ha
    obtain ‚ü®x, y, hx, hy‚ü© := ha
    rw [hy] at ha
    simp only [Finset.mem_insert, Finset.mem_singleton] at ha
    match ha with
    | Or.inl ha =>
      subst ha
      rw [getDual?, Fin.isSome_find_iff]
      exact ‚ü®y, hy ‚ñ∏ a.2‚ü©
    | Or.inr ha =>
      subst ha
      rw [getDual?, Fin.isSome_find_iff]
      use x
      rw [Finset.pair_comm]
      exact hy ‚ñ∏ a.2

lemma getDual?_isSome_of_mem (a : c.1) (i : a.1) : (c.getDual? i).isSome := by
  rw [getDual?_isSome_iff]
  exact ‚ü®‚ü®a.1, a.2‚ü©, Finset.coe_mem ..‚ü©

@[simp]
lemma getDual?_getDual?_get_get (i : Fin n) (h : (c.getDual? i).isSome) :
    c.getDual? ((c.getDual? i).get h) = some i := by
  simp [getDual?_eq_some_iff_mem]

lemma getDual?_getDual?_get_isSome (i : Fin n) (h : (c.getDual? i).isSome) :
    (c.getDual? ((c.getDual? i).get h)).isSome := by
  simp

lemma getDual?_getDual?_get_not_none (i : Fin n) (h : (c.getDual? i).isSome) :
    ¬¨ (c.getDual? ((c.getDual? i).get h)) = none := by
  simp

/-!

## Extracting parts from a contraction.

-/

/-- The smallest of the two positions in a contracted pair given a Wick contraction. -/
def fstFieldOfContract (c : WickContraction n) (a : c.1) : Fin n :=
  (a.1.sort (¬∑ ‚â§ ¬∑)).head (by
    have hx : (a.1.sort (fun x1 x2 => x1 ‚â§ x2)).length = a.1.card := Finset.length_sort ..
    by_contra hn
    simp only [hn, List.length_nil, c.2.1 a.1 a.2, OfNat.zero_ne_ofNat] at hx)

@[simp]
lemma fstFieldOfContract_congr {n m : ‚Ñï} (h : n = m) (c : WickContraction n) (a : c.1) :
    (congr h c).fstFieldOfContract (c.congrLift h a) = (finCongr h) (c.fstFieldOfContract a) := by
  subst h
  simp [congr]

/-- The largest of the two positions in a contracted pair given a Wick contraction. -/
def sndFieldOfContract (c : WickContraction n) (a : c.1) : Fin n :=
  (a.1.sort (¬∑ ‚â§ ¬∑)).tail.head (by
    have hx : (a.1.sort (fun x1 x2 => x1 ‚â§ x2)).length = a.1.card := Finset.length_sort ..
    by_contra hn
    have hn := congrArg List.length hn
    simp [c.2.1] at hn)

@[simp]
lemma sndFieldOfContract_congr {n m : ‚Ñï} (h : n = m) (c : WickContraction n) (a : c.1) :
    (congr h c).sndFieldOfContract (c.congrLift h a) = (finCongr h) (c.sndFieldOfContract a) := by
  subst h
  simp [congr]

lemma finset_eq_fstFieldOfContract_sndFieldOfContract (c : WickContraction n) (a : c.1) :
    a.1 = {c.fstFieldOfContract a, c.sndFieldOfContract a} := by
  have h1 := c.2.1 a.1 a.2
  rw [Finset.card_eq_two] at h1
  obtain ‚ü®x, y, hxy, ha‚ü© := h1
  rw [ha]
  by_cases hxyle : x ‚â§ y
  ¬∑ have ha : a.1.sort (¬∑ ‚â§ ¬∑) = [x, y] := by
      rw [ha]
      trans Finset.sort (Finset.cons x {y} (by simp [hxy])) (¬∑ ‚â§ ¬∑)
      ¬∑ congr
        simp
      rw [Finset.sort_cons]
      simp only [Finset.sort_singleton]
      intro b hb
      simp only [Finset.mem_singleton] at hb
      subst hb
      omega
    simp [fstFieldOfContract, ha, sndFieldOfContract]
  ¬∑ have ha : a.1.sort (¬∑ ‚â§ ¬∑) = [y, x] := by
      rw [ha]
      trans Finset.sort (Finset.cons y {x} (by simp only [Finset.mem_singleton]; omega)) (¬∑ ‚â§ ¬∑)
      ¬∑ congr
        simp only [Finset.cons_eq_insert]
        rw [@Finset.pair_comm]
      rw [Finset.sort_cons]
      simp only [Finset.sort_singleton]
      intro b hb
      simp only [Finset.mem_singleton] at hb
      subst hb
      omega
    simp only [fstFieldOfContract, ha, List.head_cons, sndFieldOfContract, List.tail_cons]
    rw [Finset.pair_comm]

lemma fstFieldOfContract_neq_sndFieldOfContract (c : WickContraction n) (a : c.1) :
    c.fstFieldOfContract a ‚â† c.sndFieldOfContract a := by
  have h1 := c.2.1 a.1 a.2
  have h2 := c.finset_eq_fstFieldOfContract_sndFieldOfContract a
  by_contra hn
  simp [h2, hn] at h1

lemma fstFieldOfContract_le_sndFieldOfContract (c : WickContraction n) (a : c.1) :
    c.fstFieldOfContract a ‚â§ c.sndFieldOfContract a := by
  simp only [fstFieldOfContract, sndFieldOfContract, List.head_tail]
  have h1 (n : ‚Ñï) (l : List (Fin n)) (h : l ‚â† []) (hl : l.Sorted (¬∑ ‚â§ ¬∑)) :
      ‚àÄ a ‚àà l, l.head h ‚â§ a := by
    induction l with
    | nil => simp at h
    | cons i l ih =>
      simp only [List.sorted_cons] at hl
      simpa using hl.1
  apply h1
  ¬∑ exact Finset.sort_sorted ..
  ¬∑ exact List.getElem_mem ..

lemma fstFieldOfContract_lt_sndFieldOfContract (c : WickContraction n) (a : c.1) :
    c.fstFieldOfContract a < c.sndFieldOfContract a :=
  lt_of_le_of_ne (c.fstFieldOfContract_le_sndFieldOfContract a)
    (c.fstFieldOfContract_neq_sndFieldOfContract a)

@[simp]
lemma fstFieldOfContract_mem (c : WickContraction n) (a : c.1) :
    c.fstFieldOfContract a ‚àà a.1 := by
  simp [finset_eq_fstFieldOfContract_sndFieldOfContract]

lemma fstFieldOfContract_getDual?_isSome (c : WickContraction n) (a : c.1) :
    (c.getDual? (c.fstFieldOfContract a)).isSome := by
  rw [getDual?_isSome_iff]
  exact ‚ü®a, fstFieldOfContract_mem ..‚ü©

@[simp]
lemma fstFieldOfContract_getDual? (c : WickContraction n) (a : c.1) :
    c.getDual? (c.fstFieldOfContract a) = some (c.sndFieldOfContract a) := by
  simp [getDual?_eq_some_iff_mem, ‚Üê finset_eq_fstFieldOfContract_sndFieldOfContract]

@[simp]
lemma sndFieldOfContract_mem (c : WickContraction n) (a : c.1) :
    c.sndFieldOfContract a ‚àà a.1 := by
  simp [finset_eq_fstFieldOfContract_sndFieldOfContract]

lemma sndFieldOfContract_getDual?_isSome (c : WickContraction n) (a : c.1) :
    (c.getDual? (c.sndFieldOfContract a)).isSome := by
  rw [getDual?_isSome_iff]
  exact ‚ü®a, sndFieldOfContract_mem ..‚ü©

@[simp]
lemma sndFieldOfContract_getDual? (c : WickContraction n) (a : c.1) :
    c.getDual? (c.sndFieldOfContract a) = some (c.fstFieldOfContract a) := by
  rw [getDual?_eq_some_iff_mem, Finset.pair_comm, ‚Üê finset_eq_fstFieldOfContract_sndFieldOfContract]
  exact a.2

lemma eq_fstFieldOfContract_of_mem (c : WickContraction n) (a : c.1) (i j : Fin n)
    (hi : i ‚àà a.1) (hj : j ‚àà a.1) (hij : i < j) :
    c.fstFieldOfContract a = i := by
  rw [finset_eq_fstFieldOfContract_sndFieldOfContract] at hi hj
  simp_all only [Finset.mem_insert, Finset.mem_singleton]
  match hi, hj with
  | Or.inl hi, Or.inl hj =>
    subst hi hj
    simp at hij
  | Or.inl hi, Or.inr hj =>
    subst hi
    rfl
  | Or.inr hi, Or.inl hj =>
    subst hi hj
    have hn := fstFieldOfContract_lt_sndFieldOfContract c a
    omega
  | Or.inr hi, Or.inr hj =>
    subst hi hj
    simp at hij

lemma eq_sndFieldOfContract_of_mem (c : WickContraction n) (a : c.1) (i j : Fin n)
    (hi : i ‚àà a.1) (hj : j ‚àà a.1) (hij : i < j) :
    c.sndFieldOfContract a = j := by
  rw [finset_eq_fstFieldOfContract_sndFieldOfContract] at hi hj
  simp_all only [Finset.mem_insert, Finset.mem_singleton]
  match hi, hj with
  | Or.inl hi, Or.inl hj =>
    subst hi hj
    simp at hij
  | Or.inl hi, Or.inr hj =>
    subst hi hj
    omega
  | Or.inr hi, Or.inl hj =>
    subst hi hj
    have hn := fstFieldOfContract_lt_sndFieldOfContract c a
    omega
  | Or.inr hi, Or.inr hj =>
    subst hi hj
    simp at hij

/-- As a type, any pair of contractions is equivalent to `Fin 2`
  with `0` being associated with `c.fstFieldOfContract a` and `1` being associated with
  `c.sndFieldOfContract`. -/
def contractEquivFinTwo (c : WickContraction n) (a : c.1) :
    a ‚âÉ Fin 2 where
  toFun i := if i = c.fstFieldOfContract a then 0 else 1
  invFun i :=
    match i with
    | 0 => ‚ü®c.fstFieldOfContract a, fstFieldOfContract_mem c a‚ü©
    | 1 => ‚ü®c.sndFieldOfContract a, sndFieldOfContract_mem c a‚ü©
  left_inv i := by
    simp only [Fin.isValue]
    have hi := i.2
    have ha := c.finset_eq_fstFieldOfContract_sndFieldOfContract a
    simp only [ha, Finset.mem_insert, Finset.mem_singleton] at hi
    rcases hi with hi | hi
    ¬∑ rw [hi]
      simp only [‚ÜìreduceIte, Fin.isValue]
      exact Subtype.eq hi.symm
    ¬∑ rw [hi, if_neg]
      ¬∑ exact Subtype.eq hi.symm
      ¬∑ exact Ne.symm <| fstFieldOfContract_neq_sndFieldOfContract c a
  right_inv i := by
    fin_cases i
    ¬∑ simp
    ¬∑ simp only [Fin.isValue, Fin.mk_one, ite_eq_right_iff, zero_ne_one, imp_false]
      exact Ne.symm <| fstFieldOfContract_neq_sndFieldOfContract c a

lemma prod_finset_eq_mul_fst_snd (c : WickContraction n) (a : c.1)
    (f : a.1 ‚Üí M) [CommMonoid M] :
    ‚àè (x : a), f x = f (‚ü®c.fstFieldOfContract a, fstFieldOfContract_mem c a‚ü©)
    * f (‚ü®c.sndFieldOfContract a, sndFieldOfContract_mem c a‚ü©) := by
  rw [‚Üê (c.contractEquivFinTwo a).symm.prod_comp]
  simp [contractEquivFinTwo]

/-- For a field specification `ùìï`, `œÜs` a list of `ùìï.FieldOp` and a Wick contraction
  `œÜsŒõ` of `œÜs`, the Wick contraction `œÜsŒõ` is said to be `GradingCompliant` if
  for every pair in `œÜsŒõ` the contracted fields are either both `fermionic` or both `bosonic`.
  In other words, in a `GradingCompliant` Wick contraction if
  no contracted pairs occur between `fermionic` and `bosonic` fields. -/
def GradingCompliant (œÜs : List ùìï.FieldOp) (œÜsŒõ : WickContraction œÜs.length) :=
  ‚àÄ (a : œÜsŒõ.1), (ùìï |>‚Çõ œÜs[(œÜsŒõ.fstFieldOfContract a).1]) = (ùìï |>‚Çõ œÜs[(œÜsŒõ.sndFieldOfContract a).1])

lemma gradingCompliant_congr {œÜs œÜs' : List ùìï.FieldOp} (h : œÜs = œÜs')
    (œÜsŒõ : WickContraction œÜs.length) :
    GradingCompliant œÜs œÜsŒõ ‚Üî GradingCompliant œÜs' (congr (by simp [h]) œÜsŒõ) := by
  subst h
  rfl

/-- An equivalence from the sigma type `(a : c.1) √ó a` to the subtype of `Fin n` consisting of
  those positions which are contracted. -/
def sigmaContractedEquiv : (a : c.1) √ó a ‚âÉ {x : Fin n // (c.getDual? x).isSome} where
  toFun := fun x => ‚ü®x.2, getDual?_isSome_of_mem c x.fst x.snd‚ü©
  invFun := fun x => ‚ü®
    ‚ü®{x.1, (c.getDual? x.1).get x.2}, self_getDual?_get_mem c (‚Üëx) x.prop‚ü©,
    ‚ü®x.1, by simp‚ü©‚ü©
  left_inv x := by
    have hxa (x1 x2 : (a : c.1) √ó a) (h1 : x1.1 = x2.1)
      (h2 : x1.2.val = x2.2.val) : x1 = x2 := by
      cases x1
      cases x2
      simp_all only [Sigma.mk.inj_iff, true_and]
      subst h1
      rename_i fst snd snd_1
      simp_all only [heq_eq_eq]
      obtain ‚ü®val, property‚ü© := fst
      obtain ‚ü®val_2, property_2‚ü© := snd
      subst h2
      simp_all only
    match x with
    | ‚ü®a, i‚ü© =>
    apply hxa
    ¬∑ have hc := c.2.2 a.1 a.2 {i.1, (c.getDual? ‚Üëi).get (getDual?_isSome_of_mem c a i)}
        (self_getDual?_get_mem c (‚Üëi) (getDual?_isSome_of_mem c a i))
      have hn : ¬¨ Disjoint a.1 {i.1, (c.getDual? ‚Üëi).get (getDual?_isSome_of_mem c a i)} := by
        rw [Finset.disjoint_iff_inter_eq_empty, @Finset.eq_empty_iff_forall_notMem]
        simp only [Finset.coe_mem, Finset.inter_insert_of_mem, Finset.mem_insert, Finset.mem_inter,
          Finset.mem_singleton, not_or, not_and, not_forall, Decidable.not_not]
        exact ‚ü®i, fun x ‚Ü¶ (x rfl).elim‚ü©
      simp_all only [or_false, disjoint_self, Finset.bot_eq_empty, Finset.insert_ne_empty,
        not_false_eq_true]
      exact Subtype.eq (id (Eq.symm hc))
    ¬∑ simp
  right_inv := by
    intro x
    cases x
    rfl

end WickContraction
