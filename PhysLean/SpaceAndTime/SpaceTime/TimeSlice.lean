/-
Copyright (c) 2025 Joseph Tooby-Smith. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Joseph Tooby-Smith
-/
import PhysLean.SpaceAndTime.SpaceTime.Derivatives
import PhysLean.SpaceAndTime.TimeAndSpace.Basic
import PhysLean.Meta.Informal.SemiFormal
import PhysLean.Mathematics.FDerivCurry
/-!
# Time slice

Time slicing functions on spacetime, turning them into a function
`Time ‚Üí Space d ‚Üí M`.

This is useful when going from relativistic physics (defined using `SpaceTime`)
to non-relativistic physics (defined using `Space` and `Time`).

-/

noncomputable section

namespace SpaceTime

open Time
open Space

/-- The timeslice of a function `SpaceTime d ‚Üí M` forming a function
  `Time ‚Üí Space d ‚Üí M`. -/
def timeSlice {d : ‚Ñï} {M : Type} (c : SpeedOfLight := 1) :
    (SpaceTime d ‚Üí M) ‚âÉ (Time ‚Üí Space d ‚Üí M) where
  toFun f := Function.curry (f ‚àò (toTimeAndSpace c).symm)
  invFun f := Function.uncurry f ‚àò toTimeAndSpace c
  left_inv f := by
    funext x
    simp
  right_inv f := by
    funext x t
    simp

lemma timeSlice_contDiff {d : ‚Ñï} {M : Type} [NormedAddCommGroup M]
    [NormedSpace ‚Ñù M]
    {n} (c : SpeedOfLight) (f : SpaceTime d ‚Üí M) (h : ContDiff ‚Ñù n f) :
    ContDiff ‚Ñù n ‚Üø(timeSlice c f) := by
  change ContDiff ‚Ñù n (f ‚àò (toTimeAndSpace c).symm)
  apply ContDiff.comp
  ¬∑ exact h
  ¬∑ exact ContinuousLinearEquiv.contDiff (toTimeAndSpace c).symm

lemma timeSlice_differentiable {d : ‚Ñï} {M : Type} [NormedAddCommGroup M]
    [NormedSpace ‚Ñù M] (c : SpeedOfLight)
    (f : SpaceTime d ‚Üí M) (h : Differentiable ‚Ñù f) :
    Differentiable ‚Ñù ‚Üø(timeSlice c f) := by
  change Differentiable ‚Ñù (f ‚àò (toTimeAndSpace c).symm)
  apply Differentiable.comp
  ¬∑ exact h
  ¬∑ exact ContinuousLinearEquiv.differentiable (toTimeAndSpace c).symm

/-- The timeslice of a function `SpaceTime d ‚Üí M` forming a function
  `Time ‚Üí Space d ‚Üí M`, as a linear equivalence. -/
def timeSliceLinearEquiv {d : ‚Ñï} {M : Type} [AddCommGroup M] [Module ‚Ñù M]
    (c : SpeedOfLight := 1) :
    (SpaceTime d ‚Üí M) ‚âÉ‚Çó[‚Ñù] (Time ‚Üí Space d ‚Üí M) where
  toFun := timeSlice c
  invFun := (timeSlice c).symm
  map_add' f g := by
    ext t x
    simp [timeSlice]
  map_smul' := by
    intros c f
    ext t x
    simp [timeSlice]
  left_inv f := by simp
  right_inv f := by simp

lemma timeSliceLinearEquiv_apply {d : ‚Ñï} {M : Type} [AddCommGroup M] [Module ‚Ñù M]
    (c : SpeedOfLight) (f : SpaceTime d ‚Üí M) : timeSliceLinearEquiv c f = timeSlice c f := by
  simp [timeSliceLinearEquiv, timeSlice]

lemma timeSliceLinearEquiv_symm_apply {d : ‚Ñï} {M : Type} [AddCommGroup M] [Module ‚Ñù M]
    (c : SpeedOfLight) (f : Time ‚Üí Space d ‚Üí M) :
    (timeSliceLinearEquiv c).symm f = (timeSlice c).symm f := by
  simp [timeSliceLinearEquiv, timeSlice]

/-!

## B. Time slices of distributions

-/
open Distribution SchwartzMap

/-- The time slice of a distribution on `SpaceTime d` to form a distribution
  on `Time √ó Space d`. -/
noncomputable def distTimeSlice {M d} [NormedAddCommGroup M] [NormedSpace ‚Ñù M]
    (c : SpeedOfLight := 1) :
    ((SpaceTime d) ‚Üíd[‚Ñù] M) ‚âÉL[‚Ñù] ((Time √ó Space d) ‚Üíd[‚Ñù] M) where
  toFun f :=
    f ‚àòL compCLMOfContinuousLinearEquiv (F := ‚Ñù) ‚Ñù (SpaceTime.toTimeAndSpace c (d := d))
  invFun f := f ‚àòL compCLMOfContinuousLinearEquiv
      (F := ‚Ñù) ‚Ñù (SpaceTime.toTimeAndSpace c (d := d)).symm
  left_inv f := by
    ext Œ∫
    simp only [ContinuousLinearMap.coe_comp', Function.comp_apply]
    congr
    ext x
    simp [compCLMOfContinuousLinearEquiv_apply]
  right_inv f := by
    ext Œ∫
    simp only [ContinuousLinearMap.coe_comp', Function.comp_apply]
    congr
    ext x
    simp
  map_add' f1 f2 := by
    simp
  map_smul' a f := by simp
  continuous_toFun := ((compCLMOfContinuousLinearEquiv ‚Ñù (toTimeAndSpace c)).precomp M).continuous
  continuous_invFun :=
    ((compCLMOfContinuousLinearEquiv ‚Ñù (toTimeAndSpace c).symm).precomp M).continuous

lemma distTimeSlice_apply {M d} [NormedAddCommGroup M] [NormedSpace ‚Ñù M]
    (c : SpeedOfLight) (f : (SpaceTime d) ‚Üíd[‚Ñù] M)
    (Œ∫ : ùì¢(Time √ó Space d, ‚Ñù)) : distTimeSlice c f Œ∫ =
    f (compCLMOfContinuousLinearEquiv ‚Ñù (toTimeAndSpace c) Œ∫) := by rfl

lemma distTimeSlice_symm_apply {M d} [NormedAddCommGroup M] [NormedSpace ‚Ñù M]
    (c : SpeedOfLight) (f : (Time √ó (Space d)) ‚Üíd[‚Ñù] M)
    (Œ∫ : ùì¢(SpaceTime d, ‚Ñù)) : (distTimeSlice c).symm f Œ∫ =
    f (compCLMOfContinuousLinearEquiv ‚Ñù (toTimeAndSpace c).symm Œ∫) := by rfl

/-!

### B.1. Time slices and derivatives

-/

lemma distTimeSlice_distDeriv_inl {M d} [NormedAddCommGroup M] [NormedSpace ‚Ñù M]
    {c : SpeedOfLight}
    (f : (SpaceTime d) ‚Üíd[‚Ñù] M) :
    distTimeSlice c (distDeriv (Sum.inl 0) f) =
    (1/c.val) ‚Ä¢ Space.distTimeDeriv (distTimeSlice c f) := by
  ext Œ∫
  rw [distTimeSlice_apply, distDeriv_apply, fderivD_apply]
  simp only [Fin.isValue, one_div, ContinuousLinearMap.coe_smul', Pi.smul_apply]
  rw [distTimeDeriv_apply, fderivD_apply, distTimeSlice_apply]
  simp only [Fin.isValue, smul_neg, neg_inj]
  rw [‚Üê map_smul]
  congr
  ext x
  change fderiv ‚Ñù (Œ∫ ‚àò toTimeAndSpace c) x (Lorentz.Vector.basis (Sum.inl 0)) =
    c.val‚Åª¬π ‚Ä¢ fderiv ‚Ñù Œ∫ (toTimeAndSpace c x) (1, 0)
  rw [fderiv_comp]
  simp only [toTimeAndSpace_fderiv, Fin.isValue, ContinuousLinearMap.coe_comp',
    ContinuousLinearEquiv.coe_coe, Function.comp_apply, smul_eq_mul]
  rw [toTimeAndSpace_basis_inl']
  rw [map_smul]
  simp only [one_div, smul_eq_mul]
  ¬∑ apply Differentiable.differentiableAt
    exact SchwartzMap.differentiable Œ∫
  ¬∑ fun_prop

lemma distDeriv_inl_distTimeSlice_symm {M d} [NormedAddCommGroup M] [NormedSpace ‚Ñù M]
    {c : SpeedOfLight}
    (f : (Time √ó Space d) ‚Üíd[‚Ñù] M) :
    distDeriv (Sum.inl 0) ((distTimeSlice c).symm f) =
    (1/c.val) ‚Ä¢ (distTimeSlice c).symm (Space.distTimeDeriv f) := by
  obtain ‚ü®f, rfl‚ü© := (distTimeSlice c).surjective f
  simp only [ContinuousLinearEquiv.symm_apply_apply]
  apply (distTimeSlice c).injective
  simp only [Fin.isValue, one_div, map_smul, ContinuousLinearEquiv.apply_symm_apply]
  rw [distTimeSlice_distDeriv_inl]
  simp

lemma distTimeSlice_distDeriv_inr {M d} [NormedAddCommGroup M] [NormedSpace ‚Ñù M]
    {c : SpeedOfLight}
    (i : Fin d) (f : (SpaceTime d) ‚Üíd[‚Ñù] M) :
    distTimeSlice c (distDeriv (Sum.inr i) f) =
    Space.distSpaceDeriv i (distTimeSlice c f) := by
  ext Œ∫
  rw [distTimeSlice_apply, distDeriv_apply, fderivD_apply]
  rw [distSpaceDeriv_apply, fderivD_apply, distTimeSlice_apply]
  simp only [neg_inj]
  congr 1
  ext x
  change fderiv ‚Ñù (Œ∫ ‚àò toTimeAndSpace c) x (Lorentz.Vector.basis (Sum.inr i)) =
    fderiv ‚Ñù Œ∫ (toTimeAndSpace c x) (0, Space.basis i)
  rw [fderiv_comp]
  simp only [toTimeAndSpace_fderiv, ContinuousLinearMap.coe_comp', ContinuousLinearEquiv.coe_coe,
    Function.comp_apply]
  rw [toTimeAndSpace_basis_inr]
  ¬∑ apply Differentiable.differentiableAt
    exact SchwartzMap.differentiable Œ∫
  ¬∑ fun_prop

lemma distDeriv_inr_distTimeSlice_symm {M d} [NormedAddCommGroup M] [NormedSpace ‚Ñù M]
    {c : SpeedOfLight}
    (i : Fin d) (f : (Time √ó Space d) ‚Üíd[‚Ñù] M) :
    distDeriv (Sum.inr i) ((distTimeSlice c).symm f) =
    (distTimeSlice c).symm (Space.distSpaceDeriv i f) := by
  obtain ‚ü®f, rfl‚ü© := (distTimeSlice c).surjective f
  simp only [ContinuousLinearEquiv.symm_apply_apply]
  apply (distTimeSlice c).injective
  simp only [ContinuousLinearEquiv.apply_symm_apply]
  rw [distTimeSlice_distDeriv_inr]

end SpaceTime

end
