/-
Copyright (c) 2025 Joseph Tooby-Smith. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Zhi Kai Pong, Joseph Tooby-Smith, Lode Vermeulen
-/
import PhysLean.SpaceAndTime.Space.Derivatives.Laplacian
/-!

# Curl on Space

## i. Overview

In this module we define the curl of functions and distributions on 3-dimensional
space `Space 3`.

We also prove some basic vector-identities involving of the curl operator.

## ii. Key results

- `curl` : The curl operator on functions from `Space 3` to `EuclideanSpace â„ (Fin 3)`.
- `distCurl` : The curl operator on distributions from `Space 3` to `EuclideanSpace â„ (Fin 3)`.
- `div_of_curl_eq_zero` : The divergence of the curl of a function is zero.
- `distCurl_distGrad_eq_zero` : The curl of the gradient of a distribution is zero.

## iii. Table of contents

- A. The curl on functions
  - A.1. The curl on the zero function
  - A.2. The curl on a constant function
  - A.3. The curl distributes over addition
  - A.4. The curl distributes over scalar multiplication
  - A.5. The curl of a linear map is a linear map
  - A.6. Preliminary lemmas about second derivatives
  - A.7. The div of a curl is zero
  - A.8. The curl of a curl
- B. The curl on distributions
  - B.1. The components of the curl
  - B.2. Basic equalities
  - B.3. The curl of a grad is zero

## iv. References

-/

namespace Space

/-!

## A. The curl on functions

-/

/-- The vector calculus operator `curl`. -/
noncomputable def curl (f : Space â†’ EuclideanSpace â„ (Fin 3)) :
    Space â†’ EuclideanSpace â„ (Fin 3) := fun x =>
  -- get i-th component of `f`
  let fi i x := coord i (f x)
  -- derivative of i-th component in j-th coordinate
  -- âˆ‚fáµ¢/âˆ‚xâ±¼
  let df i j x := âˆ‚[j] (fi i) x
  WithLp.toLp 2 fun i =>
    match i with
    | 0 => df 2 1 x - df 1 2 x
    | 1 => df 0 2 x - df 2 0 x
    | 2 => df 1 0 x - df 0 1 x

@[inherit_doc curl]
macro (name := curlNotation) "âˆ‡" "Ã—" f:term:100 : term => `(curl $f)

/-!

### A.1. The curl on the zero function

-/

@[simp]
lemma curl_zero : âˆ‡ Ã— (0 : Space â†’ EuclideanSpace â„ (Fin 3)) = 0 := by
  unfold curl Space.deriv
  simp only [Fin.isValue, Pi.ofNat_apply, fderiv_fun_const, ContinuousLinearMap.zero_apply,
    sub_self]
  ext x i
  fin_cases i <;>
  rfl

/-!

### A.2. The curl on a constant function

-/

@[simp]
lemma curl_const : âˆ‡ Ã— (fun _ : Space => vâ‚ƒ) = 0 := by
  unfold curl Space.deriv
  simp only [Fin.isValue, fderiv_fun_const, Pi.ofNat_apply, ContinuousLinearMap.zero_apply,
    sub_self]
  ext x i
  fin_cases i <;>
  rfl

/-!

### A.3. The curl distributes over addition

-/

lemma curl_add (f1 f2 : Space â†’ EuclideanSpace â„ (Fin 3))
    (hf1 : Differentiable â„ f1) (hf2 : Differentiable â„ f2) :
    âˆ‡ Ã— (f1 + f2) = âˆ‡ Ã— f1 + âˆ‡ Ã— f2 := by
  unfold curl coord basis
  ext x i
  fin_cases i <;>
  Â· simp only [Fin.isValue, Pi.add_apply, EuclideanSpace.basisFun_apply, PiLp.inner_apply,
    PiLp.add_apply, EuclideanSpace.single_apply, RCLike.inner_apply, conj_trivial, ite_mul, one_mul,
    zero_mul, Finset.sum_ite_eq', Finset.mem_univ, â†“reduceIte, Fin.zero_eta]
    repeat rw [deriv_coord_add]
    simp only [Fin.isValue, Pi.add_apply]
    ring
    repeat assumption

/-!

### A.4. The curl distributes over scalar multiplication

-/

lemma curl_smul (f : Space â†’ EuclideanSpace â„ (Fin 3)) (k : â„)
    (hf : Differentiable â„ f) :
    âˆ‡ Ã— (k â€¢ f) = k â€¢ âˆ‡ Ã— f := by
  unfold curl coord basis
  ext x i
  fin_cases i <;>
  Â· simp only [Fin.isValue, Pi.smul_apply, EuclideanSpace.basisFun_apply, PiLp.inner_apply,
    PiLp.smul_apply, smul_eq_mul, EuclideanSpace.single_apply, RCLike.inner_apply, conj_trivial,
    ite_mul, one_mul, zero_mul, Finset.sum_ite_eq', Finset.mem_univ, â†“reduceIte, Fin.zero_eta]
    rw [deriv_coord_smul, deriv_coord_smul, mul_sub]
    simp only [Fin.isValue, Pi.smul_apply, smul_eq_mul]
    repeat fun_prop

/-!

### A.5. The curl of a linear map is a linear map

-/

variable {W} [NormedAddCommGroup W] [NormedSpace â„ W]

lemma curl_linear_map (f : W â†’ Space 3 â†’ EuclideanSpace â„ (Fin 3))
    (hf : âˆ€ w, Differentiable â„ (f w))
    (hf' : IsLinearMap â„ f) :
    IsLinearMap â„ (fun w => âˆ‡ Ã— (f w)) := by
  constructor
  Â· intro w w'
    rw [hf'.map_add]
    rw [curl_add]
    repeat fun_prop
  Â· intros k w
    rw [hf'.map_smul]
    rw [curl_smul]
    fun_prop

/-!

### A.6. Preliminary lemmas about second derivatives

-/

/-- Second derivatives distribute coordinate-wise over addition (all three components for div). -/
lemma deriv_coord_2nd_add (f : Space â†’ EuclideanSpace â„ (Fin 3)) (hf : ContDiff â„ 2 f) :
    âˆ‚[i] (fun x => âˆ‚[u] (fun x => f x u) x + (âˆ‚[v] (fun x => f x v) x + âˆ‚[w] (fun x => f x w) x)) =
    (âˆ‚[i] (âˆ‚[u] (fun x => f x u))) + (âˆ‚[i] (âˆ‚[v] (fun x => f x v))) +
    (âˆ‚[i] (âˆ‚[w] (fun x => f x w))) := by
  unfold deriv
  ext x
  rw [fderiv_fun_add, fderiv_fun_add]
  simp only [ContinuousLinearMap.add_apply, Pi.add_apply]
  ring
  repeat fun_prop

/-- Second derivatives distribute coordinate-wise over subtraction (two components for curl). -/
lemma deriv_coord_2nd_sub (f : Space â†’ EuclideanSpace â„ (Fin 3)) (hf : ContDiff â„ 2 f) :
    âˆ‚[u] (fun x => âˆ‚[v] (fun x => f x w) x - âˆ‚[w] (fun x => f x v) x) =
    (âˆ‚[u] (âˆ‚[v] (fun x => f x w))) - (âˆ‚[u] (âˆ‚[w] (fun x => f x v))) := by
  unfold deriv
  ext x
  simp only [Pi.sub_apply]
  rw [fderiv_fun_sub]
  simp only [ContinuousLinearMap.coe_sub', Pi.sub_apply]
  repeat fun_prop

/-!

### A.7. The div of a curl is zero

-/

lemma div_of_curl_eq_zero (f : Space â†’ EuclideanSpace â„ (Fin 3)) (hf : ContDiff â„ 2 f) :
    âˆ‡ â¬ (âˆ‡ Ã— f) = 0 := by
  unfold div curl Finset.sum coord basis
  ext x
  simp only [Fin.isValue, EuclideanSpace.basisFun_apply, PiLp.inner_apply,
    EuclideanSpace.single_apply, RCLike.inner_apply, conj_trivial, ite_mul, one_mul, zero_mul,
    Finset.sum_ite_eq', Finset.mem_univ, â†“reduceIte, Fin.univ_val_map, List.ofFn_succ,
    Fin.succ_zero_eq_one, Fin.succ_one_eq_two, List.ofFn_zero, Multiset.sum_coe, List.sum_cons,
    List.sum_nil, add_zero, Pi.zero_apply]
  rw [deriv_coord_2nd_sub, deriv_coord_2nd_sub, deriv_coord_2nd_sub]
  simp only [Fin.isValue, Pi.sub_apply]
  rw [deriv_commute fun x => f x 0, deriv_commute fun x => f x 1,
    deriv_commute fun x => f x 2]
  simp only [Fin.isValue, sub_add_sub_cancel', sub_self]
  repeat
    try apply contDiff_euclidean.mp
    exact hf

/-!

### A.8. The curl of a curl

-/

lemma curl_of_curl (f : Space â†’ EuclideanSpace â„ (Fin 3)) (hf : ContDiff â„ 2 f) :
    âˆ‡ Ã— (âˆ‡ Ã— f) = âˆ‡ (âˆ‡ â¬ f) - Î” f := by
  unfold laplacianVec laplacian div grad curl Finset.sum coord basis
  simp only [Fin.isValue, EuclideanSpace.basisFun_apply, PiLp.inner_apply,
    EuclideanSpace.single_apply, RCLike.inner_apply, conj_trivial, ite_mul, one_mul, zero_mul,
    Finset.sum_ite_eq', Finset.mem_univ, â†“reduceIte, Fin.univ_val_map, List.ofFn_succ,
    Fin.succ_zero_eq_one, Fin.succ_one_eq_two, List.ofFn_zero, Multiset.sum_coe, List.sum_cons,
    List.sum_nil, add_zero]
  ext x i
  fin_cases i <;>
  Â· simp only [Fin.isValue, Fin.reduceFinMk, Pi.sub_apply]
    rw [deriv_coord_2nd_sub, deriv_coord_2nd_sub]
    simp only [Fin.isValue, Pi.sub_apply, PiLp.sub_apply]
    rw [deriv_coord_2nd_add]
    rw [deriv_commute fun x => f x 0, deriv_commute fun x => f x 1,
      deriv_commute fun x => f x 2]
    simp only [Fin.isValue, Pi.add_apply]
    ring
    repeat
      try apply contDiff_euclidean.mp
      exact hf

/-!

## B. The curl on distributions

-/

open MeasureTheory SchwartzMap InnerProductSpace Distribution

/-- The curl of a distribution `Space â†’d[â„] (EuclideanSpace â„ (Fin 3))` as a distribution
  `Space â†’d[â„] (EuclideanSpace â„ (Fin 3))`. -/
noncomputable def distCurl : (Space â†’d[â„] (EuclideanSpace â„ (Fin 3))) â†’â‚—[â„]
    (Space) â†’d[â„] (EuclideanSpace â„ (Fin 3)) where
  toFun f :=
    let curl : (Space â†’L[â„] (EuclideanSpace â„ (Fin 3))) â†’L[â„] (EuclideanSpace â„ (Fin 3)) := {
      toFun dfdx:= WithLp.toLp 2 fun i =>
        match i with
        | 0 => dfdx (basis 2) 1 - dfdx (basis 1) 2
        | 1 => dfdx (basis 0) 2 - dfdx (basis 2) 0
        | 2 => dfdx (basis 1) 0 - dfdx (basis 0) 1
      map_add' v1 v2 := by
        ext i
        fin_cases i
        all_goals
          simp only [Fin.isValue, ContinuousLinearMap.add_apply, PiLp.add_apply, Fin.zero_eta]
          ring
      map_smul' a v := by
        ext i
        fin_cases i
        all_goals
          simp only [Fin.isValue, ContinuousLinearMap.coe_smul', Pi.smul_apply, PiLp.smul_apply,
            smul_eq_mul, RingHom.id_apply, Fin.reduceFinMk]
          ring
      cont := by
        apply Continuous.comp
        Â· fun_prop
        rw [continuous_pi_iff]
        intro i
        fin_cases i
        all_goals
          fun_prop
        }
    curl.comp (Distribution.fderivD â„ f)
  map_add' f1 f2 := by
    ext x
    simp
  map_smul' a f := by
    ext x
    simp

/-!

### B.1. The components of the curl

-/

lemma distCurl_apply_zero (f : Space â†’d[â„] (EuclideanSpace â„ (Fin 3))) (Î· : ğ“¢(Space, â„)) :
    distCurl f Î· 0 = - f (SchwartzMap.evalCLM (ğ•œ := â„) (basis 2) (fderivCLM â„ Î·)) 1
    + f (SchwartzMap.evalCLM (ğ•œ := â„) (basis 1) (fderivCLM â„ Î·)) 2 := by
  simp [distCurl]
  rw [fderivD_apply, fderivD_apply]
  simp

lemma distCurl_apply_one (f : Space â†’d[â„] (EuclideanSpace â„ (Fin 3))) (Î· : ğ“¢(Space, â„)) :
    distCurl f Î· 1 = - f (SchwartzMap.evalCLM (ğ•œ := â„) (basis 0) (fderivCLM â„ Î·)) 2
    + f (SchwartzMap.evalCLM (ğ•œ := â„) (basis 2) (fderivCLM â„ Î·)) 0 := by
  simp [distCurl]
  rw [fderivD_apply, fderivD_apply]
  simp

lemma distCurl_apply_two (f : Space â†’d[â„] (EuclideanSpace â„ (Fin 3))) (Î· : ğ“¢(Space, â„)) :
    distCurl f Î· 2 = - f (SchwartzMap.evalCLM (ğ•œ := â„) (basis 1) (fderivCLM â„ Î·)) 0
    + f (SchwartzMap.evalCLM (ğ•œ := â„) (basis 0) (fderivCLM â„ Î·)) 1 := by
  simp [distCurl]
  rw [fderivD_apply, fderivD_apply]
  simp

/-!

### B.2. Basic equalities

-/

lemma distCurl_apply (f : Space â†’d[â„] (EuclideanSpace â„ (Fin 3))) (Î· : ğ“¢(Space, â„)) :
    distCurl f Î· = WithLp.toLp 2 fun
    | 0 => - f (SchwartzMap.evalCLM (ğ•œ := â„) (basis 2) (fderivCLM â„ Î·)) 1
      + f (SchwartzMap.evalCLM (ğ•œ := â„) (basis 1) (fderivCLM â„ Î·)) 2
    | 1 => - f (SchwartzMap.evalCLM (ğ•œ := â„) (basis 0) (fderivCLM â„ Î·)) 2
      + f (SchwartzMap.evalCLM (ğ•œ := â„) (basis 2) (fderivCLM â„ Î·)) 0
    | 2 => - f (SchwartzMap.evalCLM (ğ•œ := â„) (basis 1) (fderivCLM â„ Î·)) 0
      + f (SchwartzMap.evalCLM (ğ•œ := â„) (basis 0) (fderivCLM â„ Î·)) 1 := by
  ext i
  fin_cases i
  Â· simp [distCurl_apply_zero]
  Â· simp [distCurl_apply_one]
  Â· simp [distCurl_apply_two]

/-!

### B.3. The curl of a grad is zero

-/

/-- The curl of a grad is equal to zero. -/
@[simp]
lemma distCurl_distGrad_eq_zero (f : (Space) â†’d[â„] â„) :
    distCurl (distGrad f) = 0 := by
  ext Î· i
  fin_cases i
  all_goals
  Â· dsimp
    try rw [distCurl_apply_zero]
    try rw [distCurl_apply_one]
    try rw [distCurl_apply_two]
    rw [distGrad_eq_sum_basis, distGrad_eq_sum_basis]
    simp [basis_apply]
    rw [â† map_neg, â† map_add, â† ContinuousLinearMap.map_zero f]
    congr
    ext x
    simp only [Fin.isValue, add_apply, zero_apply]
    rw [schwartMap_fderiv_comm]
    change ((SchwartzMap.evalCLM (ğ•œ := â„) _)
      ((fderivCLM â„) ((SchwartzMap.evalCLM (ğ•œ := â„) _) ((fderivCLM â„) Î·)))) x +
      - ((SchwartzMap.evalCLM (ğ•œ := â„) _)
        ((fderivCLM â„) ((SchwartzMap.evalCLM (ğ•œ := â„) _) ((fderivCLM â„) Î·)))) x = _
    simp

end Space
