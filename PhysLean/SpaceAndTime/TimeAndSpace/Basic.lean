/-
Copyright (c) 2025 Joseph Tooby-Smith. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Zhi Kai Pong, Joseph Tooby-Smith
-/
import PhysLean.SpaceAndTime.Space.Derivatives.Curl
/-!

# Functions and distributions on Time and Space d

## i. Overview

In this module we define and prove basic lemmas about derivatives of functions and
distributions on both `Time` and `Space d`.

We put these results in the namespace `Space` by convention.

## ii. Key results

- `distTimeDeriv` : The derivative of a distribution on `Time Ã— Space d` along the
  temporal coordinate.
- `distSpaceDeriv` : The derivative of a distribution on `Time Ã— Space d` along the
  spatial `i` coordinate.
- `distSpaceGrad` : The spatial gradient of a distribution on `Time Ã— Space d`.
- `distSpaceDiv` : The spatial divergence of a distribution on `Time Ã— Space d`.
- `distSpaceCurl` : The spatial curl of a distribution on `Time Ã— Space 3`.

## iii. Table of contents

- A. Derivatives involving time and space
  - A.1. Space and time derivatives in terms of curried functions
  - A.2. Commuting time and space derivatives
  - A.3. Differentiablity conditions
  - A.4. Time derivative commute with curl
  - A.5. Constant of time deriative and space derivatives zero
  - A.6. Equal up to a constant of time and space derivatives equal
- B. Derivatives of distributions on Time Ã— Space d
  - B.1. Time derivatives
  - B.2. Space derivatives
    - B.2.1. Space derivatives commute
  - B.3. Time and space derivatives commute
  - B.4. The spatial gradient
  - B.5. The spatial divergence
  - B.6. The spatial curl

## iv. References

-/
namespace Space

/-!

## A. Derivatives involving time and space

-/

/-!

### A.1. Space and time derivatives in terms of curried functions

-/

lemma fderiv_space_eq_fderiv_curry {M} [NormedAddCommGroup M] [NormedSpace â„ M]
    (f : Time â†’ Space d â†’ M) (t : Time) (x dx : Space d)
    (hf : Differentiable â„ â†¿f) :
    fderiv â„ (fun x' => f t x') x dx = fderiv â„ â†¿f (t, x) (0, dx) := by
  change fderiv â„ (â†¿f âˆ˜ fun x' => (t, x')) x dx = _
  rw [fderiv_comp]
  simp only [ContinuousLinearMap.coe_comp', Function.comp_apply]
  rw [DifferentiableAt.fderiv_prodMk]
  simp only [fderiv_fun_const, Pi.zero_apply, fderiv_id', ContinuousLinearMap.prod_apply,
    ContinuousLinearMap.zero_apply, ContinuousLinearMap.coe_id', id_eq]
  repeat' fun_prop

lemma fderiv_time_eq_fderiv_curry {M} [NormedAddCommGroup M] [NormedSpace â„ M]
    (f : Time â†’ Space d â†’ M) (t dt : Time) (x : Space d)
    (hf : Differentiable â„ â†¿f) :
    fderiv â„ (fun t' => f t' x) t dt = fderiv â„ â†¿f (t, x) (dt, 0) := by
  change fderiv â„ (â†¿f âˆ˜ fun t' => (t', x)) t dt = _
  rw [fderiv_comp]
  simp only [ContinuousLinearMap.coe_comp', Function.comp_apply]
  rw [DifferentiableAt.fderiv_prodMk]
  simp only [fderiv_id', fderiv_fun_const, Pi.zero_apply, ContinuousLinearMap.prod_apply,
    ContinuousLinearMap.coe_id', id_eq, ContinuousLinearMap.zero_apply]
  repeat' fun_prop

/-!

### A.2. Commuting time and space derivatives

-/

/-- Derivatives along space coordinates and time commute. -/
lemma fderiv_time_commute_fderiv_space {M} [NormedAddCommGroup M] [NormedSpace â„ M]
    (f : Time â†’ Space d â†’ M) (t dt : Time) (x dx : Space d)
    (hf : ContDiff â„ 2 â†¿f) :
    fderiv â„ (fun t' => fderiv â„ (fun x' => f t' x') x dx) t dt
    = fderiv â„ (fun x' => fderiv â„ (fun t' => f t' x') t dt) x dx := by
  trans fderiv â„ (fun t' => (fderiv â„ (â†¿f) (t', x) (0, dx))) t dt
  Â· congr
    funext t'
    apply fderiv_space_eq_fderiv_curry
    exact hf.differentiable (by simp)
  trans fderiv â„ (fun x => (fderiv â„ (â†¿f) x (0, dx))) (t, x) (dt, 0)
  Â· let f' : Time â†’ Space d â†’ M := fun t x => fderiv â„ (â†¿f) (t, x) (0, dx)
    change (fderiv â„ (fun t' => f' t' x) t) dt = _
    rw [fderiv_time_eq_fderiv_curry]
    rfl
    fun_prop
  symm
  trans fderiv â„ (fun x' => (fderiv â„ (â†¿f) (t, x') (dt, 0))) x dx
  Â· congr
    funext x'
    apply fderiv_time_eq_fderiv_curry
    exact hf.differentiable (by simp)
  trans fderiv â„ (fun t => (fderiv â„ (â†¿f) t (dt, 0))) (t, x) (0, dx)
  Â· let f'' : Time â†’ Space d â†’ M := fun t x => fderiv â„ (â†¿f) (t, x) (dt, 0)
    change (fderiv â„ (fun x' => f'' t x') x) dx = _
    rw [fderiv_space_eq_fderiv_curry]
    rfl
    fun_prop
  rw [fderiv_clm_apply, fderiv_clm_apply]
  simp only [fderiv_fun_const, Pi.ofNat_apply, ContinuousLinearMap.comp_zero, zero_add,
    ContinuousLinearMap.flip_apply]
  rw [IsSymmSndFDerivAt.eq]
  Â· apply ContDiffAt.isSymmSndFDerivAt
    apply ContDiff.contDiffAt
    exact hf
    simp
  repeat' fun_prop

lemma time_deriv_comm_space_deriv {d i} {M} [NormedAddCommGroup M] [NormedSpace â„ M]
    {f : Time â†’ Space d â†’ M} (hf : ContDiff â„ 2 â†¿f) (t : Time) (x : Space d) :
    Time.deriv (fun t' => Space.deriv i (f t') x) t
    = Space.deriv i (fun x' => Time.deriv (fun t' => f t' x') t) x := by
  simp only [Time.deriv_eq, Space.deriv_eq_fderiv_basis]
  exact fderiv_time_commute_fderiv_space f t 1 x (Space.basis i) hf

/-!

### A.3. Differentiablity conditions

-/

@[fun_prop]
lemma space_deriv_differentiable_time {d i} {M} [NormedAddCommGroup M] [NormedSpace â„ M]
    {f : Time â†’ Space d â†’ M} (hf : ContDiff â„ 2 â†¿f) (x : Space d) :
    Differentiable â„ (fun t => Space.deriv i (f t) x) := by
  conv =>
    enter [2, t];
    rw [Space.deriv_eq_fderiv_basis]
  apply Differentiable.clm_apply
  Â· have hdd : Differentiable â„ â†¿f := hf.differentiable (by simp)
    have h1 (t : Time) : fderiv â„ (fun x => f t x) x
      = fderiv â„ (â†¿f) (t, x) âˆ˜L (ContinuousLinearMap.inr â„ Time (Space d)) := by
      ext w
      simp only [ContinuousLinearMap.coe_comp', Function.comp_apply, ContinuousLinearMap.inr_apply]
      rw [â† fderiv_space_eq_fderiv_curry f t x w hdd]
    conv =>
      enter [2, y]
      change fderiv â„ (fun x => f y x) x
      rw [h1]
    fun_prop
  Â· fun_prop

@[fun_prop]
lemma time_deriv_differentiable_space {d } {M} [NormedAddCommGroup M] [NormedSpace â„ M]
    {f : Time â†’ Space d â†’ M} (hf : ContDiff â„ 2 â†¿f) (t : Time) :
    Differentiable â„ (fun x => Time.deriv (f Â· x) t) := by
  conv =>
    enter [2, x];
    rw [Time.deriv_eq]
  apply Differentiable.clm_apply
  Â· have hdd : Differentiable â„ â†¿f := hf.differentiable (by simp)
    have h1 (x : Space d) : fderiv â„ (fun t => f t x) t
      = fderiv â„ (â†¿f) (t, x) âˆ˜L (ContinuousLinearMap.inl â„ Time (Space d)) := by
      ext w
      simp only [ContinuousLinearMap.coe_comp', Function.comp_apply, ContinuousLinearMap.inl_apply]
      rw [â† fderiv_time_eq_fderiv_curry f t w x hdd]
    conv =>
      enter [2, t']
      change fderiv â„ (fun x => f x t') t
      rw [h1]
    fun_prop
  Â· fun_prop

@[fun_prop]
lemma curl_differentiable_time
    (fâ‚œ : Time â†’ Space â†’ EuclideanSpace â„ (Fin 3))
    (hf : ContDiff â„ 2 â†¿fâ‚œ) (x : Space) :
    Differentiable â„ (fun t => (âˆ‡ Ã— fâ‚œ t) x) := by
  rw [differentiable_euclidean]
  intro i
  fin_cases i
  all_goals
    simp [curl, Space.coord_apply]
    fun_prop

/-!

### A.4. Time derivative commute with curl

-/
open Time

/-- Curl and time derivative commute. -/
lemma time_deriv_curl_commute (fâ‚œ : Time â†’ Space â†’ EuclideanSpace â„ (Fin 3))
    (t : Time) (x : Space) (hf : ContDiff â„ 2 â†¿fâ‚œ) :
    âˆ‚â‚œ (fun t => (âˆ‡ Ã— fâ‚œ t) x) t = (âˆ‡ Ã— fun x => (âˆ‚â‚œ (fun t => fâ‚œ t x) t)) x:= by
  ext i
  rw [â† Time.deriv_euclid]
  Â· fin_cases i
    all_goals
    simp [curl, Space.coord_apply]
    rw [Time.deriv_eq]
    rw [fderiv_fun_sub]
    simp [â† Time.deriv_eq]
    rw [time_deriv_comm_space_deriv, time_deriv_comm_space_deriv]
    congr
    Â· funext x'
      rw [Time.deriv_euclid]
      have h1 := hf.differentiable (by simp)
      fun_prop
    Â· funext x'
      rw [Time.deriv_euclid]
      have h1 := hf.differentiable (by simp)
      fun_prop
    repeat' fun_prop
    Â· apply Differentiable.differentiableAt
      fun_prop
    Â· apply Differentiable.differentiableAt
      fun_prop
  Â· fun_prop

/-!

### A.5. Constant of time deriative and space derivatives zero

-/

lemma space_fun_of_time_deriv_eq_zero {d} {M} [NormedAddCommGroup M] [NormedSpace â„ M]
    {f : Time â†’ Space d â†’ M} (hf : Differentiable â„ â†¿f)
    (h : âˆ€ t x, âˆ‚â‚œ (f Â· x) t = 0) :
    âˆƒ (g : Space d â†’ M), âˆ€ t x, f t x = g x := by
  use fun x => f 0 x
  intro t x
  simp only
  change (fun t' => f t' x) t = (fun t' => f t' x) 0
  apply is_const_of_fderiv_eq_zero (f := fun t' => f t' x) (ð•œ := â„)
  Â· fun_prop
  intro t
  ext r
  simp only [ContinuousLinearMap.zero_apply]
  trans r.val â€¢ (fderiv â„ (fun t' => f t' x) t) 1
  Â· rw [â† map_smul]
    congr
    ext
    simp
  simp only [smul_eq_zero]
  right
  rw [â† h t x]
  rfl

lemma time_fun_of_space_deriv_eq_zero {d} {M} [NormedAddCommGroup M] [NormedSpace â„ M]
    {f : Time â†’ Space d â†’ M} (hf : Differentiable â„ â†¿f)
    (h : âˆ€ t x i, Space.deriv i (f t) x = 0) :
    âˆƒ (g : Time â†’ M), âˆ€ t x, f t x = g t := by
  use fun t => f t 0
  intro t x
  simp only
  change (fun x' => f t x') x = (fun x' => f t x') 0
  apply is_const_of_fderiv_eq_zero (f := fun x' => f t x') (ð•œ := â„)
  Â· fun_prop
  intro x
  have h1 : (fderiv â„ (fun x' => f t x') x).toLinearMap = 0 := by
    apply (Space.basis (d := d)).toBasis.ext
    intro i
    simp only [OrthonormalBasis.coe_toBasis, ContinuousLinearMap.coe_coe, LinearMap.zero_apply]
    rw [â† h t x i]
    rw [Space.deriv_eq_fderiv_basis]
  ext r
  change (fderiv â„ (fun x' => f t x') x).toLinearMap r = 0
  rw [h1]
  simp

lemma const_of_time_deriv_space_deriv_eq_zero {d} {M} [NormedAddCommGroup M] [NormedSpace â„ M]
    {f : Time â†’ Space d â†’ M} (hf : Differentiable â„ â†¿f)
    (hâ‚ : âˆ€ t x, âˆ‚â‚œ (f Â· x) t = 0)
    (hâ‚‚ : âˆ€ t x i, Space.deriv i (f t) x = 0) :
    âˆƒ (c : M), âˆ€ t x, f t x = c := by
  obtain âŸ¨g, hgâŸ© := space_fun_of_time_deriv_eq_zero hf hâ‚
  obtain âŸ¨k, hkâŸ© := time_fun_of_space_deriv_eq_zero hf hâ‚‚
  use g 0
  intro t x
  have h1 : âˆ€ t x, g x = k t := by
    intro t x
    rw [â† hg t x]
    rw [hk t x]
  rw [hk]
  rw [â† h1 t 0]

/-!

### A.6. Equal up to a constant of time and space derivatives equal

-/

lemma equal_up_to_const_of_deriv_eq {d} {M} [NormedAddCommGroup M] [NormedSpace â„ M]
    {f g : Time â†’ Space d â†’ M} (hf : Differentiable â„ â†¿f) (hg : Differentiable â„ â†¿g)
    (hâ‚ : âˆ€ t x, âˆ‚â‚œ (f Â· x) t = âˆ‚â‚œ (g Â· x) t)
    (hâ‚‚ : âˆ€ t x i, Space.deriv i (f t) x = Space.deriv i (g t) x) :
    âˆƒ (c : M), âˆ€ t x, f t x = g t x + c := by
  suffices h : âˆƒ c', âˆ€ t x, f t x - g t x = c' by
    obtain âŸ¨c', hc'âŸ© := h
    use c'
    intro t x
    rw [â† hc' t x]
    simp
  apply const_of_time_deriv_space_deriv_eq_zero
  Â· exact Differentiable.fun_sub hf hg
  Â· intro t x
    rw [Time.deriv_eq]
    rw [fderiv_fun_sub]
    simp [â† Time.deriv_eq, hâ‚]
    Â· fun_prop
    Â· fun_prop
  Â· intro t x i
    rw [Space.deriv_eq_fderiv_basis]
    rw [fderiv_fun_sub]
    simp [â† Space.deriv_eq_fderiv_basis, hâ‚‚]
    Â· fun_prop
    Â· fun_prop
/-!

## B. Derivatives of distributions on Time Ã— Space d

-/

open Distribution SchwartzMap

/-!

### B.1. Time derivatives

-/

/-- The time derivative of a distribution dependent on time and space. -/
noncomputable def distTimeDeriv {M d} [NormedAddCommGroup M] [NormedSpace â„ M] :
    ((Time Ã— Space d) â†’d[â„] M) â†’â‚—[â„] (Time Ã— Space d) â†’d[â„] M where
  toFun f :=
    let ev : ((Time Ã— Space d) â†’L[â„] M) â†’L[â„] M := {
      toFun v := v (1, 0)
      map_add' v1 v2 := by
        simp only [ContinuousLinearMap.add_apply]
      map_smul' a v := by
        simp
    }
    ev.comp (Distribution.fderivD â„ f)
  map_add' f1 f2 := by
    simp
  map_smul' a f := by simp

lemma distTimeDeriv_apply {M d} [NormedAddCommGroup M] [NormedSpace â„ M]
    (f : (Time Ã— Space d) â†’d[â„] M) (Îµ : ð“¢(Time Ã— Space d, â„)) :
    (distTimeDeriv f) Îµ = fderivD â„ f Îµ (1, 0) := by
  simp [distTimeDeriv]

/-!

### B.2. Space derivatives

-/

/-- The space derivative of a distribution dependent on time and space. -/
noncomputable def distSpaceDeriv {M d} [NormedAddCommGroup M] [NormedSpace â„ M]
    (i : Fin d) : ((Time Ã— Space d) â†’d[â„] M) â†’â‚—[â„] (Time Ã— Space d) â†’d[â„] M where
  toFun f :=
    let ev : (Time Ã— Space d â†’L[â„] M) â†’L[â„] M := {
      toFun v := v (0, basis i)
      map_add' v1 v2 := by
        simp only [ContinuousLinearMap.add_apply]
      map_smul' a v := by
        simp
    }
    ev.comp (Distribution.fderivD â„ f)
  map_add' f1 f2 := by
    simp
  map_smul' a f := by simp

lemma distSpaceDeriv_apply {M d} [NormedAddCommGroup M] [NormedSpace â„ M]
    (i : Fin d) (f : (Time Ã— Space d) â†’d[â„] M) (Îµ : ð“¢(Time Ã— Space d, â„)) :
    (distSpaceDeriv i f) Îµ = fderivD â„ f Îµ (0, basis i) := by
  simp [distSpaceDeriv]

/-!

#### B.2.1. Space derivatives commute

-/

lemma distSpaceDeriv_commute {M d} [NormedAddCommGroup M] [NormedSpace â„ M]
    (i j : Fin d) (f : (Time Ã— Space d) â†’d[â„] M) :
    distSpaceDeriv i (distSpaceDeriv j f) = distSpaceDeriv j (distSpaceDeriv i f) := by
  ext Îº
  rw [distSpaceDeriv_apply, distSpaceDeriv_apply, fderivD_apply, fderivD_apply]
  rw [distSpaceDeriv_apply, distSpaceDeriv_apply, fderivD_apply, fderivD_apply]
  simp only [neg_neg]
  congr 1
  ext x
  change fderiv â„ (fun x => fderiv â„ Îº x (0, basis i)) x (0, basis j) =
    fderiv â„ (fun x => fderiv â„ Îº x (0, basis j)) x (0, basis i)
  rw [fderiv_clm_apply, fderiv_clm_apply]
  simp only [fderiv_fun_const, Pi.ofNat_apply, ContinuousLinearMap.comp_zero, zero_add,
    ContinuousLinearMap.flip_apply]
  rw [IsSymmSndFDerivAt.eq]
  Â· apply ContDiffAt.isSymmSndFDerivAt
    apply ContDiff.contDiffAt
    exact smooth Îº âŠ¤
    simp only [minSmoothness_of_isRCLikeNormedField]
    exact ENat.LEInfty.out
  Â· have h1 := smooth Îº 2
    fun_prop
  Â· fun_prop
  Â· have h1 := smooth Îº 2
    fun_prop
  Â· fun_prop

/-!

### B.3. Time and space derivatives commute

-/

lemma distTimeDeriv_commute_distSpaceDeriv {M d} [NormedAddCommGroup M] [NormedSpace â„ M]
    (i : Fin d) (f : (Time Ã— Space d) â†’d[â„] M) :
    distTimeDeriv (distSpaceDeriv i f) = distSpaceDeriv i (distTimeDeriv f) := by
  ext Îº
  rw [distTimeDeriv_apply, distSpaceDeriv_apply, fderivD_apply, fderivD_apply]
  rw [distTimeDeriv_apply, distSpaceDeriv_apply, fderivD_apply, fderivD_apply]
  simp only [neg_neg]
  congr 1
  ext x
  change fderiv â„ (fun x => fderiv â„ Îº x (1, 0)) x (0, basis i) =
    fderiv â„ (fun x => fderiv â„ Îº x (0, basis i)) x (1, 0)
  rw [fderiv_clm_apply, fderiv_clm_apply]
  simp only [fderiv_fun_const, Pi.ofNat_apply, ContinuousLinearMap.comp_zero, zero_add,
    ContinuousLinearMap.flip_apply]
  rw [IsSymmSndFDerivAt.eq]
  Â· apply ContDiffAt.isSymmSndFDerivAt
    apply ContDiff.contDiffAt
    exact smooth Îº âŠ¤
    simp only [minSmoothness_of_isRCLikeNormedField]
    exact ENat.LEInfty.out
  Â· have h1 := smooth Îº 2
    fun_prop
  Â· fun_prop
  Â· have h1 := smooth Îº 2
    fun_prop
  Â· fun_prop

/-!

### B.4. The spatial gradient

-/

/-- The spatial gradient of a distribution dependent on time and space. -/
noncomputable def distSpaceGrad {d} :
    ((Time Ã— Space d) â†’d[â„] â„) â†’â‚—[â„] (Time Ã— Space d) â†’d[â„] (EuclideanSpace â„ (Fin d)) where
  toFun f := {
      toFun := fun Îµ => WithLp.toLp 2 fun i => distSpaceDeriv i f Îµ
      map_add' Îµ1 Îµ2 := by ext i; simp
      map_smul' a Îµ := by ext i; simp
      cont := by fun_prop}
  map_add' f1 f2 := by
    ext x
    simp
  map_smul' a f := by
    ext x
    simp

lemma distSpaceGrad_apply {d} (f : (Time Ã— Space d) â†’d[â„] â„) (Îµ : ð“¢(Time Ã— Space d, â„)) :
    distSpaceGrad f Îµ = fun i => distSpaceDeriv i f Îµ := by
  rfl

/-!

### B.5. The spatial divergence

-/

/-- The spatial divergence of a distribution dependent on time and space. -/
noncomputable def distSpaceDiv {d} :
    ((Time Ã— Space d) â†’d[â„] (EuclideanSpace â„ (Fin d))) â†’â‚—[â„] (Time Ã— Space d) â†’d[â„] â„ where
  toFun f := {
    toFun Îµ := âˆ‘ i, distSpaceDeriv i f Îµ i
    map_add' Îµ1 Îµ2 := by simp [Finset.sum_add_distrib]
    map_smul' a Îµ := by simp [Finset.mul_sum]
    cont := by fun_prop}
  map_add' f1 f2 := by
    ext x
    simp [Finset.sum_add_distrib]
  map_smul' a f := by
    ext x
    simp [Finset.mul_sum]

lemma distSpaceDiv_apply_eq_sum_distSpaceDeriv {d}
    (f : (Time Ã— Space d) â†’d[â„] EuclideanSpace â„ (Fin d)) (Î· : ð“¢(Time Ã—Space d, â„)) :
    distSpaceDiv f Î· = âˆ‘ i, distSpaceDeriv i f Î· i := by rfl

/-!

### B.6. The spatial curl

-/

/-- The curl of a distribution dependent on time and space. -/
noncomputable def distSpaceCurl : ((Time Ã— Space 3) â†’d[â„] (EuclideanSpace â„ (Fin 3))) â†’â‚—[â„]
    (Time Ã— Space 3) â†’d[â„] (EuclideanSpace â„ (Fin 3)) where
  toFun f :={
    toFun Îµ := WithLp.toLp 2 fun i =>
      match i with
      | 0 => distSpaceDeriv 2 f Îµ 1 - distSpaceDeriv 1 f Îµ 2
      | 1 => distSpaceDeriv 0 f Îµ 2 - distSpaceDeriv 2 f Îµ 0
      | 2 => distSpaceDeriv 1 f Îµ 0 - distSpaceDeriv 0 f Îµ 1
    map_add' Îµ1 Îµ2 := by
      ext i
      fin_cases i
      all_goals
        simp only [Fin.isValue, map_add, PiLp.add_apply, Fin.reduceFinMk]
        ring
    map_smul' a Îµ := by
      ext i
      fin_cases i
      all_goals
        simp only [Fin.isValue, map_smul, PiLp.smul_apply, smul_eq_mul, RingHom.id_apply,
          Fin.zero_eta]
        ring
    cont := by
      apply Continuous.comp
      Â· fun_prop
      rw [continuous_pi_iff]
      intro i
      fin_cases i <;> fun_prop
      }
  map_add' f1 f2 := by
    ext x i
    fin_cases i
    all_goals
      simp only [Fin.isValue, map_add, ContinuousLinearMap.add_apply, PiLp.add_apply, Fin.zero_eta,
        ContinuousLinearMap.coe_mk', LinearMap.coe_mk, AddHom.coe_mk]
      ring
  map_smul' a f := by
    ext x i
    fin_cases i
    all_goals
      simp only [Fin.isValue, map_smul, ContinuousLinearMap.coe_smul', Pi.smul_apply,
        PiLp.smul_apply, smul_eq_mul, Fin.reduceFinMk, ContinuousLinearMap.coe_mk',
        LinearMap.coe_mk, AddHom.coe_mk, RingHom.id_apply]
      ring

end Space
