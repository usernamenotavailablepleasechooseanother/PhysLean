/-
Copyright (c) 2025 Joseph Tooby-Smith. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Joseph Tooby-Smith
-/
import PhysLean.Electromagnetism.Electrostatics.Basic
import PhysLean.Electromagnetism.Kinematics.ElectricField
import PhysLean.Electromagnetism.Distributions.Potential
import PhysLean.SpaceAndTime.TimeAndSpace.ConstantTimeDist
import PhysLean.SpaceAndTime.Space.DistConst
import PhysLean.SpaceAndTime.Space.Norm
import PhysLean.Mathematics.Distribution.PowMul
/-!

# The electrostatics of a stationary point particle in 1d

## i. Overview

In this module we give the electromagnetic properties of a point particle
sitting at the origin in 1d space.

The electric field is given by the Heaviside step function, and the scalar potential
is given by a function proportional to the absolute value of the distance from the particle.

Note this currently has two versions of many of the results.
The ones not in the `DistElectromagneticPotential` namespace are old, and will slowly be
replaced.

## ii. Key results

- `oneDimPointParticleCurrentDensity` : The Lorentz current density of a point particle
  stationary at the origin of 1d space.
- `oneDimPointParticle` : The electromagnetic potential of a point particle
  stationary at the origin of 1d space.
- `oneDimPointParticle_gradLagrangian` : The variational gradient of the Lagrangian
  for a point particle stationary at the origin of 1d space is zero for the
  given electromagnetic potential. (i.e. Maxwell's equations are satisfied).

## iii. Table of contents

- A. The electromagnetic potential
- B. The Potentials
  - B.1. The electromagnetic potential
  - B.2. The vector potential is zero
  - B.3. The scalar potential
- C. The electric field
  - C.1. Time derivative of the electric field is zero
- D. Maxwell's equations
  - D.1. Gauss' law
  - D.2. The variational gradient of the Lagrangian is zero

## iv. References

-/

namespace Electromagnetism
open Distribution SchwartzMap
open Space StaticElectricField MeasureTheory

/-!

## A. The electromagnetic potential

-/

/-- The current density of of a point particle stationary at the origin
  of 1d space. -/
noncomputable def oneDimPointParticleCurrentDensity (q : ‚Ñù) : LorentzCurrentDensityD 1 :=
  LorentzCurrentDensityD.toComponents.symm fun Œº =>
  match Œº with
  | Sum.inl 0 => SpaceTime.distTimeSlice.symm <| constantTime (q ‚Ä¢ diracDelta ‚Ñù 0)
  | Sum.inr _ => 0

@[simp]
lemma oneDimPointParticleCurrentDensity_apply_inr_eq_zero (q : ‚Ñù) (i : Fin 1) :
    (oneDimPointParticleCurrentDensity q).toComponents (Sum.inr i) = 0 := by
  simp [oneDimPointParticleCurrentDensity]

/-!

## B. The Potentials

-/

/-!

### B.1. The electromagnetic potential

-/

/-- The electromagnetic potential of a point particle stationary at the origin
  of 1d space. -/
noncomputable def oneDimPointParticle (q : ‚Ñù) : ElectromagneticPotentialD 1 :=
  ElectromagneticPotentialD.toComponents.symm fun Œº =>
  match Œº with
  | Sum.inl 0 => SpaceTime.distTimeSlice.symm <| Space.constantTime
    (- distOfFunction (fun x => (q/(2)) ‚Ä¢ ‚Äñx‚Äñ)
    (by
      apply IsDistBounded.const_smul
      convert IsDistBounded.pow (n := 1) (by simp)
      simp))
  | Sum.inr i => 0

/-- The electromagnetic potential of a point particle stationary at the origin
  of 1d space. -/
noncomputable def DistElectromagneticPotential.oneDimPointParticle (q : ‚Ñù) :
    DistElectromagneticPotential 1 := SpaceTime.distTimeSlice.symm <| Space.constantTime <|
  distOfFunction (fun x => ((- q/(2)) * ‚Äñx‚Äñ) ‚Ä¢ Lorentz.Vector.basis (Sum.inl 0)) (by fun_prop)

/-

### B.2. The vector potential is zero

-/

@[simp]
lemma oneDimPointParticle_vectorPotential (q : ‚Ñù) :
    (oneDimPointParticle q).vectorPotential = 0 := by
  rw [Electromagnetism.ElectromagneticPotentialD.vectorPotential]
  ext i
  simp [oneDimPointParticle]

@[simp]
lemma DistElectromagneticPotential.oneDimPointParticle_vectorPotential(q : ‚Ñù) :
    (DistElectromagneticPotential.oneDimPointParticle q).vectorPotential 1 = 0 := by
  ext Œµ i
  simp [vectorPotential, Lorentz.Vector.spatialCLM,
    oneDimPointParticle, constantTime_apply, distOfFunction_vector_eval]

/-!

### B.3. The scalar potential

-/

lemma oneDimPointParticle_scalarPotential (q : ‚Ñù) :
    (oneDimPointParticle q).scalarPotential =
    Space.constantTime (- distOfFunction (fun x => (q/(2)) ‚Ä¢ ‚Äñx‚Äñ)
    (by
      apply IsDistBounded.const_smul
      convert IsDistBounded.pow (n := 1) (by simp)
      simp)) := by
  rw [Electromagnetism.ElectromagneticPotentialD.scalarPotential]
  ext x
  simp [oneDimPointParticle]

lemma DistElectromagneticPotential.oneDimPointParticle_scalarPotential (q : ‚Ñù) :
    (DistElectromagneticPotential.oneDimPointParticle q).scalarPotential 1 =
    Space.constantTime (distOfFunction (fun x => - (q/(2)) ‚Ä¢ ‚Äñx‚Äñ) (by fun_prop)) := by
  ext Œµ
  simp only [scalarPotential, Lorentz.Vector.temporalCLM, Fin.isValue, SpeedOfLight.val_one,
    one_smul, oneDimPointParticle, LinearMap.coe_mk, AddHom.coe_mk,
    ContinuousLinearEquiv.apply_symm_apply, ContinuousLinearMap.coe_comp',
    LinearMap.coe_toContinuousLinearMap', Function.comp_apply, constantTime_apply,
    distOfFunction_vector_eval, Lorentz.Vector.apply_smul, Lorentz.Vector.basis_apply, ‚ÜìreduceIte,
    mul_one, smul_eq_mul, neg_mul]
  congr
  funext x
  ring

/-!

## C. The electric field

-/

set_option maxHeartbeats 400000 in
lemma oneDimPointParticle_electricField_eq_heavisideStep (q : ‚Ñù) :
    (oneDimPointParticle q).electricField = constantTime (q ‚Ä¢
    ((heavisideStep 0).smulRight (basis 0) - (1 / (2 : ‚Ñù)) ‚Ä¢ distConst 1 (basis 0))) := by
  suffices hE : - Space.distGrad (- distOfFunction (fun x => (q/(2)) ‚Ä¢ ‚Äñx‚Äñ)
      (by
        apply IsDistBounded.const_smul
        convert IsDistBounded.pow (n := 1) (by simp)
        simp)) = ((q) ‚Ä¢ ((heavisideStep 0).smulRight (basis 0) -
      (1/(2 : ‚Ñù)) ‚Ä¢ distConst 1 (basis 0))) by
    rw [Electromagnetism.ElectromagneticPotentialD.electricField]
    simp only [LinearMap.coe_mk, AddHom.coe_mk, oneDimPointParticle_vectorPotential, map_zero,
      sub_zero, Nat.succ_eq_add_one, Nat.reduceAdd, Fin.isValue, one_div, map_smul, map_sub]
    rw [oneDimPointParticle_scalarPotential, constantTime_distSpaceGrad, ‚Üê map_neg, hE]
    simp
  /- Some preamble for results which are used throughout this proof. -/
  let s : Set (EuclideanSpace ‚Ñù (Fin 1)) :=
    {x : EuclideanSpace ‚Ñù (Fin 1) | 0 < x (Fin.last 0)}
  have hs : NullMeasurableSet s volume := by
    simp [s]
    refine nullMeasurableSet_lt ?_ ?_
    ¬∑ fun_prop
    ¬∑ change AEMeasurable oneEquivCLE volume
      fun_prop
  /- We are showing equality of two distributions of the from
    `(Space 1) ‚Üíd[‚Ñù] EuclideanSpace ‚Ñù (Fin 1)`. Two such distributions `f` and `g` are equal
    if and only if for all Schwartz maps Œ∑ `‚ü™f, Œ∑‚ü´ 0 = ‚ü™g, Œ∑‚ü´ 0` -/
  ext Œ∑ i
  fin_cases i
  calc _

    /- By the definition of the gradiant on distributions
      `-‚ü™‚àá (- q/(2 * Œµ) |x|), Œ∑‚ü´ 0 = - ‚ü™(-q/(2 * Œµ) |x|), -dŒ∑/dx‚ü´`
      which is equal to `- ‚ü™(q/(2 * Œµ) |x|), dŒ∑/dx‚ü´`.
      By definition of `(q/(2 * Œµ) |x|)` as a distribution this is equal to
      `- ‚à´ x, dŒ∑/dx ‚Ä¢ (q/(2 * Œµ) |x|)`.
    -/
    _ = - (‚à´ x, fderiv ‚Ñù Œ∑ x (basis 0) ‚Ä¢ (q/(2)) ‚Ä¢ ‚Äñx‚Äñ) := by
      simp only [Nat.succ_eq_add_one, Nat.reduceAdd, smul_eq_mul, map_neg, neg_neg, Fin.zero_eta,
        Fin.isValue, distGrad_eq_sum_basis, Finset.univ_unique, Fin.default_eq_zero, neg_smul,
        Finset.sum_neg_distrib, Finset.sum_singleton, PiLp.neg_apply, PiLp.smul_apply, basis_self,
        mul_one, neg_inj]
      rw [distOfFunction_apply]
      rfl
    /- Pulling out the scalar `q/(2 * Œµ)` gives
      `- ‚à´ x, dŒ∑/dx ‚Ä¢ (q/(2 * Œµ) |x|) = - q/(2 * Œµ) ‚à´ x, dŒ∑/dx ‚Ä¢ |x|`.
      With the bounds of the integral explicit this is
      `- q/(2 * Œµ) ‚à´_(-‚àû)^(‚àû) x, dŒ∑/dx ‚Ä¢ |x|`
    -/
    _ = - (q/(2)) * (‚à´ x, fderiv ‚Ñù Œ∑ x (basis 0) ‚Ä¢ ‚Äñx‚Äñ) := by
      rw [‚Üê integral_const_mul, ‚Üê integral_neg]
      congr
      funext x
      simp only [Fin.isValue, smul_eq_mul, neg_mul, neg_inj]
      ring
    /- We split the integral
      `- q/(2 * Œµ) ‚à´_(-‚àû)^(‚àû) x, dŒ∑/dx ‚Ä¢ |x|`
      into two halfs
      `- q/(2 * Œµ) ‚à´_0^(‚àû) x, dŒ∑/dx ‚Ä¢ |x| - q/(2 * Œµ) ‚à´_(-‚àû)^0 x, dŒ∑/dx ‚Ä¢ |x| `
    -/
    _ = - (q/(2)) * (‚à´ x in s, fderiv ‚Ñù Œ∑ x (basis 0) ‚Ä¢ ‚Äñx‚Äñ) +
        - (q/(2)) * (‚à´ x in s·∂ú, fderiv ‚Ñù Œ∑ x (basis 0) ‚Ä¢ ‚Äñx‚Äñ) := by
      rw [‚Üê integral_add_compl‚ÇÄ hs ?_]
      ¬∑ ring
      change Integrable (fun x : EuclideanSpace ‚Ñù (Fin 1) =>
        ((SchwartzMap.evalCLM (ùïú := ‚Ñù) (basis 0)) ((fderivCLM ‚Ñù) Œ∑)) x ‚Ä¢ ‚Äñx‚Äñ)
      apply IsDistBounded.integrable_space
      convert IsDistBounded.pow (n := 1) (by simp)
      simp
    /- In the first of these integrals `|x|=x` whilst in the second `|x| = -x` giving
      us
      `- q/(2 * Œµ) ‚à´_0^(‚àû) x, dŒ∑/dx ‚Ä¢ x - q/(2 * Œµ) ‚à´_(-‚àû)^0 x, dŒ∑/dx ‚Ä¢ (-x)` -/
    _ = - (q/(2)) * (‚à´ x in s, fderiv ‚Ñù Œ∑ x (basis 0) ‚Ä¢ x 0) +
        - (q/(2)) * (‚à´ x in s·∂ú, fderiv ‚Ñù Œ∑ x (basis 0) ‚Ä¢ (- x 0)) := by
      congr 2
      ¬∑ refine setIntegral_congr_ae‚ÇÄ hs ?_
        filter_upwards with x hx
        congr
        rw [@PiLp.norm_eq_of_L2]
        simp only [Finset.univ_unique, Fin.default_eq_zero, Fin.isValue, Real.norm_eq_abs, sq_abs,
          Finset.sum_singleton]
        refine Real.sqrt_eq_cases.mpr ?_
        left
        apply And.intro
        ¬∑ exact Eq.symm (Lean.Grind.Semiring.pow_two (x 0))
        ¬∑ simp [s] at hx
          apply le_of_lt hx
      ¬∑ refine setIntegral_congr_ae‚ÇÄ ?_ ?_
        ¬∑ simpa using hs
        filter_upwards with x hx
        congr
        rw [@PiLp.norm_eq_of_L2]
        simp only [Finset.univ_unique, Fin.default_eq_zero, Fin.isValue, Real.norm_eq_abs, sq_abs,
          Finset.sum_singleton]
        refine Real.sqrt_eq_cases.mpr ?_
        left
        simp only [Fin.isValue, mul_neg, neg_mul, neg_neg, Left.nonneg_neg_iff]
        apply And.intro
        ¬∑ exact Eq.symm (Lean.Grind.Semiring.pow_two (x 0))
        ¬∑ simp [s] at hx
          exact hx
    /- The next couple of steps are setting things up to use the
      result `MeasureTheory.integral_Ioi_of_hasDerivAt_of_tendsto`. -/
    /- So far our integral has really being over `Space 1` we now transorm it
      into an integral over `‚Ñù`, using `oneEquivCLE`.
      Here `(Œ∑.compCLMOfContinuousLinearEquiv ‚Ñù oneEquivCLE.symm)` is just `Œ∑` as
      a Schwartz map from `‚Ñù` rather then from `Space 1`.
      So symatically we have exactly the same thing as above
      `- q/(2 * Œµ) ‚à´_0^(‚àû) x, dŒ∑/dx ‚Ä¢ x - q/(2 * Œµ) ‚à´_(-‚àû)^0 x, dŒ∑/dx ‚Ä¢ (-x)`
      exacpt `x` is now `‚Ñù` rather then `Space 1`.
        -/
    _ = - (q/(2)) * (‚à´ x in Set.Ioi (0 : ‚Ñù),
        deriv (Œ∑.compCLMOfContinuousLinearEquiv ‚Ñù oneEquivCLE.symm) x * x) +
        - (q/(2)) * (‚à´ x in Set.Iic (0 : ‚Ñù),
        deriv (Œ∑.compCLMOfContinuousLinearEquiv ‚Ñù oneEquivCLE.symm) x * (-x)) := by
      rw [‚Üê oneEquiv_symm_measurePreserving.setIntegral_preimage_emb
        (oneEquiv_symm_measurableEmbedding)]
      rw [‚Üê oneEquiv_symm_measurePreserving.setIntegral_preimage_emb
        (oneEquiv_symm_measurableEmbedding)]
      congr 3
      ¬∑ simp only [Fin.isValue, smul_eq_mul, compCLMOfContinuousLinearEquiv_apply]
        funext x
        congr 1
        rw [‚Üê fderiv_deriv]
        rw [ContinuousLinearEquiv.comp_right_fderiv]
        simp only [Fin.isValue, ContinuousLinearMap.coe_comp', ContinuousLinearEquiv.coe_coe,
          Function.comp_apply]
        congr 1
        ext i
        fin_cases i
        simp only [Fin.isValue, Fin.zero_eta, basis_self, oneEquivCLE]
        rfl
      ¬∑ congr
        simp only [Fin.reduceLast, Fin.isValue, Set.preimage_compl, Set.preimage_setOf_eq, s]
        ext x
        simp [oneEquiv_symm_apply]
      ¬∑ simp only [Fin.isValue, smul_eq_mul, mul_neg, compCLMOfContinuousLinearEquiv_apply]
        funext x
        congr 1
        rw [‚Üê fderiv_deriv]
        rw [ContinuousLinearEquiv.comp_right_fderiv]
        simp only [Fin.isValue, ContinuousLinearMap.coe_comp', ContinuousLinearEquiv.coe_coe,
          Function.comp_apply]
        congr 2
        ext i
        fin_cases i
        simp only [Fin.isValue, Fin.zero_eta, basis_self, oneEquivCLE]
        rfl
        exact 2
    /- We use the fact that e.g. `(d(Œ∑ ‚Ä¢ x)/dx - Œ∑ x) = d Œ∑/dx ‚Ä¢ x` to rewrite
    `- q/(2 * Œµ) ‚à´_0^(‚àû) x, dŒ∑/dx ‚Ä¢ x - q/(2 * Œµ) ‚à´_(-‚àû)^0 x, dŒ∑/dx ‚Ä¢ (-x)`
    as
    `- q/(2 * Œµ) ‚à´_0^(‚àû) x, (d(Œ∑ ‚Ä¢ x)/dx - Œ∑ x) - q/(2 * Œµ) ‚à´_(-‚àû)^0 x, (d(Œ∑ ‚Ä¢ (-x))/dx + Œ∑ x)` -/
    _ = - (q/(2)) * (‚à´ x in Set.Ioi (0 : ‚Ñù),
        deriv (fun x => Œ∑.compCLMOfContinuousLinearEquiv ‚Ñù oneEquivCLE.symm x * (fun x => x) x) x
        - Œ∑.compCLMOfContinuousLinearEquiv ‚Ñù oneEquivCLE.symm x) +
        - (q/(2)) * (‚à´ x in Set.Iic (0 : ‚Ñù),
        deriv (fun x => Œ∑.compCLMOfContinuousLinearEquiv ‚Ñù oneEquivCLE.symm x * (fun x => - x) x) x
        + Œ∑.compCLMOfContinuousLinearEquiv ‚Ñù oneEquivCLE.symm x) := by
      congr
      ¬∑ funext x
        rw [deriv_fun_mul]
        simp only [compCLMOfContinuousLinearEquiv_apply, Function.comp_apply, deriv_id'', mul_one,
          add_sub_cancel_right]
        ¬∑ exact SchwartzMap.differentiableAt _
        ¬∑ fun_prop
      ¬∑ funext x
        rw [deriv_fun_mul]
        simp only [compCLMOfContinuousLinearEquiv_apply, mul_neg, Function.comp_apply, deriv_neg'',
          mul_one, neg_add_cancel_right]
        ¬∑ exact SchwartzMap.differentiableAt _
        ¬∑ fun_prop
    /- By definition of `powOneMul` we rewrite `Œ∑ ‚Ä¢ x` using `powOneMul`. Symatically we now have
      `- q/(2 * Œµ) ‚à´_0^(‚àû) x, (d(Œ∑ ‚Ä¢ x)/dx - Œ∑ x) - q/(2 * Œµ) ‚à´_(-‚àû)^0 x, (d(- (Œ∑ ‚Ä¢ x)))/dx + Œ∑ x)`
      things are just written in different ways. -/
    _ = - (q/(2)) * (‚à´ x in Set.Ioi (0 : ‚Ñù),
        deriv (powOneMul ‚Ñù (Œ∑.compCLMOfContinuousLinearEquiv ‚Ñù oneEquivCLE.symm)) x
        - Œ∑.compCLMOfContinuousLinearEquiv ‚Ñù oneEquivCLE.symm x) +
        - (q/(2)) * (‚à´ x in Set.Iic (0 : ‚Ñù),
        deriv (-powOneMul ‚Ñù (Œ∑.compCLMOfContinuousLinearEquiv ‚Ñù oneEquivCLE.symm)) x
        + Œ∑.compCLMOfContinuousLinearEquiv ‚Ñù oneEquivCLE.symm x) := by
      congr
      ¬∑ funext x
        congr
        funext x
        simp [powOneMul_apply]
        rw [mul_comm]
      ¬∑ funext x
        congr
        funext x
        change _ = - ((powOneMul ‚Ñù) ((compCLMOfContinuousLinearEquiv ‚Ñù oneEquivCLE.symm) Œ∑)) x
        simp [powOneMul_apply]
        rw [mul_comm]
    /- We seperate the integrals to get
      `- q/(2 * Œµ) (‚à´_0^(‚àû) x, d(Œ∑ ‚Ä¢ x)/dx - ‚à´_0^(‚àû) x, Œ∑ x) `
      `- q/(2 * Œµ) (‚à´_(-‚àû)^0 x, d(- (Œ∑ ‚Ä¢ x)))/dx + ‚à´_(-‚àû)^0 x, Œ∑ x)`. -/
    _ = - (q/(2)) * ((‚à´ x in Set.Ioi (0 : ‚Ñù),
        deriv (powOneMul ‚Ñù (Œ∑.compCLMOfContinuousLinearEquiv ‚Ñù oneEquivCLE.symm)) x)
        - ‚à´ x in Set.Ioi (0 : ‚Ñù), Œ∑.compCLMOfContinuousLinearEquiv ‚Ñù oneEquivCLE.symm x) +
        - (q/(2)) * ((‚à´ x in Set.Iic (0 : ‚Ñù),
        deriv (-powOneMul ‚Ñù (Œ∑.compCLMOfContinuousLinearEquiv ‚Ñù oneEquivCLE.symm)) x)
        + ‚à´ x in Set.Iic (0 : ‚Ñù), Œ∑.compCLMOfContinuousLinearEquiv ‚Ñù oneEquivCLE.symm x) := by
      rw [integral_sub, integral_add]
      ¬∑ refine Integrable.restrict ?_
        change Integrable (derivCLM ‚Ñù
          (-(powOneMul ‚Ñù) ((compCLMOfContinuousLinearEquiv ‚Ñù oneEquivCLE.symm) Œ∑))) volume
        exact integrable
            ((derivCLM ‚Ñù) (-(powOneMul ‚Ñù) ((compCLMOfContinuousLinearEquiv ‚Ñù oneEquivCLE.symm) Œ∑)))
      ¬∑ refine Integrable.restrict ?_
        exact integrable _
      ¬∑ refine Integrable.restrict ?_
        change Integrable (derivCLM ‚Ñù
          (powOneMul ‚Ñù ((compCLMOfContinuousLinearEquiv ‚Ñù oneEquivCLE.symm) Œ∑))) volume
        exact integrable
            ((derivCLM ‚Ñù) (powOneMul ‚Ñù ((compCLMOfContinuousLinearEquiv ‚Ñù oneEquivCLE.symm) Œ∑)))
      ¬∑ refine Integrable.restrict ?_
        exact integrable _
    /- We are now in a position to use `MeasureTheory.integral_Ioi_of_hasDerivAt_of_tendsto`
      which rewrites `‚à´_0^(‚àû) x, d(Œ∑ ‚Ä¢ x)/dx = 0 - (Œ∑ 0 ‚Ä¢ 0)`
      and `‚à´_(-‚àû)^0 x, d(- (Œ∑ ‚Ä¢ x)))/dx = (- Œ∑ 0 ‚Ä¢ 0) - 0`. This gives us
      `- q/(2 * Œµ) ((0 - (Œ∑ 0 ‚Ä¢ 0))- ‚à´_0^(‚àû) x, Œ∑ x)`
      `- q/(2 * Œµ) (((- Œ∑ 0 ‚Ä¢ 0) - 0)+ ‚à´_(-‚àû)^0 x, Œ∑ x)`. -/
    _ = - (q/(2)) * ((0 -
        (powOneMul ‚Ñù (Œ∑.compCLMOfContinuousLinearEquiv ‚Ñù oneEquivCLE.symm)) 0)
        - ‚à´ x in Set.Ioi (0 : ‚Ñù), Œ∑.compCLMOfContinuousLinearEquiv ‚Ñù oneEquivCLE.symm x) +
        - (q/(2)) *
        (((-powOneMul ‚Ñù (Œ∑.compCLMOfContinuousLinearEquiv ‚Ñù oneEquivCLE.symm)) 0 - 0)
        + ‚à´ x in Set.Iic (0 : ‚Ñù), Œ∑.compCLMOfContinuousLinearEquiv ‚Ñù oneEquivCLE.symm x) := by
      congr
      ¬∑ apply MeasureTheory.integral_Ioi_of_hasDerivAt_of_tendsto
        ¬∑ apply Continuous.continuousWithinAt
          fun_prop
        ¬∑ intro x hx
          refine DifferentiableAt.hasDerivAt ?_
          exact SchwartzMap.differentiableAt _
        ¬∑ apply Integrable.integrableOn
          change Integrable (derivCLM ‚Ñù ((powOneMul ‚Ñù)
            ((compCLMOfContinuousLinearEquiv ‚Ñù oneEquivCLE.symm) Œ∑))) volume
          exact integrable
            ((derivCLM ‚Ñù) ((powOneMul ‚Ñù) ((compCLMOfContinuousLinearEquiv ‚Ñù oneEquivCLE.symm) Œ∑)))
        ¬∑ exact Filter.Tendsto.mono_left ((powOneMul ‚Ñù)
            ((compCLMOfContinuousLinearEquiv ‚Ñù oneEquivCLE.symm) Œ∑)).toZeroAtInfty.zero_at_infty'
            atTop_le_cocompact
      ¬∑ apply MeasureTheory.integral_Iic_of_hasDerivAt_of_tendsto
        ¬∑ apply Continuous.continuousWithinAt
          fun_prop
        ¬∑ intro x hx
          refine DifferentiableAt.hasDerivAt ?_
          exact SchwartzMap.differentiableAt _
        ¬∑ apply Integrable.integrableOn
          change Integrable (derivCLM ‚Ñù (- (powOneMul ‚Ñù)
            ((compCLMOfContinuousLinearEquiv ‚Ñù oneEquivCLE.symm) Œ∑)))
          exact integrable
            ((derivCLM ‚Ñù) (- (powOneMul ‚Ñù) ((compCLMOfContinuousLinearEquiv ‚Ñù oneEquivCLE.symm) Œ∑)))
        ¬∑ apply Filter.Tendsto.mono_left
            ((- (powOneMul ‚Ñù)
            ((compCLMOfContinuousLinearEquiv ‚Ñù oneEquivCLE.symm) Œ∑)).toZeroAtInfty.zero_at_infty')
          exact atBot_le_cocompact
    /- We simplify the `(Œ∑ 0 ‚Ä¢ 0)` and `(- Œ∑ 0 ‚Ä¢ 0)` terms to be `0`. Giving us
    `- q/(2 * Œµ) ((0 - 0)- ‚à´_0^(‚àû) x, Œ∑ x)`
    `- q/(2 * Œµ) ((0 - 0)+ ‚à´_(-‚àû)^0 x, Œ∑ x)`. -/
    _ = - (q/(2)) * ((0 - 0)
        - ‚à´ x in Set.Ioi (0 : ‚Ñù), Œ∑.compCLMOfContinuousLinearEquiv ‚Ñù oneEquivCLE.symm x) +
        - (q/(2)) * ((0 - 0)
        + ‚à´ x in Set.Iic (0 : ‚Ñù), Œ∑.compCLMOfContinuousLinearEquiv ‚Ñù oneEquivCLE.symm x) := by
      congr
      ¬∑ simp [powOneMul_apply]
      ¬∑ change - ((powOneMul ‚Ñù) ((compCLMOfContinuousLinearEquiv ‚Ñù oneEquivCLE.symm) Œ∑)) 0 = 0
        simp [powOneMul_apply]
    /- Simplifying further gives
    `q/(2 * Œµ) ‚à´_0^(‚àû) x, Œ∑ x + - q/(2 * Œµ) ‚à´_(-‚àû)^0 x, Œ∑ x)`. -/
    _ = (q/(2)) *
        (‚à´ x in Set.Ioi (0 : ‚Ñù), Œ∑.compCLMOfContinuousLinearEquiv ‚Ñù oneEquivCLE.symm x) +
        - (q/(2)) *
          (‚à´ x in Set.Iic (0 : ‚Ñù), Œ∑.compCLMOfContinuousLinearEquiv ‚Ñù oneEquivCLE.symm x) := by
      simp
    /- We now turn back to integrals over `Space 1` instead of integrals over `x`.
    Schematically the integral remains the same.
    `q/(2 * Œµ) ‚à´_0^(‚àû) x, Œ∑ x + - q/(2 * Œµ) ‚à´_(-‚àû)^0 x, Œ∑ x)`. -/
    _ = (q/(2)) * (‚à´ x in s, Œ∑ x) + - (q/2) * (‚à´ x in s·∂ú, Œ∑ x) := by
      rw [‚Üê oneEquiv_symm_measurePreserving.setIntegral_preimage_emb
        (oneEquiv_symm_measurableEmbedding)]
      rw [‚Üê oneEquiv_symm_measurePreserving.setIntegral_preimage_emb
        (oneEquiv_symm_measurableEmbedding)]
      congr
      ext x
      simp [oneEquiv_symm_apply, s]
    /- We rewrite the second integral `‚à´_(-‚àû)^0 = ‚à´_(-‚àû)^‚àû - ‚à´_0^‚àû` to give
    `q/(2 * Œµ) ‚à´_0^(‚àû) x, Œ∑ x + - q/(2 * Œµ) (‚à´_(-‚àû)^‚àû x, Œ∑ x - ‚à´_0^‚àû x, Œ∑ x)`. -/
    _ = (q/(2)) * (‚à´ x in s, Œ∑ x) + - (q/(2)) * ((‚à´ x, Œ∑ x) - ‚à´ x in s, Œ∑ x) := by
      congr 2
      rw [‚Üê integral_add_compl‚ÇÄ hs]
      ¬∑ ring
      exact integrable Œ∑
    /- Simplifying we get:
    `q/(Œµ) ‚à´_0^(‚àû) x, Œ∑ x + - q/(2 * Œµ) ‚à´_(-‚àû)^‚àû x, Œ∑ x`. -/
    _ = (q) * (‚à´ x in s, Œ∑ x) + - (q/(2)) * (‚à´ x, Œ∑ x) := by
      ring
  /- Both sides are now essentially equal, by the definition of the heaviside step,
    and the constant distribution. What is left is some small tidying up. -/
  simp [mul_sub]
  congr 2
  rw [‚Üê mul_assoc]
  congr 1
  simp [distConst, const_apply]
  rw [integral_smul_const]
  simp

lemma DistElectromagneticPotential.oneDimPointParticle_electricField (q : ‚Ñù) :
    (DistElectromagneticPotential.oneDimPointParticle q).electricField 1 =
    (q / 2) ‚Ä¢ constantTime (distOfFunction (fun x : Space 1 => ‚Äñx‚Äñ ^ (- 1 : ‚Ñ§) ‚Ä¢ x)
      (IsDistBounded.zpow_smul_self (- 1 : ‚Ñ§) (by omega))) := by
  have h1 := Space.distGrad_distOfFunction_norm_zpow (d := 0) 1 (by grind)
  simp at h1
  simp only [electricField, LinearMap.coe_mk, AddHom.coe_mk, oneDimPointParticle_scalarPotential,
    smul_eq_mul, neg_mul, oneDimPointParticle_vectorPotential, map_zero, sub_zero, Int.reduceNeg,
    zpow_neg, zpow_one]
  rw [constantTime_distSpaceGrad, distOfFunction_neg, distOfFunction_mul_fun _ (by fun_prop)]
  simp only [map_neg, map_smul, neg_neg, h1]

/-!

### C.1. Time derivative of the electric field is zero

-/

@[simp]
lemma oneDimPointParticle_electricField_timeDeriv_eq_zero (q : ‚Ñù) :
    distTimeDeriv (oneDimPointParticle q).electricField = 0 := by
  rw [oneDimPointParticle_electricField_eq_heavisideStep]
  rw [constantTime_distTimeDeriv]

/-!

## D. Maxwell's equations

-/

/-!

### D.1. Gauss' law

-/

lemma oneDimPointParticle_gaussLaw (q : ‚Ñù) :
    distSpaceDiv (oneDimPointParticle q).electricField = constantTime (q ‚Ä¢ diracDelta ‚Ñù 0) := by
  ext Œ∑
  rw [oneDimPointParticle_electricField_eq_heavisideStep]
  rw [constantTime_distSpaceDiv]
  congr
  ext Œ∑
  change (distDiv ((q) ‚Ä¢ (ContinuousLinearMap.smulRight (heavisideStep 0) (basis 0) -
    (1 / 2) ‚Ä¢ distConst 1 (basis 0)))) Œ∑ = (q ‚Ä¢ diracDelta ‚Ñù 0) Œ∑
  haveI : SMulZeroClass ‚Ñù ((Space 1)‚Üíd[‚Ñù] ‚Ñù) := by infer_instance
  simp only [Nat.succ_eq_add_one, Nat.reduceAdd, Fin.isValue, one_div, map_smul, map_sub,
    distDiv_distConst, ContinuousLinearMap.coe_smul', ContinuousLinearMap.coe_sub', Pi.smul_apply,
    Pi.sub_apply, ContinuousLinearMap.zero_apply, smul_eq_mul, mul_zero, sub_zero, diracDelta_apply]
  field_simp
  congr 1
  rw [distDiv_apply_eq_sum_fderivD]
  simp only [Finset.univ_unique, Fin.default_eq_zero, Fin.isValue, Finset.sum_singleton]
  rw [fderivD_apply]
  simp only [Fin.isValue, ContinuousLinearMap.smulRight_apply, PiLp.neg_apply, PiLp.smul_apply,
    basis_self, smul_eq_mul, mul_one]
  rw [heavisideStep_apply]
  simp only [Nat.succ_eq_add_one, Nat.reduceAdd, Fin.reduceLast, Fin.isValue]
  rw [‚Üê MeasureTheory.MeasurePreserving.setIntegral_preimage_emb
    (Œº := volume) (ŒΩ := volume) (f := oneEquiv.symm)]
  simp only [Fin.isValue, Set.preimage_setOf_eq]
  let f' := SchwartzMap.compCLMOfContinuousLinearEquiv ‚Ñù oneEquivCLE.symm
    ((SchwartzMap.evalCLM (ùïú := ‚Ñù) (basis 0)) ((fderivCLM ‚Ñù) Œ∑))
  let f := SchwartzMap.compCLMOfContinuousLinearEquiv ‚Ñù oneEquivCLE.symm Œ∑
  change -‚à´ (x : ‚Ñù) in Set.Ioi 0, f' x = _
  rw [neg_eq_iff_eq_neg]
  trans 0 - f 0
  ¬∑ apply MeasureTheory.integral_Ioi_of_hasDerivAt_of_tendsto
      (f' := f')
      (f := f)
    ¬∑ apply Continuous.continuousWithinAt
      fun_prop
    ¬∑ have hf : f' = (SchwartzMap.derivCLM ‚Ñù) f := by
        ext x
        simp [f']
        change fderiv ‚Ñù Œ∑ (oneEquivCLE.symm x) (basis 0) = _
        trans fderiv ‚Ñù Œ∑ (oneEquivCLE.symm x) (oneEquivCLE.symm 1)
        ¬∑ congr 1
          ext i
          fin_cases i
          simp
          rfl
        rw [‚Üê fderiv_deriv]
        dsimp [f]
        rw [ContinuousLinearEquiv.comp_right_fderiv]
        rfl
      rw [hf]
      simpa using fun x hx => SchwartzMap.differentiableAt f
    ¬∑ exact (integrable f').integrableOn
    ¬∑ exact Filter.Tendsto.mono_left f.toZeroAtInfty.zero_at_infty' atTop_le_cocompact
  ¬∑ simp [f]
  ¬∑ exact oneEquiv_symm_measurePreserving
  ¬∑ exact oneEquiv_symm_measurableEmbedding

lemma DistElectromagneticPotential.oneDimPointParticle_gaussLaw (q : ‚Ñù) :
    distSpaceDiv ((DistElectromagneticPotential.oneDimPointParticle q).electricField 1) =
    constantTime (q ‚Ä¢ diracDelta ‚Ñù 0) := by
  rw [DistElectromagneticPotential.oneDimPointParticle_electricField]
  simp only [Int.reduceNeg, zpow_neg, zpow_one, map_smul]
  have h1 := Space.distDiv_inv_pow_eq_dim (d := 0)
  simp at h1
  rw [constantTime_distSpaceDiv, h1]
  simp only [map_smul]
  suffices h : volume.real (Metric.ball (0 : Space 1) 1) = 2 by
    rw [h]
    simp [smul_smul]
  simp [MeasureTheory.Measure.real]
  rw [InnerProductSpace.volume_ball_of_dim_odd (k := 0)]
  ¬∑ simp
  ¬∑ simp

/-!

### D.2. The variational gradient of the Lagrangian is zero

-/

lemma oneDimPointParticle_gradLagrangian (q : ‚Ñù) :
    (oneDimPointParticle q).gradLagrangian (oneDimPointParticleCurrentDensity q) = 0 := by
  rw [ElectromagneticPotentialD.gradLagrangian_one_dimension_electricField]
  funext Œº
  match Œº with
  | Sum.inl 0 =>
    simp [oneDimPointParticleCurrentDensity]
    rw [oneDimPointParticle_gaussLaw]
    simp
  | Sum.inr 0 =>
    simp

end Electromagnetism
