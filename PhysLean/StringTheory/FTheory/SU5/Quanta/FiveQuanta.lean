/-
Copyright (c) 2025 Joseph Tooby-Smith. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Joseph Tooby-Smith
-/
import PhysLean.Particles.SuperSymmetry.SU5.ChargeSpectrum.MinimallyAllowsTerm.OfFinset
import PhysLean.StringTheory.FTheory.SU5.Fluxes.NoExotics.Completeness
/-!

# Quanta of 5-d representations

## i. Overview

The 5-bar representations of the `SU(5)Ã—U(1)` carry
the quantum numbers of their `U(1)` charges and their fluxes.

In this module we define the data structure for these quanta and
properties thereof.

## ii. Key results

- `FiveQuanta` is the type of quanta of 5-bar representations.
- `FiveQuanta.toFluxesFive` is the underlying `FluxesFive` of a `FiveQuanta`.
- `FiveQuanta.toCharges` is the underlying Multiset charges of a `FiveQuanta`.
- `FiveQuanta.reduce` is the reduction of a `FiveQuanta` which adds together
  all the fluxes corresponding to the same charge (i.e. representation).
- `FiveQuanta.liftCharges` given a charge `c` the `FiveQuanta` which have
  charge `c` and no exotics or zero fluxes.
- `FiveQuanta.anomalyCoefficient` is the anomaly coefficient associated with a `FiveQuanta`.

## iii. Table of contents

- A. The definition of `FiveQuanta`
  - A.1. The map to underlying fluxes
  - A.2. The map to underlying charges
  - A.3. The map from charges to fluxes
- B. The reduction of a `FiveQuanta`
  - B.1. The reduced `FiveQuanta` has no duplicate elements
  - B.2. The underlying charges of the reduced `FiveQuanta` are the deduped charges
  - B.3. Membership condition on the reduced `FiveQuanta`
  - B.4. Filter of the reduced `FiveQuanta` by a charge
  - B.5. The reduction is idempotent
  - B.6. Preservation of certain sums under reduction
  - B.7. Reduction does nothing if no duplicate charges
  - B.8. The charge map is preserved by reduction
  - B.9. A fluxes in the reduced `FiveQuanta` is a sum of fluxes in the original `FiveQuanta`
  - B.10. No exotics condition on the reduced `FiveQuanta`
    - B.10.1. Number of chiral `L`
    - B.10.2. Number of anti-chiral `L`
    - B.10.3. Number of chiral `D`
    - B.10.4. Number of anti-chiral `D`
    - B.10.5. The `NoExotics` condition on the reduced `FiveQuanta`
  - B.11. Reduce member of `FluxesFive.elemsNoExotics`
- C. Decomposition of a `FiveQuanta` into basic fluxes
  - C.1. Decomposition of fluxes
  - C.2. Decomposition of a `FiveQuanta` (with no exotics)
    - C.2.1. Decomposition distributes over addition
    - C.2.2. Decomposition commutes with filtering charges
    - C.2.3. Decomposition preserves the charge map
    - C.2.4. Decomposition preserves the charges
    - C.2.5. Decomposition preserves the reduction
    - C.2.6. Fluxes of the decomposition of a `FiveQuanta`
- D. Lifting charges to `FiveQuanta`
  - D.1. `liftCharge c`: multiset of five-quanta for a finite set of charges `c` with no exotics
  - D.2. FiveQuanta in `liftCharge c` have a finite set of charges `c`
  - D.3. FiveQuanta in `liftCharge c` have no duplicate charges
  - D.4. Membership in `liftCharge c` iff is reduction of `FiveQuanta` with given fluxes
  - D.5. FiveQuanta in `liftCharge c` do not have zero fluxes
  - D.6. FiveQuanta in `liftCharge c` have no exotics
  - D.7. Membership in `liftCharge c` iff have no exotics, no zero fluxes, and charges `c`
  - D.8. `liftCharge c` is preserved under a map if reduced
- E. Anomaly cancellation coefficients
  - E.1. Anomaly coefficients of a `FiveQuanta`
  - E.2. Anomaly coefficients under a map
  - E.3. Anomaly coefficients is preserved under `reduce`

## iv. References

A reference for the anomaly cancellation conditions is arXiv:1401.5084.

-/
namespace FTheory

namespace SU5
open SU5

/-!

## A. The definition of `FiveQuanta`

-/

/-- The quanta of 5-bar representations corresponding to a multiset of
  `(q, M, N)` for each particle. `(M, N)` are defined in the `FluxesFive` module. -/
abbrev FiveQuanta (ð“© : Type := â„¤) : Type := Multiset (ð“© Ã— Fluxes)

namespace FiveQuanta

variable {ð“© : Type}

/-!

### A.1. The map to underlying fluxes

-/

/-- The underlying `FluxesFive` from a `FiveQuanta`. -/
def toFluxesFive (x : FiveQuanta ð“©) : FluxesFive := x.map Prod.snd

/-!

### A.2. The map to underlying charges

-/

/-- The underlying Multiset charges from a `FiveQuanta`. -/
def toCharges (x : FiveQuanta ð“©) : Multiset ð“© := x.map Prod.fst

/-!

### A.3. The map from charges to fluxes

-/

/-- The map which takes a charge to the overall flux it
  corresponds to in a `FiveQuanta`. -/
def toChargeMap [DecidableEq ð“©] (x : FiveQuanta ð“©) : ð“© â†’ Fluxes :=
  fun z => ((x.filter fun p => p.1 = z).map Prod.snd).sum

lemma toChargeMap_of_not_mem [DecidableEq ð“©] (x : FiveQuanta ð“©) {z : ð“©} (h : z âˆ‰ x.toCharges) :
    x.toChargeMap z = 0 := by
  simp [toChargeMap]
  have hl : (Multiset.filter (fun p => p.1 = z) x) = 0 := by
    simp only [Multiset.filter_eq_nil, Prod.forall]
    intro a b f
    by_contra hn
    subst hn
    simp [toCharges] at h
    exact h b f
  rw [hl]
  simp

/-!

## B. The reduction of a `FiveQuanta`

-/

section reduce

variable [DecidableEq ð“©]

/-- The `reduce` of `FiveQuanta` is a new `FiveQuanta` with all the fluxes
  corresponding to the same charge (i.e. representation) added together. -/
def reduce (x : FiveQuanta ð“©) : FiveQuanta ð“© :=
  x.toCharges.dedup.map fun q5 => (q5, ((x.filter (fun f => f.1 = q5)).map (fun y => y.2)).sum)

/-!

### B.1. The reduced `FiveQuanta` has no duplicate elements

-/

lemma reduce_nodup (x : FiveQuanta ð“©) : x.reduce.Nodup := by
  simp [reduce, toCharges]
  refine Multiset.Nodup.map ?_ ?_
  Â· intro q1 q2 h
    simp at h
    exact h.1
  Â· exact Multiset.nodup_dedup (Multiset.map Prod.fst x)

@[simp]
lemma reduce_dedup (x : FiveQuanta ð“©) : x.reduce.dedup = x.reduce :=
  Multiset.Nodup.dedup x.reduce_nodup

/-!

### B.2. The underlying charges of the reduced `FiveQuanta` are the deduped charges

-/

lemma reduce_toCharges (x : FiveQuanta ð“©) : x.reduce.toCharges = x.toCharges.dedup := by
  simp [reduce, toCharges]

/-!

### B.3. Membership condition on the reduced `FiveQuanta`

-/

lemma mem_reduce_iff (x : FiveQuanta ð“©) (p : ð“© Ã— Fluxes) :
    p âˆˆ x.reduce â†” p.1 âˆˆ x.toCharges âˆ§
      p.2 = ((x.filter (fun f => f.1 = p.1)).map (fun y => y.2)).sum := by
  simp [reduce]
  constructor
  Â· intro h
    obtain âŸ¨q, h1, rflâŸ© := h
    simp_all
  Â· simp
    intro h1 h2
    use p.1
    simp_all
    rw [â† h2]

/-!

### B.4. Filter of the reduced `FiveQuanta` by a charge

-/

lemma reduce_filter (x : FiveQuanta ð“©) (q : ð“©) (h : q âˆˆ x.toCharges) :
    x.reduce.filter (fun f => f.1 = q) =
    {(q, ((x.filter (fun f => f.1 = q)).map (fun y => y.2)).sum)} := by
  simp [reduce]
  rw [Multiset.filter_map]
  simp only [Function.comp_apply]
  have hx : (Multiset.filter (fun x => x = q) x.toCharges.dedup) = {q} := by
    refine (Multiset.Nodup.ext ?_ ?_).mpr ?_
    Â· refine Multiset.Nodup.filter (fun x => x = q) ?_
      exact Multiset.nodup_dedup x.toCharges
    Â· exact Multiset.nodup_singleton q
    intro a
    simp only [Multiset.mem_filter, Multiset.mem_dedup, Multiset.mem_singleton,
      and_iff_right_iff_imp]
    intro h'
    subst h'
    exact h
  rw [hx]
  simp

/-!

### B.5. The reduction is idempotent

-/

@[simp]
lemma reduce_reduce (x : FiveQuanta ð“©) :
    x.reduce.reduce = x.reduce := by
  refine Multiset.Nodup.toFinset_inj ?_ ?_ ?_
  Â· exact reduce_nodup x.reduce
  Â· exact reduce_nodup x
  ext p
  simp only [Multiset.mem_toFinset]
  rw [mem_reduce_iff, reduce_toCharges, mem_reduce_iff]
  simp only [Multiset.mem_dedup, and_congr_right_iff]
  intro hp
  have h1 (a b c : Fluxes) (h : b = c) : a = b â†” a = c := by subst h; rfl
  apply h1
  rw [reduce_filter]
  simp only [Multiset.map_singleton, Multiset.sum_singleton]
  exact hp

/-!

### B.6. Preservation of certain sums under reduction

-/

lemma reduce_sum_eq_sum_toCharges {M} [AddCommMonoid M] (x : FiveQuanta ð“©) (f : ð“© â†’ Fluxes â†’+ M) :
    (x.reduce.map fun (q5, x) => f q5 x).sum = (x.map fun (q5, x) => f q5 x).sum := by
  calc _
      _ = âˆ‘ q5 âˆˆ x.toCharges.toFinset,
          f q5 ((x.filter (fun f => f.1 = q5)).map (fun y => y.2)).sum := by
        rw [reduce]
        simp [Finset.sum]
      _ = âˆ‘ q5 âˆˆ x.toCharges.toFinset,
          (((x.filter (fun f => f.1 = q5)).map (fun y => f q5 y.2))).sum := by
        congr
        funext q5
        rw [AddMonoidHom.map_multiset_sum, Multiset.map_map]
        rfl
      _ = (x.toCharges.dedup.bind fun q5 =>
          ((x.filter (fun f => f.1 = q5)).map (fun y => f q5 y.2))).sum := by
        rw [Multiset.sum_bind]
        simp [Finset.sum]
      _ = (((x.toCharges.dedup.bind fun q5 =>
          ((x.filter (fun f => f.1 = q5)))).map (fun y => f y.1 y.2))).sum := by
        congr
        rw [Multiset.map_bind]
        congr
        funext q5
        refine Multiset.map_congr rfl ?_
        intro y hy
        simp at hy
        rw [hy.2]
      _ = ((x.map (fun y => f y.1 y.2))).sum := by
        congr
        apply Multiset.ext.mpr
        intro p
        trans ((x.map Prod.fst).dedup.map (fun y => if p.1 = y then x.count p else 0)).sum
        Â· rw [@Multiset.count_bind]
          congr
          funext q5
          rw [Multiset.count_filter]
        by_cases h_mem : p.1 âˆˆ x.map Prod.fst
        Â· have h_mem_dedup : p.1 âˆˆ (x.map Prod.fst).dedup := by rwa [Multiset.mem_dedup]
          rw [Multiset.sum_map_eq_nsmul_single p.1]
          simp only [â†“reduceIte, smul_eq_mul]
          have h_count_one : Multiset.count p.1 (Multiset.map Prod.fst x).dedup = 1 := by
            refine Multiset.count_eq_one_of_mem ?_ h_mem_dedup
            exact Multiset.nodup_dedup (Multiset.map Prod.fst x)
          simp [h_count_one]
          intro q5' h h2
          simp_all [eq_comm]
        Â· rw [Multiset.sum_eq_zero]
          refine Eq.symm (Multiset.count_eq_zero_of_notMem ?_)
          intro h
          have h_mem : p.1 âˆˆ Multiset.map Prod.fst x := by
            simp_all
          (expose_names; exact h_mem_1 h_mem)
          intro p' hp
          simp at hp
          obtain âŸ¨q5', âŸ¨f1, hfâŸ©, hp'âŸ© := hp
          by_cases h_eq : p.1 = q5'
          Â· simp_all
          Â· simp_all

/-!

### B.7. Reduction does nothing if no duplicate charges

-/

lemma reduce_eq_self_of_ofCharges_nodup (x : FiveQuanta ð“©) (h : x.toCharges.Nodup) :
    x.reduce = x := by
  rw [reduce, Multiset.Nodup.dedup h]
  simp [toCharges]
  conv_rhs => rw [â† Multiset.map_id x]
  apply Multiset.map_congr rfl
  intro p hp
  simp only [id_eq]
  have x_noDup : x.Nodup := Multiset.Nodup.of_map Prod.fst h
  suffices (Multiset.filter (fun f => f.1 = p.1) x) = {p} by simp [this]
  refine (Multiset.Nodup.ext ?_ ?_).mpr ?_
  Â· exact Multiset.Nodup.filter (fun f => f.1 = p.1) x_noDup
  Â· exact Multiset.nodup_singleton p
  intro p'
  simp only [Multiset.mem_filter, Multiset.mem_singleton]
  constructor
  Â· rintro âŸ¨h1, h2âŸ©
    simp [toCharges] at h
    rw [propext (Multiset.nodup_map_iff_inj_on x_noDup)] at h
    apply h
    Â· exact h1
    Â· exact hp
    Â· exact h2
  Â· rintro âŸ¨rflâŸ©
    simp_all

/-!

### B.8. The charge map is preserved by reduction

-/

lemma reduce_toChargeMap_eq (x : FiveQuanta ð“©) :
    x.reduce.toChargeMap = x.toChargeMap := by
  funext q
  by_cases h : q âˆˆ x.toCharges
  Â· rw [toChargeMap, reduce_filter]
    Â· simp
      rfl
    Â· exact h
  Â· rw [toChargeMap_of_not_mem, toChargeMap_of_not_mem]
    Â· exact h
    Â· rw [reduce_toCharges]
      simp only [Multiset.mem_dedup]
      exact h

/-!

### B.9. A fluxes in the reduced `FiveQuanta` is a sum of fluxes in the original `FiveQuanta`

-/

lemma mem_powerset_sum_of_mem_reduce_toFluxesFive {F : FiveQuanta ð“©}
    {f : Fluxes} (hf : f âˆˆ F.reduce.toFluxesFive) :
    f âˆˆ (Multiset.powerset F.toFluxesFive).map fun s => s.sum := by
  rw [toFluxesFive, Multiset.mem_map] at hf
  obtain âŸ¨âŸ¨q, fâŸ©, hp, rflâŸ© := hf
  rw [mem_reduce_iff] at hp
  simp at hp
  obtain âŸ¨hq, rflâŸ© := hp
  simp only [Multiset.mem_map, Multiset.mem_powerset]
  use (Multiset.map (fun x => x.2) (Multiset.filter (fun x => x.1 = q) F))
  simp only [and_true]
  rw [toFluxesFive]
  refine Multiset.map_le_map ?_
  exact Multiset.filter_le (fun x => x.1 = q) F

lemma mem_powerset_sum_of_mem_reduce_toFluxesFive_filter {F : FiveQuanta ð“©}
    {f : Fluxes} (hf : f âˆˆ F.reduce.toFluxesFive) :
    f âˆˆ (F.toFluxesFive.powerset.filter fun s => s â‰  âˆ…).map fun s => s.sum := by
  rw [toFluxesFive, Multiset.mem_map] at hf
  obtain âŸ¨âŸ¨q, fâŸ©, hp, rflâŸ© := hf
  rw [mem_reduce_iff] at hp
  simp at hp
  obtain âŸ¨hq, rflâŸ© := hp
  simp only [Multiset.mem_map]
  use (Multiset.map (fun x => x.2) (Multiset.filter (fun x => x.1 = q) F))
  simp only [and_true]
  rw [Multiset.mem_filter]
  apply And.intro
  simp only [Multiset.mem_powerset]
  rw [toFluxesFive]
  refine Multiset.map_le_map ?_
  exact Multiset.filter_le (fun x => x.1 = q) F
  simp [Multiset.empty_eq_zero, ne_eq, Multiset.map_eq_zero, Multiset.filter_eq_nil,
    Prod.forall, not_forall, Decidable.not_not]
  rw [toCharges, Multiset.mem_map] at hq
  obtain âŸ¨a, ha, rflâŸ© := hq
  use a.2

/-!

### B.10. No exotics condition on the reduced `FiveQuanta`

-/

/-!

#### B.10.1. Number of chiral `L`

-/

lemma reduce_numChiralL_of_mem_elemsNoExotics {F : FiveQuanta ð“©}
    (hx : F.toFluxesFive âˆˆ FluxesFive.elemsNoExotics) :
    F.reduce.toFluxesFive.numChiralL = 3 := by
  have hE : F.toFluxesFive.NoExotics := by
    rw [â† FluxesFive.noExotics_iff_mem_elemsNoExotics] at hx
    exact hx.1
  rw [â† hE.1, FluxesFive.numChiralL, FluxesFive.numChiralL, FluxesFive.chiralIndicesOfL]
  trans (F.reduce.toFluxesFive.map (fun f => f.M + f.N)).sum
  Â· congr
    refine Multiset.filter_eq_self.mpr ?_
    intro a ha
    rw [Multiset.mem_map] at ha
    obtain âŸ¨f, hf, rflâŸ© := ha
    replace hf := mem_powerset_sum_of_mem_reduce_toFluxesFive hf
    generalize F.toFluxesFive = G at *
    revert f
    revert G
    decide
  Â· let f : ð“© â†’ Fluxes â†’+ â„¤ := fun q5 => âŸ¨âŸ¨fun x => x.M + x.N, by simpâŸ©,
      fun x y => by simp [add_add_add_comm]âŸ©
    rw [toFluxesFive, Multiset.map_map]
    change (F.reduce.map (fun (q5, x) => f q5 x)).sum = _
    rw [reduce_sum_eq_sum_toCharges]
    congr
    rw [FluxesFive.chiralIndicesOfL, toFluxesFive, Multiset.map_map]
    refine (Multiset.filter_eq_self.mpr ?_).symm
    have h' : Multiset.map (fun x => (f x.1) x.2) F = F.toFluxesFive.map (fun f => f.M + f.N) := by
      simp [toFluxesFive, Multiset.map_map]
      rfl
    rw [h']
    clear h'
    generalize F.toFluxesFive = G at *
    revert G
    decide

/-!

#### B.10.2. Number of anti-chiral `L`

-/

lemma reduce_numAntiChiralL_of_mem_elemsNoExotics {F : FiveQuanta ð“©}
    (hx : F.toFluxesFive âˆˆ FluxesFive.elemsNoExotics) :
    F.reduce.toFluxesFive.numAntiChiralL = 0 := by
  rw [FluxesFive.numAntiChiralL, FluxesFive.chiralIndicesOfL]
  have hx : (Multiset.filter (fun x => x < 0) (F.reduce.toFluxesFive.map (fun f => f.M + f.N)))
      = 0 := by
    refine Multiset.filter_eq_nil.mpr ?_
    intro a ha
    rw [Multiset.mem_map] at ha
    obtain âŸ¨f, hf, rflâŸ© := ha
    replace hf := mem_powerset_sum_of_mem_reduce_toFluxesFive hf
    generalize F.toFluxesFive = G at *
    revert f
    revert G
    decide
  rw [hx]
  rfl

/-!

#### B.10.3. Number of chiral `D`

-/

lemma reduce_numChiralD_of_mem_elemsNoExotics {F : FiveQuanta ð“©}
    (hx : F.toFluxesFive âˆˆ FluxesFive.elemsNoExotics) :
    F.reduce.toFluxesFive.numChiralD = 3 := by
  have hE : F.toFluxesFive.NoExotics := by
    rw [â† FluxesFive.noExotics_iff_mem_elemsNoExotics] at hx
    exact hx.1
  rw [â† hE.2.2.1, FluxesFive.numChiralD, FluxesFive.numChiralD, FluxesFive.chiralIndicesOfD]
  trans (F.reduce.toFluxesFive.map (fun f => f.M)).sum
  Â· congr
    refine Multiset.filter_eq_self.mpr ?_
    intro a ha
    rw [Multiset.mem_map] at ha
    obtain âŸ¨f, hf, rflâŸ© := ha
    replace hf := mem_powerset_sum_of_mem_reduce_toFluxesFive hf
    generalize F.toFluxesFive = G at *
    revert f
    revert G
    decide
  Â· let f : ð“© â†’ Fluxes â†’+ â„¤ := fun q5 => âŸ¨âŸ¨fun x => x.M, by simpâŸ©,
      fun x y => by simpâŸ©
    rw [toFluxesFive, Multiset.map_map]
    change (F.reduce.map (fun (q5, x) => f q5 x)).sum = _
    rw [reduce_sum_eq_sum_toCharges]
    congr
    rw [FluxesFive.chiralIndicesOfD, toFluxesFive, Multiset.map_map]
    refine (Multiset.filter_eq_self.mpr ?_).symm
    have h' : Multiset.map (fun x => (f x.1) x.2) F = F.toFluxesFive.map (fun f => f.M) := by
      simp [toFluxesFive, Multiset.map_map]
      rfl
    rw [h']
    clear h'
    generalize F.toFluxesFive = G at *
    revert G
    decide

/-!

#### B.10.4. Number of anti-chiral `D`

-/

lemma reduce_numAntiChiralD_of_mem_elemsNoExotics {F : FiveQuanta ð“©}
    (hx : F.toFluxesFive âˆˆ FluxesFive.elemsNoExotics) :
    F.reduce.toFluxesFive.numAntiChiralD = 0 := by
  rw [FluxesFive.numAntiChiralD, FluxesFive.chiralIndicesOfD]
  have hx : (Multiset.filter (fun x => x < 0) (F.reduce.toFluxesFive.map (fun f => f.M)))
      = 0 := by
    refine Multiset.filter_eq_nil.mpr ?_
    intro a ha
    rw [Multiset.mem_map] at ha
    obtain âŸ¨f, hf, rflâŸ© := ha
    replace hf := mem_powerset_sum_of_mem_reduce_toFluxesFive hf
    generalize F.toFluxesFive = G at *
    revert f
    revert G
    decide
  rw [hx]
  rfl

/-!

#### B.10.5. The `NoExotics` condition on the reduced `FiveQuanta`

-/
lemma reduce_noExotics_of_mem_elemsNoExotics {F : FiveQuanta ð“©}
    (hx : F.toFluxesFive âˆˆ FluxesFive.elemsNoExotics) :
    F.reduce.toFluxesFive.NoExotics := by
  rw [FluxesFive.NoExotics]
  rw [reduce_numChiralL_of_mem_elemsNoExotics hx, reduce_numAntiChiralL_of_mem_elemsNoExotics hx,
    reduce_numChiralD_of_mem_elemsNoExotics hx, reduce_numAntiChiralD_of_mem_elemsNoExotics hx]
  simp

/-!

### B.11. Reduce member of `FluxesFive.elemsNoExotics`

-/

lemma reduce_mem_elemsNoExotics {F : FiveQuanta ð“©}
    (hx : F.toFluxesFive âˆˆ FluxesFive.elemsNoExotics) :
    F.reduce.toFluxesFive âˆˆ FluxesFive.elemsNoExotics := by
  rw [â† FluxesFive.noExotics_iff_mem_elemsNoExotics]
  apply And.intro
  Â· exact reduce_noExotics_of_mem_elemsNoExotics hx
  Â· intro h
    replace h := mem_powerset_sum_of_mem_reduce_toFluxesFive_filter h
    generalize F.toFluxesFive = G at *
    revert G
    decide

end reduce

/-!

## C. Decomposition of a `FiveQuanta` into basic fluxes

-/

/-!

### C.1. Decomposition of fluxes

-/

/-- The decomposition of a flux into `âŸ¨1, -1âŸ©` and `âŸ¨0, 1âŸ©`. -/
def decomposeFluxes (f : Fluxes) : Multiset Fluxes :=
  Multiset.replicate (Int.natAbs f.M) âŸ¨1, -1âŸ© +
  Multiset.replicate (Int.natAbs (f.M + f.N)) âŸ¨0, 1âŸ©

lemma decomposeFluxes_sum_of_noExotics (f : Fluxes) (hf : âˆƒ F âˆˆ FluxesFive.elemsNoExotics, f âˆˆ F) :
    (decomposeFluxes f).sum = f := by
  obtain âŸ¨F, hF, hfFâŸ© := hf
  revert f
  revert F
  decide

/-!

### C.2. Decomposition of a `FiveQuanta` (with no exotics)

-/

/-- The decomposition of a `FiveQuanta` into a `FiveQuanta` which has the
  same `reduce` by has fluxes `âŸ¨1, -1âŸ©` and `âŸ¨0,1âŸ©` only. -/
def decompose (x : FiveQuanta ð“©) : FiveQuanta ð“© :=
  x.bind fun p => (decomposeFluxes p.2).map fun f => (p.1, f)

/-!

#### C.2.1. Decomposition distributes over addition

-/

lemma decompose_add (x y : FiveQuanta ð“©) :
    (x + y).decompose = x.decompose + y.decompose := by
  simp [decompose]

/-!

#### C.2.2. Decomposition commutes with filtering charges

-/

lemma decompose_filter_charge [DecidableEq ð“©] (x : FiveQuanta ð“©) (q : ð“©) :
    (x.decompose).filter (fun p => p.1 = q) =
    decompose (x.filter (fun p => p.1 = q)) := by
  rw [decompose]
  revert x
  apply Multiset.induction
  Â· simp [decompose]
  Â· intro a x ih
    simp only [Multiset.cons_bind, Multiset.filter_add]
    rw [Multiset.filter_cons, decompose_add, ih]
    congr
    match a with
    | (q', f) =>
    simp [decomposeFluxes]
    by_cases h : q' = q
    Â· subst h
      simp [decompose, decomposeFluxes]
      congr
      all_goals
      Â· refine Multiset.filter_eq_self.mpr ?_
        intro a ha
        simp [Multiset.mem_replicate] at ha
        rw [ha.2]
    Â· simp [h, decompose]
      apply And.intro
      all_goals
      Â· intro a b h
        simp [Multiset.mem_replicate] at h
        simp_all

/-!

#### C.2.3. Decomposition preserves the charge map

-/

lemma decompose_toChargeMap [DecidableEq ð“©] (x : FiveQuanta ð“©)
    (hx : x.toFluxesFive âˆˆ FluxesFive.elemsNoExotics) :
    x.decompose.toChargeMap = x.toChargeMap := by
  ext q
  rw [toChargeMap, decompose_filter_charge]
  simp [decompose]
  rw [Multiset.map_bind]
  simp only [Multiset.map_map, Function.comp_apply, Multiset.map_id', Multiset.sum_bind]
  rw [toChargeMap]
  congr 1
  apply Multiset.map_congr
  Â· rfl
  intro a ha
  apply decomposeFluxes_sum_of_noExotics
  use x.toFluxesFive
  simp_all [toFluxesFive]
  use a.1
  exact ha.1

/-!

#### C.2.4. Decomposition preserves the charges

-/

lemma decompose_toCharges_dedup [DecidableEq ð“©] (x : FiveQuanta ð“©)
    (hx : x.toFluxesFive âˆˆ FluxesFive.elemsNoExotics) :
    x.decompose.toCharges.dedup = x.toCharges.dedup := by
  refine Multiset.dedup_ext.mpr ?_
  intro q
  simp [decompose, toCharges, -existsAndEq]
  constructor
  Â· rintro âŸ¨a, b, c, h1, h2, rflâŸ©
    exact âŸ¨c, h1âŸ©
  Â· rintro âŸ¨c, h1âŸ©
    have hn : (decomposeFluxes c) â‰  0 := by
      have c_mem_f : c âˆˆ x.toFluxesFive := by
        simp [toFluxesFive]
        use q
      generalize x.toFluxesFive = F at *
      clear h1
      revert c
      revert F
      decide
    apply Multiset.exists_mem_of_ne_zero at hn
    obtain âŸ¨c', hâŸ© := hn
    use c', q, c

/-!

#### C.2.5. Decomposition preserves the reduction

-/

lemma decompose_reduce (x : FiveQuanta ð“©) [DecidableEq ð“©]
    (hx : x.toFluxesFive âˆˆ FluxesFive.elemsNoExotics) :
    x.decompose.reduce = x.reduce := by
  rw [reduce, reduce]
  apply Multiset.map_congr
  Â· rw [decompose_toCharges_dedup x hx]
  Â· intro q hx'
    simp only [Prod.mk.injEq, true_and]
    change x.decompose.toChargeMap q = x.toChargeMap q
    rw [decompose_toChargeMap x hx]

/-!

#### C.2.6. Fluxes of the decomposition of a `FiveQuanta`

-/

lemma decompose_toFluxesFive (x : FiveQuanta ð“©)
    (hx : x.toFluxesFive âˆˆ FluxesFive.elemsNoExotics) :
    x.decompose.toFluxesFive = {âŸ¨1, -1âŸ©, âŸ¨1, -1âŸ©, âŸ¨1, -1âŸ©, âŸ¨0, 1âŸ©, âŸ¨0, 1âŸ©, âŸ¨0, 1âŸ©} := by
  rw [toFluxesFive, decompose]
  rw [Multiset.map_bind]
  simp only [Multiset.map_map, Function.comp_apply, Multiset.map_id', Int.reduceNeg,
    Multiset.insert_eq_cons]
  trans (Multiset.bind x.toFluxesFive fun a => decomposeFluxes a)
  Â· rw [toFluxesFive, Multiset.bind_map]
  Â· generalize x.toFluxesFive = F at *
    revert F
    decide

/-!

## D. Lifting charges to `FiveQuanta`

-/

section ofChargesExpand

open SuperSymmetry.SU5.ChargeSpectrum

variable [DecidableEq ð“©]

/-!

### D.1. `liftCharge c`: multiset of five-quanta for a finite set of charges `c` with no exotics

This is an efficient definition, we will later show that it gives the correct answer

-/

/-- Given a finite set of charges `c` the `FiveQuanta`
  which do not have exotics, duplicate charges or zero fluxes, which map down to `c`. -/
def liftCharge (c : Finset ð“©) : Multiset (FiveQuanta ð“©) :=
  /- The multisets of cardinality 3 containing 3 elements of `c`. -/
  let S53 : Multiset (Multiset ð“©) := toMultisetsThree c
  /- Pairs of multisets (s1, s2) such that s1 and s2 are cardinality of `3` containing
    elements of `c` and that all elements of `c` are in `s1 + s2`. -/
  let S5p : Multiset (Multiset ð“© Ã— Multiset ð“©) :=
    (S53.product S53).filter fun (s1, s2) => c.val â‰¤ s1 + s2
  let Fp : Multiset (FiveQuanta ð“©) :=
    S5p.map (fun y => y.1.map (fun z => (z, âŸ¨1, -1âŸ©)) + y.2.map (fun z => (z, âŸ¨0, 1âŸ©)))
  Fp.map reduce

/-!

### D.2. FiveQuanta in `liftCharge c` have a finite set of charges `c`

-/

lemma toCharges_toFinset_of_mem_liftCharge (c : Finset ð“©) {x : FiveQuanta ð“©}
    (h : x âˆˆ liftCharge c) : x.toCharges.toFinset = c := by
  simp [liftCharge] at h
  obtain âŸ¨s1, s2, âŸ¨âŸ¨âŸ¨s1_subset, s1_cardâŸ©, âŸ¨s2_subset, s2_cardâŸ©âŸ©, hsumâŸ©, rflâŸ© := h
  rw [â† Multiset.toFinset_dedup, reduce_toCharges]
  simp only [Int.reduceNeg, Multiset.dedup_idem, Multiset.toFinset_dedup]
  simp [toCharges]
  trans (s1 + s2).toFinset
  Â· exact Eq.symm (Multiset.toFinset_add s1 s2)
  ext a
  simp only [Multiset.toFinset_add, Finset.mem_union, Multiset.mem_toFinset]
  constructor
  Â· intro hr
    rcases hr with hr | hr
    Â· apply s1_subset
      simpa using hr
    Â· apply s2_subset
      simpa using hr
  Â· intro hr
    simpa using Multiset.mem_of_le hsum hr

/-!

### D.3. FiveQuanta in `liftCharge c` have no duplicate charges

-/

lemma toCharges_nodup_of_mem_liftCharge (c : Finset ð“©) {x : FiveQuanta ð“©}
    (h : x âˆˆ liftCharge c) : x.toCharges.Nodup := by
  rw [liftCharge, Multiset.mem_map] at h
  obtain âŸ¨x, h, rflâŸ© := h
  rw [reduce_toCharges]
  exact Multiset.nodup_dedup x.toCharges

/-!

### D.4. Membership in `liftCharge c` iff is reduction of `FiveQuanta` with given fluxes

-/

lemma exists_toCharges_toFluxesFive_of_mem_liftCharge (c : Finset ð“©) {x : FiveQuanta ð“©}
    (h : x âˆˆ liftCharge c) :
    âˆƒ a : FiveQuanta ð“©, a.reduce = x âˆ§ a.toCharges.toFinset = c âˆ§ a.toFluxesFive =
      {âŸ¨1, -1âŸ©, âŸ¨1, -1âŸ©, âŸ¨1, -1âŸ©, âŸ¨0, 1âŸ©, âŸ¨0, 1âŸ©, âŸ¨0, 1âŸ©} := by
  have h' := h
  rw [liftCharge, Multiset.mem_map] at h
  obtain âŸ¨a, h, rflâŸ© := h
  use a
  simp only [Int.reduceNeg, Multiset.insert_eq_cons, true_and]
  apply And.intro
  Â· trans a.toCharges.dedup.toFinset
    Â· simp
    rw [â† reduce_toCharges]
    exact toCharges_toFinset_of_mem_liftCharge c h'
  Â· simp at h
    obtain âŸ¨s1, s2, âŸ¨âŸ¨âŸ¨s1_subset, s1_cardâŸ©, âŸ¨s2_subset, s2_cardâŸ©âŸ©, hsumâŸ©, rflâŸ© := h
    simp [toFluxesFive, s1_card, s2_card]
    decide

lemma mem_liftCharge_of_exists_toCharges_toFluxesFive (c : Finset ð“©) {x : FiveQuanta ð“©}
    (h : âˆƒ a : FiveQuanta ð“©, a.reduce = x âˆ§ a.toCharges.toFinset = c âˆ§ a.toFluxesFive =
      {âŸ¨1, -1âŸ©, âŸ¨1, -1âŸ©, âŸ¨1, -1âŸ©, âŸ¨0, 1âŸ©, âŸ¨0, 1âŸ©, âŸ¨0, 1âŸ©}) :
    x âˆˆ liftCharge c := by
  obtain âŸ¨x, rfl, h, h2âŸ© := h
  rw [liftCharge, Multiset.mem_map]
  use x
  simp only [Int.reduceNeg, Multiset.mem_map, Multiset.mem_filter, Multiset.mem_product,
    mem_toMultisetsThree_iff, Prod.exists, and_true]
  let s1 := (x.filter (fun y => y.2 = âŸ¨1, -1âŸ©)).map Prod.fst
  let s2 := (x.filter (fun y => y.2 = âŸ¨0, 1âŸ©)).map Prod.fst
  use s1, s2
  have hx : Multiset.filter (fun y => y.2 = âŸ¨0, 1âŸ©) x
        = Multiset.filter (fun y => Â¬ y.2 = âŸ¨1, -1âŸ©) x := by
    refine Multiset.filter_congr ?_
    intro p hp
    have h1 : p.2 âˆˆ x.toFluxesFive := by simp [toFluxesFive]; use p.1
    rw [h2] at h1
    simp_all
    rcases h1 with hp | hp
    Â· simp [hp]
    Â· simp [hp]
  refine âŸ¨âŸ¨âŸ¨âŸ¨?_, ?_âŸ©, âŸ¨?_, ?_âŸ©âŸ©, ?_âŸ©, ?_âŸ©
  Â· simp [s1, â† h, toCharges]
  Â· simp [s1]
    trans (Multiset.filter (fun y => y = âŸ¨1, -1âŸ©) (x.toFluxesFive)).card
    Â· rw [toFluxesFive, Multiset.filter_map]
      simp
    rw [h2]
    decide
  Â· simp [s2, â† h, toCharges]
  Â· simp [s2]
    trans (Multiset.filter (fun y => y = âŸ¨0, 1âŸ©) (x.toFluxesFive)).card
    Â· rw [toFluxesFive, Multiset.filter_map]
      simp
    rw [h2]
    decide
  Â· rw [â† h]
    simp [s1, s2, toCharges]
    rw [â† Multiset.map_add]
    refine (Multiset.le_iff_subset (Multiset.nodup_dedup (Multiset.map Prod.fst x))).mpr ?_
    simp only [Multiset.dedup_subset']
    refine Multiset.map_subset_map ?_
    rw [hx, Multiset.filter_add_not]
    exact fun â¦ƒaâ¦„ a => a
  Â· simp [s1, s2]
    have h1 : Multiset.map (fun x => (x.1, âŸ¨1, -1âŸ©)) (Multiset.filter (fun y => y.2 = âŸ¨1, -1âŸ©) x)
        = (Multiset.filter (fun y => y.2 = âŸ¨1, -1âŸ©) x) := by
      trans Multiset.map (fun x => x) (Multiset.filter (fun y => y.2 = âŸ¨1, -1âŸ©) x)
      Â· apply Multiset.map_congr
        Â· rfl
        Â· intro y hx
          simp at hx
          rw [â† hx.2]
      simp
    have h2 : Multiset.map (fun x => (x.1, âŸ¨0, 1âŸ©)) (Multiset.filter (fun y => y.2 = âŸ¨0, 1âŸ©) x)
        = (Multiset.filter (fun y => y.2 = âŸ¨0, 1âŸ©) x) := by
      trans Multiset.map (fun x => x) (Multiset.filter (fun y => y.2 = âŸ¨0, 1âŸ©) x)
      Â· apply Multiset.map_congr
        Â· rfl
        Â· intro y hx
          simp at hx
          rw [â† hx.2]
      simp
    rw [h1, h2, hx]
    exact Multiset.filter_add_not (fun y => y.2 = âŸ¨1, -1âŸ©) x

lemma mem_liftCharge_iff_exists (c : Finset ð“©) {x : FiveQuanta ð“©} :
    x âˆˆ liftCharge c â†” âˆƒ a : FiveQuanta ð“©, a.reduce = x âˆ§
      a.toCharges.toFinset = c âˆ§ a.toFluxesFive =
      {âŸ¨1, -1âŸ©, âŸ¨1, -1âŸ©, âŸ¨1, -1âŸ©, âŸ¨0, 1âŸ©, âŸ¨0, 1âŸ©, âŸ¨0, 1âŸ©} :=
  âŸ¨exists_toCharges_toFluxesFive_of_mem_liftCharge c,
    mem_liftCharge_of_exists_toCharges_toFluxesFive câŸ©

/-!

### D.5. FiveQuanta in `liftCharge c` do not have zero fluxes

-/

lemma hasNoZero_of_mem_liftCharge (c : Finset ð“©) {x : FiveQuanta ð“©}
    (h : x âˆˆ liftCharge c) : x.toFluxesFive.HasNoZero := by
  rw [mem_liftCharge_iff_exists] at h
  obtain âŸ¨x, rfl, h1, h2âŸ© := h
  intro hf
  have hx := mem_powerset_sum_of_mem_reduce_toFluxesFive_filter hf
  rw [h2] at hx
  revert hx
  decide

/-!

### D.6. FiveQuanta in `liftCharge c` have no exotics

-/

lemma noExotics_of_mem_liftCharge (c : Finset ð“©) (F : FiveQuanta ð“©)
    (h : F âˆˆ liftCharge c) :
    F.toFluxesFive.NoExotics := by
  rw [mem_liftCharge_iff_exists] at h
  obtain âŸ¨x, rfl, h1, h2âŸ© := h
  apply reduce_noExotics_of_mem_elemsNoExotics
  rw [h2]
  decide

/-!

### D.7. Membership in `liftCharge c` iff have no exotics, no zero fluxes, and charges `c`

-/

lemma mem_liftCharge_of_mem_noExotics_hasNoZero (c : Finset ð“©) {x : FiveQuanta ð“©}
    (h1 : x.toFluxesFive.NoExotics) (h2 : x.toFluxesFive.HasNoZero)
    (h3 : x.toCharges.toFinset = c) (h4 : x.toCharges.Nodup) :
    x âˆˆ liftCharge c := by
  have hf : x.toFluxesFive âˆˆ FluxesFive.elemsNoExotics := by
    rw [â† FluxesFive.noExotics_iff_mem_elemsNoExotics]
    simp_all
    exact h2
  rw [mem_liftCharge_iff_exists]
  use x.decompose
  apply And.intro
  Â· rw [decompose_reduce x hf]
    exact reduce_eq_self_of_ofCharges_nodup x h4
  Â· constructor
    Â· trans x.decompose.toCharges.dedup.toFinset
      Â· simp
      Â· rw [decompose_toCharges_dedup x hf, â† h3]
        simp
    Â· rw [decompose_toFluxesFive x hf]

lemma mem_liftCharge_iff (c : Finset ð“©) (x : FiveQuanta ð“©) :
    x âˆˆ liftCharge c â†” x.toFluxesFive âˆˆ FluxesFive.elemsNoExotics
      âˆ§ x.toCharges.toFinset = c âˆ§ x.toCharges.Nodup := by
  constructor
  Â· intro h
    refine âŸ¨?_, ?_, ?_âŸ©
    Â· rw [â† FluxesFive.noExotics_iff_mem_elemsNoExotics]
      refine âŸ¨?_, ?_âŸ©
      Â· exact noExotics_of_mem_liftCharge c x h
      Â· exact hasNoZero_of_mem_liftCharge c h
    Â· exact toCharges_toFinset_of_mem_liftCharge c h
    Â· exact toCharges_nodup_of_mem_liftCharge c h
  Â· intro âŸ¨h1, h2, h3âŸ©
    rw [â† FluxesFive.noExotics_iff_mem_elemsNoExotics] at h1
    exact mem_liftCharge_of_mem_noExotics_hasNoZero c h1.1 h1.2 h2 h3

/-!

### D.8. `liftCharge c` is preserved under a map if reduced

-/

lemma map_liftCharge {ð“© ð“©1 : Type}[DecidableEq ð“©] [DecidableEq ð“©1] [CommRing ð“©] [CommRing ð“©1]
    (f : ð“© â†’+* ð“©1) (c : Finset ð“©) (F : FiveQuanta ð“©) (h : F âˆˆ liftCharge c) :
    FiveQuanta.reduce (F.map fun y => (f y.1, y.2)) âˆˆ liftCharge (c.image f) := by
  rw [mem_liftCharge_iff] at h âŠ¢
  refine âŸ¨?_, ?_, ?_âŸ©
  Â· apply reduce_mem_elemsNoExotics
    simpa [toFluxesFive, Multiset.map_map] using h.1
  Â· rw [reduce_toCharges]
    simp [â† h.2.1, â† Multiset.toFinset_map, toCharges]
  Â· rw [reduce_toCharges]
    exact Multiset.nodup_dedup (toCharges (Multiset.map (fun y => (f y.1, y.2)) F))

end ofChargesExpand

/-!

## E. Anomaly cancellation coefficients

-/

section ACCs

variable [CommRing ð“©]

/-!

### E.1. Anomaly coefficients of a `FiveQuanta`

-/

/--
  The anomaly coefficient of a `FiveQuanta` is given by the pair of integers:
  `(âˆ‘áµ¢ qáµ¢ Náµ¢, âˆ‘áµ¢ qáµ¢Â² Náµ¢)`.

  The first components is for the mixed U(1)-MSSM, see equation (22) of arXiv:1401.5084.
  The second component is for the mixed U(1)Y-U(1)-U(1) gauge anomaly,
  see equation (23) of arXiv:1401.5084.
-/
def anomalyCoefficient (F : FiveQuanta ð“©) : ð“© Ã— ð“© :=
  ((F.map fun x => x.2.2 â€¢ x.1).sum, (F.map fun x => x.2.2 â€¢ (x.1 * x.1)).sum)

/-!

### E.2. Anomaly coefficients under a map

-/

@[simp]
lemma anomalyCoefficient_of_map {ð“© ð“©1 : Type} [CommRing ð“©] [CommRing ð“©1]
    (f : ð“© â†’+* ð“©1) (F : FiveQuanta ð“©) :
    FiveQuanta.anomalyCoefficient (F.map fun y => (f y.1, y.2) : FiveQuanta ð“©1) =
    (f.prodMap f) F.anomalyCoefficient := by
  simp [FiveQuanta.anomalyCoefficient, map_multiset_sum, Multiset.map_map]

/-!

### E.3. Anomaly coefficients is preserved under `reduce`

-/

lemma anomalyCoefficient_of_reduce (F : FiveQuanta ð“©) [DecidableEq ð“©] :
    F.reduce.anomalyCoefficient = F.anomalyCoefficient := by
  simp [anomalyCoefficient]
  constructor
  Â· let f : ð“© â†’ Fluxes â†’+ ð“© := fun q5 => {
      toFun := fun x => x.2 â€¢ q5
      map_zero' := by simp
      map_add' := by
        intros x y
        simp [add_mul] }
    simpa [f] using reduce_sum_eq_sum_toCharges F f
  Â· let f : ð“© â†’ Fluxes â†’+ ð“© := fun q5 => {
      toFun := fun x => x.2 â€¢ (q5 * q5)
      map_zero' := by simp
      map_add' := by
        intros x y
        simp [add_mul] }
    simpa [f] using reduce_sum_eq_sum_toCharges F f

end ACCs

end FiveQuanta

end SU5
end FTheory
