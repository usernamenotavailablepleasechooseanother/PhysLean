/-
Copyright (c) 2025 Joseph Tooby-Smith. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Zhi Kai Pong, Joseph Tooby-Smith, Lode Vermeulen
-/
import Mathlib.Analysis.Calculus.FDeriv.Symmetric
import Mathlib.Analysis.Calculus.Gradient.Basic
import PhysLean.SpaceAndTime.Space.DistOfFunction
import Mathlib.MeasureTheory.SpecificCodomains.WithLp
/-!

# Derivatives on Space

## i. Overview

In this module we define derivatives of functions and distributions on space `Space d`,
in the standard directions.

## ii. Key results

- `deriv` : The derivative of a function on space in a given direction.
- `distDeriv` : The derivative of a distribution on space in a given direction.

## iii. Table of contents

- A. Derivatives of functions on `Space d`
  - A.1. Basic equalities
  - A.2. Derivative of the constant function
  - A.3. Derivative distributes over addition
  - A.4. Derivative distributes over scalar multiplication
  - A.5. Two spatial derivatives commute
  - A.6. Derivative of a component
  - A.7. Derivative of a component squared
  - A.8. Derivivatives of components
  - A.9. Derivative of a norm squared
    - A.9.1. Differentiability of the norm squared function
    - A.9.2. Derivative of the norm squared function
  - A.10. Derivative of the inner product
    - A.10.1. Differentiability of the inner product function
    - A.10.2. Derivative of the inner product function
    - A.10.3. Derivative of the inner product on one side
  - A.11. Differentiability of derivatives
- B. Derivatives of distributions on `Space d`
  - B.1. The definition
  - B.2. Basic equality
  - B.3. Commutation of derivatives

## iv. References

-/

namespace Space

/-!

## A. Derivatives of functions on `Space d`

-/

/-- Given a function `f : Space d â†’ M` the derivative of `f` in direction `Î¼`. -/
noncomputable def deriv {M d} [AddCommGroup M] [Module â„ M] [TopologicalSpace M]
    (Î¼ : Fin d) (f : Space d â†’ M) : Space d â†’ M :=
  (fun x => fderiv â„ f x (EuclideanSpace.single Î¼ (1:â„)))

@[inherit_doc deriv]
macro "âˆ‚[" i:term "]" : term => `(deriv $i)

/-!

### A.1. Basic equalities

-/

lemma deriv_eq [AddCommGroup M] [Module â„ M] [TopologicalSpace M]
    (Î¼ : Fin d) (f : Space d â†’ M) (x : Space d) :
    deriv Î¼ f x = fderiv â„ f x (EuclideanSpace.single Î¼ (1:â„)) := by
  rfl

lemma deriv_eq_fderiv_basis [AddCommGroup M] [Module â„ M] [TopologicalSpace M]
    (Î¼ : Fin d) (f : Space d â†’ M) (x : Space d) :
    deriv Î¼ f x = fderiv â„ f x (basis Î¼) := by
  rw [deriv_eq]
  congr 1
  ext i
  simp only [EuclideanSpace.single_apply, basis_apply]
  congr 1
  exact Lean.Grind.eq_congr' rfl rfl

lemma fderiv_eq_sum_deriv {M d} [AddCommGroup M] [Module â„ M] [TopologicalSpace M]
    (f : Space d â†’ M) (x y : Space d) :
    fderiv â„ f x y = âˆ‘ i : Fin d, y i â€¢ âˆ‚[i] f x := by
  have h1 : y = âˆ‘ i, y i â€¢ basis i := by
    exact Eq.symm (OrthonormalBasis.sum_repr basis y)
  conv_lhs => rw [h1]
  simp [deriv_eq_fderiv_basis]

/-!

### A.2. Derivative of the constant function

-/

@[simp]
lemma deriv_const [NormedAddCommGroup M] [NormedSpace â„ M] (m : M) (Î¼ : Fin d) :
    deriv Î¼ (fun _ => m) t = 0 := by
  rw [deriv]
  simp

/-!

### A.3. Derivative distributes over addition

-/

/-- Derivatives on space distribute over addition. -/
lemma deriv_add [NormedAddCommGroup M] [NormedSpace â„ M]
    (f1 f2 : Space d â†’ M) (hf1 : Differentiable â„ f1) (hf2 : Differentiable â„ f2) :
    âˆ‚[u] (f1 + f2) = âˆ‚[u] f1 + âˆ‚[u] f2 := by
  unfold deriv
  simp only
  ext x
  rw [fderiv_add]
  rfl
  repeat fun_prop

/-- Derivatives on space distribute coordinate-wise over addition. -/
lemma deriv_coord_add (f1 f2 : Space d â†’ EuclideanSpace â„ (Fin d))
    (hf1 : Differentiable â„ f1) (hf2 : Differentiable â„ f2) :
    (âˆ‚[u] (fun x => f1 x i + f2 x i)) =
      (âˆ‚[u] (fun x => f1 x i)) + (âˆ‚[u] (fun x => f2 x i)) := by
  unfold deriv
  simp only
  ext x
  rw [fderiv_fun_add]
  simp only [ContinuousLinearMap.add_apply, Pi.add_apply]
  repeat fun_prop

/-!

### A.4. Derivative distributes over scalar multiplication

-/

/-- Scalar multiplication on space derivatives. -/
lemma deriv_smul [NormedAddCommGroup M] [NormedSpace â„ M]
    (f : Space d â†’ M) (k : â„) (hf : Differentiable â„ f) :
    âˆ‚[u] (k â€¢ f) = (k â€¢ âˆ‚[u] f) := by
  unfold deriv
  ext x
  rw [fderiv_const_smul]
  rfl
  fun_prop

/-- Coordinate-wise scalar multiplication on space derivatives. -/
lemma deriv_coord_smul (f : Space d â†’ EuclideanSpace â„ (Fin d)) (k : â„)
    (hf : Differentiable â„ f) :
    âˆ‚[u] (fun x => k * f x i) x= (k â€¢ âˆ‚[u] (fun x => f x i)) x:= by
  unfold deriv
  rw [fderiv_const_mul]
  simp only [ContinuousLinearMap.coe_smul', Pi.smul_apply, smul_eq_mul]
  fun_prop

/-!

### A.5. Two spatial derivatives commute

-/

/-- Derivatives on space commute with one another. -/
lemma deriv_commute [NormedAddCommGroup M] [NormedSpace â„ M]
    (f : Space d â†’ M) (hf : ContDiff â„ 2 f) : âˆ‚[u] (âˆ‚[v] f) = âˆ‚[v] (âˆ‚[u] f) := by
  unfold deriv
  ext x
  rw [fderiv_clm_apply, fderiv_clm_apply]
  simp only [fderiv_fun_const, Pi.ofNat_apply, ContinuousLinearMap.comp_zero, zero_add,
    ContinuousLinearMap.flip_apply]
  rw [IsSymmSndFDerivAt.eq]
  apply ContDiffAt.isSymmSndFDerivAt
  exact ContDiff.contDiffAt hf
  simp only [minSmoothness_of_isRCLikeNormedField, le_refl]
  repeat fun_prop

/-!

### A.6. Derivative of a component

-/

@[simp]
lemma deriv_component_same (Î¼ : Fin d) (x : Space d) :
    âˆ‚[Î¼] (fun x => x Î¼) x = 1 := by
  conv_lhs =>
    enter [2, x]
    rw [â† Space.coord_apply Î¼ x]
  change deriv Î¼ (Space.coordCLM Î¼) x = 1
  simp only [deriv_eq, ContinuousLinearMap.fderiv]
  simp [Space.coordCLM, Space.coord]

lemma deriv_component_diff (Î¼ Î½ : Fin d) (x : Space d) (h : Î¼ â‰  Î½) :
    (deriv Î¼ (fun x => x Î½) x) = 0 := by
  conv_lhs =>
    enter [2, x]
    rw [â† Space.coord_apply _ x]
  change deriv Î¼ (Space.coordCLM Î½) x = 0
  simp only [deriv_eq, ContinuousLinearMap.fderiv]
  simpa [Space.coordCLM, Space.coord] using h.symm

lemma deriv_component (Î¼ Î½ : Fin d) (x : Space d) :
    (deriv Î½ (fun x => x Î¼) x) = if Î½ = Î¼ then 1 else 0 := by
  by_cases h' : Î½ = Î¼
  Â· subst h'
    simp
  Â· rw [deriv_component_diff Î½ Î¼]
    simp only [right_eq_ite_iff, zero_ne_one, imp_false]
    simpa using h'
    simpa using h'

/-!

### A.7. Derivative of a component squared

-/

lemma deriv_component_sq {d : â„•} {Î½ Î¼ : Fin d} (x : Space d) :
    (deriv Î½ (fun x => (x Î¼) ^ 2) x) = if Î½ = Î¼ then 2 * x Î¼ else 0:= by
  rw [deriv_eq_fderiv_basis]
  rw [fderiv_pow]
  simp only [Nat.add_one_sub_one, pow_one, nsmul_eq_mul, Nat.cast_ofNat,
    ContinuousLinearMap.coe_smul', Pi.smul_apply, smul_eq_mul]
  rw [â† deriv_eq_fderiv_basis, deriv_component]
  simp only [mul_ite, mul_one, mul_zero]
  fun_prop

/-!

### A.8. Derivivatives of components

-/

lemma deriv_euclid {d Î½ Î¼} {f : Space d â†’ EuclideanSpace â„ (Fin n)}
    (hf : Differentiable â„ f) (x : Space d) :
    deriv Î½ (fun x => f x Î¼) x = deriv Î½ (fun x => f x) x Î¼ := by
  rw [deriv_eq_fderiv_basis]
  change fderiv â„ (EuclideanSpace.proj Î¼ âˆ˜ fun x => f x) x (basis Î½) = _
  rw [fderiv_comp]
  Â· simp
    rw [â† deriv_eq_fderiv_basis]
  Â· fun_prop
  Â· fun_prop

lemma deriv_lorentz_vector {d Î½ Î¼} {f : Space d â†’ Lorentz.Vector d}
    (hf : Differentiable â„ f) (x : Space d) :
    deriv Î½ (fun x => f x Î¼) x = deriv Î½ (fun x => f x) x Î¼ := by
  rw [deriv_eq_fderiv_basis]
  change fderiv â„ (Lorentz.Vector.coordCLM Î¼ âˆ˜ fun x => f x) x (basis Î½) = _
  rw [fderiv_comp]
  Â· simp
    rw [â† deriv_eq_fderiv_basis]
    rfl
  Â· fun_prop
  Â· fun_prop

/-!

### A.9. Derivative of a norm squared

-/

/-!

#### A.9.1. Differentiability of the norm squared function

-/
@[fun_prop]
lemma norm_sq_differentiable : Differentiable â„ (fun x : Space d => â€–xâ€– ^ 2) := by
  simp [@PiLp.norm_sq_eq_of_L2]
  fun_prop

/-!

#### A.9.2. Derivative of the norm squared function

-/

lemma deriv_norm_sq (x : Space d) (i : Fin d) :
    deriv i (fun x => â€–xâ€– ^ 2) x = 2 * x i := by
  simp [@PiLp.norm_sq_eq_of_L2]
  rw [deriv_eq_fderiv_basis]
  rw [fderiv_fun_sum]
  simp only [ContinuousLinearMap.coe_sum', Finset.sum_apply]
  conv_lhs =>
    enter [2, j]
    rw [â† deriv_eq_fderiv_basis]
    simp
  simp [deriv_component_sq]
  intro i hi
  fun_prop

/-!

### A.10. Derivative of the inner product

-/

open InnerProductSpace

/-!

#### A.10.1. Differentiability of the inner product function

-/

/-- The inner product is differentiable. -/
lemma inner_differentiable {d : â„•} :
    Differentiable â„ (fun y : Space d => âŸªy, yâŸ«_â„) := by
  simp only [PiLp.inner_apply, RCLike.inner_apply, conj_trivial]
  fun_prop

/-!

#### A.10.2. Derivative of the inner product function

-/

lemma deriv_eq_inner_self (x : Space d) (i : Fin d) :
    deriv i (fun x => âŸªx, xâŸ«_â„) x = 2 * x i := by
  convert deriv_norm_sq x i
  exact real_inner_self_eq_norm_sq _

/-!

#### A.10.3. Derivative of the inner product on one side

-/

@[simp]
lemma deriv_inner_left {d} (x1 x2 : Space d) (i : Fin d) :
    deriv i (fun x => âŸªx, x2âŸ«_â„) x1 = x2 i := by
  rw [deriv_eq_fderiv_basis]
  rw [fderiv_inner_apply]
  simp only [fderiv_fun_const, Pi.zero_apply, ContinuousLinearMap.zero_apply, inner_zero_right,
    fderiv_id', ContinuousLinearMap.coe_id', id_eq, basis_inner, zero_add]
  Â· fun_prop
  Â· fun_prop

@[simp]
lemma deriv_inner_right {d} (x1 x2 : Space d) (i : Fin d) :
    deriv i (fun x => âŸªx1, xâŸ«_â„) x2 = x1 i := by
  rw [deriv_eq_fderiv_basis]
  rw [fderiv_inner_apply]
  simp only [fderiv_id', ContinuousLinearMap.coe_id', id_eq, inner_basis, fderiv_fun_const,
    Pi.ofNat_apply, ContinuousLinearMap.zero_apply, inner_zero_left, add_zero]
  Â· fun_prop
  Â· fun_prop
/-!

### A.11. Differentiability of derivatives

-/

lemma deriv_differentiable {M} [NormedAddCommGroup M]
    [NormedSpace â„ M] {d : â„•} {f : Space d â†’ M}
    (hf : ContDiff â„ 2 f) (i : Fin d) :
    Differentiable â„ (deriv i f) := by
  have h1 : Differentiable â„ (fun x => fderiv â„ f x (basis i)) := by
    fun_prop
  convert h1 using 1
  funext x
  rw [deriv_eq_fderiv_basis]

/-!

## B. Derivatives of distributions on `Space d`

-/

open Distribution SchwartzMap

/-!

### B.1. The definition

-/
/-- Given a distribution (function) `f : Space d â†’d[â„] M` the derivative
  of `f` in direction `Î¼`. -/
noncomputable def distDeriv {M d} [NormedAddCommGroup M] [NormedSpace â„ M]
    (Î¼ : Fin d) : ((Space d) â†’d[â„] M) â†’â‚—[â„] (Space d) â†’d[â„] M where
  toFun f :=
    let ev : (Space d â†’L[â„] M) â†’L[â„] M := {
      toFun v := v (basis Î¼)
      map_add' v1 v2 := by
        simp only [ContinuousLinearMap.add_apply]
      map_smul' a v := by
        simp
    }
    ev.comp (Distribution.fderivD â„ f)
  map_add' f1 f2 := by
    simp
  map_smul' a f := by simp

/-!

### B.2. Basic equality

-/

lemma distDeriv_apply {M d} [NormedAddCommGroup M] [NormedSpace â„ M]
    (Î¼ : Fin d) (f : (Space d) â†’d[â„] M) (Îµ : ğ“¢(Space d, â„)) :
    (distDeriv Î¼ f) Îµ = fderivD â„ f Îµ (basis Î¼) := by
  simp [distDeriv, Distribution.fderivD]

/-!

### B.3. Commutation of derivatives

-/

lemma schwartMap_fderiv_comm { d}
    (Î¼ Î½ : Fin d) (x : Space d) (Î· : ğ“¢(Space d, â„)) :
    ((SchwartzMap.evalCLM (ğ•œ := â„) (basis Î¼))
      ((fderivCLM â„) ((SchwartzMap.evalCLM (ğ•œ := â„) (basis Î½)) ((fderivCLM â„) Î·)))) x =
    ((SchwartzMap.evalCLM (ğ•œ := â„) (basis Î½))
      ((fderivCLM â„) ((SchwartzMap.evalCLM (ğ•œ := â„) (basis Î¼)) ((fderivCLM â„) Î·)))) x := by
  have h1 := Î·.smooth
  have h2 := h1 2
  change fderiv â„ (fun x => fderiv â„ Î· x (basis Î½)) x (basis Î¼) =
    fderiv â„ (fun x => fderiv â„ Î· x (basis Î¼)) x (basis Î½)
  rw [fderiv_clm_apply, fderiv_clm_apply]
  simp only [fderiv_fun_const, Pi.ofNat_apply, ContinuousLinearMap.comp_zero, zero_add,
    ContinuousLinearMap.flip_apply]
  rw [IsSymmSndFDerivAt.eq]
  apply ContDiffAt.isSymmSndFDerivAt (n := 2)
  Â· refine ContDiff.contDiffAt ?_
    exact h2
  Â· simp
  Â· fun_prop
  Â· exact differentiableAt_const (basis Î¼)
  Â· fun_prop
  Â· exact differentiableAt_const (basis Î½)

lemma distDeriv_commute {M d} [NormedAddCommGroup M] [NormedSpace â„ M]
    (Î¼ Î½ : Fin d) (f : (Space d) â†’d[â„] M) :
    (distDeriv Î½ (distDeriv Î¼ f)) = (distDeriv Î¼ (distDeriv Î½ f)) := by
  ext Î·
  simp [distDeriv, Distribution.fderivD]
  congr 1
  ext x
  rw [schwartMap_fderiv_comm Î¼ Î½ x Î·]

end Space
