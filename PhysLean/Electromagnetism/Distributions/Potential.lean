/-
Copyright (c) 2025 Joseph Tooby-Smith. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Joseph Tooby-Smith
-/
import PhysLean.Electromagnetism.Basic
import PhysLean.SpaceAndTime.SpaceTime.TimeSlice
import PhysLean.Relativity.Tensors.RealTensor.CoVector.Basic
import PhysLean.Mathematics.VariationalCalculus.HasVarGradient
/-!

# The electromagnetic potential for distributions

## i. Overview

In this file we make the basic definitions of the electromagnetic potential,
the field strength tensor, the electric and magnetic fields, and the
Lagrangian gradient in the context of distributions.

Note that all of these quantities depend linearly on the electromagnetic potential,
allowing them to be defined in the context of distributions.

Unlike in the function case, many of the properties here can be defined as linear maps,
due to the no need to check things like differentiability.

## ii. Key results

- `ElectromagneticPotentialD` : The type of electromagnetic potentials as distributions.
- `ElectromagneticPotentialD.scalarPotential` : The scalar potential as a distribution.
- `ElectromagneticPotentialD.vectorPotential` : The vector potential as a distribution.
- `ElectromagneticPotentialD.electricField` : The electric field as a distribution.
- `ElectromagneticPotentialD.magneticField` : The magnetic field as a distribution.
- `LorentzCurrentDensityD` : The type of Lorentz current densities as distributions.
- `ElectromagneticPotentialD.gradLagrangian` : The variational gradient of the
  electromagnetic Lagrangian as a distribution.

## iii. Table of contents

- A. The electromagnetic potential
  - A.1. The components of the electromagnetic potential
- B. The field strength tensor matrix
  - B.1. Diagonal of the field strength matrix
  - B.2. Antisymmetry of the field strength matrix
- C. The scalar and vector potentials
  - C.1. The scalar potential
  - C.2. The vector potential
- D. The electric and magnetic fields
  - D.1. Linear map to components
  - D.2. The electric field in d-dimensions
    - D.2.1. The electric field in terms of the field strength matrix
    - D.2.2. The first column of the field strength matrix in terms of the electric field
    - D.2.3. The first row of the field strength matrix in terms of the electric field
  - D.3. The magnetic field in 3-dimensions
- E. The Lorentz current density
  - E.1. The components of the Lorentz current density
- F. The Lagrangian variational gradient
  - F.1. The variational gradient in 1-dimension

## iv. References

-/
namespace Electromagnetism
open Module realLorentzTensor
open IndexNotation
open TensorSpecies
open Tensor

/-!

## A. The electromagnetic potential

We define the electromagnetic potential as a function from spacetime to
contravariant Lorentz vectors, and prove some simple results about it.

-/

/-- The electromagnetic potential is a tensor `A^Œº`. -/
noncomputable abbrev ElectromagneticPotentialD (d : ‚Ñï := 3) :=
  (SpaceTime d) ‚Üíd[‚Ñù] Lorentz.Vector d

namespace ElectromagneticPotentialD

open TensorSpecies
open Tensor
open SpaceTime
open TensorProduct
open PiTensorProduct
open minkowskiMatrix
attribute [-simp] Fintype.sum_sum_type
attribute [-simp] Nat.succ_eq_add_one

/-!

### A.1. The components of the electromagnetic potential
-/

/-- The linear map from an electromagnetic potential to its components. -/
noncomputable def toComponents {d : ‚Ñï} :
    ElectromagneticPotentialD d ‚âÉ‚Çó[‚Ñù] ((Fin 1 ‚äï Fin d) ‚Üí (SpaceTime d) ‚Üíd[‚Ñù] ‚Ñù) where
  toFun A := fun Œº => {
    toFun := fun Œµ => A Œµ Œº
    map_add' := by
      intro Œµ1 Œµ2
      simp
    map_smul' := by
      intro c Œµ
      simp
    cont := by fun_prop}
  invFun A := {
    toFun := fun Œµ Œº => A Œº Œµ
    map_add' := by
      intro Œµ1 Œµ2
      funext Œº
      simp
    map_smul' := by
      intro c Œµ
      funext Œº
      simp
    cont := by
      apply Lorentz.Vector.continuous_of_apply
      fun_prop}
  left_inv := by
    intro A
    ext Œµ
    simp
  right_inv := by
    intro A
    ext Œº Œµ
    simp
  map_add' := by
    intro A1 A2
    ext Œº Œµ
    simp
  map_smul' := by
    intro c A
    ext Œº Œµ
    simp

/-!

## B. The field strength tensor matrix

-/

/-- The field strength matrix with indices `F^Œº^ŒΩ`. -/
noncomputable def fieldStrengthMatrix {d : ‚Ñï} :
    ElectromagneticPotentialD d ‚Üí‚Çó[‚Ñù]
    ((Fin 1 ‚äï Fin d) √ó (Fin 1 ‚äï Fin d) ‚Üí (SpaceTime d) ‚Üíd[‚Ñù] ‚Ñù) where
  toFun A := fun (Œº, ŒΩ) => Œ∑ Œº Œº ‚Ä¢ SpaceTime.distDeriv Œº (A.toComponents ŒΩ) -
    Œ∑ ŒΩ ŒΩ ‚Ä¢ SpaceTime.distDeriv ŒΩ (A.toComponents Œº)
  map_add' A1 A2 := by
    ext ŒºŒΩ
    match ŒºŒΩ with
    | (Œº, ŒΩ) =>
    simp only [map_add, Pi.add_apply, smul_add, ContinuousLinearMap.coe_sub', Pi.sub_apply,
      ContinuousLinearMap.add_apply, ContinuousLinearMap.coe_smul', Pi.smul_apply, smul_eq_mul]
    ring
  map_smul' a A := by
    ext ŒºŒΩ
    match ŒºŒΩ with
    | (Œº, ŒΩ) =>
    simp only [map_smul, Pi.smul_apply, ContinuousLinearMap.coe_sub', ContinuousLinearMap.coe_smul',
      Pi.sub_apply, smul_eq_mul, RingHom.id_apply]
    ring

/-!

### B.1. Diagonal of the field strength matrix

-/

@[simp]
lemma fieldStrengthMatrix_same_same {d : ‚Ñï} (A : ElectromagneticPotentialD d) (Œº : Fin 1 ‚äï Fin d) :
    A.fieldStrengthMatrix (Œº, Œº) = 0 := by
  ext Œµ
  simp [fieldStrengthMatrix]

/-!

### B.2. Antisymmetry of the field strength matrix

-/

lemma fieldStrengthMatrix_antisymm {d : ‚Ñï} (A : ElectromagneticPotentialD d)
    (Œº ŒΩ : Fin 1 ‚äï Fin d) :
    A.fieldStrengthMatrix (Œº, ŒΩ) = - A.fieldStrengthMatrix (ŒΩ, Œº) := by
  ext Œµ
  simp [fieldStrengthMatrix]
/-!

## C. The scalar and vector potentials

-/

/-!

### C.1. The scalar potential

-/

/-- The scalar potential from an electromagnetic potential which is a
  distribution. -/
noncomputable def scalarPotential {d} :
    ElectromagneticPotentialD d ‚Üí‚Çó[‚Ñù] (Time √ó Space d) ‚Üíd[‚Ñù] ‚Ñù where
  toFun A := distTimeSlice 1 <| A.toComponents (Sum.inl 0)
  map_add' A1 A2 := by
    ext Œµ
    simp
  map_smul' a A := by
    ext Œµ
    simp

/-!

### C.2. The vector potential

-/

/-- The vector potential from an electromagnetic potential which is a
  distribution. -/
noncomputable def vectorPotential {d}:
    ElectromagneticPotentialD d ‚Üí‚Çó[‚Ñù] (Time √ó Space d) ‚Üíd[‚Ñù] EuclideanSpace ‚Ñù (Fin d) where
  toFun A := {
    toFun := fun Œ∫ => WithLp.toLp 2 fun i => (distTimeSlice 1 <| A.toComponents (Sum.inr i)) Œ∫
    map_add' := by
      intro Œ∫1 Œ∫2
      ext i
      simp
    map_smul' := by
      intro c Œ∫
      ext i
      simp
    cont := by fun_prop
    }
  map_add' A1 A2 := by
    ext Œ∫ i
    simp
  map_smul' a A := by
    ext Œ∫ i
    simp

/-!

## D. The electric and magnetic fields

-/

/-!

### D.1. Linear map to components

-/

/-- The linear map taking a distribution on Euclidean space to its components. -/
noncomputable def toComponentsEuclidean {d : ‚Ñï} :
    ((Time √ó Space d) ‚Üíd[‚Ñù] EuclideanSpace ‚Ñù (Fin d)) ‚âÉ‚Çó[‚Ñù]
    (Fin d ‚Üí (Time √ó Space d) ‚Üíd[‚Ñù] ‚Ñù) where
  toFun J := fun Œº => {
    toFun := fun Œµ => J Œµ Œº
    map_add' := by
      intro Œµ1 Œµ2
      simp
    map_smul' := by
      intro c Œµ
      simp
    cont := by fun_prop}
  invFun J := {
    toFun := fun Œµ => WithLp.toLp 2 fun Œº => J Œº Œµ
    map_add' := by
      intro Œµ1 Œµ2
      ext Œº
      simp
    map_smul' := by
      intro c Œµ
      ext Œº
      simp
    cont := by fun_prop}
  left_inv := by
    intro J
    ext Œµ
    simp
  right_inv := by
    intro J
    ext Œº Œµ
    simp
  map_add' := by
    intro J1 J2
    ext Œº Œµ
    simp
  map_smul' := by
    intro c J
    ext Œº Œµ
    simp

open SchwartzMap
@[simp]
lemma toComponentsEuclidean_apply {d : ‚Ñï} (E : (Time √ó Space d) ‚Üíd[‚Ñù] EuclideanSpace ‚Ñù (Fin d))
    (i : Fin d) (Œµ : ùì¢(Time √ó Space d, ‚Ñù)) :
    (toComponentsEuclidean E i) Œµ = E Œµ i := by rfl

/-!

### D.2. The electric field in d-dimensions

-/

/-- The electric field associated with a electromagnetic potential which is a distribution. -/
noncomputable def electricField {d} :
    ElectromagneticPotentialD d ‚Üí‚Çó[‚Ñù] (Time √ó Space d) ‚Üíd[‚Ñù] EuclideanSpace ‚Ñù (Fin d) where
  toFun A :=
    - Space.distSpaceGrad (A.scalarPotential) - Space.distTimeDeriv (A.vectorPotential)
  map_add' A1 A2 := by
    ext Œ∫ i
    simp only [map_add, neg_add_rev, ContinuousLinearMap.coe_sub', Pi.sub_apply,
      ContinuousLinearMap.add_apply, ContinuousLinearMap.neg_apply, PiLp.sub_apply, PiLp.add_apply,
      PiLp.neg_apply]
    ring
  map_smul' a A := by
    ext Œ∫ i
    simp only [map_smul, ContinuousLinearMap.coe_sub', ContinuousLinearMap.coe_smul', Pi.sub_apply,
      ContinuousLinearMap.neg_apply, Pi.smul_apply, PiLp.sub_apply, PiLp.neg_apply, PiLp.smul_apply,
      smul_eq_mul, RingHom.id_apply]
    ring

/-!

#### D.2.1. The electric field in terms of the field strength matrix

-/

lemma electricField_fieldStrengthMatrix {d} {A : ElectromagneticPotentialD d} (i : Fin d) :
    toComponentsEuclidean A.electricField i =
    distTimeSlice 1 (A.fieldStrengthMatrix (Sum.inr i, Sum.inl 0)) := by
  rw [electricField]
  simp [fieldStrengthMatrix]
  ext Œµ
  simp [distTimeSlice_distDeriv_inl, distTimeSlice_distDeriv_inr, Space.distSpaceGrad_apply]
  ring_nf
  rfl

/-!

#### D.2.2. The first column of the field strength matrix in terms of the electric field

-/

lemma fieldStrengthMatrix_col_eq_electricField {d} {A : ElectromagneticPotentialD d}
    (i : Fin d) :
    (A.fieldStrengthMatrix (Sum.inr i, Sum.inl 0)) =
    (distTimeSlice 1).symm (toComponentsEuclidean A.electricField i) := by
  rw [electricField_fieldStrengthMatrix]
  simp

/-!

#### D.2.3. The first row of the field strength matrix in terms of the electric field

-/

lemma fieldStrengthMatrix_row_eq_electricField {d} {A : ElectromagneticPotentialD d}
    (i : Fin d) :
    (A.fieldStrengthMatrix (Sum.inl 0, Sum.inr i)) =
    - (distTimeSlice 1).symm (toComponentsEuclidean A.electricField i) := by
  rw [fieldStrengthMatrix_antisymm, electricField_fieldStrengthMatrix]
  simp

/-!

### D.3. The magnetic field in 3-dimensions

-/

/-- The magnetic field associated with a electromagnetic potential in 3 dimensions. -/
noncomputable def magneticField :
    ElectromagneticPotentialD 3 ‚Üí‚Çó[‚Ñù] (Time √ó Space 3) ‚Üíd[‚Ñù] EuclideanSpace ‚Ñù (Fin 3) where
  toFun A := Space.distSpaceCurl A.vectorPotential
  map_add' A1 A2 := by
    ext Œ∫ i
    simp
  map_smul' a A := by
    ext Œ∫ i
    simp

end ElectromagneticPotentialD

/-!

## E. The Lorentz current density

-/

/-- The Lorentz current density (aka four-current) as a distribution. -/
abbrev LorentzCurrentDensityD (d : ‚Ñï := 3) :=
  (SpaceTime d) ‚Üíd[‚Ñù] Lorentz.Vector d

namespace LorentzCurrentDensityD

/-!

### E.1. The components of the Lorentz current density

-/

/-- The linear map taking a Lorentz current density to its components. -/
noncomputable def toComponents {d : ‚Ñï} :
    LorentzCurrentDensityD d ‚âÉ‚Çó[‚Ñù] ((Fin 1 ‚äï Fin d) ‚Üí (SpaceTime d) ‚Üíd[‚Ñù] ‚Ñù) where
  toFun J := fun Œº => {
    toFun := fun Œµ => J Œµ Œº
    map_add' := by
      intro Œµ1 Œµ2
      simp
    map_smul' := by
      intro c Œµ
      simp
    cont := by fun_prop}
  invFun J := {
    toFun := fun Œµ Œº => J Œº Œµ
    map_add' := by
      intro Œµ1 Œµ2
      funext Œº
      simp
    map_smul' := by
      intro c Œµ
      funext Œº
      simp
    cont := by
      apply Lorentz.Vector.continuous_of_apply
      fun_prop}
  left_inv := by
    intro J
    ext Œµ
    simp
  right_inv := by
    intro J
    ext Œº Œµ
    simp
  map_add' := by
    intro J1 J2
    ext Œº Œµ
    simp
  map_smul' := by
    intro c J
    ext Œº Œµ
    simp

end LorentzCurrentDensityD

namespace ElectromagneticPotentialD

open minkowskiMatrix

/-!

## F. The Lagrangian variational gradient

The variational gradient of the Lagrangian density with respect to the electromagnetic potential
which is a distribution. We do not prove this is correct, the proof
is done for the function case.

We take the definition to be:

```
‚àë ŒΩ, (Œ∑ ŒΩ ŒΩ ‚Ä¢ (‚àë Œº, ‚àÇ_ Œº (fun x => (A.fieldStrengthMatrix x) (Œº, ŒΩ)) x - J x ŒΩ)
  ‚Ä¢ Lorentz.Vector.basis ŒΩ)
```

which matches the result of the calculation from the function case.
-/

/-- The variational gradient of the lagrangian for an electromagnetic potential
  which is a distribution. This is defined nor proved for distributions. -/
noncomputable def gradLagrangian {d : ‚Ñï} (A : ElectromagneticPotentialD d)
    (J : LorentzCurrentDensityD d) :
    (Fin 1 ‚äï Fin d) ‚Üí (SpaceTime d) ‚Üíd[‚Ñù] ‚Ñù := fun ŒΩ =>
  Œ∑ ŒΩ ŒΩ ‚Ä¢ (‚àë Œº, SpaceTime.distDeriv Œº (A.fieldStrengthMatrix (Œº, ŒΩ)) - J.toComponents ŒΩ)

/-!

### F.1. The variational gradient in 1-dimension

We simplify the variational gradient in 1-dimension.

-/

lemma gradLagrangian_one_dimension_electricField (A : ElectromagneticPotentialD 1)
    (J : LorentzCurrentDensityD 1) :
    A.gradLagrangian J = fun Œº =>
      match Œº with
      | Sum.inl 0 => SpaceTime.distTimeSlice.symm
          (Space.distSpaceDiv A.electricField) - J.toComponents (Sum.inl 0)
      | Sum.inr 0 => J.toComponents (Sum.inr 0) +
        SpaceTime.distTimeSlice.symm
        (toComponentsEuclidean (Space.distTimeDeriv A.electricField) 0) := by
  funext Œº
  match Œº with
  | Sum.inl 0 =>
    simp [gradLagrangian]
    rw [fieldStrengthMatrix_col_eq_electricField]
    simp [SpaceTime.distDeriv_inr_distTimeSlice_symm]
    have h1 : ((Space.distSpaceDeriv 0) (toComponentsEuclidean A.electricField 0)) =
      Space.distSpaceDiv (A.electricField) := by
      ext Œµ
      rw [Space.distSpaceDiv_apply_eq_sum_distSpaceDeriv]
      simp only [Fin.isValue, Finset.univ_unique, Fin.default_eq_zero, Finset.sum_singleton]
      rw [Space.distSpaceDeriv_apply, Space.distSpaceDeriv_apply, Distribution.fderivD_apply,
        Distribution.fderivD_apply]
      simp
    rw [h1]
  | Sum.inr 0 =>
    simp [gradLagrangian]
    rw [fieldStrengthMatrix_row_eq_electricField]
    simp only [Fin.isValue, map_neg, sub_neg_eq_add, add_right_inj]
    rw [SpaceTime.distDeriv_inl_distTimeSlice_symm]
    have h1 : (Space.distTimeDeriv (toComponentsEuclidean A.electricField 0))
      = toComponentsEuclidean (Space.distTimeDeriv (A.electricField)) 0:= by
      ext Œµ
      simp [Space.distTimeDeriv_apply, Distribution.fderivD_apply,
        Distribution.fderivD_apply]
    rw [h1]
    simp

end ElectromagneticPotentialD

end Electromagnetism
