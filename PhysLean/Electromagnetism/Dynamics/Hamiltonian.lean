/-
Copyright (c) 2025 Joseph Tooby-Smith. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Joseph Tooby-Smith
-/
import PhysLean.Electromagnetism.Dynamics.Lagrangian
/-!

# The Hamiltonian in electromagnetism

## i. Overview

In this module we define the canonical momentum and the Hamiltonian for the
electromagnetic field in presence of a current density. We prove properties of these
quantities, and express the Hamiltonian in terms of the electric and magnetic fields
in the case of three spatial dimensions.

## ii. Key results

- `canonicalMomentum` : The canonical momentum for the electromagnetic field in presence of a
  Lorentz current density.
- `hamiltonian` : The Hamiltonian for the electromagnetic field in presence of a
  Lorentz current density.
- `hamiltonian_eq_electricField_magneticField` : The Hamiltonian expressed
  in terms of the electric and magnetic fields.

## iii. Table of contents

- A. The canonical momentum
  - A.1. The canonical momentum in terms of the kinetic term
  - A.2. The canonical momentum in terms of the field strength tensor
  - A.3. The canonical momentum in terms of the electric field
- B. The Hamiltonian
  - B.1. The hamiltonian in terms of the vector potential
  - B.2. The hamiltonian in terms of the electric and magnetic fields

## iv. References

- https://quantummechanics.ucsd.edu/ph130a/130_notes/node452.html
- https://ph.qmul.ac.uk/sites/default/files/EMT10new.pdf
-/

namespace Electromagnetism
open Module realLorentzTensor
open IndexNotation
open TensorSpecies
open Tensor ContDiff

namespace ElectromagneticPotential

open TensorSpecies
open Tensor
open SpaceTime
open TensorProduct
open minkowskiMatrix
open InnerProductSpace
open Lorentz.Vector
attribute [-simp] Fintype.sum_sum_type
attribute [-simp] Nat.succ_eq_add_one

/-!

## A. The canonical momentum

We define the canonical momentum for the lagrangian
`L(A, âˆ‚ A)` as gradient of `v â†¦ L(A + t v, âˆ‚ (A + t v)) - t * L(A + v, âˆ‚(A + v))` at `v = 0`
This is equivalent to `âˆ‚ L/âˆ‚ (âˆ‚_0 A)`.

-/

/-- The canonical momentum associated with the lagrangian of an electromagnetic potential
  and a Lorentz current density. -/
noncomputable def canonicalMomentum (ğ“• : FreeSpace) (A : ElectromagneticPotential d)
    (J : LorentzCurrentDensity d) :
    SpaceTime d â†’ Lorentz.Vector d := fun x =>
  gradient (fun (v : Lorentz.Vector d) =>
    lagrangian ğ“• (fun x => A x + x (Sum.inl 0) â€¢ v) J x) 0
  - x (Sum.inl 0) â€¢ gradient (fun (v : Lorentz.Vector d) =>
    lagrangian ğ“• (fun x => A x + v) J x) 0

/-!

### A.1. The canonical momentum in terms of the kinetic term

-/
lemma canonicalMomentum_eq_gradient_kineticTerm {d}
    {ğ“• : FreeSpace} (A : ElectromagneticPotential d)
    (hA : ContDiff â„ 2 A) (J : LorentzCurrentDensity d) :
    A.canonicalMomentum ğ“• J = fun x =>
    gradient (fun (v : Lorentz.Vector d) =>
    kineticTerm ğ“• (fun x => A x + x (Sum.inl 0) â€¢ v) x) 0:= by
  funext x
  apply ext_inner_right (ğ•œ := â„)
  intro v
  rw [gradient, canonicalMomentum]
  simp only [Fin.isValue, toDual_symm_apply]
  rw [inner_sub_left, inner_smul_left]
  simp [gradient]
  conv_lhs =>
    enter [2]
    simp [lagrangian_add_const]
  have hx : DifferentiableAt â„ (fun v => kineticTerm ğ“• (fun x => A x + x (Sum.inl 0) â€¢ v) x) 0 := by
    apply Differentiable.differentiableAt _
    conv =>
      enter [2, v]
      rw [kineticTerm_add_time_mul_const _ (hA.differentiable (by simp))]
    fun_prop

  conv_lhs =>
    enter [1]
    simp only [lagrangian, Fin.isValue, map_add, map_smul,
      LinearMap.smul_apply, smul_eq_mul]
    rw [fderiv_fun_sub hx (by simp [freeCurrentPotential]; fun_prop)]
    simp only [Fin.isValue, freeCurrentPotential, map_add, map_smul, ContinuousLinearMap.add_apply,
      ContinuousLinearMap.coe_smul', Pi.smul_apply, smul_eq_mul, fderiv_const_add,
      ContinuousLinearMap.coe_sub', Pi.sub_apply]
    rw [fderiv_const_mul (by fun_prop)]
  simp only [Fin.isValue, ContinuousLinearMap.coe_smul', Pi.smul_apply, smul_eq_mul]
  rw [fderiv_fun_sub (by fun_prop) (by fun_prop)]
  simp

/-!

### A.2. The canonical momentum in terms of the field strength tensor

-/

lemma canonicalMomentum_eq {d} {ğ“• : FreeSpace} (A : ElectromagneticPotential d)
    (hA : ContDiff â„ 2 A) (J : LorentzCurrentDensity d) :
    A.canonicalMomentum ğ“• J = fun x => fun Î¼ =>
      (1/ğ“•.Î¼â‚€) * Î· Î¼ Î¼ â€¢ A.fieldStrengthMatrix x (Î¼, Sum.inl 0) := by
  rw [canonicalMomentum_eq_gradient_kineticTerm A hA J]
  funext x
  apply ext_inner_right (ğ•œ := â„)
  intro v
  simp [gradient]
  conv_lhs =>
    enter [1, 2, v]
    rw [kineticTerm_add_time_mul_const _ (hA.differentiable (by simp))]
  simp only [Fin.isValue, Finset.sum_sub_distrib, one_div, fderiv_const_add]
  rw [fderiv_fun_add (by fun_prop) (by fun_prop)]
  rw [fderiv_const_mul (by fun_prop)]
  rw [fderiv_const_mul (by fun_prop)]
  rw [fderiv_fun_sub (by fun_prop) (by fun_prop)]
  rw [fderiv_fun_sum (by fun_prop)]
  rw [fderiv_fun_sum (by fun_prop)]
  conv_lhs =>
    enter [1, 1, 2, 1, 2, i]
    rw [fderiv_fun_add (by fun_prop) (by fun_prop)]
    rw [fderiv_mul_const (by fun_prop)]
    rw [fderiv_mul_const (by fun_prop)]
    rw [fderiv_const_mul (by fun_prop)]
    rw [fderiv_const_mul (by fun_prop)]
    rw [fderiv_pow _ (by fun_prop)]
    simp
  conv_lhs =>
    enter [1, 1, 2, 2, 2, i]
    rw [fderiv_mul_const (by fun_prop)]
    rw [fderiv_const_mul (by fun_prop)]
    simp
  rw [fderiv_pow _ (by fun_prop)]
  simp [Lorentz.Vector.coordCLM]
  rw [â† Finset.sum_sub_distrib]
  rw [Finset.mul_sum]
  congr
  ext Î¼
  simp only [Fin.isValue, RCLike.inner_apply, conj_trivial]
  simp only [Fin.isValue, equivEuclid_apply]
  rw [fieldStrengthMatrix, toFieldStrength_basis_repr_apply_eq_single]
  simp only [Fin.isValue, inl_0_inl_0, one_mul]
  ring_nf
  simp

/-!

### A.3. The canonical momentum in terms of the electric field

-/

lemma canonicalMomentum_eq_electricField {d} {ğ“• : FreeSpace} (A : ElectromagneticPotential d)
    (hA : ContDiff â„ 2 A) (J : LorentzCurrentDensity d) :
    A.canonicalMomentum ğ“• J = fun x => fun Î¼ =>
      match Î¼ with
      | Sum.inl 0 => 0
      | Sum.inr i => - (1/(ğ“•.Î¼â‚€ * ğ“•.c)) * A.electricField ğ“•.c (x.time ğ“•.c) x.space i := by
  rw [canonicalMomentum_eq A hA J]
  funext x Î¼
  match Î¼ with
  | Sum.inl 0 => simp
  | Sum.inr i =>
  simp only [one_div, inr_i_inr_i, Fin.isValue, smul_eq_mul, neg_mul, one_mul, mul_neg, mul_inv_rev,
    neg_inj]
  rw [electricField_eq_fieldStrengthMatrix]
  simp only [Fin.isValue, toTimeAndSpace_symm_apply_time_space, neg_mul, mul_neg]
  field_simp
  exact fieldStrengthMatrix_antisymm A x (Sum.inr i) (Sum.inl 0)
  exact hA.differentiable (by simp)
/-!

## B. The Hamiltonian

-/

/-- The Hamiltonian associated with an electromagnetic potential
  and a Lorentz current density. -/
noncomputable def hamiltonian (ğ“• : FreeSpace) (A : ElectromagneticPotential d)
    (J : LorentzCurrentDensity d) (x : SpaceTime d) : â„ :=
    âˆ‘ Î¼, A.canonicalMomentum ğ“• J x Î¼ * âˆ‚_ (Sum.inl 0) A x Î¼ - lagrangian ğ“• A J x

/-!

### B.1. The hamiltonian in terms of the vector potential
-/

open Time

lemma hamiltonian_eq_electricField_vectorPotential {d} {ğ“• : FreeSpace}
    (A : ElectromagneticPotential d) (hA : ContDiff â„ 2 A)
    (J : LorentzCurrentDensity d) (x : SpaceTime d) :
    A.hamiltonian ğ“• J x =
      - (1/ ğ“•.c.val^2 * ğ“•.Î¼â‚€â»Â¹) * âˆ‘ i, A.electricField ğ“•.c (x.time ğ“•.c) x.space i *
      (âˆ‚â‚œ (A.vectorPotential ğ“•.c Â· x.space) (x.time ğ“•.c) i) - lagrangian ğ“• A J x := by
  rw [hamiltonian]
  congr 1
  simp [Fintype.sum_sum_type, canonicalMomentum_eq_electricField A hA J]
  rw [Finset.mul_sum]
  congr
  funext i
  rw [SpaceTime.deriv_sum_inl ğ“•.c]
  rw [â† Time.deriv_euclid]
  simp [vectorPotential, timeSlice]
  ring_nf
  congr
  rw [â† Time.deriv_lorentzVector]
  rfl
  Â· change Differentiable â„ (A âˆ˜ fun t =>((toTimeAndSpace ğ“•.c).symm
      (t, ((toTimeAndSpace ğ“•.c) x).2)))
    apply Differentiable.comp
    Â· exact hA.differentiable (by simp)
    Â· fun_prop
  Â· apply vectorPotential_differentiable_time
    exact hA.differentiable (by simp)
  Â· exact hA.differentiable (by simp)

lemma hamiltonian_eq_electricField_scalarPotential {d} {ğ“• : FreeSpace}
    (A : ElectromagneticPotential d) (hA : ContDiff â„ 2 A)
    (J : LorentzCurrentDensity d) (x : SpaceTime d) :
    A.hamiltonian ğ“• J x =
      (1/ ğ“•.c.val^2 * ğ“•.Î¼â‚€â»Â¹) * (â€–A.electricField ğ“•.c (x.time ğ“•.c) x.spaceâ€– ^ 2
      + âŸªA.electricField ğ“•.c (x.time ğ“•.c) x.space,
        Space.grad (A.scalarPotential ğ“•.c (x.time ğ“•.c) Â·) x.spaceâŸ«_â„)
        - lagrangian ğ“• A J x := by
  rw [hamiltonian_eq_electricField_vectorPotential A hA J x]
  congr 1
  conv_lhs =>
    enter [2, 2, i]
    rw [time_deriv_vectorPotential_eq_electricField]
  simp [mul_sub, Finset.sum_sub_distrib]
  rw [EuclideanSpace.norm_sq_eq]
  ring_nf
  congr 1
  Â· simp
  congr
  funext i
  simp only [RCLike.inner_apply, conj_trivial]
  ring

/-!

### B.2. The hamiltonian in terms of the electric and magnetic fields

-/

lemma hamiltonian_eq_electricField_magneticField (A : ElectromagneticPotential d)
    (hA : ContDiff â„ 2 A) (J : LorentzCurrentDensity d) (x : SpaceTime d) :
    A.hamiltonian ğ“• J x = 1/2 * ğ“•.Îµâ‚€ * (â€–A.electricField ğ“•.c (x.time ğ“•.c) x.spaceâ€– ^ 2
      + ğ“•.c ^ 2 / 2 * âˆ‘ i, âˆ‘ j, â€–A.magneticFieldMatrix ğ“•.c (x.time ğ“•.c) x.space (i, j)â€– ^ 2)
      + ğ“•.Îµâ‚€ * âŸªA.electricField ğ“•.c (x.time ğ“•.c) x.space,
        Space.grad (A.scalarPotential ğ“•.c (x.time ğ“•.c) Â·) x.spaceâŸ«_â„
      + A.scalarPotential ğ“•.c (x.time ğ“•.c) x.space * J.chargeDensity ğ“•.c (x.time ğ“•.c) x.space
      - âˆ‘ i, A.vectorPotential ğ“•.c (x.time ğ“•.c) x.space i *
        J.currentDensity ğ“•.c (x.time ğ“•.c) x.space i := by
  rw [hamiltonian_eq_electricField_scalarPotential A hA J x]
  rw [lagrangian_eq_electric_magnetic A hA J x]
  simp [FreeSpace.c_sq ğ“•]
  field_simp
  ring

end ElectromagneticPotential

end Electromagnetism
