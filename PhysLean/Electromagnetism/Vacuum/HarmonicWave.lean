/-
Copyright (c) 2025 Zhi Kai Pong. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Zhi Kai Pong, Joseph Tooby-Smith
-/
import PhysLean.Electromagnetism.Vacuum.IsPlaneWave
/-!

# Harmonic Wave in Vacuum

## i. Overview

In this module we define the electromagnetic potential for a monochromatic harmonic wave
travelling in the x-direction in free space, and prove various properties about it,
including that it satisfies Maxwell's equations in free space, that it is a plane wave.

We work here in a general dimension `d` so we use the magnetic field is the
form of a matrix rather than a vector.

## ii. Key results

- `harmonicWaveX` : Definition of the electromagnetic
  potential for a harmonic wave travelling in the x-direction.
- `harmonicWaveX_isExtrema` : The harmonic wave satisfies Maxwell's equations in free space.
- `harmonicWaveX_isPlaneWave` : The harmonic wave is a plane wave.
- `harmonicWaveX_polarization_ellipse` : The polarization ellipse equation for the harmonic wave.

## iii. Table of contents

- A. The electromagnetic potential for a harmonic wave
  - A.1. Differentiability of the electromagnetic potential
  - A.2. Smoothness of the electromagnetic potential
- B. The scalar potential
- C. The vector potential
  - C.1. Components of the vector potential
  - C.2. Space derivatives of the vector potential
- D. The electric field
  - D.1. Components of the electric field
  - D.2. Spatial derivatives of the electric field
  - D.3. Time derivatives of the electric field
  - D.4. Divergence of the electric field
- E. The magnetic field matrix for a harmonic wave
  - E.1. Components of the magnetic field matrix
  - E.2. Space derivatives of the magnetic field matrix
- F. Maxwell's equations for a harmonic wave
- G. The harmonic wave is a plane wave
- H. Polarization ellipse of the harmonic wave

## iv. References

-/
namespace Electromagnetism

open Space Module
open Time
open ClassicalMechanics

variable (OM : OpticalMedium)
open Matrix
open Real
namespace ElectromagneticPotential
open InnerProductSpace

/-!

## A. The electromagnetic potential for a harmonic wave

-/

/-- The electromagnetic potential for a Harmonic wave travelling in the `x`-direction
  with wave number `k`. -/
noncomputable def harmonicWaveX (ğ“• : FreeSpace) (k : â„) (Eâ‚€ : Fin d â†’ â„)
  (Ï† : Fin d â†’ â„) : ElectromagneticPotential d.succ := fun x Î¼ =>
  match Î¼ with
  | Sum.inl 0 => 0
  | Sum.inr 0 => 0
  | Sum.inr âŸ¨Nat.succ i, hâŸ© => -Eâ‚€ âŸ¨i, Nat.succ_lt_succ_iff.mp hâŸ© * 1 / (ğ“•.c * k) *
      Real.sin (k * (ğ“•.c * x.time ğ“•.c - x.space 0) + Ï† âŸ¨i, Nat.succ_lt_succ_iff.mp hâŸ©)

/-!

### A.1. Differentiability of the electromagnetic potential

-/

lemma harmonicWaveX_differentiable {d} (ğ“• : FreeSpace) (k : â„)
    (Eâ‚€ : Fin d â†’ â„) (Ï† : Fin d â†’ â„) :
    Differentiable â„ (harmonicWaveX ğ“• k Eâ‚€ Ï†) := by
  rw [â† Lorentz.Vector.differentiable_apply]
  intro Î¼
  match Î¼ with
  | Sum.inl 0 => simp [harmonicWaveX]
  | Sum.inr âŸ¨0, hâŸ© => simp [harmonicWaveX]
  | Sum.inr âŸ¨Nat.succ i, hâŸ© =>
    simp [harmonicWaveX]
    apply Differentiable.const_mul
    fun_prop

/-!

### A.2. Smoothness of the electromagnetic potential

-/

lemma harmonicWaveX_contDiff {d} (n : WithTop â„•âˆ) (ğ“• : FreeSpace) (k : â„)
    (Eâ‚€ : Fin d â†’ â„) (Ï† : Fin d â†’ â„) :
    ContDiff â„ n (harmonicWaveX ğ“• k Eâ‚€ Ï†) := by
  rw [â† Lorentz.Vector.contDiff_apply]
  intro Î¼
  match Î¼ with
  | Sum.inl 0 => simp [harmonicWaveX]; fun_prop
  | Sum.inr âŸ¨0, hâŸ© => simp [harmonicWaveX]; fun_prop
  | Sum.inr âŸ¨Nat.succ i, hâŸ© =>
    simp [harmonicWaveX]
    apply ContDiff.mul
    Â· fun_prop
    Â· fun_prop

/-!

## B. The scalar potential

The scalar potential of the harmonic wave is zero.

-/

@[simp]
lemma harmonicWaveX_scalarPotential_eq_zero {d} (ğ“• : FreeSpace) (k : â„)
    (Eâ‚€ : Fin d â†’ â„) (Ï† : Fin d â†’ â„) :
    (harmonicWaveX ğ“• k Eâ‚€ Ï†).scalarPotential ğ“•.c = 0 := by
  ext x
  simp [harmonicWaveX, scalarPotential]
  rfl

/-!

## C. The vector potential

-/

/-!

### C.1. Components of the vector potential

-/

@[simp]
lemma harmonicWaveX_vectorPotential_zero_eq_zero {d} (ğ“• : FreeSpace) (k : â„)
    (Eâ‚€ : Fin d â†’ â„) (Ï† : Fin d â†’ â„) (t : Time) (x : Space d.succ) :
    (harmonicWaveX ğ“• k Eâ‚€ Ï†).vectorPotential ğ“•.c t x 0 = 0 := by
  simp [harmonicWaveX, vectorPotential, SpaceTime.timeSlice]
  rfl

lemma harmonicWaveX_vectorPotential_succ {d} (ğ“• : FreeSpace) (k : â„)
    (Eâ‚€ : Fin d â†’ â„) (Ï† : Fin d â†’ â„) (t : Time) (x : Space d.succ) (i : Fin d) :
    (harmonicWaveX ğ“• k Eâ‚€ Ï†).vectorPotential ğ“•.c t x i.succ =
    - Eâ‚€ i * 1 / (ğ“•.c * k) * Real.sin (k * (t.val * ğ“•.c - x 0) + Ï† i) := by
  simp [harmonicWaveX, vectorPotential, SpaceTime.timeSlice, Fin.succ]
  left
  ring_nf

lemma harmonicWaveX_vectorPotential_succ' {d} (ğ“• : FreeSpace) (k : â„)
    (Eâ‚€ : Fin d â†’ â„) (Ï† : Fin d â†’ â„) (t : Time) (x : Space d.succ) (i : â„•)
    (hi : i.succ < d.succ) :
    (harmonicWaveX ğ“• k Eâ‚€ Ï†).vectorPotential ğ“•.c t x âŸ¨i.succ, hiâŸ© =
    - Eâ‚€ âŸ¨i, by grindâŸ© * 1 / (ğ“•.c * k) * Real.sin (k * (t.val * ğ“•.c - x 0) + Ï† âŸ¨i, by grindâŸ©) := by
  simp [harmonicWaveX, vectorPotential, SpaceTime.timeSlice]
  left
  ring_nf

/-!

### C.2. Space derivatives of the vector potential

-/

open Space
@[simp]
lemma harmonicWaveX_vectorPotential_space_deriv_succ {d} (ğ“• : FreeSpace) (k : â„)
    (Eâ‚€ : Fin d â†’ â„) (Ï† : Fin d â†’ â„) (t : Time) (x : Space d.succ) (j : Fin d)
    (i : Fin d.succ) :
    Space.deriv j.succ (fun x => vectorPotential ğ“•.c (harmonicWaveX ğ“• k Eâ‚€ Ï†) t x i) x
    = 0 := by
  match i with
  | 0 => simp
  | âŸ¨Nat.succ i, hiâŸ© =>
    simp [harmonicWaveX_vectorPotential_succ']
    rw [Space.deriv_eq]
    rw [fderiv_const_mul (by fun_prop)]
    simp only [ContinuousLinearMap.coe_smul', Pi.smul_apply, smul_eq_mul, mul_eq_zero,
      div_eq_zero_iff, neg_eq_zero, SpeedOfLight.val_ne_zero, false_or]
    rw [fderiv_sin (by fun_prop)]
    simp only [fderiv_add_const, ContinuousLinearMap.coe_smul', Pi.smul_apply, smul_eq_mul,
      mul_eq_zero]
    right
    right
    rw [fderiv_const_mul (by fun_prop)]
    rw [fderiv_const_sub]
    simp only [smul_neg, ContinuousLinearMap.neg_apply, ContinuousLinearMap.coe_smul',
      Pi.smul_apply, smul_eq_mul, neg_eq_zero, mul_eq_zero]
    rw [â† Space.deriv_eq]
    rw [Space.deriv_component]
    simp

open Space
@[simp]
lemma harmonicWaveX_vectorPotential_succ_space_deriv_zero {d} (ğ“• : FreeSpace) (k : â„) (hk : k â‰  0)
    (Eâ‚€ : Fin d â†’ â„) (Ï† : Fin d â†’ â„) (t : Time) (x : Space d.succ) (i : Fin d) :
    Space.deriv 0 (fun x => vectorPotential ğ“•.c (harmonicWaveX ğ“• k Eâ‚€ Ï†) t x i.succ) x
    = Eâ‚€ i / ğ“•.c.val * Real.cos (ğ“•.c.val * k * t.val - k * x 0 + Ï† i) := by
  simp [harmonicWaveX_vectorPotential_succ]
  rw [Space.deriv_eq_fderiv_basis]
  rw [fderiv_const_mul (by fun_prop)]
  simp only [ContinuousLinearMap.coe_smul', Pi.smul_apply, smul_eq_mul]
  rw [fderiv_sin (by fun_prop)]
  simp only [fderiv_add_const, ContinuousLinearMap.coe_smul', Pi.smul_apply, smul_eq_mul]
  rw [fderiv_const_mul (by fun_prop)]
  rw [fderiv_const_sub]
  simp only [smul_neg, ContinuousLinearMap.neg_apply, ContinuousLinearMap.coe_smul', Pi.smul_apply,
    smul_eq_mul, mul_neg]
  rw [â† Space.deriv_eq_fderiv_basis]
  rw [Space.deriv_component]
  simp only [â†“reduceIte, mul_one]
  field_simp

/-!

## D. The electric field

-/

/-!

### D.1. Components of the electric field

-/
lemma harmonicWaveX_electricField_zero {d} (ğ“• : FreeSpace) (k : â„)
    (Eâ‚€ : Fin d â†’ â„) (Ï† : Fin d â†’ â„) (t : Time) (x : Space d.succ) :
    (harmonicWaveX ğ“• k Eâ‚€ Ï†).electricField ğ“•.c t x 0 = 0 := by
  simp [ElectromagneticPotential.electricField]
  rw [â† Time.deriv_euclid]
  simp only [harmonicWaveX_vectorPotential_zero_eq_zero, Time.deriv_const]
  refine vectorPotential_differentiable_time (harmonicWaveX ğ“• k Eâ‚€ Ï†) ?_ x
  exact harmonicWaveX_differentiable ğ“• k Eâ‚€ Ï†

lemma harmonicWaveX_electricField_succ {d} (ğ“• : FreeSpace) (k : â„) (hk : k â‰  0)
    (Eâ‚€ : Fin d â†’ â„) (Ï† : Fin d â†’ â„) (t : Time) (x : Space d.succ) (i : Fin d) :
    (harmonicWaveX ğ“• k Eâ‚€ Ï†).electricField ğ“•.c t x i.succ =
    Eâ‚€ i * Real.cos (k * ğ“•.c * t.val - k * x 0 + Ï† i) := by
  simp [ElectromagneticPotential.electricField]
  rw [â† Time.deriv_euclid]
  simp [harmonicWaveX_vectorPotential_succ]
  rw [Time.deriv_eq]
  rw [fderiv_const_mul]
  simp only [ContinuousLinearMap.coe_smul', Pi.smul_apply, smul_eq_mul]
  rw [fderiv_sin (by fun_prop)]
  rw [fderiv_add_const]
  rw [fderiv_const_mul (by fun_prop)]
  simp only [ContinuousLinearMap.coe_smul', Pi.smul_apply, smul_eq_mul]
  rw [fderiv_sub_const]
  rw [fderiv_mul_const (by fun_prop)]
  simp only [ContinuousLinearMap.coe_smul', Pi.smul_apply, fderiv_val, smul_eq_mul, mul_one]
  field_simp
  Â· fun_prop
  Â· refine vectorPotential_differentiable_time (harmonicWaveX ğ“• k Eâ‚€ Ï†) ?_ x
    exact harmonicWaveX_differentiable ğ“• k Eâ‚€ Ï†

/-!

### D.2. Spatial derivatives of the electric field

-/

lemma harmonicWaveX_electricField_space_deriv_same {d} (ğ“• : FreeSpace) (k : â„) (hk : k â‰  0)
    (Eâ‚€ : Fin d â†’ â„) (Ï† : Fin d â†’ â„) (t : Time) (x : Space d.succ) (i : Fin d.succ) :
    Space.deriv i (fun x => electricField ğ“•.c (harmonicWaveX ğ“• k Eâ‚€ Ï†) t x i) x
    = 0 := by
  match i with
  | 0 => simp [harmonicWaveX_electricField_zero]
  | âŸ¨Nat.succ i, hiâŸ© =>
    rw [â† Fin.succ_mk _ _ (by grind)]
    conv_lhs =>
      enter [2, x]
      rw [harmonicWaveX_electricField_succ _ _ hk]
    rw [Space.deriv_eq]
    rw [fderiv_const_mul (by fun_prop)]
    simp only [Nat.succ_eq_add_one, Fin.succ_mk, ContinuousLinearMap.coe_smul', Pi.smul_apply,
      smul_eq_mul, mul_eq_zero]
    rw [fderiv_cos (by fun_prop)]
    simp only [fderiv_add_const, neg_smul, ContinuousLinearMap.neg_apply,
      ContinuousLinearMap.coe_smul', Pi.smul_apply, smul_eq_mul, neg_eq_zero, mul_eq_zero]
    right
    right
    rw [fderiv_const_sub]
    simp only [ContinuousLinearMap.neg_apply, neg_eq_zero]
    rw [fderiv_const_mul (by fun_prop)]
    simp only [ContinuousLinearMap.coe_smul', Pi.smul_apply, smul_eq_mul, mul_eq_zero]
    rw [â† Space.deriv_eq]
    rw [Space.deriv_component]
    simp

/-!

### D.3. Time derivatives of the electric field

-/

lemma harmonicWaveX_electricField_succ_time_deriv {d} (ğ“• : FreeSpace) (k : â„) (hk : k â‰  0)
    (Eâ‚€ : Fin d â†’ â„) (Ï† : Fin d â†’ â„) (t : Time) (x : Space d.succ) (i : Fin d) :
    Time.deriv (fun t => electricField ğ“•.c (harmonicWaveX ğ“• k Eâ‚€ Ï†) t x i.succ) t
    = - k * ğ“•.c * Eâ‚€ i * Real.sin (k * ğ“•.c * t.val - k * x 0 + Ï† i) := by
  conv_lhs =>
    enter [1, t]
    rw [harmonicWaveX_electricField_succ _ _ hk]
  rw [Time.deriv_eq]
  rw [fderiv_const_mul (by fun_prop)]
  simp only [Nat.succ_eq_add_one, ContinuousLinearMap.coe_smul', Pi.smul_apply, smul_eq_mul,
    neg_mul]
  rw [fderiv_cos (by fun_prop)]
  simp only [fderiv_add_const, neg_smul, ContinuousLinearMap.neg_apply,
    ContinuousLinearMap.coe_smul', Pi.smul_apply, smul_eq_mul, mul_neg, neg_inj]
  rw [fderiv_sub_const]
  rw [fderiv_const_mul (by fun_prop)]
  simp only [ContinuousLinearMap.coe_smul', Pi.smul_apply, fderiv_val, smul_eq_mul, mul_one]
  ring

/-!

### D.4. Divergence of the electric field

-/

@[simp]
lemma harmonicWaveX_div_electricField_eq_zero {d} (ğ“• : FreeSpace) (k : â„) (hk : k â‰  0)
    (Eâ‚€ : Fin d â†’ â„) (Ï† : Fin d â†’ â„) (t : Time) (x : Space d.succ) :
    Space.div (fun x => electricField ğ“•.c (harmonicWaveX ğ“• k Eâ‚€ Ï†) t x) x = 0 := by
  simp [Space.div, Space.coord]
  apply Finset.sum_eq_zero
  intro i _
  exact harmonicWaveX_electricField_space_deriv_same ğ“• k hk Eâ‚€ Ï† t x i

/-!

## E. The magnetic field matrix for a harmonic wave
-/

/-!

### E.1. Components of the magnetic field matrix

-/

@[simp]
lemma harmonicWaveX_magneticFieldMatrix_succ_succ {d} (ğ“• : FreeSpace) (k : â„)
    (Eâ‚€ : Fin d â†’ â„) (Ï† : Fin d â†’ â„) (t : Time) (x : Space d.succ)
    (i j : Fin d) :
    (harmonicWaveX ğ“• k Eâ‚€ Ï†).magneticFieldMatrix ğ“•.c t x (i.succ, j.succ) = 0 := by
  rw [magneticFieldMatrix_eq_vectorPotential]
  simp only [Nat.succ_eq_add_one, harmonicWaveX_vectorPotential_space_deriv_succ, sub_self]
  exact harmonicWaveX_differentiable ğ“• k Eâ‚€ Ï†

lemma harmonicWaveX_magneticFieldMatrix_zero_succ {d} (ğ“• : FreeSpace) (k : â„) (hk : k â‰  0)
    (Eâ‚€ : Fin d â†’ â„) (Ï† : Fin d â†’ â„) (t : Time) (x : Space d.succ)
    (i : Fin d) :
    (harmonicWaveX ğ“• k Eâ‚€ Ï†).magneticFieldMatrix ğ“•.c t x (0, i.succ) =
    (- Eâ‚€ i / ğ“•.c.val) * cos (ğ“•.c.val * k * t.val - k * x 0 + Ï† i) := by
  rw [magneticFieldMatrix_eq_vectorPotential]
  simp only [Nat.succ_eq_add_one, harmonicWaveX_vectorPotential_zero_eq_zero, Space.deriv_const,
    zero_sub]
  rw [harmonicWaveX_vectorPotential_succ_space_deriv_zero]
  simp only [Nat.succ_eq_add_one]
  ring
  grind
  exact harmonicWaveX_differentiable ğ“• k Eâ‚€ Ï†

lemma harmonicWaveX_magneticFieldMatrix_succ_zero {d} (ğ“• : FreeSpace) (k : â„) (hk : k â‰  0)
    (Eâ‚€ : Fin d â†’ â„) (Ï† : Fin d â†’ â„) (t : Time) (x : Space d.succ)
    (i : Fin d) :
    (harmonicWaveX ğ“• k Eâ‚€ Ï†).magneticFieldMatrix ğ“•.c t x (i.succ, 0) =
    (Eâ‚€ i / ğ“•.c.val) * cos (ğ“•.c.val * k * t.val - k * x 0 + Ï† i) := by
  rw [magneticFieldMatrix_eq_vectorPotential]
  simp only [Nat.succ_eq_add_one, harmonicWaveX_vectorPotential_zero_eq_zero, Space.deriv_const,
    sub_zero]
  rw [harmonicWaveX_vectorPotential_succ_space_deriv_zero]
  simp only [ne_eq]
  grind
  exact harmonicWaveX_differentiable ğ“• k Eâ‚€ Ï†

/-!

### E.2. Space derivatives of the magnetic field matrix

-/

lemma harmonicWaveX_magneticFieldMatrix_space_deriv_succ {d} (ğ“• : FreeSpace) (k : â„)
    (hk : k â‰  0)
    (Eâ‚€ : Fin d â†’ â„) (Ï† : Fin d â†’ â„) (t : Time) (x : Space d.succ)
    (i j : Fin d.succ) (l : Fin d) :
    Space.deriv l.succ (fun x => magneticFieldMatrix ğ“•.c (harmonicWaveX ğ“• k Eâ‚€ Ï†) t x (i, j)) x
    = 0 := by
  match i, j with
  | 0, 0 => simp
  | âŸ¨Nat.succ i, hiâŸ©, âŸ¨Nat.succ j, hjâŸ© =>
    conv_lhs =>
      enter [2, x]
      rw [â† Fin.succ_mk _ _ (by grind)]
      rw [â† Fin.succ_mk _ _ (by grind)]
      rw [harmonicWaveX_magneticFieldMatrix_succ_succ _ _]
    simp
  | 0, âŸ¨Nat.succ j, hjâŸ© =>
    conv_lhs =>
      enter [2, x]
      rw [â† Fin.succ_mk _ _ (by grind)]
      rw [harmonicWaveX_magneticFieldMatrix_zero_succ _ k hk]
    have h1 (i : Fin d) : Space.deriv l.succ
        (fun x => - Eâ‚€ i / ğ“•.c.val * cos (ğ“•.c.val * k * t.val - k * x 0 + Ï† i)) x
        = 0 := by
      rw [Space.deriv_eq]
      rw [fderiv_const_mul]
      simp only [ContinuousLinearMap.coe_smul', Pi.smul_apply, smul_eq_mul, mul_eq_zero,
        div_eq_zero_iff, neg_eq_zero, SpeedOfLight.val_ne_zero, or_false]
      rw [fderiv_cos (by fun_prop)]
      simp only [fderiv_add_const, neg_smul, ContinuousLinearMap.neg_apply,
        ContinuousLinearMap.coe_smul', Pi.smul_apply, smul_eq_mul, neg_eq_zero, mul_eq_zero]
      right
      right
      rw [fderiv_const_sub]
      simp only [ContinuousLinearMap.neg_apply, neg_eq_zero]
      rw [fderiv_const_mul (by fun_prop)]
      simp only [ContinuousLinearMap.coe_smul', Pi.smul_apply, smul_eq_mul, mul_eq_zero]
      rw [â† Space.deriv_eq]
      rw [Space.deriv_component]
      simp only [Fin.succ_ne_zero, â†“reduceIte, or_true]
      fun_prop
    rw [â† h1 âŸ¨j, by grindâŸ©]

  | âŸ¨Nat.succ j, hjâŸ©, 0 =>
    conv_lhs =>
      enter [2, x]
      rw [â† Fin.succ_mk _ _ (by grind)]
      rw [harmonicWaveX_magneticFieldMatrix_succ_zero _ k hk]
    have h1 (i : Fin d) : Space.deriv l.succ
        (fun x => Eâ‚€ i / ğ“•.c.val * cos (ğ“•.c.val * k * t.val - k * x 0 + Ï† i)) x
        = 0 := by
      rw [Space.deriv_eq]
      rw [fderiv_const_mul]
      simp only [ContinuousLinearMap.coe_smul', Pi.smul_apply, smul_eq_mul, mul_eq_zero,
        div_eq_zero_iff, SpeedOfLight.val_ne_zero, or_false]
      rw [fderiv_cos (by fun_prop)]
      simp only [fderiv_add_const, neg_smul, ContinuousLinearMap.neg_apply,
        ContinuousLinearMap.coe_smul', Pi.smul_apply, smul_eq_mul, neg_eq_zero, mul_eq_zero]
      right
      right
      rw [fderiv_const_sub]
      simp only [ContinuousLinearMap.neg_apply, neg_eq_zero]
      rw [fderiv_const_mul (by fun_prop)]
      simp only [ContinuousLinearMap.coe_smul', Pi.smul_apply, smul_eq_mul, mul_eq_zero]
      rw [â† Space.deriv_eq]
      rw [Space.deriv_component]
      simp only [Fin.succ_ne_zero, â†“reduceIte, or_true]
      fun_prop
    rw [â† h1 âŸ¨j, by grindâŸ©]

lemma harmonicWaveX_magneticFieldMatrix_zero_succ_space_deriv_zero {d} (ğ“• : FreeSpace) (k : â„)
    (hk : k â‰  0)
    (Eâ‚€ : Fin d â†’ â„) (Ï† : Fin d â†’ â„) (t : Time) (x : Space d.succ)
    (i : Fin d) :
    Space.deriv 0 (fun x => magneticFieldMatrix ğ“•.c (harmonicWaveX ğ“• k Eâ‚€ Ï†) t x (0, i.succ)) x
    = -Eâ‚€ i * k / ğ“•.c.val * sin (ğ“•.c.val * k * t.val - k * x 0 + Ï† i) := by
  conv_lhs =>
    enter [2, x]
    rw [harmonicWaveX_magneticFieldMatrix_zero_succ _ k hk]
  rw [Space.deriv_eq]
  rw [fderiv_const_mul]
  simp only [Nat.succ_eq_add_one, ContinuousLinearMap.coe_smul', Pi.smul_apply, smul_eq_mul,
    neg_mul]
  rw [fderiv_cos (by fun_prop)]
  simp only [fderiv_add_const, neg_smul, ContinuousLinearMap.neg_apply,
    ContinuousLinearMap.coe_smul', Pi.smul_apply, smul_eq_mul, mul_neg]
  rw [fderiv_const_sub]
  simp only [ContinuousLinearMap.neg_apply, mul_neg, neg_neg]
  rw [fderiv_const_mul (by fun_prop)]
  simp only [ContinuousLinearMap.coe_smul', Pi.smul_apply, smul_eq_mul]
  rw [â† Space.deriv_eq]
  rw [Space.deriv_component]
  simp only [â†“reduceIte, mul_one]
  ring
  fun_prop

/-!

## F. Maxwell's equations for a harmonic wave

-/

lemma harmonicWaveX_isExtrema {d} (ğ“• : FreeSpace) (k : â„) (hk : k â‰  0)
    (Eâ‚€ : Fin d â†’ â„) (Ï† : Fin d â†’ â„) :
    IsExtrema ğ“• (harmonicWaveX ğ“• k Eâ‚€ Ï†) 0 := by
  rw [isExtrema_iff_gauss_ampere_magneticFieldMatrix]
  intro t x
  apply And.intro
  /- Gauss's law -/
  Â· simp
    rw [harmonicWaveX_div_electricField_eq_zero ğ“• k hk Eâ‚€ Ï† t x]
  /- AmpÃ¨re's law -/
  Â· intro i
    rw [Fin.sum_univ_succ]
    conv_rhs =>
      enter [1, 2, 2, i]
      rw [harmonicWaveX_magneticFieldMatrix_space_deriv_succ _ _ hk]
    simp
    rcases Fin.eq_zero_or_eq_succ i with rfl | âŸ¨i, rflâŸ©
    Â· simp
      rw [â† Time.deriv_euclid]
      conv_lhs =>
        enter [1, t]
        rw [harmonicWaveX_electricField_zero ğ“• k Eâ‚€]
      simp only [Time.deriv_const]
      refine electricField_differentiable_time ?_ x
      exact harmonicWaveX_contDiff 2 ğ“• k Eâ‚€ Ï†
    rw [harmonicWaveX_magneticFieldMatrix_zero_succ_space_deriv_zero _ k hk]
    rw [â† Time.deriv_euclid]
    rw [harmonicWaveX_electricField_succ_time_deriv _ _ hk]
    field_simp
    simp [ğ“•.c_sq]
    field_simp
    ring_nf
    left
    trivial
    apply electricField_differentiable_time
    exact harmonicWaveX_contDiff 2 ğ“• k Eâ‚€ Ï†
  Â· apply harmonicWaveX_contDiff
  Â· change ContDiff â„ _ (fun _ => 0)
    fun_prop

/-!

## G. The harmonic wave is a plane wave

-/

lemma harmonicWaveX_isPlaneWave {d} (ğ“• : FreeSpace) (k : â„) (hk : k â‰  0)
    (Eâ‚€ : Fin d â†’ â„) (Ï† : Fin d â†’ â„) :
    IsPlaneWave ğ“• (harmonicWaveX ğ“• k Eâ‚€ Ï†) âŸ¨Space.basis 0, by simpâŸ© := by
  apply And.intro
  Â· use fun u => WithLp.toLp 2 fun i =>
      match i with
      | 0 => 0
      | âŸ¨Nat.succ i, hâŸ© => Eâ‚€ âŸ¨i, by grindâŸ© * cos (-k * u + Ï† âŸ¨i, by grindâŸ©)
    ext t x i
    match i with
    | 0 =>
      simp [harmonicWaveX_electricField_zero, planeWave]
      rfl
    | âŸ¨Nat.succ i, hâŸ© =>
      simp only [Nat.succ_eq_add_one, neg_mul]
      rw [â† Fin.succ_mk _ _ (by grind)]
      rw [harmonicWaveX_electricField_succ _ _ hk]
      simp [planeWave]
      left
      congr
      ring
  Â· use fun u ij =>
      match ij with
      | (0, 0) => 0
      | (0, âŸ¨Nat.succ j, hjâŸ©) =>
        (- Eâ‚€ âŸ¨j, by grindâŸ© / ğ“•.c.val) * cos (-k * u + Ï† âŸ¨j, by grindâŸ©)
      | (âŸ¨Nat.succ i, hiâŸ©, 0) =>
        (Eâ‚€ âŸ¨i, by grindâŸ© / ğ“•.c.val) * cos (-k * u + Ï† âŸ¨i, by grindâŸ©)
      | (âŸ¨Nat.succ i, hiâŸ©, âŸ¨Nat.succ j, hjâŸ©) => 0
    intro t x
    ext ij
    match ij with
    | (0, 0) =>
      simp only [Nat.succ_eq_add_one, magneticFieldMatrix_diag_eq_zero, inner_basis, neg_mul]
      rfl
    | (âŸ¨0, h0âŸ©, âŸ¨Nat.succ j, hjâŸ©) =>
      simp only [Nat.succ_eq_add_one, Fin.zero_eta, inner_basis, neg_mul]
      rw [â† Fin.succ_mk _ _ (by grind)]
      rw [harmonicWaveX_magneticFieldMatrix_zero_succ _ k hk]
      simp only [Nat.succ_eq_add_one, mul_eq_mul_left_iff, div_eq_zero_iff, neg_eq_zero,
        SpeedOfLight.val_ne_zero, or_false]
      left
      congr
      ring
    | (âŸ¨Nat.succ i, hiâŸ©, âŸ¨0, h0âŸ©) =>
      simp only [Nat.succ_eq_add_one, Fin.zero_eta, inner_basis, neg_mul]
      rw [â† Fin.succ_mk _ _ (by grind)]
      rw [harmonicWaveX_magneticFieldMatrix_succ_zero _ k hk]
      simp only [Nat.succ_eq_add_one, mul_eq_mul_left_iff, div_eq_zero_iff,
        SpeedOfLight.val_ne_zero, or_false]
      left
      congr
      ring
    | (âŸ¨Nat.succ i, hiâŸ©, âŸ¨Nat.succ j, hjâŸ©) =>
      simp only [Nat.succ_eq_add_one]
      rw [â† Fin.succ_mk _ _ (by grind)]
      rw [â† Fin.succ_mk _ _ (by grind)]
      rw [harmonicWaveX_magneticFieldMatrix_succ_succ _ _]

/-!

## H. Polarization ellipse of the harmonic wave

-/

open Real in
lemma harmonicWaveX_polarization_ellipse {d} (ğ“• : FreeSpace) (k : â„) (hk : k â‰  0)
    (Eâ‚€ : Fin d â†’ â„) (Ï† : Fin d â†’ â„) (t : Time) (x : Space d.succ) (hi : âˆ€ i, Eâ‚€ i â‰  0) :
    2 * d * âˆ‘ i : Fin d, ((harmonicWaveX ğ“• k Eâ‚€ Ï†).electricField ğ“•.c t x i.succ / Eâ‚€ i) ^ 2 -
    2 * âˆ‘ i, âˆ‘ j, ((harmonicWaveX ğ“• k Eâ‚€ Ï†).electricField ğ“•.c t x i.succ / Eâ‚€ i) *
      ((harmonicWaveX ğ“• k Eâ‚€ Ï†).electricField ğ“•.c t x j.succ / Eâ‚€ j) *
      Real.cos (Ï† j - Ï† i) =
    âˆ‘ i, âˆ‘ j, Real.sin (Ï† j - Ï† i) ^ 2 := by
  have h1 (i : Fin d) : (harmonicWaveX ğ“• k Eâ‚€ Ï†).electricField ğ“•.c t x i.succ / Eâ‚€ i
    = Real.cos (k * ğ“•.c * t.val - k * x 0 + Ï† i) := by
    rw [harmonicWaveX_electricField_succ ğ“• k hk Eâ‚€ Ï† t x i]
    specialize hi i
    field_simp
  conv_lhs =>
    enter [1, 2, 2, i]
    rw [h1]
  conv_lhs =>
    enter [2, 2, 2, i, 2, j]
    rw [h1, h1]
  let Ï„ := k * ğ“•.c * t.val - k * x 0
  have hij (i j : Fin d) :
      cos (Ï„ + Ï† i) ^ 2 + cos (Ï„ + Ï† j) ^ 2
      - 2 * cos (Ï„ + Ï† i) * cos (Ï„ + Ï† j) * cos (Ï† j - Ï† i) = sin (Ï† j - Ï† i) ^ 2 := by
    calc _
      _ = cos (Ï„ + Ï† i) ^ 2 * (sin (Ï† j) ^ 2 + cos (Ï† j) ^ 2) + cos (Ï„ + Ï† j) ^ 2
        * (sin (Ï† i) ^ 2 + cos (Ï† i) ^ 2)
        - 2 * cos (Ï„ + Ï† i) * cos (Ï„ + Ï† j) * cos (Ï† j - Ï† i) := by simp
      _ = (cos (Ï„) * sin (Ï† j - Ï† i)) ^ 2 + (sin (Ï„) * sin (Ï† j - Ï† i)) ^ 2 := by
        have h1 : cos (Ï„ + Ï† i) * sin (Ï† j) - cos (Ï„ + Ï† j) * sin (Ï† i) =
            cos Ï„ * sin (Ï† j - Ï† i) := by
          field_simp
          symm
          rw [cos_add, cos_add, sin_sub]
          ring
        have h2 : cos (Ï„ + Ï† i) * cos (Ï† j) - cos (Ï„ + Ï† j) * cos (Ï† i) =
            sin Ï„ * sin (Ï† j - Ï† i) := by
          field_simp
          conv_lhs => enter [1]; rw [cos_add]
          conv_lhs => enter [2]; rw [cos_add]
          conv_rhs => enter [2]; rw [sin_sub]
          ring
        rw [â† h1, â† h2]
        rw [cos_sub]
        ring
      _ = sin (Ï† j - Ï† i) ^ 2 * (cos (Ï„) ^ 2 + sin (Ï„) ^ 2) := by ring
      _ = sin (Ï† j - Ï† i) ^ 2 := by simp
  symm
  calc _
    _ = âˆ‘ (i : Fin d), âˆ‘ (j : Fin d), (cos (Ï„ + Ï† i) ^ 2 + cos (Ï„ + Ï† j) ^ 2
        - 2 * cos (Ï„ + Ï† i) * cos (Ï„ + Ï† j) * cos (Ï† j - Ï† i)) := by
      simp [â† hij]
    _ = 2 * âˆ‘ (i : Fin d), âˆ‘ (j : Fin d), cos (Ï„ + Ï† j) ^ 2
        - 2 * âˆ‘ (i : Fin d), âˆ‘ (j : Fin d), cos (Ï„ + Ï† i) * cos (Ï„ + Ï† j) * cos (Ï† j - Ï† i) := by
      rw [two_mul]
      conv_rhs =>
        enter [1, 1]
        rw [Finset.sum_comm]
      rw [â† Finset.sum_add_distrib, Finset.mul_sum, â† Finset.sum_sub_distrib]
      congr
      funext i
      rw [â† Finset.sum_add_distrib, Finset.mul_sum, â† Finset.sum_sub_distrib]
      congr
      funext j
      ring
    _ = 2 * d * âˆ‘ (j : Fin d), cos (Ï„ + Ï† j) ^ 2
        - 2 * âˆ‘ (i : Fin d), âˆ‘ (j : Fin d), cos (Ï„ + Ï† i) * cos (Ï„ + Ï† j) * cos (Ï† j - Ï† i) := by
      rw [Finset.sum_const, Finset.card_fin]
      ring

end ElectromagneticPotential

end Electromagnetism
