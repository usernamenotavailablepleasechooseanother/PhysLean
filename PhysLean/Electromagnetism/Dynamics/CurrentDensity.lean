/-
Copyright (c) 2025 Joseph Tooby-Smith. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Joseph Tooby-Smith
-/
import PhysLean.SpaceAndTime.SpaceTime.TimeSlice
/-!

# The Lorentz Current Density

## i. Overview

In this module we define the Lorentz current density
and its decomposition into charge density and current density.
The Lorentz current density is often called the four-current and given then the symbol `J`.

The current density is given in terms of the charge density `Ï` and the current density
` \vec j` as `J = (c Ï, \vec j)`.

## ii. Key results

- `LorentzCurrentDensity` : The type of Lorentz current densities.
- `LorentzCurrentDensity.chargeDensity` : The charge density associated with a
  Lorentz current density.
- `LorentzCurrentDensity.currentDensity` : The current density associated with a
  Lorentz current density.

## iii. Table of contents

- A. The Lorentz Current Density
- B. The underlying charge
  - B.1. Charge density of zero Lorentz current density
  - B.2. Differentiability of the charge density
  - B.3. Smoothness of the charge density
- C. The underlying current density
  - C.1. current density of zero Lorentz current density
  - C.2. Differentiability of the current density
  - C.3. Smoothness of the current density

## iv. References

-/

namespace Electromagnetism
open TensorSpecies
open SpaceTime
open TensorProduct
open minkowskiMatrix
open InnerProductSpace

attribute [-simp] Fintype.sum_sum_type
attribute [-simp] Nat.succ_eq_add_one

/-!

## A. The Lorentz Current Density

The Lorentz current density is a Lorentz Vector field on spacetime.

-/

/-- The Lorentz current density, also called four-current. -/
abbrev LorentzCurrentDensity (d : â„• := 3) := SpaceTime d â†’ Lorentz.Vector d

namespace LorentzCurrentDensity

/-!

## B. The underlying charge

-/

/-- The underlying charge density associated with a Lorentz current density. -/
noncomputable def chargeDensity (c : SpeedOfLight := 1)
    (J : LorentzCurrentDensity d) : Time â†’ Space d â†’ â„ :=
  fun t x => (1 / (c : â„)) * J ((toTimeAndSpace c).symm (t, x)) (Sum.inl 0)

lemma chargeDensity_eq_timeSlice {d : â„•} {c : SpeedOfLight} {J : LorentzCurrentDensity d} :
    J.chargeDensity c = timeSlice c (fun x => (1 / (c : â„)) â€¢ J x (Sum.inl 0)) := by rfl

/-!

### B.1. Charge density of zero Lorentz current density

-/

@[simp]
lemma chargeDensity_zero {d : â„•} {c : SpeedOfLight}:
    chargeDensity c (0 : LorentzCurrentDensity d) = 0 := by
  simp [chargeDensity_eq_timeSlice, timeSlice]
  rfl

/-!

### B.2. Differentiability of the charge density
-/

lemma chargeDensity_differentiable {d : â„•} {c : SpeedOfLight} {J : LorentzCurrentDensity d}
    (hJ : Differentiable â„ J) : Differentiable â„ â†¿(J.chargeDensity c) := by
  rw [chargeDensity_eq_timeSlice]
  apply timeSlice_differentiable
  have h1 : âˆ€ i, Differentiable â„ (fun x => J x i) := by
    rw [SpaceTime.differentiable_vector]
    exact hJ
  apply Differentiable.fun_const_smul
  exact h1 (Sum.inl 0)

lemma chargeDensity_differentiable_space {d : â„•} {c : SpeedOfLight} {J : LorentzCurrentDensity d}
    (hJ : Differentiable â„ J) (t : Time) :
    Differentiable â„ (fun x => J.chargeDensity c t x) := by
  change Differentiable â„ (â†¿(J.chargeDensity c) âˆ˜ fun x => (t, x))
  refine Differentiable.comp ?_ ?_
  Â· exact chargeDensity_differentiable hJ
  Â· fun_prop

/-!

### B.3. Smoothness of the charge density

-/

lemma chargeDensity_contDiff {d : â„•} {c : SpeedOfLight} {J : LorentzCurrentDensity d}
    (hJ : ContDiff â„ n J) : ContDiff â„ n â†¿(J.chargeDensity c) := by
  rw [chargeDensity_eq_timeSlice]
  apply timeSlice_contDiff
  have h1 : âˆ€ i, ContDiff â„ n (fun x => J x i) := by
    rw [SpaceTime.contDiff_vector]
    exact hJ
  apply ContDiff.const_smul
  exact h1 (Sum.inl 0)

/-!

## C. The underlying current density

-/

/-- The underlying (non-Lorentz) current density associated with a Lorentz current density. -/
noncomputable def currentDensity (c : SpeedOfLight := 1) (J : LorentzCurrentDensity d) :
    Time â†’ Space d â†’ EuclideanSpace â„ (Fin d) :=
  fun t x => WithLp.toLp 2 fun i => J ((toTimeAndSpace c).symm (t, x)) (Sum.inr i)

lemma currentDensity_eq_timeSlice {d : â„•} {J : LorentzCurrentDensity d} :
    J.currentDensity c = timeSlice c (fun x => WithLp.toLp 2
      fun i => J x (Sum.inr i)) := by rfl

/-!

### C.1. current density of zero Lorentz current density

-/

@[simp]
lemma currentDensity_zero {d : â„•} {c : SpeedOfLight}:
    currentDensity c (0 : LorentzCurrentDensity d) = 0 := by
  simp [currentDensity_eq_timeSlice, timeSlice]
  rfl
/-!

### C.2. Differentiability of the current density

-/

lemma currentDensity_differentiable {d : â„•} {c : SpeedOfLight} {J : LorentzCurrentDensity d}
    (hJ : Differentiable â„ J) : Differentiable â„ â†¿(J.currentDensity c) := by
  rw [currentDensity_eq_timeSlice]
  apply timeSlice_differentiable
  have h1 : âˆ€ i, Differentiable â„ (fun x => J x i) := by
    rw [SpaceTime.differentiable_vector]
    exact hJ
  exact differentiable_euclidean.mpr fun i => h1 (Sum.inr i)

lemma currentDensity_apply_differentiable {d : â„•} {c : SpeedOfLight} {J : LorentzCurrentDensity d}
    (hJ : Differentiable â„ J) (i : Fin d) :
    Differentiable â„ â†¿(fun t x => J.currentDensity c t x i) := by
  change Differentiable â„ (EuclideanSpace.proj i âˆ˜ â†¿(J.currentDensity c))
  refine Differentiable.comp ?_ ?_
  Â· exact ContinuousLinearMap.differentiable (ð•œ := â„) (EuclideanSpace.proj i)
  Â· exact currentDensity_differentiable hJ

lemma currentDensity_differentiable_space {d : â„•} {c : SpeedOfLight} {J : LorentzCurrentDensity d}
    (hJ : Differentiable â„ J) (t : Time) :
    Differentiable â„ (fun x => J.currentDensity c t x) := by
  change Differentiable â„ (â†¿(J.currentDensity c) âˆ˜ fun x => (t, x))
  refine Differentiable.comp ?_ ?_
  Â· exact currentDensity_differentiable hJ
  Â· fun_prop

lemma currentDensity_apply_differentiable_space {d : â„•} {c : SpeedOfLight}
    {J : LorentzCurrentDensity d}
    (hJ : Differentiable â„ J) (t : Time) (i : Fin d) :
    Differentiable â„ (fun x => J.currentDensity c t x i) := by
  change Differentiable â„ (EuclideanSpace.proj i âˆ˜ (â†¿(J.currentDensity c) âˆ˜ fun x => (t, x)))
  refine Differentiable.comp ?_ ?_
  Â· exact ContinuousLinearMap.differentiable (ð•œ := â„) _
  Â· apply Differentiable.comp ?_ ?_
    Â· exact currentDensity_differentiable hJ
    Â· fun_prop

lemma currentDensity_differentiable_time {d : â„•} {c : SpeedOfLight} {J : LorentzCurrentDensity d}
    (hJ : Differentiable â„ J) (x : Space d) :
    Differentiable â„ (fun t => J.currentDensity c t x) := by
  change Differentiable â„ (â†¿(J.currentDensity c) âˆ˜ fun t => (t, x))
  refine Differentiable.comp ?_ ?_
  Â· exact currentDensity_differentiable hJ
  Â· fun_prop

lemma currentDensity_apply_differentiable_time {d : â„•} {c : SpeedOfLight}
    {J : LorentzCurrentDensity d}
    (hJ : Differentiable â„ J) (x : Space d) (i : Fin d) :
    Differentiable â„ (fun t => J.currentDensity c t x i) := by
  change Differentiable â„ (EuclideanSpace.proj i âˆ˜ (â†¿(J.currentDensity c) âˆ˜ fun t => (t, x)))
  refine Differentiable.comp ?_ ?_
  Â· exact ContinuousLinearMap.differentiable (ð•œ := â„) _
  Â· apply Differentiable.comp ?_ ?_
    Â· exact currentDensity_differentiable hJ
    Â· fun_prop

/-!

### C.3. Smoothness of the current density

-/

lemma currentDensity_ContDiff {d : â„•} {c : SpeedOfLight} {J : LorentzCurrentDensity d}
    (hJ : ContDiff â„ n J) : ContDiff â„ n â†¿(J.currentDensity c) := by
  rw [currentDensity_eq_timeSlice]
  apply timeSlice_contDiff
  have h1 : âˆ€ i, ContDiff â„ n (fun x => J x i) := by
    rw [SpaceTime.contDiff_vector]
    exact hJ
  exact contDiff_euclidean.mpr fun i => h1 (Sum.inr i)

end LorentzCurrentDensity

end Electromagnetism
