/-
Copyright (c) 2025 Joseph Tooby-Smith. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Zhi Kai Pong, Joseph Tooby-Smith, Lode Vermeulen
-/
import PhysLean.SpaceAndTime.Space.Derivatives.Basic
/-!

# Gradient of functions and distributions on `Space d`

## i. Overview

This module defines and proves basic properties of the gradient operator
on functions from `Space d` to `â„` and on distributions from `Space d` to `â„`.

The gradient operator returns a vector field whose components are the partial derivatives
of the input function with respect to each spatial coordinate.

## ii. Key results

- `grad` : The gradient operator on functions from `Space d` to `â„`.
- `distGrad` : The gradient operator on distributions from `Space d` to `â„`.

## iii. Table of contents

- A. The gradient of functions on `Space d`
  - A.1. Gradient of the zero function
  - A.2. Gradient distributes over addition
  - A.3. Gradient of a constant function
  - A.4. Gradient distributes over scalar multiplication
  - A.5. Gradient distributes over negation
  - A.6. Expansion in terms of basis vectors
  - A.7. Components of the gradient
  - A.8. Inner product with a gradient
  - A.9. Gradient is equal to `gradient` from Mathlib
  - A.10. Value of gradient in the direction of the position vector
  - A.11. Gradient of the norm squared function
  - A.12. Gradient of the inner product function
  - A.13. Integrability with bounded functions
- B. Gradient of distributions
  - B.1. The definition
  - B.2. The gradient of inner products
  - B.3. The gradient as a sum over basis vectors
  - B.4. The underlying function of the gradient distribution
  - B.5. The gradient applied to a Schwartz function

## iv. References

-/

namespace Space

/-!

## A. The gradient of functions on `Space d`

-/

/-- The vector calculus operator `grad`. -/
noncomputable def grad {d} (f : Space d â†’ â„) :
  Space d â†’ EuclideanSpace â„ (Fin d) := fun x => WithLp.toLp 2 fun i => âˆ‚[i] f x

@[inherit_doc grad]
scoped[Space] notation "âˆ‡" => grad

/-!

### A.1. Gradient of the zero function

-/

@[simp]
lemma grad_zero : âˆ‡ (0 : Space d â†’ â„) = 0 := by
  unfold grad Space.deriv
  simp only [fderiv_zero, Pi.zero_apply, ContinuousLinearMap.zero_apply]
  rfl

/-!

### A.2. Gradient distributes over addition

-/

lemma grad_add (f1 f2 : Space d â†’ â„)
    (hf1 : Differentiable â„ f1) (hf2 : Differentiable â„ f2) :
    âˆ‡ (f1 + f2) = âˆ‡ f1 + âˆ‡ f2 := by
  unfold grad
  ext x i
  simp only [Pi.add_apply]
  rw [deriv_add]
  rfl
  exact hf1
  exact hf2

/-!

### A.3. Gradient of a constant function

-/

@[simp]
lemma grad_const : âˆ‡ (fun _ : Space d => c) = 0 := by
  unfold grad Space.deriv
  simp only [fderiv_fun_const, Pi.ofNat_apply, ContinuousLinearMap.zero_apply]
  rfl

/-!

### A.4. Gradient distributes over scalar multiplication

-/

lemma grad_smul (f : Space d â†’ â„) (k : â„)
    (hf : Differentiable â„ f) :
    âˆ‡ (k â€¢ f) = k â€¢ âˆ‡ f := by
  unfold grad
  ext x i
  simp only [Pi.smul_apply]
  rw [deriv_smul]
  rfl
  exact hf

/-!

### A.5. Gradient distributes over negation

-/

lemma grad_neg (f : Space d â†’ â„) :
    âˆ‡ (- f) = - âˆ‡ f := by
  unfold grad
  ext x i
  simp only [Pi.neg_apply]
  rw [Space.deriv_eq, fderiv_neg]
  rfl

/-!

### A.6. Expansion in terms of basis vectors

-/

lemma grad_eq_sum {d} (f : Space d â†’ â„) (x : Space d) :
    âˆ‡ f x = âˆ‘ i, deriv i f x â€¢ basis i := by
  ext i
  simp [grad, deriv_eq]
  rw [Finset.sum_eq_single i]
  Â· simp [basis]
  Â· intro j hj
    simp [basis]
    exact fun a a_1 => False.elim (a (id (Eq.symm a_1)))
  Â· simp

/-!

### A.7. Components of the gradient

-/

lemma grad_apply {d} (f : Space d â†’ â„) (x : Space d) (i : Fin d) :
    (âˆ‡ f x) i = deriv i f x := by
  rw [grad_eq_sum]
  simp [basis_apply]

/-!

### A.8. Inner product with a gradient

-/

open InnerProductSpace

lemma grad_inner_eq {d} (f : Space d â†’ â„) (x : Space d) (y : Space d) :
    âŸªâˆ‡ f x, yâŸ«_â„ = (fderiv â„ f x) y:= by
  rw [grad_eq_sum]
  have hy : y = âˆ‘ i, y i â€¢ basis i := by
      conv_lhs => rw [â† OrthonormalBasis.sum_repr basis y]
      dsimp [basis]
  rw [hy]
  simp only [PiLp.inner_apply, RCLike.inner_apply, conj_trivial, map_sum, map_smul, smul_eq_mul]
  conv_lhs =>
    enter [2, x]
    simp [Fintype.sum_apply, Fintype.sum_apply]
  simp [basis_apply]
  congr
  funext x
  rw [deriv_eq_fderiv_basis]

lemma inner_grad_eq {d} (f : Space d â†’ â„) (x : Space d) (y : Space d) :
    âŸªx, âˆ‡ f yâŸ«_â„ = (fderiv â„ f y) x := by
  rw [â† grad_inner_eq]
  exact real_inner_comm (âˆ‡ f y) x

/-!

### A.9. Gradient is equal to `gradient` from Mathlib

-/

lemma grad_eq_gradiant {d} (f : Space d â†’ â„) :
    âˆ‡ f = gradient f := by
  funext x
  have hx (y : Space d) : âŸªgradient f x, yâŸ«_â„ =
      âŸªâˆ‡ f x, yâŸ«_â„ := by
    rw [gradient, toDual_symm_apply]
    exact Eq.symm (grad_inner_eq f x y)
  have h1 : âˆ€ y, âŸªgradient f x - âˆ‡ f x, yâŸ«_â„ = 0 := by
    intro y
    rw [inner_sub_left, hx y]
    simp
  have h2 := h1 (gradient f x - âˆ‡ f x)
  rw [inner_self_eq_zero, sub_eq_zero] at h2
  rw [h2]

/-!

### A.10. Value of gradient in the direction of the position vector

-/

/-- The gradient in the direction of the space position. -/
lemma grad_inner_space_unit_vector {d} (x : Space d) (f : Space d â†’ â„) (hd : Differentiable â„ f) :
    âŸªâˆ‡ f x, â€–xâ€–â»Â¹ â€¢ xâŸ«_â„ = _root_.deriv (fun r => f (r â€¢ â€–xâ€–â»Â¹ â€¢ x)) â€–xâ€– := by
  by_cases hx : x = 0
  Â· subst hx
    simp
  symm
  calc _
    _ = fderiv â„ (f âˆ˜ (fun r => r â€¢ â€–xâ€–â»Â¹ â€¢ x)) â€–xâ€– 1 := by rfl
    _ = (fderiv â„ f (â€–xâ€– â€¢ â€–xâ€–â»Â¹ â€¢ x)) (_root_.deriv (fun r => r â€¢ â€–xâ€–â»Â¹ â€¢ x) â€–xâ€–) := by
      rw [fderiv_comp _ (by fun_prop) (by fun_prop)]
      simp
    _ = (fderiv â„ f x) (_root_.deriv (fun r => r â€¢ â€–xâ€–â»Â¹ â€¢ x) â€–xâ€–) := by
      have hx : â€–xâ€– â‰  0 := norm_ne_zero_iff.mpr hx
      rw [smul_smul]
      field_simp
      simp
  rw [grad_inner_eq f x (â€–xâ€–â»Â¹ â€¢ x)]
  congr
  rw [deriv_smul_const (by fun_prop)]
  simp

lemma grad_inner_space {d} (x : Space d) (f : Space d â†’ â„) (hd : Differentiable â„ f) :
    âŸªâˆ‡ f x, xâŸ«_â„ = â€–xâ€– * _root_.deriv (fun r => f (r â€¢ â€–xâ€–â»Â¹ â€¢ x)) â€–xâ€– := by
  rw [â† grad_inner_space_unit_vector _ _ hd, inner_smul_right]
  by_cases hx : x = 0
  Â· subst hx
    simp
  have hx : â€–xâ€– â‰  0 := norm_ne_zero_iff.mpr hx
  field_simp

/-!

### A.11. Gradient of the norm squared function

-/

lemma grad_norm_sq (x : Space d) :
    âˆ‡ (fun x => â€–xâ€– ^ 2) x = (2 : â„) â€¢ x := by
  ext i
  rw [grad_eq_sum]
  simp [deriv_norm_sq, basis_apply]

/-!

### A.12. Gradient of the inner product function

-/

/-- The gradient of the inner product is given by `2 â€¢ x`. -/
lemma grad_inner {d : â„•} :
    âˆ‡ (fun y : Space d => âŸªy, yâŸ«_â„) = fun z => (2:â„) â€¢ z := by
  ext z i
  simp [Space.grad]
  rw [deriv]
  simp only [fderiv_norm_sq_apply, ContinuousLinearMap.coe_smul', coe_innerSL_apply, Pi.smul_apply,
    nsmul_eq_mul, Nat.cast_ofNat, mul_eq_mul_left_iff, OfNat.ofNat_ne_zero, or_false]
  rw [â† basis_eq_single]
  simp

lemma grad_inner_left {d : â„•} (x : Space d) :
    âˆ‡ (fun y : Space d => âŸªy, xâŸ«_â„) = fun _ => x := by
  ext z i
  simp [Space.grad]

lemma grad_inner_right {d : â„•} (x : Space d) :
    âˆ‡ (fun y : Space d => âŸªx, yâŸ«_â„) = fun _ => x := by
  rw [â† grad_inner_left x]
  congr
  funext y
  exact real_inner_comm y x

/-!

### A.13. Integrability with bounded functions

-/

open InnerProductSpace Distribution SchwartzMap MeasureTheory

/- The quantity `âŸªf x, Space.grad Î· xâŸ«_â„` is integrable for `f` bounded
  and `Î·` a Schwartz map. -/
lemma integrable_isDistBounded_inner_grad_schwartzMap {dm1 : â„•}
    {f : Space dm1.succ â†’ EuclideanSpace â„ (Fin dm1.succ)}
    (hf : IsDistBounded f) (Î· : ð“¢(EuclideanSpace â„ (Fin dm1.succ), â„)) :
    Integrable (fun x => âŸªf x, Space.grad Î· xâŸ«_â„) volume := by
  conv =>
    enter [1, x]
    rw [grad_eq_sum, inner_sum]
  apply MeasureTheory.integrable_finset_sum
  intro i _
  simp [inner_smul_right]
  have integrable_lemma (i j : Fin (dm1 + 1)) :
      Integrable (fun x => (((SchwartzMap.evalCLM (ð•œ := â„) (basis i)) ((fderivCLM â„) Î·)) x â€¢ f x) j)
        volume := by
    simp only [PiLp.smul_apply]
    exact (hf.pi_comp j).integrable_space _
  convert integrable_lemma i i using 2
  rename_i x
  simp only [Nat.succ_eq_add_one, PiLp.smul_apply, smul_eq_mul, mul_eq_mul_right_iff]
  left
  rw [deriv_eq_fderiv_basis]
  rfl

lemma integrable_isDistBounded_inner_grad_schwartzMap_spherical{dm1 : â„•}
    {f : Space dm1.succ â†’ EuclideanSpace â„ (Fin dm1.succ)}
    (hf : IsDistBounded f) (Î· : ð“¢(EuclideanSpace â„ (Fin dm1.succ), â„)) :
    Integrable ((fun x => âŸªf x.1, Space.grad Î· x.1âŸ«_â„)
      âˆ˜ (homeomorphUnitSphereProd (Space dm1.succ)).symm)
      ((volume (Î± := Space dm1.succ)).toSphere.prod
      (Measure.volumeIoiPow (Module.finrank â„ (EuclideanSpace â„ (Fin dm1.succ)) - 1))) := by
  have h1 : Integrable ((fun x => âŸªf x.1, Space.grad Î· x.1âŸ«_â„))
      (.comap (Subtype.val (p := fun x => x âˆˆ ({0}á¶œ : Set _))) volume) := by
    change Integrable ((fun x => âŸªf x, Space.grad Î· xâŸ«_â„) âˆ˜ Subtype.val)
      (.comap (Subtype.val (p := fun x => x âˆˆ ({0}á¶œ : Set _))) volume)
    rw [â† MeasureTheory.integrableOn_iff_comap_subtypeVal]
    apply Integrable.integrableOn
    exact integrable_isDistBounded_inner_grad_schwartzMap hf Î·
    simp
  have he := (MeasureTheory.Measure.measurePreserving_homeomorphUnitSphereProd
    (volume (Î± := EuclideanSpace â„ (Fin dm1.succ))))
  rw [â† he.integrable_comp_emb]
  convert h1
  simp only [Nat.succ_eq_add_one, Function.comp_apply, Homeomorph.symm_apply_apply]
  exact Homeomorph.measurableEmbedding (homeomorphUnitSphereProd (EuclideanSpace â„ (Fin dm1.succ)))

/-!

## B. Gradient of distributions

-/

/-!

### B.1. The definition

-/

/-- The gradient of a distribution `(Space d) â†’d[â„] â„` as a distribution
  `(Space d) â†’d[â„] (EuclideanSpace â„ (Fin d))`. -/
noncomputable def distGrad {d} :
    ((Space d) â†’d[â„] â„) â†’â‚—[â„] (Space d) â†’d[â„] (EuclideanSpace â„ (Fin d)) where
  toFun f :=
    ((InnerProductSpace.toDual â„ (Space d)).symm.toContinuousLinearMap).comp (fderivD â„ f)
  map_add' f1 f2 := by
    ext x
    simp
  map_smul' a f := by
    ext x
    simp

/-!

### B.2. The gradient of inner products

-/

lemma distGrad_inner_eq {d} (f : (Space d) â†’d[â„] â„) (Î· : ð“¢(Space d, â„))
    (y : EuclideanSpace â„ (Fin d)) : âŸªdistGrad f Î·, yâŸ«_â„ = fderivD â„ f Î· y := by
  rw [distGrad]
  simp only [LinearIsometryEquiv.toLinearEquiv_symm, LinearMap.coe_mk, AddHom.coe_mk,
    ContinuousLinearMap.coe_comp', LinearMap.coe_toContinuousLinearMap', LinearEquiv.coe_coe,
    LinearIsometryEquiv.coe_symm_toLinearEquiv, Function.comp_apply, toDual_symm_apply]

lemma distGrad_eq_of_inner {d} (f : (Space d) â†’d[â„] â„)
    (g : (Space d) â†’d[â„] EuclideanSpace â„ (Fin d))
    (h : âˆ€ Î· y, fderivD â„ f Î· y = âŸªg Î·, yâŸ«_â„) :
    distGrad f = g := by
  ext1 Î·
  specialize h Î·
  conv at h => enter [x]; rw [â† distGrad_inner_eq]
  exact ext_inner_right (ð•œ := â„) h

/-!

### B.3. The gradient as a sum over basis vectors

-/

lemma distGrad_eq_sum_basis {d} (f : (Space d) â†’d[â„] â„) (Î· : ð“¢(Space d, â„)) :
    distGrad f Î· = âˆ‘ i, - f (SchwartzMap.evalCLM (ð•œ := â„) (basis i) (fderivCLM â„ Î·)) â€¢ basis i := by
  have h1 (y : EuclideanSpace â„ (Fin d)) :
      âŸªâˆ‘ i, - f (SchwartzMap.evalCLM (ð•œ := â„) (basis i) (fderivCLM â„ Î·)) â€¢ basis i, yâŸ«_â„ =
      fderivD â„ f Î· y := by
    have hy : y = âˆ‘ i, y i â€¢ basis i := by
      conv_lhs => rw [â† OrthonormalBasis.sum_repr basis y]
      dsimp [basis]
    rw [hy]
    simp only [PiLp.inner_apply, RCLike.inner_apply, conj_trivial, map_sum, map_smul, smul_eq_mul]
    conv_lhs =>
      enter [2, x]
      simp [Fintype.sum_apply, Fintype.sum_apply]
    simp only [basis_apply, mul_ite, mul_one, mul_zero, Finset.sum_ite_eq', Finset.mem_univ,
      â†“reduceIte]
    congr
    ext i
    rw [fderivD_apply]
    ring
  have hx (y : EuclideanSpace â„ (Fin d)) : âŸªdistGrad f Î·, yâŸ«_â„ =
      âŸªâˆ‘ i, - f (SchwartzMap.evalCLM (ð•œ := â„) (basis i) (fderivCLM â„ Î·)) â€¢ basis i, yâŸ«_â„ := by
    rw [distGrad_inner_eq, h1]
  have h1 : âˆ€ y, âŸªdistGrad f Î· -
    (âˆ‘ i, - f (SchwartzMap.evalCLM (ð•œ := â„) (basis i) (fderivCLM â„ Î·)) â€¢ basis i), yâŸ«_â„ = 0 := by
    intro y
    rw [inner_sub_left, hx y]
    simp
  have h2 := h1 (distGrad f Î· -
    (âˆ‘ i, - f (SchwartzMap.evalCLM (ð•œ := â„) (basis i) (fderivCLM â„ Î·)) â€¢ basis i))
  rw [inner_self_eq_zero, sub_eq_zero] at h2
  rw [h2]

/-!

### B.4. The underlying function of the gradient distribution

-/

lemma distGrad_toFun_eq_distDeriv {d} (f : (Space d) â†’d[â„] â„) :
    (distGrad f).toFun = fun Îµ => WithLp.toLp 2 fun i => distDeriv i f Îµ := by
  ext Îµ i
  simp only [AddHom.toFun_eq_coe, LinearMap.coe_toAddHom, ContinuousLinearMap.coe_coe]
  rw [distGrad_eq_sum_basis]
  simp only [neg_smul, sum_apply, PiLp.neg_apply, PiLp.smul_apply, smul_eq_mul,
    Finset.sum_neg_distrib]
  rw [Finset.sum_eq_single i]
  Â· simp
    rfl
  Â· intro b _ h
    simp only [mul_eq_zero]
    right
    simpa [basis_apply] using h
  Â· simp

/-!

### B.5. The gradient applied to a Schwartz function

-/

lemma distGrad_apply {d} (f : (Space d) â†’d[â„] â„) (Îµ : ð“¢(Space d, â„)) :
    (distGrad f) Îµ = fun i => distDeriv i f Îµ := by
  change (distGrad f).toFun Îµ = fun i => distDeriv i f Îµ
  rw [distGrad_toFun_eq_distDeriv]

end Space
