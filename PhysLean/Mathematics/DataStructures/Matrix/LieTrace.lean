/-
Copyright (c) 2025 Matteo Cipollina. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Matteo Cipollina
-/
import Mathlib.Analysis.Complex.Polynomial.Basic
import Mathlib.Analysis.Normed.Algebra.MatrixExponential
import PhysLean.Mathematics.SchurTriangulation

/-!
# Lie's Trace Formula

This file proves the formula `det (exp A) = exp (tr A)` for matrices, also known as Lie's trace
formula.

The proof proceeds by first showing the formula for upper-triangular matrices and then
leveraging Schur triangulation to generalize to any matrix. An upper-triangular matrix `A`
is defined in `mathlib` as `Matrix.BlockTriangular A id`.

## Main results

- `Matrix.det_exp`: The determinant of the exponential of a matrix is the exponential of its trace.
-/

namespace Matrix

open scoped BigOperators Topology

variable {ùïÇ m n : Type*}

instance [UniformSpace ùïÇ] : UniformSpace (Matrix m n ùïÇ) := by unfold Matrix; infer_instance

/-- If every term of a series is zero, then its sum is zero. -/
lemma tsum_eq_zero
    {Œ≤ : Type*} [TopologicalSpace Œ≤] [AddCommMonoid Œ≤]
    {f : ‚Ñï ‚Üí Œ≤} (h : ‚àÄ n, f n = 0) :
    (‚àë' n, f n) = 0 := by
  simp_all only [tsum_zero]

/-!
  ### The determinant of the matrix exponential
-/
section DetExp

variable [RCLike ùïÇ]

attribute [local instance] Matrix.linftyOpNormedAlgebra
attribute [local instance] Matrix.linftyOpNormedRing
attribute [local instance] Matrix.instCompleteSpace

/-- Apply a matrix `tsum` to a given entry. -/
lemma matrix_tsum_apply
    {f : ‚Ñï ‚Üí Matrix m m ùïÇ} (hf : Summable f) (i j : m) :
    (‚àë' n, f n) i j = ‚àë' n, (f n) i j := by
  have h_row_summable : Summable (fun n ‚Ü¶ (f n) i) := by
    have h := Pi.summable.1 hf
    exact h i
  have h‚ÇÅ : ((‚àë' n, f n) : Matrix m m ùïÇ) i = (‚àë' n, (f n) i) := by
    exact tsum_apply hf
  have h‚ÇÇ : ((‚àë' n, (f n) i) : m ‚Üí ùïÇ) j = (‚àë' n, (f n) i j) := by
    exact tsum_apply h_row_summable
  rw [h‚ÇÅ, h‚ÇÇ]

variable [Fintype m] [LinearOrder m]

/-- For upper-triangular matrices, the diagonal of a product is the product of the diagonals.
This is a specific case of a more general property for block-triangular matrices. -/
lemma diag_mul_of_blockTriangular_id {A B : Matrix m m ùïÇ}
    (hA : BlockTriangular A id) (hB : BlockTriangular B id) : (A * B).diag = A.diag * B.diag := by
  ext i
  simp only [diag_apply, mul_apply, Pi.mul_apply]
  apply Finset.sum_eq_single i
  ¬∑ intro j _ j_ne_i
    cases lt_or_gt_of_ne j_ne_i with
    | inl h => rw [hA h, zero_mul] -- j < i
    | inr h => rw [hB h, mul_zero] -- i < j
  ¬∑ intro; simp_all only [Finset.mem_univ, not_true_eq_false]

/-- Powers of block triangular matrices are block triangular. -/
lemma blockTriangular.pow {A : Matrix m m ùïÇ} (hA : BlockTriangular A id) (k : ‚Ñï) :
    BlockTriangular (A ^ k) id := by
  induction k with
  | zero => rw [pow_zero]; exact blockTriangular_one
  | succ k ihk => rw [pow_succ]; exact ihk.mul hA

/-- For an upper-triangular matrix, the diagonal of a power is the power of the diagonal. -/
lemma diag_pow_of_blockTriangular_id {A : Matrix m m ùïÇ}
    (hA : BlockTriangular A id) (k : ‚Ñï) : (A ^ k).diag = A.diag ^ k := by
  induction k with
  | zero => rw [pow_zero, pow_zero]; simp [diag_one]
  | succ k ih =>
    have h_pow_k : BlockTriangular (A ^ k) id := blockTriangular.pow hA k
    rw [pow_succ, pow_succ, diag_mul_of_blockTriangular_id h_pow_k hA, ih]

/-- The exponential of an upper-triangular matrix is upper-triangular. -/
lemma blockTriangular_exp_of_blockTriangular_id
    {A : Matrix m m ùïÇ} (hA : BlockTriangular A id) :
    (NormedSpace.exp ùïÇ A).BlockTriangular id := by
  intro i j hij
  rw [NormedSpace.exp_eq_tsum]
  let exp_series := fun n => ((n.factorial : ùïÇ)‚Åª¬π) ‚Ä¢ (A ^ n)
  change (‚àë' n, exp_series n) i j = 0
  rw [matrix_tsum_apply (NormedSpace.expSeries_summable' A) i j]
  apply tsum_eq_zero
  intro n
  have h_pow : BlockTriangular (A ^ n) id := by
    induction n with
    | zero => rw [pow_zero]; exact blockTriangular_one
    | succ k ihk => rw [pow_succ]; exact ihk.mul hA
  simp only [smul_apply]
  rw [h_pow hij, smul_zero]

/--
For an upper‚Äìtriangular matrix `A`, the `(i,i)` entry of the power `A ^ n`
is simply the `n`-th power of the original diagonal entry.
-/
lemma diag_pow_entry_eq_pow_diag_entry {A : Matrix m m ùïÇ}
    (hA : BlockTriangular A id) (n : ‚Ñï) (i : m) :
    (A ^ n) i i = (A i i) ^ n := by
  have h := diag_pow_of_blockTriangular_id hA n
  simpa [diag_apply, Pi.pow_apply] using congr_arg (fun d => d i) h

/-- Each term in the matrix exponential series equals the corresponding scalar term on the
diagonal -/
lemma exp_series_diag_term_eq {A : Matrix m m ùïÇ} (hA : BlockTriangular A id)
    (n : ‚Ñï) (i : m) :
    ((n.factorial : ùïÇ)‚Åª¬π ‚Ä¢ (A ^ n)) i i = (n.factorial : ùïÇ)‚Åª¬π ‚Ä¢ (A i i) ^ n := by
  simp [smul_apply, diag_pow_entry_eq_pow_diag_entry hA]

/-- The diagonal of the matrix exponential series equals the scalar exponential series -/
lemma matrix_exp_series_diag_eq_scalar_series {A : Matrix m m ùïÇ} (hA : BlockTriangular A id)
    (i : m) :
    (‚àë' n, ((n.factorial : ùïÇ)‚Åª¬π ‚Ä¢ (A ^ n)) i i) = ‚àë' n, (n.factorial : ùïÇ)‚Åª¬π ‚Ä¢ (A i i) ^ n := by
  exact tsum_congr (exp_series_diag_term_eq hA ¬∑ i)

/-- The diagonal of the exponential of an upper-triangular matrix `A` consists of the
exponentials of the diagonal entries of `A`. -/
theorem diag_exp_of_blockTriangular_id
    {A : Matrix m m ùïÇ} (hA : BlockTriangular A id) :
    (NormedSpace.exp ùïÇ A).diag = fun i => NormedSpace.exp ùïÇ (A i i) := by
  funext i
  rw [NormedSpace.exp_eq_tsum (ùïÇ := ùïÇ), diag_apply]
  simp_rw [matrix_tsum_apply (NormedSpace.expSeries_summable' A) i i]
  rw [matrix_exp_series_diag_eq_scalar_series hA i]
  rw [NormedSpace.exp_eq_tsum (ùïÇ := ùïÇ)]

/-- Lie's trace formula for upper triangular matrices. -/
lemma det_exp_of_blockTriangular_id {A : Matrix m m ùïÇ} (hA : BlockTriangular A id) :
    (NormedSpace.exp ùïÇ A).det = NormedSpace.exp ùïÇ A.trace := by
  have h_exp_upper : BlockTriangular (NormedSpace.exp ùïÇ A) id :=
    blockTriangular_exp_of_blockTriangular_id hA
  rw [det_of_upperTriangular h_exp_upper]
  have h_diag_exp : (NormedSpace.exp ùïÇ A).diag = fun i => NormedSpace.exp ùïÇ (A i i) :=
    diag_exp_of_blockTriangular_id hA
  simp_rw [‚Üê diag_apply]
  simp_rw [h_diag_exp]
  erw [‚Üê NormedSpace.exp_sum Finset.univ]
  congr 1

/-- The trace is invariant under unitary conjugation. -/
lemma trace_unitary_conj (A : Matrix m m ùïÇ) (U : unitaryGroup m ùïÇ) :
    trace ((U : Matrix m m ùïÇ) * A * star (U : Matrix m m ùïÇ)) = trace A := by
  have h_unitary : star (U : Matrix m m ùïÇ) * (U : Matrix m m ùïÇ) = 1 :=
    UnitaryGroup.star_mul_self U
  simpa [Matrix.mul_assoc, h_unitary, Matrix.one_mul] using
    (Matrix.trace_mul_cycle (U : Matrix m m ùïÇ) A (star (U : Matrix m m ùïÇ)))

/-- The determinant is invariant under unitary conjugation. -/
lemma det_unitary_conj (A : Matrix m m ùïÇ) (U : unitaryGroup m ùïÇ) :
    det ((U : Matrix m m ùïÇ) * A * star (U : Matrix m m ùïÇ)) = det A := by
  rw [det_mul_right_comm]
  simp_all only [SetLike.coe_mem, Unitary.mul_star_self_of_mem, one_mul]

/-- The exponential of a matrix commutes with unitary conjugation. -/
lemma exp_unitary_conj (A : Matrix m m ùïÇ) (U : unitaryGroup m ùïÇ) :
    NormedSpace.exp ùïÇ ((U : Matrix m m ùïÇ) * A * star (U : Matrix m m ùïÇ)) =
      (U : Matrix m m ùïÇ) * NormedSpace.exp ùïÇ A * star (U : Matrix m m ùïÇ) := by
  let Uu : (Matrix m m ùïÇ)À£ :=
    { val := (U : Matrix m m ùïÇ)
      inv := star (U : Matrix m m ùïÇ)
      val_inv := by simp
      inv_val := by simp}
  have h_units := Matrix.exp_units_conj (ùïÇ := ùïÇ) Uu A
  simpa [Uu] using h_units

lemma det_exp_unitary_conj (A : Matrix m m ùïÇ) (U : unitaryGroup m ùïÇ) :
    (NormedSpace.exp ùïÇ ((U : Matrix m m ùïÇ) * A * star (U : Matrix m m ùïÇ))).det =
    (NormedSpace.exp ùïÇ A).det := by
  rw [exp_unitary_conj, det_unitary_conj]

/-- The determinant of the exponential of a matrix is the exponential of its trace.
This is also known as **Lie's trace formula**. -/
theorem det_exp {ùïÇ m : Type*} [RCLike ùïÇ] [IsAlgClosed ùïÇ] [Fintype m] [LinearOrder m]
    (A : Matrix m m ùïÇ) :
    (NormedSpace.exp ùïÇ A).det = NormedSpace.exp ùïÇ A.trace := by
  let U := A.schurTriangulationUnitary
  let T := A.schurTriangulation
  have h_prop : T.val.IsUpperTriangular := T.property
  have h_conj : A = U * T * star U := schur_triangulation A
  have h_trace_invariant : A.trace = T.val.trace := by
    erw [h_conj, trace_unitary_conj]
  have h_det_invariant : (NormedSpace.exp ùïÇ A).det = (NormedSpace.exp ùïÇ T.val).det := by
    erw [h_conj, det_exp_unitary_conj]
  have h_triangular_case : (NormedSpace.exp ùïÇ T.val).det = NormedSpace.exp ùïÇ T.val.trace :=
    det_exp_of_blockTriangular_id h_prop
  rw [h_det_invariant, h_triangular_case, h_trace_invariant]

end DetExp

-- `Matrix.map` commutes with an absolutely convergent series.
lemma map_tsum {Œ± Œ≤ m n : Type*}
    [AddCommMonoid Œ±] [AddCommMonoid Œ≤] [TopologicalSpace Œ±] [TopologicalSpace Œ≤]
    [T2Space Œ≤]
    (f : Œ± ‚Üí+ Œ≤) (hf : Continuous f) {s : ‚Ñï ‚Üí Matrix m n Œ±} (hs : Summable s) :
    (‚àë' k, s k).map f = ‚àë' k, (s k).map f := by
  let F : Matrix m n Œ± ‚Üí+ Matrix m n Œ≤ := AddMonoidHom.mapMatrix f
  have hF : Continuous F := Continuous.matrix_map continuous_id hf
  exact (hs.hasSum.map F hF).tsum_eq.symm

attribute [local instance] Matrix.linftyOpNormedAlgebra
attribute [local instance] Matrix.linftyOpNormedRing
attribute [local instance] Matrix.instCompleteSpace

lemma map_pow {Œ± Œ≤ m : Type*}
    [Fintype m] [DecidableEq m] [Semiring Œ±] [Semiring Œ≤]
    (f : Œ± ‚Üí+* Œ≤) (A : Matrix m m Œ±) (k : ‚Ñï) :
    (A ^ k).map f = (A.map f) ^ k := by
  induction k with
  | zero =>
    rw [pow_zero, pow_zero, Matrix.map_one]; all_goals aesop
  | succ k ih =>
    rw [pow_succ, pow_succ, Matrix.map_mul, ih]

end Matrix

namespace NormedSpace

lemma exp_map_algebraMap {n : Type*} [Fintype n] [DecidableEq n]
    (A : Matrix n n ‚Ñù) :
    (exp ‚Ñù A).map (algebraMap ‚Ñù ‚ÑÇ) = exp ‚ÑÇ (A.map (algebraMap ‚Ñù ‚ÑÇ)) := by
  letI : SeminormedRing (Matrix n n ‚Ñù) := Matrix.linftyOpSemiNormedRing
  letI : NormedRing (Matrix n n ‚Ñù) := Matrix.linftyOpNormedRing
  letI : NormedAlgebra ‚Ñù (Matrix n n ‚Ñù) := Matrix.linftyOpNormedAlgebra
  letI : CompleteSpace (Matrix n n ‚Ñù) := inferInstance
  letI : SeminormedRing (Matrix n n ‚ÑÇ) := Matrix.linftyOpSemiNormedRing
  letI : NormedRing (Matrix n n ‚ÑÇ) := Matrix.linftyOpNormedRing
  letI : NormedAlgebra ‚ÑÇ (Matrix n n ‚ÑÇ) := Matrix.linftyOpNormedAlgebra
  letI : CompleteSpace (Matrix n n ‚ÑÇ) := inferInstance
  simp only [exp_eq_tsum]
  have hs : Summable (fun k => (k.factorial : ‚Ñù)‚Åª¬π ‚Ä¢ A ^ k) := by
    exact NormedSpace.expSeries_summable' A
  erw [Matrix.map_tsum (algebraMap ‚Ñù ‚ÑÇ).toAddMonoidHom RCLike.continuous_ofReal hs]
  apply tsum_congr
  intro k
  erw [Matrix.map_smul, Matrix.map_pow]
  simp_all only [Complex.coe_algebraMap]
  ext i j : 1
  simp_all only [Matrix.smul_apply, Complex.real_smul, Complex.ofReal_inv, Complex.ofReal_natCast,
    smul_eq_mul]
  intro a
  simp_all only [RingHom.toAddMonoidHom_eq_coe, smul_eq_mul, AddMonoidHom.coe_coe,
    Complex.coe_algebraMap, Complex.ofReal_mul, Complex.ofReal_inv, Complex.ofReal_natCast,
    Complex.real_smul]

end NormedSpace

section DetExp
namespace Matrix
/--
Lie's trace formula over ‚Ñù: det(exp(A)) = exp(tr(A)) for any real matrix A.
This is proved by transferring the result from ‚ÑÇ using the naturality of polynomial identities.
-/
theorem det_exp_real {n : Type*} [Fintype n] [LinearOrder n]
    (A : Matrix n n ‚Ñù) : (NormedSpace.exp ‚Ñù A).det = Real.exp A.trace := by
  let A_‚ÑÇ := A.map (algebraMap ‚Ñù ‚ÑÇ)
  have h_complex : (NormedSpace.exp ‚ÑÇ A_‚ÑÇ).det = Complex.exp A_‚ÑÇ.trace := by
    haveI : IsAlgClosed ‚ÑÇ := Complex.isAlgClosed
    rw [Complex.exp_eq_exp_‚ÑÇ, ‚Üê Matrix.det_exp]
  have h_trace_comm : A_‚ÑÇ.trace = (algebraMap ‚Ñù ‚ÑÇ) A.trace := by
    simp only [A_‚ÑÇ, trace, diag_map, map_sum];rfl
  have h_det_comm : (algebraMap ‚Ñù ‚ÑÇ) ((NormedSpace.exp ‚Ñù A).det) = (NormedSpace.exp ‚ÑÇ A_‚ÑÇ).det := by
    rw [@RingHom.map_det]
    rw [‚Üê NormedSpace.exp_map_algebraMap]; rfl
  rw [‚Üê h_det_comm] at h_complex
  rw [h_trace_comm] at h_complex
  have h_exp_comm : Complex.exp ((algebraMap ‚Ñù ‚ÑÇ) A.trace) =
      (algebraMap ‚Ñù ‚ÑÇ) (Real.exp A.trace) := by
    erw [‚Üê Complex.ofReal_exp]
    simp_all only [Complex.coe_algebraMap, Algebra.algebraMap_self, RingHom.id_apply,
      Complex.ofReal_exp, A_‚ÑÇ]
  rw [h_exp_comm] at h_complex
  exact Complex.ofReal_injective h_complex

end Matrix
