/-
Copyright (c) 2025 Joseph Tooby-Smith. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Joseph Tooby-Smith
-/
import PhysLean.Particles.SuperSymmetry.SU5.ChargeSpectrum.OfFieldLabel
import Mathlib.Tactic.Abel
/-!

# Charges associated with a potential term

## i. Overview

In this module we give the multiset of charges associated with a given type of potential term,
given a charge spectrum.

We will define two versions of this, one based on the underlying fields on the
potentials, and the charges that they carry, and one more explicit version which
is faster to compute with. The former is `ofPotentialTerm`, and the latter is
`ofPotentialTerm'`.

We will show that these two multisets have the same elements.

## ii. Key results

- `ofPotentialTerm` : The multiset of charges associated with a potential term,
  defined in terms of the fields making up that potential term, given a charge spectrum.
- `ofPotentialTerm'` : The multiset of charges associated with a potential term,
  defined explicitly, given a charge spectrum.

## iii. Table of contents

- A. Charges of a potential term from field labels
  - A.1. Monotonicity of `ofPotentialTerm`
  - A.2. Charges of potential terms for the empty charge spectrum
- B. Explicit construction of charges of a potential term
  - B.1. Explicit multisets for `ofPotentialTerm'`
  - B.2. `ofPotentialTerm'` on the empty charge spectrum
- C. Relation between two constructions of charges of potential terms
  - C.1. Showing that `ofPotentialTerm` is a subset of `ofPotentialTerm'`
  - C.2. Showing that `ofPotentialTerm'` is a subset of `ofPotentialTerm`
  - C.3. Equivalence of elements of `ofPotentialTerm` and `ofPotentialTerm'`
  - C.4. Induced monotonicity of `ofPotentialTerm'`

## iv. References

There are no known references for this material.

-/

namespace SuperSymmetry
namespace SU5

namespace ChargeSpectrum
open PotentialTerm

variable {ğ“© : Type} [AddCommGroup ğ“©]

/-!

## A. Charges of a potential term from field labels

We first define `ofPotentialTerm`, and prover properties of it.
This is slow to compute in practice.

-/

/-- Given a charges `x : Charges` associated to the representations, and a potential
  term `T`, the charges associated with instances of that potential term. -/
def ofPotentialTerm (x : ChargeSpectrum ğ“©) (T : PotentialTerm) : Multiset ğ“© :=
  let add : Multiset ğ“© â†’ Multiset ğ“© â†’ Multiset ğ“© := fun a b => (a.product b).map
      fun (x, y) => x + y
  (T.toFieldLabel.map fun F => (ofFieldLabel x F).val).foldl add {0}

/-!

### A.1. Monotonicity of `ofPotentialTerm`

We show that `ofPotentialTerm` is monotone in its charge spectrum argument.
That is if `x âŠ† y` then `ofPotentialTerm x T âŠ† ofPotentialTerm y T`.

-/
lemma ofPotentialTerm_mono {x y : ChargeSpectrum ğ“©} (h : x âŠ† y) (T : PotentialTerm) :
    x.ofPotentialTerm T âŠ† y.ofPotentialTerm T := by
  have h1 {S1 S2 T1 T2 : Multiset ğ“©} (h1 : S1 âŠ† S2) (h2 : T1 âŠ† T2) :
      (S1.product T1) âŠ† S2.product T2 :=
    Multiset.subset_iff.mpr (fun x => by simpa using fun h1' h2' => âŸ¨h1 h1', h2 h2'âŸ©)
  rw [subset_def] at h
  cases T
  all_goals
    simp [ofPotentialTerm, PotentialTerm.toFieldLabel]
    repeat'
      apply Multiset.map_subset_map <| Multiset.subset_iff.mpr <|
        h1 _ (Finset.subset_def.mp (ofFieldLabel_mono h _))
    simp

/-!

### A.2. Charges of potential terms for the empty charge spectrum

For the empty charge spectrum, the charges associated with any potential term is empty.

-/

@[simp]
lemma ofPotentialTerm_empty (T : PotentialTerm) :
    ofPotentialTerm (âˆ… : ChargeSpectrum ğ“©) T = âˆ… := by
  cases T
  all_goals
    rfl

/-!

## B. Explicit construction of charges of a potential term

We now turn to a more explicit construction of the charges associated with a potential term.
This is faster to compute with, but less obviously connected to the underlying
fields.

-/

/-- Given a charges `x : ChargeSpectrum` associated to the representations, and a potential
  term `T`, the charges associated with instances of that potential term.

  This is a more explicit form of `PotentialTerm`, which has the benefit that
  it is quick with `decide`, but it is not defined based on more fundamental
  concepts, like `ofPotentialTerm` is. -/
def ofPotentialTerm' (y : ChargeSpectrum ğ“©) (T : PotentialTerm) : Multiset ğ“© :=
  let qHd := y.qHd
  let qHu := y.qHu
  let Q5 := y.Q5
  let Q10 := y.Q10
  match T with
  | Î¼ =>
    match qHd, qHu with
    | none, _ => âˆ…
    | _, none => âˆ…
    | some qHd, some qHu => {qHd - qHu}
  | Î² =>
    match qHu with
    | none => âˆ…
    | some qHu => Q5.val.map (fun x => - qHu + x)
  | Î› => (Q5.product <| Q5.product <| Q10).val.map (fun x => x.1 + x.2.1 + x.2.2)
  | W1 => (Q5.product <| Q10.product <| Q10.product <| Q10).val.map
    (fun x => x.1 + x.2.1 + x.2.2.1 + x.2.2.2)
  | W2 =>
    match qHd with
    | none => âˆ…
    | some qHd =>
      (Q10.product <| Q10.product <| Q10).val.map (fun x => qHd + x.1 + x.2.1 + x.2.2)
  | W3 =>
    match qHu with
    | none => âˆ…
    | some qHu => (Q5.product <| Q5).val.map (fun x => -qHu - qHu + x.1 + x.2)
  | W4 =>
    match qHd, qHu with
    | none, _ => âˆ…
    | _, none => âˆ…
    | some qHd, some qHu => Q5.val.map (fun x => qHd - qHu - qHu + x)
  | K1 => (Q5.product <| Q10.product <| Q10).val.map
    (fun x => - x.1 + x.2.1 + x.2.2)
  | K2 =>
    match qHd, qHu with
    | none, _ => âˆ…
    | _, none => âˆ…
    | some qHd, some qHu => Q10.val.map (fun x => qHd + qHu + x)
  | topYukawa =>
    match qHu with
    | none => âˆ…
    | some qHu => (Q10.product <| Q10).val.map (fun x => - qHu + x.1 + x.2)
  | bottomYukawa =>
    match qHd with
    | none => âˆ…
    | some qHd => (Q5.product <| Q10).val.map (fun x => qHd + x.1 + x.2)

/-!

### B.1. Explicit multisets for `ofPotentialTerm'`

For each potential term, we give an explicit form of the multiset `ofPotentialTerm'`.

-/
lemma ofPotentialTerm'_Î¼_finset {x : ChargeSpectrum ğ“©} :
    x.ofPotentialTerm' Î¼ =
    (x.qHd.toFinset.product <| x.qHu.toFinset).val.map (fun x => x.1 - x.2) := by
  match x with
  | âŸ¨none, qHu, Q5, Q10âŸ© =>
    simp [ofPotentialTerm']
  | âŸ¨some qHd, none, Q5, Q10âŸ© =>
    simp [ofPotentialTerm']
  | âŸ¨some qHd, some qHu, Q5, Q10âŸ© =>
    simp [ofPotentialTerm']

lemma ofPotentialTerm'_Î²_finset {x : ChargeSpectrum ğ“©} :
    x.ofPotentialTerm' Î² =
    (x.qHu.toFinset.product <| x.Q5).val.map (fun x => - x.1 + x.2) := by
  match x with
  | âŸ¨qHd, none, Q5, Q10âŸ© =>
    simp [ofPotentialTerm']
  | âŸ¨qHd, some qHu, Q5, Q10âŸ© =>
    simp [ofPotentialTerm']

lemma ofPotentialTerm'_W2_finset {x : ChargeSpectrum ğ“©} :
    x.ofPotentialTerm' W2 = (x.qHd.toFinset.product <|
      x.Q10.product <| x.Q10.product <| x.Q10).val.map
    (fun x => x.1 + x.2.1 + x.2.2.1 + x.2.2.2) := by
  match x with
  | âŸ¨none, qHu, Q5, Q10âŸ© =>
    simp [ofPotentialTerm']
  | âŸ¨some qHd, qHu, Q5, Q10âŸ© =>
    simp [ofPotentialTerm']

lemma ofPotentialTerm'_W3_finset {x : ChargeSpectrum ğ“©} :
    x.ofPotentialTerm' W3 = (x.qHu.toFinset.product <| x.Q5.product <| x.Q5).val.map
    (fun x => -x.1 - x.1 + x.2.1 + x.2.2) := by
  match x with
  | âŸ¨qHd, none, Q5, Q10âŸ© =>
    simp [ofPotentialTerm']
  | âŸ¨qHd, some qHu, Q5, Q10âŸ© =>
    simp [ofPotentialTerm']

lemma ofPotentialTerm'_W4_finset {x : ChargeSpectrum ğ“©} :
    x.ofPotentialTerm' W4 = (x.qHd.toFinset.product <|
      x.qHu.toFinset.product <| x.Q5).val.map
    (fun x => x.1 - x.2.1 - x.2.1 + x.2.2) := by
  match x with
  | âŸ¨none, qHu, Q5, Q10âŸ© =>
    simp [ofPotentialTerm']
  | âŸ¨some qHd, none, Q5, Q10âŸ© =>
    simp [ofPotentialTerm']
  | âŸ¨some qHd, some qHu, Q5, Q10âŸ© =>
    simp [ofPotentialTerm']

lemma ofPotentialTerm'_K2_finset {x : ChargeSpectrum ğ“©} :
    x.ofPotentialTerm' K2 = (x.qHd.toFinset.product <|
      x.qHu.toFinset.product <| x.Q10).val.map
    (fun x => x.1 + x.2.1 + x.2.2) := by
  match x with
  | âŸ¨none, qHu, Q5, Q10âŸ© =>
    simp [ofPotentialTerm']
  | âŸ¨some qHd, none, Q5, Q10âŸ© =>
    simp [ofPotentialTerm']
  | âŸ¨some qHd, some qHu, Q5, Q10âŸ© =>
    simp [ofPotentialTerm']

lemma ofPotentialTerm'_topYukawa_finset {x : ChargeSpectrum ğ“©} :
    x.ofPotentialTerm' topYukawa = (x.qHu.toFinset.product <|
      x.Q10.product <| x.Q10).val.map
    (fun x => -x.1 + x.2.1 + x.2.2) := by
  match x with
  | âŸ¨qHd, none, Q5, Q10âŸ© =>
    simp [ofPotentialTerm']
  | âŸ¨qHd, some qHu, Q5, Q10âŸ© =>
    simp [ofPotentialTerm']

lemma ofPotentialTerm'_bottomYukawa_finset {x : ChargeSpectrum ğ“©} :
    x.ofPotentialTerm' bottomYukawa = (x.1.toFinset.product <|
      x.Q5.product <| x.Q10).val.map
    (fun x => x.1 + x.2.1 + x.2.2) := by
  match x with
  | âŸ¨none, qHu, Q5, Q10âŸ© =>
    simp [ofPotentialTerm']
  | âŸ¨some qHd, qHu, Q5, Q10âŸ© =>
    simp [ofPotentialTerm']

/-!

### B.2. `ofPotentialTerm'` on the empty charge spectrum

We show that for the empty charge spectrum, the charges associated with any potential term is empty,
as defined through `ofPotentialTerm'`.

-/

@[simp]
lemma ofPotentialTerm'_empty (T : PotentialTerm) :
    ofPotentialTerm' (âˆ… : ChargeSpectrum ğ“©) T = âˆ… := by
  cases T
  all_goals
    simp [ofPotentialTerm']
/-!

## C. Relation between two constructions of charges of potential terms

We now give the relation between `ofPotentialTerm` and `ofPotentialTerm'`.
We show that they have the same elements, by showing that they are subsets of each other.

The prove of some of these results are rather long since they involve explicit
case analysis for each potential term, due to the nature of the definition
of `ofPotentialTerm'`.

-/

/-!

### C.1. Showing that `ofPotentialTerm` is a subset of `ofPotentialTerm'`

We first show that `ofPotentialTerm` is a subset of `ofPotentialTerm'`.

-/

lemma ofPotentialTerm_subset_ofPotentialTerm' {x : ChargeSpectrum ğ“©} (T : PotentialTerm) :
    x.ofPotentialTerm T âŠ† x.ofPotentialTerm' T := by
  refine Multiset.subset_iff.mpr (fun n h => ?_)
  simp [ofPotentialTerm] at h
  cases T
  all_goals
    simp [PotentialTerm.toFieldLabel, -existsAndEq] at h
    obtain âŸ¨f1, f2, âŸ¨âŸ¨f3, f4, âŸ¨h3, f4_memâŸ©, rflâŸ©, f2_memâŸ©, f1_add_f2_eq_zeroâŸ© := h
  case' Î¼ | Î² => obtain âŸ¨rflâŸ© := h3
  case' Î› | W1 | W2 | W3 | W4 | K1 | K2 | topYukawa | bottomYukawa =>
    obtain âŸ¨f5, f6, âŸ¨h4, f6_memâŸ©, rflâŸ© := h3
  case' Î› | K1 | K2 | topYukawa | bottomYukawa => obtain âŸ¨rflâŸ© := h4
  case' W1 | W2 | W3 | W4 => obtain âŸ¨f7, f8, âŸ¨rfl, f8_memâŸ©, rflâŸ© := h4
  all_goals
    try simp [ofPotentialTerm'_W2_finset, ofPotentialTerm'_W3_finset,
      ofPotentialTerm'_Î²_finset, ofPotentialTerm'_Î¼_finset,
      ofPotentialTerm'_W4_finset, ofPotentialTerm'_K2_finset,
      ofPotentialTerm'_topYukawa_finset, ofPotentialTerm'_bottomYukawa_finset]
    try simp [ofPotentialTerm', -existsAndEq]
    simp only [SProd.sprod, Multiset.mem_product]
    simp_all [ofFieldLabel, -existsAndEq]
  case' W1 => use f2, f4, f6, f8
  case' W2 => use f2, f4, f6, f8
  case' W3 => use (-f2), f6, f8
  case' W4 => use f6, (-f4), f8
  case' K1 => use (-f2), f4, f6
  case' K2 => use f4, f6, f2
  case' Î› => use f4, f6, f2
  case' topYukawa => use (-f2), f4, f6
  case' bottomYukawa => use f2, f4, f6
  case' Î² => use (-f4), f2
  all_goals simp_all
  all_goals
    rw [â† f1_add_f2_eq_zero]
    abel

/-!

### C.2. Showing that `ofPotentialTerm'` is a subset of `ofPotentialTerm`

We now show the other direction of the subset relation, that
`ofPotentialTerm'` is a subset of `ofPotentialTerm`.

-/

lemma ofPotentialTerm'_subset_ofPotentialTerm [DecidableEq ğ“©]
    {x : ChargeSpectrum ğ“©} (T : PotentialTerm) :
    x.ofPotentialTerm' T âŠ† x.ofPotentialTerm T := by
  refine Multiset.subset_iff.mpr (fun n h => ?_)
  cases T
  all_goals
    try simp [ofPotentialTerm'_W2_finset, ofPotentialTerm'_W3_finset,
      ofPotentialTerm'_Î²_finset, ofPotentialTerm'_Î¼_finset,
      ofPotentialTerm'_W4_finset, ofPotentialTerm'_K2_finset,
      ofPotentialTerm'_topYukawa_finset, ofPotentialTerm'_bottomYukawa_finset] at h
    try simp [ofPotentialTerm', -existsAndEq] at h
    simp only [SProd.sprod, Multiset.mem_product] at h
  case' Î¼ | Î² =>
    obtain âŸ¨q1, q2, âŸ¨q1_mem, q2_memâŸ©, q_sumâŸ© := h
  case' Î› | W3 | W4 | K1 | K2 | topYukawa | bottomYukawa =>
    obtain âŸ¨q1, q2, q3, âŸ¨q1_mem, q2_mem, q3_memâŸ©, q_sumâŸ© := h
  case' W1 | W2 =>
    obtain âŸ¨q1, q2, q3, q4, âŸ¨q1_mem, q2_mem, q3_mem, q4_memâŸ©, q_sumâŸ© := h
  case' Î¼ => refine ofPotentialTerm_mono (x := âŸ¨q1, q2, âˆ…, âˆ…âŸ©) ?Î¼Sub _ ?Î¼P
  case' Î² => refine ofPotentialTerm_mono (x := âŸ¨none, q1, {q2}, âˆ…âŸ©) ?Î²Sub _ ?Î²P
  case' Î› =>
    refine ofPotentialTerm_mono (x := âŸ¨none, none, {q1, q2}, {q3}âŸ©) ?Î›Sub _ ?Î›P
  case' W1 =>
    refine ofPotentialTerm_mono (x := âŸ¨none, none, {q1}, {q2, q3, q4}âŸ©) ?W1Sub _ ?W1P
  case' W2 =>
    refine ofPotentialTerm_mono (x := âŸ¨q1, none, âˆ…, {q2, q3, q4}âŸ©) ?W2Sub _ ?W2P
  case' W3 => refine ofPotentialTerm_mono (x := âŸ¨none, q1, {q2, q3}, âˆ…âŸ©) ?W3Sub _ ?W3P
  case' W4 => refine ofPotentialTerm_mono (x := âŸ¨q1, q2, {q3}, âˆ…âŸ©) ?W4Sub _ ?W4P
  case' K1 =>
    refine ofPotentialTerm_mono (x := âŸ¨none, none, {q1}, {q2, q3}âŸ©)
      ?K1Sub _ ?K1P
  case' K2 => refine ofPotentialTerm_mono (x := âŸ¨q1, q2, âˆ…, {q3}âŸ©) ?K2Sub _ ?K2P
  case' topYukawa =>
    refine ofPotentialTerm_mono (x := âŸ¨none, q1, âˆ…, {q2, q3}âŸ©)
      ?topYukawaSub _ ?topYukawaP
  case' bottomYukawa =>
    refine ofPotentialTerm_mono (x := âŸ¨q1, none, {q2}, {q3}âŸ©)
      ?bottomYukawaSub _ ?bottomYukawaP
  case' Î¼Sub | Î²Sub | Î›Sub | W1Sub | W2Sub | W3Sub | W4Sub | K1Sub | K2Sub |
      topYukawaSub | bottomYukawaSub =>
    rw [subset_def]
    simp_all [Finset.insert_subset, -existsAndEq]
  all_goals
    simp [ofPotentialTerm, PotentialTerm.toFieldLabel, ofFieldLabel]
  case Î›P =>
    use q1, q2
    simp [â† q_sum]
  case W3P | K1P | topYukawaP =>
    use q2, q3
    simp [â† q_sum]
    abel
  case W1P | W2P =>
    use q2, q3, q4
    simp [â† q_sum]
    abel
  all_goals
    rw [â† q_sum]
    try abel

/-!

### C.3. Equivalence of elements of `ofPotentialTerm` and `ofPotentialTerm'`

We now show that a charge is in `ofPotentialTerm` if and only if it is in
`ofPotentialTerm'`. I.e. their underlying finite sets are equal.
We do not say anything about the multiplicity of elements within the multisets,
which is not important for us.

-/
lemma mem_ofPotentialTerm_iff_mem_ofPotentialTerm [DecidableEq ğ“©]
    {T : PotentialTerm} {n : ğ“©} {y : ChargeSpectrum ğ“©} :
    n âˆˆ y.ofPotentialTerm T â†” n âˆˆ y.ofPotentialTerm' T := by
  constructor
  Â· exact fun h => ofPotentialTerm_subset_ofPotentialTerm' T h
  Â· exact fun h => ofPotentialTerm'_subset_ofPotentialTerm T h

/-!

### C.4. Induced monotonicity of `ofPotentialTerm'`

Due to the equivalence of elements of `ofPotentialTerm` and `ofPotentialTerm'`,
we can now also show that `ofPotentialTerm'` is monotone in its charge spectrum argument.

-/

lemma ofPotentialTerm'_mono [DecidableEq ğ“©] {x y : ChargeSpectrum ğ“©}
    (h : x âŠ† y) (T : PotentialTerm) :
    x.ofPotentialTerm' T âŠ† y.ofPotentialTerm' T := by
  intro i
  rw [â† mem_ofPotentialTerm_iff_mem_ofPotentialTerm, â† mem_ofPotentialTerm_iff_mem_ofPotentialTerm]
  exact fun a => ofPotentialTerm_mono h T a

end ChargeSpectrum

end SU5
end SuperSymmetry
