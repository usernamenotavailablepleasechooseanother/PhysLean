/-
Copyright (c) 2025 Joseph Tooby-Smith. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Joseph Tooby-Smith
-/
import PhysLean.SpaceAndTime.Space.RadialAngularMeasure
import PhysLean.SpaceAndTime.Time.Derivatives
import Mathlib.Tactic.Cases
/-!

# Functions on `Space d` which can be made into distributions

## i. Overview

In this module, for functions `f : Space d ‚Üí F`, we define the property `IsDistBounded f`.
Functions satisfying this property can be used to create distributions `Space d ‚Üíd[‚Ñù] F`
by integrating them against Schwartz maps.

The condition `IsDistBounded f` essentially says that `f` is bounded by a finite sum of terms
of the form `c * ‚Äñx + g‚Äñ ^ p` for constants `c`, `g` and `- (d - 1) ‚â§ p ` where `d` is the dimension
of the space.

## ii. Key results

- `IsDistBounded` : The boundedness condition on functions `Space d ‚Üí F` for them to
  form distributions.
- `IsDistBounded.integrable_space` : If `f` satisfies `IsDistBounded f`, then
  `fun x => Œ∑ x ‚Ä¢ f x` is integrable for any Schwartz map `Œ∑ : ùì¢(Space d, ‚Ñù)`.
- `IsDistBounded.integrable_time_space` : If `f` satisfies `IsDistBounded f`, then
  `fun x => Œ∑ x ‚Ä¢ f x.2` is integrable for any Schwartz map
  `Œ∑ : ùì¢(Time √ó Space d, ‚Ñù)`.
- `IsDistBounded.mono` : If `f‚ÇÅ` satisfies `IsDistBounded f‚ÇÅ` and
  `‚Äñf‚ÇÇ x‚Äñ ‚â§ ‚Äñf‚ÇÅ x‚Äñ` for all `x`, then `f‚ÇÇ` satisfies `IsDistBounded f‚ÇÇ`.

## iii. Table of contents

- A. The predicate `IsDistBounded f`
- B. Integrability properties of functions satisfying `IsDistBounded f`
  - B.1. `AEStronglyMeasurable` conditions
  - B.2. Integrability with respect to Schwartz maps on space
  - B.3. Integrability with respect to Schwartz maps on time and space
  - B.4. Integrability with respect to inverse powers
- C. Integral on Schwartz maps is bounded by seminorms
- D. Construction rules for `IsDistBounded f`
  - D.1. Addition
  - D.2. Finite sums
  - D.3. Scalar multiplication
  - D.4. Components of euclidean-valued functions
  - D.5. Compositions with additions and subtractions
  - D.6. Congruence with respect to the norm
  - D.7. Monotonicity with respect to the norm
  - D.8. Inner products
- E. Specific functions that are `IsDistBounded`
  - E.1. Constant functions
  - E.2. Powers of norms
- F. Multiplication by norms and components

## iv. References

-/
open SchwartzMap NNReal
noncomputable section

variable (ùïú : Type) {E F F' : Type} [RCLike ùïú] [NormedAddCommGroup E] [NormedAddCommGroup F]
  [NormedAddCommGroup F'] [NormedSpace ‚Ñù F] [NormedSpace ‚Ñù F']

namespace Space

variable [NormedSpace ‚Ñù E]

open MeasureTheory

/-!

## A. The predicate `IsDistBounded f`

-/

/-- The boundedness condition on a function ` EuclideanSpace ‚Ñù (Fin dm1.succ) ‚Üí F`
  for it to form a distribution. -/
@[fun_prop]
def IsDistBounded {d : ‚Ñï} (f : Space d ‚Üí F) : Prop :=
  AEStronglyMeasurable (fun x => f x) volume ‚àß
  ‚àÉ n, ‚àÉ c : Fin n ‚Üí ‚Ñù, ‚àÉ g : Fin n ‚Üí Space d,
    ‚àÉ p : Fin n ‚Üí ‚Ñ§,
    (‚àÄ i, 0 ‚â§ c i) ‚àß
    (‚àÄ i, - (d - 1 : ‚Ñï) ‚â§ p i) ‚àß
    ‚àÄ x, ‚Äñf x‚Äñ ‚â§ ‚àë i, c i * ‚Äñx + g i‚Äñ ^ p i

namespace IsDistBounded

/-!

## B. Integrability properties of functions satisfying `IsDistBounded f`

-/

/-!

### B.1. `AEStronglyMeasurable` conditions

-/
omit [NormedSpace ‚Ñù F] in
@[fun_prop]
lemma aestronglyMeasurable {d : ‚Ñï} {f : Space d ‚Üí F} (hf : IsDistBounded f) :
    AEStronglyMeasurable (fun x => f x) volume := hf.1

@[fun_prop]
lemma aeStronglyMeasurable_schwartzMap_smul {d : ‚Ñï} {f : Space d ‚Üí F}
    (hf : IsDistBounded f) (Œ∑ : ùì¢(Space d, ‚Ñù)) :
    AEStronglyMeasurable (fun x => Œ∑ x ‚Ä¢ f x) := by
  fun_prop

@[fun_prop]
lemma aeStronglyMeasurable_fderiv_schwartzMap_smul {d : ‚Ñï} {f : Space d ‚Üí F}
    (hf : IsDistBounded f) (Œ∑ : ùì¢(Space d, ‚Ñù)) (y : Space d) :
    AEStronglyMeasurable (fun x => fderiv ‚Ñù Œ∑ x y ‚Ä¢ f x) := by
  fun_prop

@[fun_prop]
lemma aeStronglyMeasurable_inv_pow {d r : ‚Ñï} {f : Space d ‚Üí F}
    (hf : IsDistBounded f) :
    AEStronglyMeasurable (fun x => ‚Äñ((1 + ‚Äñx‚Äñ) ^ r)‚Åª¬π‚Äñ ‚Ä¢ f x) := by
  apply AEStronglyMeasurable.smul
  ¬∑ apply AEMeasurable.aestronglyMeasurable
    fun_prop
  ¬∑ fun_prop

@[fun_prop]
lemma aeStronglyMeasurable_time_schwartzMap_smul {d : ‚Ñï} {f : Space d ‚Üí F}
    (hf : IsDistBounded f) (Œ∑ : ùì¢(Time √ó Space d, ‚Ñù)) :
    AEStronglyMeasurable (fun x => Œ∑ x ‚Ä¢ f x.2) := by
  apply AEStronglyMeasurable.smul
  ¬∑ fun_prop
  ¬∑ apply MeasureTheory.AEStronglyMeasurable.comp_snd
    fun_prop

/-!

### B.2. Integrability with respect to Schwartz maps on space

-/

@[fun_prop]
lemma integrable_space {d : ‚Ñï} {f : Space d ‚Üí F} (hf : IsDistBounded f)
    (Œ∑ : ùì¢(Space d, ‚Ñù)) :
    Integrable (fun x : Space d => Œ∑ x ‚Ä¢ f x) volume := by
  /- Reducing the problem to `Integrable (fun x : Space d => Œ∑ x * ‚Äñx + c‚Äñ ^ p)` -/
  suffices h2 : ‚àÄ (p : ‚Ñ§) (hp : - (d - 1 : ‚Ñï) ‚â§ p) (c : Space d) (Œ∑ : ùì¢(Space d, ‚Ñù)),
      Integrable (fun x : Space d => Œ∑ x * ‚Äñx + c‚Äñ ^ p) volume by
    obtain ‚ü®n, c, g, p, c_nonneg, p_bound, bound‚ü© := hf.2
    apply Integrable.mono (g := fun x => ‚àë i, (c i * (‚ÄñŒ∑ x‚Äñ * ‚Äñx + g i‚Äñ ^ p i))) _
    ¬∑ fun_prop
    ¬∑ filter_upwards with x
      rw [norm_smul]
      apply le_trans (mul_le_mul_of_nonneg_left (bound x) (norm_nonneg (Œ∑ x)))
      apply le_of_eq
      simp only [Real.norm_eq_abs]
      rw [Finset.abs_sum_of_nonneg (fun i _ => mul_nonneg (c_nonneg i) (by positivity)),
        Finset.mul_sum]
      ring_nf
    ¬∑ apply MeasureTheory.integrable_finset_sum
      intro i _
      apply Integrable.const_mul
      specialize h2 (p i) (p_bound i) (g i) Œ∑
      rw [‚Üê MeasureTheory.integrable_norm_iff] at h2
      simpa using h2
      apply AEMeasurable.aestronglyMeasurable
      fun_prop
  /- Reducing the problem to `Integrable (fun x : Space d => Œ∑ x * ‚Äñx‚Äñ ^ p)` -/
  suffices h0 : ‚àÄ (p : ‚Ñ§) (hp : - (d - 1 : ‚Ñï) ‚â§ p) (Œ∑ : ùì¢(Space d, ‚Ñù)),
      Integrable (fun x : Space d => Œ∑ x * ‚Äñx‚Äñ ^ p) volume by
    intro p hp c Œ∑
    suffices h1 : Integrable (fun x => Œ∑ ((x + c) - c) * ‚Äñx + c‚Äñ ^ p) volume by
      simpa using h1
    apply MeasureTheory.Integrable.comp_add_right (g := c) (f := fun x => Œ∑ (x - c) * ‚Äñx‚Äñ ^ p)
    apply h0 p hp (Œ∑.compCLM (ùïú := ‚Ñù) ?_ ?_)
    ¬∑ apply Function.HasTemperateGrowth.of_fderiv (k := 1) (C := 1 + ‚Äñc‚Äñ)
      ¬∑ convert Function.HasTemperateGrowth.const (ContinuousLinearMap.id ‚Ñù (Space d))
        simp [fderiv_sub_const]
      ¬∑ fun_prop
      ¬∑ refine fun x => (norm_sub_le _ _).trans (le_of_sub_nonneg ?_)
        ring_nf
        positivity
    ¬∑ refine ‚ü®1, (1 + ‚Äñc‚Äñ), fun x => (norm_le_norm_add_norm_sub' x c).trans (le_of_sub_nonneg ?_)‚ü©
      ring_nf
      positivity
  /- Proving `Integrable (fun x : Space d => Œ∑ x * ‚Äñx + c‚Äñ ^ p)` -/
  intro p hp Œ∑
  have h1 : AEStronglyMeasurable (fun (x : Space d) => ‚Äñx‚Äñ ^ p) volume :=
      AEMeasurable.aestronglyMeasurable <| by fun_prop
  rw [‚Üê MeasureTheory.integrable_norm_iff (by fun_prop)]
  simp only [norm_mul, norm_zpow, norm_norm]
  match d with
  | 0 => simp only [Real.norm_eq_abs, Integrable.of_finite]
  | d + 1 =>
  by_cases hp' : p = 0
  ¬∑ subst hp'
    simp only [zpow_zero, mul_one]
    apply Integrable.norm
    exact Œ∑.integrable
  suffices h1 : Integrable (fun x => ‚ÄñŒ∑ x‚Äñ * ‚Äñx‚Äñ ^ (p + d)) (radialAngularMeasure (d := (d + 1))) by
    rw [integrable_radialAngularMeasure_iff] at h1
    convert h1 using 1
    funext x
    have hx : 0 ‚â§ ‚Äñx‚Äñ := norm_nonneg x
    generalize ‚Äñx‚Äñ = r at *
    simp only [Real.norm_eq_abs, add_tsub_cancel_right, one_div, smul_eq_mul]
    trans |Œ∑ x| * ((r ^ d)‚Åª¬π *r ^ (p + d)); swap
    ¬∑ ring
    congr
    by_cases hr : r = 0
    ¬∑ subst hr
      simp [zero_pow_eq, zero_zpow_eq, hp']
      omega
    field_simp
    rw [zpow_add‚ÇÄ hr]
    rfl
  convert integrable_pow_mul_iteratedFDeriv radialAngularMeasure Œ∑ (p + d).toNat 0 using 1
  funext x
  simp only [Real.norm_eq_abs, norm_iteratedFDeriv_zero]
  rw [mul_comm]
  congr 1
  rw [‚Üê zpow_natCast]
  congr
  refine Int.eq_natCast_toNat.mpr ?_
  omega

@[fun_prop]
lemma integrable_space_mul {d : ‚Ñï} {f : Space d ‚Üí ‚Ñù} (hf : IsDistBounded f)
    (Œ∑ : ùì¢(Space d, ‚Ñù)) :
    Integrable (fun x : Space d => Œ∑ x * f x) volume := by
  exact hf.integrable_space Œ∑

@[fun_prop]
lemma integrable_space_fderiv {d : ‚Ñï} {f : Space d ‚Üí F} (hf : IsDistBounded f)
    (Œ∑ : ùì¢(Space d, ‚Ñù)) (y : Space d) :
    Integrable (fun x : Space d => fderiv ‚Ñù Œ∑ x y ‚Ä¢ f x) volume := by
  exact hf.integrable_space (SchwartzMap.pderivCLM ‚Ñù y Œ∑)

@[fun_prop]
lemma integrable_space_fderiv_mul {d : ‚Ñï} {f : Space d ‚Üí ‚Ñù} (hf : IsDistBounded f)
    (Œ∑ : ùì¢(Space d, ‚Ñù)) (y : Space d) :
    Integrable (fun x : Space d => fderiv ‚Ñù Œ∑ x y * f x) volume := by
  exact hf.integrable_space (SchwartzMap.pderivCLM ‚Ñù y Œ∑)

/-!

### B.3. Integrability with respect to Schwartz maps on time and space

-/

instance {D1 : Type} [NormedAddCommGroup D1] [MeasurableSpace D1]
    {D2 : Type} [NormedAddCommGroup D2] [MeasurableSpace D2]
    (Œº1 : Measure D1) (Œº2 : Measure D2)
    [Measure.HasTemperateGrowth Œº1] [Measure.HasTemperateGrowth Œº2]
    [OpensMeasurableSpace (D1 √ó D2)] :
    Measure.HasTemperateGrowth (Œº1.prod Œº2) where
  exists_integrable := by
    obtain ‚ü®rt1, h1‚ü© := Measure.HasTemperateGrowth.exists_integrable (Œº := Œº1)
    obtain ‚ü®rt2, h2‚ü© := Measure.HasTemperateGrowth.exists_integrable (Œº := Œº2)
    use rt1 + rt2
    apply Integrable.mono' (h1.mul_prod h2)
    ¬∑ apply AEMeasurable.aestronglyMeasurable
      fun_prop
    filter_upwards with x
    simp only [Nat.cast_add, neg_add_rev, Real.norm_eq_abs, Real.rpow_neg_natCast, zpow_neg,
      zpow_natCast]
    calc _
      _ = |(1 + ‚Äñx‚Äñ) ^ (-(rt1 : ‚Ñù)) * (1 + ‚Äñx‚Äñ) ^ (-(rt2 : ‚Ñù))| := by
        rw [Real.rpow_add (by positivity), mul_comm]
      _ = (1 + ‚Äñx‚Äñ) ^ (-(rt1 : ‚Ñù)) * (1 + ‚Äñx‚Äñ) ^ (-(rt2 : ‚Ñù)) := by
        rw [abs_of_nonneg (by positivity)]
    simp only [Real.rpow_neg_natCast, zpow_neg, zpow_natCast]
    apply mul_le_mul _ _ (by positivity) (by positivity)
    ¬∑ refine inv_anti‚ÇÄ (by positivity) (pow_le_pow_left‚ÇÄ (by positivity) ?_ rt1)
      rcases x
      simp
    ¬∑ refine inv_anti‚ÇÄ (by positivity) (pow_le_pow_left‚ÇÄ (by positivity) ?_ rt2)
      rcases x
      simp

@[fun_prop]
lemma integrable_time_space {d : ‚Ñï} {f : Space d ‚Üí F} (hf : IsDistBounded f)
    (Œ∑ : ùì¢(Time √ó Space d, ‚Ñù)) :
    Integrable (fun x : Time √ó Space d => Œ∑ x ‚Ä¢ f x.2) volume := by
  /- Reducing the problem to `Integrable (fun x : Time √ó Space d => Œ∑ x * ‚Äñx.2 + c‚Äñ ^ p)` -/
  suffices h2 : ‚àÄ (p : ‚Ñ§) (hp : - (d - 1 : ‚Ñï) ‚â§ p) (c : Space d) (Œ∑ : ùì¢(Time √ó Space d, ‚Ñù)),
      Integrable (fun x : Time √ó Space d => Œ∑ x * ‚Äñx.2 + c‚Äñ ^ p) volume by
    obtain ‚ü®n, c, g, p, c_nonneg, p_bound, bound‚ü© := hf.2
    apply Integrable.mono (g := fun x => ‚àë i, (c i * (‚ÄñŒ∑ x‚Äñ * ‚Äñx.2 + g i‚Äñ ^ p i))) _
    ¬∑ fun_prop
    ¬∑ filter_upwards with x
      rw [norm_smul]
      apply le_trans (mul_le_mul_of_nonneg_left (bound x.2) (norm_nonneg (Œ∑ x)))
      apply le_of_eq
      simp only [Real.norm_eq_abs]
      rw [Finset.abs_sum_of_nonneg (fun i _ => mul_nonneg (c_nonneg i) (by positivity)),
        Finset.mul_sum]
      ring_nf
    ¬∑ apply MeasureTheory.integrable_finset_sum
      intro i _
      apply Integrable.const_mul
      specialize h2 (p i) (p_bound i) (g i) Œ∑
      rw [‚Üê MeasureTheory.integrable_norm_iff] at h2
      simpa using h2
      apply AEMeasurable.aestronglyMeasurable
      fun_prop
  /- Reducing the problem to `Integrable (fun x : Space d => Œ∑ x * ‚Äñx‚Äñ ^ p)` -/
  suffices h0 : ‚àÄ (p : ‚Ñ§) (hp : - (d - 1 : ‚Ñï) ‚â§ p) (Œ∑ : ùì¢(Time √ó Space d, ‚Ñù)),
      Integrable (fun x : Time √ó Space d => Œ∑ x * ‚Äñx.2‚Äñ ^ p) volume by
    intro p hp c Œ∑
    suffices h1 : Integrable (fun (x : Time √ó Space d) =>
        Œ∑ ((x + (0, c)) - (0, c)) * ‚Äñ(x + (0, c)).2‚Äñ ^ p) (volume.prod volume) by
      simpa using h1
    apply MeasureTheory.Integrable.comp_add_right (g := (0, c))
      (f := fun x => Œ∑ (x - (0, c)) * ‚Äñx.2‚Äñ ^ p)
    apply h0 p hp (Œ∑.compCLM (ùïú := ‚Ñù) ?_ ?_)
    ¬∑ apply Function.HasTemperateGrowth.of_fderiv (k := 1) (C := 1 + ‚Äñc‚Äñ)
      ¬∑ convert Function.HasTemperateGrowth.const (ContinuousLinearMap.id ‚Ñù (Time √ó Space d))
        simp [fderiv_sub_const]
      ¬∑ fun_prop
      ¬∑ refine fun x => (norm_sub_le _ _).trans (le_of_sub_nonneg ?_)
        ring_nf
        simp only [Prod.norm_mk, norm_zero, norm_nonneg, sup_of_le_right,
          add_add_sub_cancel]
        positivity
    ¬∑ refine ‚ü®1, (1 + ‚Äñ((0, c) : Time √ó Space d)‚Äñ),
        fun x => (norm_le_norm_add_norm_sub' x (0,c)).trans (le_of_sub_nonneg ?_)‚ü©
      ring_nf
      positivity
  /- Proving `Integrable (fun x : Space d => Œ∑ x * ‚Äñx.2‚Äñ ^ p)` -/
  intro p hp Œ∑
  rw [‚Üê MeasureTheory.integrable_norm_iff (AEMeasurable.aestronglyMeasurable (by fun_prop))]
  simp only [norm_mul, norm_zpow, norm_norm]
  by_cases hp : p = 0
  ¬∑ subst hp
    simp only [zpow_zero, mul_one]
    apply Integrable.norm
    change Integrable (‚áëŒ∑) (volume.prod volume)
    exact Œ∑.integrable
  suffices h1 : Integrable (fun x => ‚ÄñŒ∑ x‚Äñ * ‚Äñx.2‚Äñ ^ (p + (d - 1 : ‚Ñï)))
      (volume.prod (radialAngularMeasure (d := d))) by
    match d with
    | 0 => simpa using h1
    | d + 1 =>
    rw [radialAngularMeasure, MeasureTheory.prod_withDensity_right] at h1
    erw [integrable_withDensity_iff_integrable_smul‚ÇÄ (by fun_prop)] at h1
    convert h1 using 1
    funext x
    simp only [Real.norm_eq_abs, one_div]
    rw [Real.toNNReal_of_nonneg, NNReal.smul_def]
    simp only [inv_nonneg, norm_nonneg, pow_nonneg, coe_mk, smul_eq_mul]
    ring_nf
    rw [mul_assoc]
    congr
    have hx : 0 ‚â§ ‚Äñx.2‚Äñ := norm_nonneg x.2
    generalize ‚Äñx.2‚Äñ = r at *
    by_cases hr : r = 0
    ¬∑ subst hr
      simp only [inv_zero]
      rw [zero_pow_eq, zero_zpow_eq, zero_zpow_eq]
      split_ifs <;> simp
      any_goals omega
    ¬∑ simp only [inv_pow]
      field_simp
      rw [zpow_add‚ÇÄ hr]
      simp
    ¬∑ simp
    ¬∑ fun_prop
  apply Integrable.mono' (integrable_pow_mul_iteratedFDeriv _ Œ∑ (p + (d - 1 : ‚Ñï)).toNat 0)
  ¬∑ apply AEMeasurable.aestronglyMeasurable
    fun_prop
  filter_upwards with x
  simp only [Real.norm_eq_abs, norm_iteratedFDeriv_zero]
  rw [mul_comm]
  rw [‚Üê zpow_natCast]
  rw [abs_of_nonneg (by positivity)]
  apply mul_le_mul _ (by rfl) (by positivity) (by positivity)
  rw [zpow_natCast]
  trans ‚Äñx.2‚Äñ ^ ((p + (d - 1 : ‚Ñï)).toNat : ‚Ñ§)
  ¬∑ apply le_of_eq
    congr
    refine Int.eq_natCast_toNat.mpr (by omega)
  rw [zpow_natCast]
  ring_nf
  apply pow_le_pow_left‚ÇÄ (by positivity) _ (p + (d - 1 : ‚Ñï)).toNat
  rcases x
  simp

/-!

### B.4. Integrability with respect to inverse powers

-/

lemma integrable_mul_inv_pow {d : ‚Ñï}
    {f : Space d ‚Üí F} (hf : IsDistBounded f) :
    ‚àÉ r, Integrable (fun x => ‚Äñ((1 + ‚Äñx‚Äñ) ^ r)‚Åª¬π‚Äñ ‚Ä¢ f x) volume := by
  suffices h0 : ‚àÄ pmax, ‚àÉ r, ‚àÄ (p : ‚Ñ§) (hp : - (d - 1 : ‚Ñï) ‚â§ p) (c : Space d)
      (p_le : p ‚â§ pmax),
      Integrable (fun x => ‚Äñ((1 + ‚Äñx‚Äñ) ^ r)‚Åª¬π‚Äñ * ‚Äñx + c‚Äñ ^ p) volume by
    obtain ‚ü®n, c, g, p, c_nonneg, p_bound, bound‚ü© := hf.2
    match n with
    | 0 => simp at bound; simp [bound]
    | n + 1 =>
    let pMax := Finset.max' (Finset.image p Finset.univ) (by simp)
    have pMax_max (i : Fin n.succ) : p i ‚â§ pMax := by
      simp [pMax]
      apply Finset.le_max'
      simp
    obtain ‚ü®r, hr‚ü© := h0 pMax
    use r
    apply Integrable.mono (g := fun x => ‚àë i, (c i * (‚Äñ((1 + ‚Äñx‚Äñ) ^ r)‚Åª¬π‚Äñ * ‚Äñx + g i‚Äñ ^ p i))) _
    ¬∑ fun_prop
    ¬∑ filter_upwards with x
      rw [norm_smul]
      apply le_trans (mul_le_mul_of_nonneg_left (bound x) (by positivity))
      apply le_of_eq
      simp only [norm_inv, norm_pow, Real.norm_eq_abs, abs_abs]
      rw [Finset.abs_sum_of_nonneg (fun i _ => mul_nonneg (c_nonneg i) (by positivity)),
        Finset.mul_sum]
      ring_nf
    ¬∑ apply MeasureTheory.integrable_finset_sum
      intro i _
      apply Integrable.const_mul
      apply (hr (p i) (p_bound i) (g i) (pMax_max i)).mono
      ¬∑ fun_prop
      ¬∑ filter_upwards with x
        simp
  match d with
  | 0 => simp
  | d + 1 =>
  suffices h0 : ‚àÄ (q : ‚Ñï) (c : Space (d + 1)), Integrable (fun x => ‚Äñx + c‚Äñ ^ (q - d : ‚Ñ§)
        * ‚Äñ((1 + ‚Äñx‚Äñ) ^ (q + (radialAngularMeasure (d := d + 1)).integrablePower))‚Åª¬π‚Äñ) volume by
    intro pMax
    use (pMax + d).toNat + (radialAngularMeasure (d := d + 1)).integrablePower
    intro p hp c p_le
    apply (h0 (p + d).toNat c).mono
    ¬∑ fun_prop
    ¬∑ filter_upwards with x
      simp only [norm_inv, norm_pow, Real.norm_eq_abs, norm_mul, abs_abs, norm_zpow,
        Int.ofNat_toNat]
      rw [mul_comm]
      refine mul_le_mul ?_ ?_ (by positivity) (by positivity)
      ¬∑ rw [max_eq_left (by omega)]
        simp
      ¬∑ refine inv_pow_le_inv_pow_of_le ?_ ?_
        ¬∑ rw [abs_of_nonneg (by positivity)]
          simp
        ¬∑ simp_all
  let m := (radialAngularMeasure (d := (d + 1))).integrablePower
  suffices h0 : ‚àÄ (q : ‚Ñï) (c : Space (d + 1)),
      Integrable (fun x => ‚Äñx‚Äñ ^ (q - d : ‚Ñ§) * ‚Äñ((1 + ‚Äñx - c‚Äñ) ^ (q + m))‚Åª¬π‚Äñ) volume by
    intro q c
    convert (h0 q c).comp_add_right c using 1
    funext x
    simp [m]
  suffices h0 : ‚àÄ (q : ‚Ñï) (v : Space (d + 1)),
      Integrable (fun x => ‚Äñx‚Äñ ^ q * ‚Äñ((1 + ‚Äñx - v‚Äñ) ^ (q + m))‚Åª¬π‚Äñ) radialAngularMeasure by
    intro q v
    specialize h0 q v
    rw [integrable_radialAngularMeasure_iff] at h0
    apply Integrable.congr h0
    rw [Filter.eventuallyEq_iff_exists_mem]
    use {0}·∂ú
    constructor
    ¬∑ rw [compl_mem_ae_iff, measure_singleton]
    intro x hx
    simp [‚Üê mul_assoc]
    left
    rw [zpow_sub‚ÇÄ (by simpa using hx), zpow_natCast, zpow_natCast]
    field_simp
  intro q v
  have hr1 (x : Space (d + 1)) :
        ‚Äñ((1 + ‚Äñx - v‚Äñ) ^ (q + m))‚Åª¬π‚Äñ = ((1 + ‚Äñx - v‚Äñ) ^ (q + m))‚Åª¬π := by
      simp only [norm_inv, norm_pow, Real.norm_eq_abs, inv_inj]
      rw [abs_of_nonneg (by positivity)]
  apply integrable_of_le_of_pow_mul_le (C‚ÇÅ := 1) (C‚ÇÇ :=2 ^ (q + m - 1) * (‚Äñv‚Äñ ^ (q + m) + 1))
  ¬∑ simp
    intro x
    refine inv_le_one_of_one_le‚ÇÄ ?_
    rw [abs_of_nonneg (by positivity)]
    refine one_le_pow‚ÇÄ ?_
    simp
  ¬∑ intro x
    rw [hr1]
    refine mul_inv_le_of_le_mul‚ÇÄ ?_ (by positivity) ?_
    ¬∑ positivity
    change ‚Äñx‚Äñ^ (q + m) ‚â§ _
    by_cases hzero : m = 0 ‚àß q = 0
    ¬∑ rcases hzero with ‚ü®hm, hq‚ü©
      generalize hm : m = m' at *
      subst hm hq
      rw [pow_zero, pow_zero]
      simp
    trans (‚Äñv‚Äñ + ‚Äñx - v‚Äñ) ^ (q + m)
    ¬∑ rw [pow_le_pow_iff_left‚ÇÄ]
      ¬∑ apply norm_le_norm_add_norm_sub'
      ¬∑ positivity
      ¬∑ positivity
      simp only [ne_eq, Nat.add_eq_zero, not_and]
      intro hq
      omega
    apply (add_pow_le _ _ _).trans
    trans 2 ^ (q + m - 1) * (‚Äñv‚Äñ ^ (q + m) + ‚Äñx - v‚Äñ ^ (q + m)) + (2 ^ (q + m - 1)
      + 2 ^ (q + m - 1) * ‚Äñv‚Äñ ^ (q + m) * ‚Äñx - v‚Äñ ^ (q + m))
    ¬∑ simp
      positivity
    trans (2 ^ (q + m - 1) * (‚Äñv‚Äñ ^ (q + m) + 1)) * (1 + ‚Äñx - v‚Äñ ^ (q + m))
    ¬∑ ring_nf
      apply le_of_eq
      rfl
    refine mul_le_mul_of_nonneg (by rfl) ?_ ?_ ?_
    ¬∑ trans 1 ^ (q + m) + ‚Äñx - v‚Äñ ^ (q + m)
      ¬∑ simp
      apply pow_add_pow_le
      ¬∑ simp
      ¬∑ positivity
      ¬∑ simp
        omega
    ¬∑ positivity
    ¬∑ positivity
    ¬∑ positivity
    ¬∑ positivity
  ¬∑ refine Measurable.aestronglyMeasurable ?_
    fun_prop

/-!

## C. Integral on Schwartz maps is bounded by seminorms

-/

lemma integral_mul_schwartzMap_bounded {d : ‚Ñï} {f : Space d ‚Üí F} (hf : IsDistBounded f) :
    ‚àÉ (s : Finset (‚Ñï √ó ‚Ñï)) (C : ‚Ñù),
    0 ‚â§ C ‚àß ‚àÄ (Œ∑ : ùì¢(Space d, ‚Ñù)),
    ‚Äñ‚à´ (x : Space d), Œ∑ x ‚Ä¢ f x‚Äñ ‚â§ C * (s.sup (schwartzSeminormFamily ‚Ñù (Space d) ‚Ñù)) Œ∑ := by
  obtain ‚ü®r, hr‚ü© := hf.integrable_mul_inv_pow
  use Finset.Iic (r, 0), 2 ^ r * ‚à´ x, ‚Äñf x‚Äñ * ‚Äñ((1 + ‚Äñx‚Äñ) ^ r)‚Åª¬π‚Äñ
  refine ‚ü®by positivity, fun Œ∑ ‚Ü¶ (norm_integral_le_integral_norm _).trans ?_‚ü©
  rw [‚Üê integral_const_mul, ‚Üê integral_mul_const]
  refine integral_mono_of_nonneg ?_ ?_ ?_
  ¬∑ filter_upwards with x
    positivity
  ¬∑ apply Integrable.mul_const
    apply Integrable.const_mul
    apply Integrable.congr' hr
    ¬∑ apply AEStronglyMeasurable.mul
      ¬∑ fun_prop
      ¬∑ apply AEMeasurable.aestronglyMeasurable
        fun_prop
    filter_upwards with x
    simp [norm_smul, mul_comm]
  ¬∑ filter_upwards with x
    simp [norm_smul]
    trans (2 ^ r *
      ((Finset.Iic (r, 0)).sup (schwartzSeminormFamily ‚Ñù (Space d) ‚Ñù)) Œ∑
      *(|1 + ‚Äñx‚Äñ| ^ r)‚Åª¬π) * ‚Äñf x‚Äñ; swap
    ¬∑ apply le_of_eq
      ring
    apply mul_le_mul_of_nonneg ?_ (by rfl) (by positivity) (by positivity)
    have h0 := one_add_le_sup_seminorm_apply (ùïú := ‚Ñù) (m := (r, 0))
      (k := r) (n := 0) le_rfl le_rfl Œ∑ x
    rw [Lean.Grind.Field.IsOrdered.le_mul_inv_iff_mul_le _ _ (by positivity)]
    convert h0 using 1
    simp only [norm_iteratedFDeriv_zero, Real.norm_eq_abs]
    ring_nf
    congr
    rw [abs_of_nonneg (by positivity)]

/-!

## D. Construction rules for `IsDistBounded f`

-/

section constructors

variable (ùïú : Type) {E F F' : Type} [RCLike ùïú] [NormedAddCommGroup E] [NormedAddCommGroup F]
  [NormedAddCommGroup F'] [NormedSpace ‚Ñù F']

@[fun_prop]
lemma zero {d} : IsDistBounded (0 : Space d ‚Üí F) := by
  apply And.intro
  ¬∑ fun_prop
  use 1, fun _ => 0, fun _ => 0, fun _ => 0
  simp

/-!

### D.1. Addition

-/
@[fun_prop]
lemma add {d : ‚Ñï} {f g : Space d ‚Üí F}
    (hf : IsDistBounded f) (hg : IsDistBounded g) : IsDistBounded (f + g) := by
  apply And.intro
  ¬∑ fun_prop
  rcases hf with ‚ü®hae1, ‚ü®n1, c1, g1, p1, c1_nonneg, p1_bound, bound1‚ü©‚ü©
  rcases hg with ‚ü®hae2, ‚ü®n2, c2, g2, p2, c2_nonneg, p2_bound, bound2‚ü©‚ü©
  refine ‚ü®n1 + n2, Fin.append c1 c2, Fin.append g1 g2, Fin.append p1 p2, ?_, ?_, ?_‚ü©
  ¬∑ intro i
    obtain ‚ü®i, rfl‚ü© := finSumFinEquiv.surjective i
    match i with
    | .inl i =>
      simp only [finSumFinEquiv_apply_left, Fin.append_left, ge_iff_le]
      exact c1_nonneg i
    | .inr i =>
      simp only [finSumFinEquiv_apply_right, Fin.append_right, ge_iff_le]
      exact c2_nonneg i
  ¬∑ intro i
    obtain ‚ü®i, rfl‚ü© := finSumFinEquiv.surjective i
    match i with
    | .inl i =>
      simp only [finSumFinEquiv_apply_left, Fin.append_left, ge_iff_le]
      exact p1_bound i
    | .inr i =>
      simp only [finSumFinEquiv_apply_right, Fin.append_right, ge_iff_le]
      exact p2_bound i
  ¬∑ intro x
    apply (norm_add_le _ _).trans
    apply (add_le_add (bound1 x) (bound2 x)).trans
    apply le_of_eq
    rw [‚Üê finSumFinEquiv.sum_comp]
    simp

@[fun_prop]
lemma fun_add {d : ‚Ñï} {f g : Space d ‚Üí F}
    (hf : IsDistBounded f) (hg : IsDistBounded g) : IsDistBounded (fun x => f x + g x) := by
  exact hf.add hg

/-!

### D.2. Finite sums

-/

lemma sum {Œπ : Type*} {s : Finset Œπ} {d : ‚Ñï} {f : Œπ ‚Üí Space d ‚Üí F}
    (hf : ‚àÄ i ‚àà s, IsDistBounded (f i)) : IsDistBounded (‚àë i ‚àà s, f i) := by
  classical
  induction' s using Finset.induction with i s hi ih
  ¬∑ simp
    fun_prop
  rw [Finset.sum_insert]
  apply IsDistBounded.add
  ¬∑ exact hf i (s.mem_insert_self i)
  ¬∑ exact ih (fun j hj => hf j (s.mem_insert_of_mem hj))
  exact hi

lemma sum_fun {Œπ : Type*} {s : Finset Œπ} {d : ‚Ñï} {f : Œπ ‚Üí Space d ‚Üí F}
    (hf : ‚àÄ i ‚àà s, IsDistBounded (f i)) : IsDistBounded (fun x => ‚àë i ‚àà s, f i x) := by
  convert sum hf using 1
  funext x
  simp

/-!

### D.3. Scalar multiplication

-/

@[fun_prop]
lemma const_smul {d : ‚Ñï} [NormedSpace ‚Ñù F] {f : Space d ‚Üí F}
    (hf : IsDistBounded f) (c : ‚Ñù) : IsDistBounded (c ‚Ä¢ f) := by
  rcases hf with ‚ü®hae1, ‚ü®n1, c1, g1, p1, c1_nonneg, p1_bound, bound1‚ü©‚ü©
  apply And.intro
  ¬∑ fun_prop
  refine ‚ü®n1, ‚Äñc‚Äñ ‚Ä¢ c1, g1, p1, ?_, p1_bound, ?_‚ü©
  ¬∑ intro i
    simp only [Real.norm_eq_abs, Pi.smul_apply, smul_eq_mul]
    have hi := c1_nonneg i
    positivity
  ¬∑ intro x
    simp [norm_smul]
    conv_rhs => enter [2, x]; rw [mul_assoc]
    rw [‚Üê Finset.mul_sum]
    refine mul_le_mul (by rfl) (bound1 x) ?_ ?_
    ¬∑ exact norm_nonneg (f x)
    ¬∑ exact abs_nonneg c

@[fun_prop]
lemma const_fun_smul {d : ‚Ñï} [NormedSpace ‚Ñù F] {f : Space d ‚Üí F}
    (hf : IsDistBounded f) (c : ‚Ñù) : IsDistBounded (fun x => c ‚Ä¢ f x) := by
  convert hf.const_smul c using 1

@[fun_prop]
lemma const_mul_fun {d : ‚Ñï}
    {f : Space d ‚Üí ‚Ñù}
    (hf : IsDistBounded f) (c : ‚Ñù) : IsDistBounded (fun x => c * f x) := by
  convert hf.const_smul c using 1

@[fun_prop]
lemma mul_const_fun {d : ‚Ñï}
    {f : Space d ‚Üí ‚Ñù}
    (hf : IsDistBounded f) (c : ‚Ñù) : IsDistBounded (fun x => f x * c) := by
  convert hf.const_smul c using 2
  simp only [Pi.smul_apply, smul_eq_mul]
  ring

/-!

### D.4. Components of euclidean-valued functions

-/

lemma pi_comp {d n : ‚Ñï}
    {f : Space d ‚Üí EuclideanSpace ‚Ñù (Fin n)}
    (hf : IsDistBounded f) (j : Fin n) : IsDistBounded (fun x => f x j) := by
  rcases hf with ‚ü®hae1, ‚ü®n1, c1, g1, p1, c1_nonneg, p1_bound, bound1‚ü©‚ü©
  apply And.intro
  ¬∑ fun_prop
  refine ‚ü®n1, c1, g1, p1, c1_nonneg, p1_bound, ?_‚ü©
  intro x
  apply le_trans ?_ (bound1 x)
  simp only [Real.norm_eq_abs]
  rw [@PiLp.norm_eq_of_L2]
  refine Real.abs_le_sqrt ?_
  trans ‚àë i ‚àà {j}, ‚Äñ(f x) i‚Äñ ^ 2
  ¬∑ simp
  apply Finset.sum_le_univ_sum_of_nonneg
  intro y
  exact sq_nonneg ‚Äñf x y‚Äñ

/-!

### D.5. Compositions with additions and subtractions

-/

lemma comp_add_right {d : ‚Ñï} {f : Space d ‚Üí F}
    (hf : IsDistBounded f) (c : Space d) :
    IsDistBounded (fun x => f (x + c)) := by
  rcases hf with ‚ü®hae1, ‚ü®n1, c1, g1, p1, c1_nonneg, p1_bound, bound1‚ü©‚ü©
  apply And.intro
  ¬∑ simp
    apply AEStronglyMeasurable.comp_measurable
    ¬∑ rw [Measure.IsAddRightInvariant.map_add_right_eq_self]
      fun_prop
    ¬∑ fun_prop
  refine ‚ü®n1, c1, fun i => g1 i + c, p1, c1_nonneg, p1_bound, ?_‚ü©
  intro x
  apply (bound1 (x + c)).trans
  apply le_of_eq
  congr 1
  funext x
  congr 3
  module

lemma comp_sub_right {d : ‚Ñï} {f : Space d ‚Üí F}
    (hf : IsDistBounded f) (c : Space d) :
    IsDistBounded (fun x => f (x - c)) := by
  convert hf.comp_add_right (- c) using 1

/-!

### D.6. Congruence with respect to the norm

-/

omit [NormedSpace ‚Ñù F'] in
lemma congr {d : ‚Ñï} {f : Space d ‚Üí F}
    {g : Space d ‚Üí F'}
    (hf : IsDistBounded f) (hae : AEStronglyMeasurable g) (hfg : ‚àÄ x, ‚Äñg x‚Äñ = ‚Äñf x‚Äñ) :
      IsDistBounded g := by
  rcases hf with ‚ü®hae1, ‚ü®n1, c1, g1, p1, c1_nonneg, p1_bound, bound1‚ü©‚ü©
  apply And.intro
  ¬∑ exact hae
  refine ‚ü®n1, c1, g1, p1, c1_nonneg, p1_bound, ?_‚ü©
  intro x
  rw [hfg x]
  exact bound1 x

/-!

### D.7. Monotonicity with respect to the norm

-/

omit [NormedSpace ‚Ñù F'] in
lemma mono {d : ‚Ñï} {f : Space d ‚Üí F}
    {g : Space d ‚Üí F'}
    (hf : IsDistBounded f) (hae : AEStronglyMeasurable g)
    (hfg : ‚àÄ x, ‚Äñg x‚Äñ ‚â§ ‚Äñf x‚Äñ) : IsDistBounded g := by
  rcases hf with ‚ü®hae1, ‚ü®n1, c1, g1, p1, c1_nonneg, p1_bound, bound1‚ü©‚ü©
  apply And.intro
  ¬∑ exact hae
  refine ‚ü®n1, c1, g1, p1, c1_nonneg, p1_bound, ?_‚ü©
  intro x
  exact (hfg x).trans (bound1 x)

/-!

### D.8. Inner products

-/

open InnerProductSpace
@[fun_prop]
lemma inner_left {d n : ‚Ñï}
    {f : Space d ‚Üí EuclideanSpace ‚Ñù (Fin n) }
    (hf : IsDistBounded f) (y : EuclideanSpace ‚Ñù (Fin n)) :
    IsDistBounded (fun x => ‚ü™f x, y‚ü´_‚Ñù) := by
  rcases hf with ‚ü®hae1, ‚ü®n1, c1, g1, p1, c1_nonneg, p1_bound, bound1‚ü©‚ü©
  apply And.intro
  ¬∑ fun_prop
  refine ‚ü®n1, fun i => ‚Äñy‚Äñ * c1 i, g1, p1, ?_, p1_bound, ?_‚ü©
  ¬∑ intro i
    simp only
    have hi := c1_nonneg i
    positivity
  ¬∑ intro x
    apply (norm_inner_le_norm (f x) y).trans
    rw [mul_comm]
    conv_rhs => enter [2, i]; rw [mul_assoc]
    rw [‚Üê Finset.mul_sum]
    refine mul_le_mul (by rfl) (bound1 x) ?_ ?_
    ¬∑ exact norm_nonneg (f x)
    ¬∑ exact norm_nonneg y

/-!

## E. Specific functions that are `IsDistBounded`

-/

/-!

### E.1. Constant functions

-/

@[fun_prop]
lemma const {d : ‚Ñï} (f : F) :
    IsDistBounded (d := d) (fun _ : Space d => f) := by
  apply And.intro
  ¬∑ fun_prop
  use 1, fun _ => ‚Äñf‚Äñ, fun _ => 0, fun _ => 0
  simp

/-!

### E.2. Powers of norms

-/

@[fun_prop]
lemma pow {d : ‚Ñï} (n : ‚Ñ§) (hn : - (d - 1 : ‚Ñï) ‚â§ n) :
    IsDistBounded (d := d) (fun x => ‚Äñx‚Äñ ^ n) := by
  apply And.intro
  ¬∑ apply AEMeasurable.aestronglyMeasurable
    fun_prop
  refine ‚ü®1, fun _ => 1, fun _ => 0, fun _ => n, ?_, ?_, ?_‚ü©
  ¬∑ simp
  ¬∑ simp
    exact hn
  ¬∑ intro x
    simp

@[fun_prop]
lemma pow_shift {d : ‚Ñï} (n : ‚Ñ§)
    (g : Space d) (hn : - (d - 1 : ‚Ñï) ‚â§ n) :
    IsDistBounded (d := d) (fun x => ‚Äñx - g‚Äñ ^ n) := by
  apply And.intro
  ¬∑ apply AEMeasurable.aestronglyMeasurable
    fun_prop
  refine ‚ü®1, fun _ => 1, fun _ => (- g), fun _ => n, ?_, ?_, ?_‚ü©
  ¬∑ simp
  ¬∑ simp
    exact hn
  ¬∑ intro x
    simp
    rfl

@[fun_prop]
lemma nat_pow {d : ‚Ñï} (n : ‚Ñï) :
    IsDistBounded (d := d) (fun x => ‚Äñx‚Äñ ^ n) := by
  exact IsDistBounded.pow (d := d) (n : ‚Ñ§) (by omega)

@[fun_prop]
lemma norm_add_nat_pow {d : ‚Ñï} (n : ‚Ñï) (a : ‚Ñù) :
    IsDistBounded (d := d) (fun x => (‚Äñx‚Äñ + a) ^ n) := by
  conv =>
    enter [1, x]
    rw [add_pow]
  apply IsDistBounded.sum_fun
  intro i _
  fun_prop

@[fun_prop]
lemma norm_add_pos_nat_zpow {d : ‚Ñï} (n : ‚Ñ§) (a : ‚Ñù) (ha : 0 < a) :
    IsDistBounded (d := d) (fun x => (‚Äñx‚Äñ + a) ^ n) := by
  match n with
  | Int.ofNat n => fun_prop
  | Int.negSucc n =>
    apply IsDistBounded.mono (f := fun x => (a ^ ((n + 1)))‚Åª¬π)
    ¬∑ fun_prop
    ¬∑ apply AEMeasurable.aestronglyMeasurable
      fun_prop
    ¬∑ intro x
      simp only [zpow_negSucc, norm_inv, norm_pow, Real.norm_eq_abs]
      refine inv_anti‚ÇÄ (by positivity) ?_
      refine (pow_le_pow_iff_left‚ÇÄ (by positivity) (by positivity) (by simp)).mpr ?_
      rw [abs_of_nonneg (by positivity), abs_of_nonneg (by positivity)]
      simp

@[fun_prop]
lemma nat_pow_shift {d : ‚Ñï} (n : ‚Ñï)
    (g : Space d) :
    IsDistBounded (d := d) (fun x => ‚Äñx - g‚Äñ ^ n) := by
  exact IsDistBounded.pow_shift (d := d) (n : ‚Ñ§) g (by omega)

@[fun_prop]
lemma inv {n : ‚Ñï} :
    IsDistBounded (d := n.succ.succ) (fun x => ‚Äñx‚Äñ‚Åª¬π) := by
  convert IsDistBounded.pow (d := n.succ.succ) (-1) (by simp) using 1
  ext1 x
  simp

@[fun_prop]
lemma norm {d : ‚Ñï} : IsDistBounded (d := d) (fun x => ‚Äñx‚Äñ) := by
  convert IsDistBounded.nat_pow (d := d) 1 using 1
  ext1 x
  simp

@[fun_prop]
lemma log_norm {d : ‚Ñï} :
    IsDistBounded (d := d.succ.succ) (fun x => Real.log ‚Äñx‚Äñ) := by
  apply IsDistBounded.mono (f := fun x => ‚Äñx‚Äñ‚Åª¬π + ‚Äñx‚Äñ)
  ¬∑ fun_prop
  ¬∑ apply AEMeasurable.aestronglyMeasurable
    fun_prop
  ¬∑ intro x
    simp only [Nat.succ_eq_add_one, Real.norm_eq_abs]
    conv_rhs => rw [abs_of_nonneg (by positivity)]
    have h1 := Real.neg_inv_le_log (x := ‚Äñx‚Äñ) (by positivity)
    have h2 := Real.log_le_rpow_div (x := ‚Äñx‚Äñ) (by positivity) (Œµ := 1) (by positivity)
    simp_all
    rw [abs_le']
    generalize Real.log ‚Äñx‚Äñ = r at *
    apply And.intro
    ¬∑ apply h2.trans
      simp
    ¬∑ rw [neg_le]
      apply le_trans _ h1
      simp

lemma zpow_smul_self {d : ‚Ñï} (n : ‚Ñ§) (hn : - (d - 1 : ‚Ñï) - 1 ‚â§ n) :
    IsDistBounded (d := d) (fun x => ‚Äñx‚Äñ ^ n ‚Ä¢ x) := by
  by_cases hzero : n = -1
  ¬∑ apply IsDistBounded.mono (f := fun x => (1 : ‚Ñù))
    ¬∑ fun_prop
    ¬∑ apply AEMeasurable.aestronglyMeasurable
      fun_prop
    ¬∑ intro x
      simp [norm_smul]
      subst hzero
      simp only [Int.reduceNeg, zpow_neg, zpow_one]
      by_cases hx : x = 0
      ¬∑ subst hx
        simp
      rw [inv_mul_cancel‚ÇÄ]
      simpa using hx
  apply IsDistBounded.congr (f := fun x => ‚Äñx‚Äñ ^ (n + 1))
  ¬∑ apply pow
    omega
  ¬∑ apply AEMeasurable.aestronglyMeasurable
    fun_prop
  ¬∑ intro x
    by_cases hx : x = 0
    ¬∑ subst hx
      simp only [norm_zero, smul_zero, norm_zpow]
      rw [@zero_zpow_eq]
      rw [if_neg]
      omega
    ¬∑ simp [norm_smul]
      rw [zpow_add‚ÇÄ]
      simp only [zpow_one]
      ring_nf
      simpa using hx

lemma inv_pow_smul_self {d : ‚Ñï} (n : ‚Ñï) (hn : - (d - 1 : ‚Ñï) - 1 ‚â§ (- n : ‚Ñ§)) :
    IsDistBounded (d := d) (fun x => ‚Äñx‚Äñ‚Åª¬π ^ n ‚Ä¢ x) := by
  convert zpow_smul_self (n := - (n : ‚Ñ§)) (by omega) using 1
  funext x
  simp
/-!

## F. Multiplication by norms and components

-/

lemma norm_smul_nat_pow {d} (p : ‚Ñï) (c : Space d) :
    IsDistBounded (fun x => ‚Äñx‚Äñ * ‚Äñx + c‚Äñ ^ p) := by
  apply IsDistBounded.mono (f := fun x => ‚Äñx‚Äñ * (‚Äñx‚Äñ + ‚Äñc‚Äñ) ^ p)
  ¬∑ conv =>
      enter [1, x]
      rw [add_pow]
      rw [Finset.mul_sum]
    apply IsDistBounded.sum_fun
    intro i _
    conv =>
      enter [1, x]
      rw [‚Üê mul_assoc, ‚Üê mul_assoc]
    apply IsDistBounded.mul_const_fun
    apply IsDistBounded.mul_const_fun
    convert IsDistBounded.nat_pow (n := i + 1) using 1
    funext x
    ring
  ¬∑ apply AEMeasurable.aestronglyMeasurable
    fun_prop
  ¬∑ intro x
    simp [norm_mul, norm_pow, Real.norm_eq_abs]
    rw [abs_of_nonneg (by positivity)]
    have h1 : ‚Äñx + c‚Äñ ‚â§ ‚Äñx‚Äñ + ‚Äñc‚Äñ := norm_add_le x c
    have h2 : ‚Äñx + c‚Äñ ^ p ‚â§ (‚Äñx‚Äñ + ‚Äñc‚Äñ) ^ p := by
      refine pow_le_pow_left‚ÇÄ (by positivity) h1 p
    apply (mul_le_mul (by rfl) h2 (by positivity) (by positivity)).trans
    rfl

lemma norm_smul_zpow {d} (p : ‚Ñ§) (c : Space d) (hn : - (d - 1 : ‚Ñï) ‚â§ p) :
    IsDistBounded (fun x => ‚Äñx‚Äñ * ‚Äñx + c‚Äñ ^ p) := by
  match p with
  | Int.ofNat p => exact norm_smul_nat_pow p c
  | Int.negSucc p =>
    suffices h0 : IsDistBounded (fun x => ‚Äñx - c‚Äñ * (‚Äñx‚Äñ ^ (p + 1))‚Åª¬π) by
      convert h0.comp_sub_right (- c) using 1
      funext x
      simp
    suffices h0 : IsDistBounded (fun x => (‚Äñx‚Äñ + ‚Äñc‚Äñ) * (‚Äñx‚Äñ ^ (p + 1))‚Åª¬π) by
      apply h0.mono
      ¬∑ fun_prop
      ¬∑ intro x
        simp [norm_mul, norm_inv, norm_pow, Real.norm_eq_abs]
        rw [abs_of_nonneg (by positivity)]
        apply mul_le_mul (norm_sub_le x c) (by rfl) (by positivity) (by positivity)
    suffices h0 : IsDistBounded (fun x => ‚Äñx‚Äñ * (‚Äñx‚Äñ ^ (p + 1))‚Åª¬π + ‚Äñc‚Äñ * (‚Äñx‚Äñ ^ (p + 1))‚Åª¬π) by
      convert h0 using 1
      funext x
      ring
    suffices h0 : IsDistBounded (fun x => ‚Äñx‚Äñ * (‚Äñx‚Äñ ^ (p + 1))‚Åª¬π) by
      apply h0.add
      ¬∑ apply IsDistBounded.const_mul_fun
        exact IsDistBounded.pow (d := d) (n := -(p + 1)) (by grind)
    by_cases hp : p = 0
    ¬∑ subst hp
      simp only [zero_add, pow_one]
      apply IsDistBounded.mono (f := fun x => (1 : ‚Ñù))
      ¬∑ fun_prop
      ¬∑ apply AEMeasurable.aestronglyMeasurable
        fun_prop
      ¬∑ intro x
        simp only [norm_mul, norm_norm, norm_inv, one_mem, CStarRing.norm_of_mem_unitary]
        by_cases hx : ‚Äñx‚Äñ ‚â† 0
        ¬∑ rw [mul_inv_cancel‚ÇÄ (by positivity)]
        ¬∑ simp at hx
          subst hx
          simp
    convert IsDistBounded.pow (d := d) (n := - p) (by grind) using 1
    funext x
    trans (‚Äñx‚Äñ ^ p)‚Åª¬π; swap
    ¬∑ rw [@zpow_neg]
      simp
    by_cases hx : ‚Äñx‚Äñ ‚â† 0
    field_simp
    ring
    simp at hx
    subst hx
    simp only [norm_zero, ne_eq, Nat.add_eq_zero, one_ne_zero, and_false, not_false_eq_true,
      zero_pow, inv_zero, mul_zero, zero_eq_inv]
    rw [@zero_pow_eq]
    simp [hp]

@[fun_prop]
lemma norm_smul_isDistBounded {d : ‚Ñï} [NormedSpace ‚Ñù F] {f : Space d ‚Üí F}
    (hf : IsDistBounded f) :
    IsDistBounded (fun x => ‚Äñx‚Äñ ‚Ä¢ f x) := by
  obtain ‚ü®hae, ‚ü®n, c, g, p, c_nonneg, p_bound, bound‚ü©‚ü© := hf
  apply IsDistBounded.mono (f := fun x => ‚Äñx‚Äñ * ‚àë i, (c i * ‚Äñx + g i‚Äñ ^ (p i)))
  ¬∑ apply IsDistBounded.congr (f := fun x => ‚àë i, (c i * (‚Äñx‚Äñ * ‚Äñx + g i‚Äñ ^ (p i))))
    ¬∑ apply IsDistBounded.sum_fun
      intro i _
      apply IsDistBounded.const_mul_fun
      exact norm_smul_zpow (p i) (g i) (p_bound i)
    ¬∑ fun_prop
    ¬∑ intro x
      congr
      rw [Finset.mul_sum]
      congr
      funext i
      ring
  ¬∑ fun_prop
  ¬∑ intro x
    simp [_root_.norm_smul]
    apply (mul_le_mul (by rfl) (bound x) (by positivity) (by positivity)).trans
    rw [abs_of_nonneg]
    apply Finset.sum_nonneg
    intro i _
    apply mul_nonneg
    ¬∑ exact c_nonneg i
    ¬∑ positivity

@[fun_prop]
lemma norm_mul_isDistBounded {d : ‚Ñï} {f : Space d ‚Üí ‚Ñù}
    (hf : IsDistBounded f) :
    IsDistBounded (fun x => ‚Äñx‚Äñ * f x) := by
  convert hf.norm_smul_isDistBounded using 1

@[fun_prop]
lemma component_smul_isDistBounded {d : ‚Ñï} [NormedSpace ‚Ñù F] {f : Space d ‚Üí F}
    (hf : IsDistBounded f) (i : Fin d) :
    IsDistBounded (fun x => x i ‚Ä¢ f x) := by
  apply IsDistBounded.mono (f := fun x => ‚Äñx‚Äñ ‚Ä¢ f x)
  ¬∑ fun_prop
  ¬∑ apply AEStronglyMeasurable.smul
    ¬∑ have h1 : AEStronglyMeasurable (fun x => Space.coordCLM i x) := by
        fun_prop
      convert h1 using 1
      funext i
      simp [coordCLM_apply, coord_apply]
    ¬∑ fun_prop
  ¬∑ intro x
    simp [norm_smul]
    apply mul_le_mul ?_ (by rfl) (by positivity) (by positivity)
    rw [@PiLp.norm_eq_of_L2]
    refine Real.abs_le_sqrt ?_
    apply le_trans _ (Finset.sum_le_univ_sum_of_nonneg (s := {i}) _)
    ¬∑ simp
    ¬∑ intro i
      positivity

@[fun_prop]
lemma component_mul_isDistBounded {d : ‚Ñï} {f : Space d ‚Üí ‚Ñù}
    (hf : IsDistBounded f) (i : Fin d) :
    IsDistBounded (fun x => x i * f x) := by
  convert hf.component_smul_isDistBounded i using 2

@[fun_prop]
lemma isDistBounded_smul_self {d : ‚Ñï} {f : Space d ‚Üí ‚Ñù}
    (hf : IsDistBounded f) : IsDistBounded (fun x => f x ‚Ä¢ x) := by
  apply IsDistBounded.congr (f := fun x => ‚Äñx‚Äñ * f x)
  ¬∑ fun_prop
  ¬∑ apply AEStronglyMeasurable.smul
    ¬∑ fun_prop
    ¬∑ fun_prop
  ¬∑ intro x
    simp [norm_smul]
    ring

@[fun_prop]
lemma isDistBounded_smul_inner {d : ‚Ñï} [NormedSpace ‚Ñù F] {f : Space d ‚Üí F}
    (hf : IsDistBounded f) (y : Space d) : IsDistBounded (fun x => ‚ü™y, x‚ü´_‚Ñù ‚Ä¢ f x) := by
  have h1 (x : Space d) : ‚ü™y, x‚ü´_‚Ñù ‚Ä¢ f x = ‚àë i, (y i * x i) ‚Ä¢ f x := by
    rw [inner_eq_sum, ‚Üê Finset.sum_smul]
  conv =>
    enter [1, x]
    rw [h1 x]
  apply IsDistBounded.sum_fun
  intro i _
  simp [‚Üê smul_smul]
  refine const_fun_smul ?_ (y i)
  fun_prop

@[fun_prop]
lemma isDistBounded_mul_inner {d : ‚Ñï} {f : Space d ‚Üí ‚Ñù}
    (hf : IsDistBounded f) (y : Space d) : IsDistBounded (fun x => ‚ü™y, x‚ü´_‚Ñù * f x) := by
  convert hf.isDistBounded_smul_inner y using 2

lemma isDistBounded_mul_inner' {d : ‚Ñï} {f : Space d ‚Üí ‚Ñù}
    (hf : IsDistBounded f) (y : Space d) : IsDistBounded (fun x => ‚ü™x, y‚ü´_‚Ñù * f x) := by
  convert hf.isDistBounded_smul_inner y using 2
  rw [real_inner_comm]
  simp

@[fun_prop]
lemma mul_inner_pow_neg_two {d : ‚Ñï}
    (y : Space d.succ.succ) :
    IsDistBounded (fun x => ‚ü™y, x‚ü´_‚Ñù * ‚Äñx‚Äñ ^ (- 2 : ‚Ñ§)) := by
  apply IsDistBounded.mono (f := fun x => (‚Äñy‚Äñ * ‚Äñx‚Äñ) * ‚Äñx‚Äñ ^ (- 2 : ‚Ñ§))
  ¬∑ simp [mul_assoc]
    apply IsDistBounded.const_mul_fun
    apply IsDistBounded.congr (f := fun x => ‚Äñx‚Äñ ^ (- 1 : ‚Ñ§))
    ¬∑ apply IsDistBounded.pow (d := d.succ.succ) (-1) (by simp)
    ¬∑ apply AEMeasurable.aestronglyMeasurable
      fun_prop
    ¬∑ intro x
      simp only [norm_mul, norm_norm, norm_inv, norm_zpow, Int.reduceNeg, zpow_neg, zpow_one]
      by_cases hx : x = 0
      ¬∑ subst hx
        simp
      have hx' : ‚Äñx‚Äñ ‚â† 0 := by
        simpa using hx
      field_simp
  ¬∑ apply AEMeasurable.aestronglyMeasurable
    fun_prop
  ¬∑ intro x
    simp
    apply mul_le_mul_of_nonneg _ (by rfl) (by positivity) (by positivity)
    exact abs_real_inner_le_norm y x

end constructors
end IsDistBounded
