/-
Copyright (c) 2025 Joseph Tooby-Smith. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Joseph Tooby-Smith
-/
import PhysLean.Particles.SuperSymmetry.SU5.ChargeSpectrum.OfPotentialTerm
import Mathlib.Tactic.FinCases
/-!

# Charges allowing terms

## i. Overview

To each charge spectrum `x : ChargeSpectrum ğ“©` we say it
allows the potential term `T : PotentialTerm`, if one of the charges associated with that
potential term is zero.

What this means, is that there is a choice of charges from the charge spectrum `x` that
can be assigned to the fields in the potential term `T` such that the total charge is zero,
and therefore that the term is present in the potential. The presence of
absence of certain terms is of phenomenological importance.

This concept is captured by the proposition `AllowsTerm`.

In addition to this, for each potential term `T`, we define a function `allowsTermForm`
which takes three elements of `ğ“©`, `a`, `b`, and `c` and returns a charge spectrum
which allows the term `T`. We will show in `allowsTerm_iff_subset_allowsTermForm`
that any charge spectrum that allows a term `T` has a subset which can be expressed as
`allowsTermForm a b c T` for some `a`, `b`, and `c`.

We also define the propositions `AllowsTermQ5 x q5 T` and `AllowsTermQ10 x q10 T`
which correspond to the condition that adding a charge `q5` to the `Q5` charges of
the charge spectrum `x`, or adding a charge `q10` to the `Q10` charges of the
charge spectrum `x`, leads to a zero charge in the charges of potential term `T`.

## ii. Key results

- `AllowsTerm` : The proposition that a charge spectrum allows a potential term.
- `allowsTermForm` : A function which for each potential term `T` and three charges
  `a`, `b`, and `c` returns a charge spectrum which allows the term `T`,
  and such that any charge spectrum allowing `T` has a subset of this form.
- `AllowsTermQ5` : The proposition that adding a charge `q5` to the `Q5` charges
  of a charge spectrum `x` allows the potential term `T` due to the addition of that charge.
- `AllowsTermQ10` : The proposition that adding a charge `q10` to the `Q10` charges
  of a charge spectrum `x` allows the potential term `T` due to the addition of that charge.

## iii. Table of contents

- A. Charge spectrums allowing potential terms
  - A.1. Decidability of `AllowsTerm`
  - A.2. Monotonicity of `AllowsTerm`
- B. Forms of charges which allow potential terms
  - B.1. `allowsTermForm` allows the potential term
  - B.2. Subset relations for `allowsTermForm`
  - B.3. Card of `allowsTermForm`
  - B.4. If `AllowsTerm` then subset equal to `allowsTermForm a b c T`
  - B.5. `AllowsTerm` if and only if subset equal to `allowsTermForm a b c T`
  - B.6. Cardinality of subset allowing potential term
- C. Allowing a potential term by insertion of a `Q5` charge
  - C.1. Decidability of `AllowsTermQ5`
  - C.2. AllowsTermQ5 or AllowsTerm from AllowsTerm with inserted of `Q5` charge
  - C.3. AllowsTerm with inserted of `Q5` charge from AllowsTermQ5
  - C.4. AllowsTerm with inserted of `Q5` charge iff AllowsTermQ5 or AllowsTerm
- D. Allowing a potential term by insertion of a `Q10` charge
  - D.1. Decidability of `AllowsTermQ5`
  - D.2. AllowsTermQ10 or AllowsTerm from AllowsTerm with inserted of `Q10` charge
  - D.3. AllowsTerm with inserted of `Q10` charge from AllowsTermQ5
  - D.4. AllowsTerm with inserted of `Q10` charge iff AllowsTermQ10 or AllowsTerm

## iv. References

There are no known references for the results in this file.

-/

namespace SuperSymmetry
namespace SU5

namespace ChargeSpectrum

variable {ğ“© : Type} [AddCommGroup ğ“©]

/-!

## A. Charge spectrums allowing potential terms

We first define the proposition `AllowsTerm`, which for a charge spectrum `x : ChargeSpectrum ğ“©`
and a potential term `T : PotentialTerm`, is true if the zero charge is in the set of
charges associated with that potential term.

That is, if there is some choice of representations present in the theory which will allow that
potential term via symmetry.

-/

/-- The charges of representations `x : Charges` allow a potential term `T : PotentialTerm`
if the zero charge is in the set of charges associated with that potential term. -/
def AllowsTerm (x : ChargeSpectrum ğ“©) (T : PotentialTerm) : Prop := 0 âˆˆ ofPotentialTerm x T

/-!

### A.1. Decidability of `AllowsTerm`

We define the decidability of `AllowsTerm` through `ofPotentialTerm'` rather than
`ofPotentialTerm` due to the speed of the former compared to the latter.

-/

lemma allowsTerm_iff_zero_mem_ofPotentialTerm' [DecidableEq ğ“©]
    {x : ChargeSpectrum ğ“©} {T : PotentialTerm} :
    x.AllowsTerm T â†” 0 âˆˆ x.ofPotentialTerm' T := by
  rw [AllowsTerm]
  exact mem_ofPotentialTerm_iff_mem_ofPotentialTerm

instance [DecidableEq ğ“©] (x : ChargeSpectrum ğ“©) (T : PotentialTerm) : Decidable (x.AllowsTerm T) :=
  decidable_of_iff (0 âˆˆ x.ofPotentialTerm' T) allowsTerm_iff_zero_mem_ofPotentialTerm'.symm

/-!

### A.2. Monotonicity of `AllowsTerm`

The proposition `AllowsTerm` is monotone in its charge spectrum argument.
That is if a charge spectrum `y` is a subset of a charge spectrum `x`,
and `y` allows a potential term `T`, then `x` also allows that potential term `T`.

-/

lemma allowsTerm_mono {T : PotentialTerm} {y x : ChargeSpectrum ğ“©}
    (h : y âŠ† x) (hy : y.AllowsTerm T) : x.AllowsTerm T := ofPotentialTerm_mono h T hy

/-!

## B. Forms of charges which allow potential terms

We now define the function `allowsTermForm` which for each potential term `T`
and three charges `a`, `b`, and `c` returns a charge spectrum which allows the term `T`.

These charges are in a minimal form, in the sense that any charge spectrum allowing `T`
has a subset of this form.

-/

variable [DecidableEq ğ“©]

/-- A element of `Charges` from three integers `a b c : â„¤` for a given potential term `T`.
  Defined such that `allowsTermForm a b c T` always allows the potential term `T`,
  and if any over charge `x` allows `T` then it is due to a subset of the form
  `allowsTermForm a b c T`. -/
def allowsTermForm (a b c : ğ“©) : (T : PotentialTerm) â†’ ChargeSpectrum ğ“©
  | .Î¼ => âŸ¨some a, some a, âˆ…, âˆ…âŸ©
  | .Î² => âŸ¨none, some a, {a}, âˆ…âŸ©
  | .Î› => âŸ¨none, none, {a, b}, {- a - b}âŸ©
  | .W1 => âŸ¨none, none, {- a - b - c}, {a, b, c}âŸ©
  | .W2 => âŸ¨some (- a - b - c), none, âˆ…, {a, b, c}âŸ©
  | .W3 => âŸ¨none, some (- a), {b, - b - 2 â€¢ a}, âˆ…âŸ©
  | .W4 => âŸ¨some (- c - 2 â€¢ b), some (- b), {c}, âˆ…âŸ©
  | .K1 => âŸ¨none, none, {-a}, {b, - a - b}âŸ©
  | .K2 => âŸ¨some a, some b, âˆ…, {- a - b}âŸ©
  | .topYukawa => âŸ¨none, some (-a), âˆ…, {b, - a - b}âŸ©
  | .bottomYukawa => âŸ¨some a, none, {b}, {- a - b}âŸ©

/-!

### B.1. `allowsTermForm` allows the potential term

Any charge spectrum of the form `allowsTermForm a b c T` allows the potential term `T`.

-/

/-- The charge spectrum `allowsTermForm a b c T` allows the potential term `T`. -/
lemma allowsTermForm_allowsTerm {a b c : ğ“©} {T : PotentialTerm} :
    (allowsTermForm a b c T).AllowsTerm T := by
  simp [AllowsTerm, ofPotentialTerm, allowsTermForm]
  cases T
  all_goals
    simp [PotentialTerm.toFieldLabel, ofFieldLabel]
  case Î› =>
    use a, b
    simp
  case W3 =>
    use b, -b - 2 â€¢ a
    simp only [true_or, or_true, and_self, true_and]
    abel
  case K1 =>
    use b, - a - b
    simp
  case topYukawa =>
    use b, - a - b
    simp
  case W1 =>
    use a, b, c
    simp only [true_or, or_true, and_self, true_and]
    abel
  case W2 =>
    use a, b, c
    simp only [true_or, or_true, and_self, true_and]
    abel
  all_goals abel

lemma allowsTerm_of_eq_allowsTermForm {T : PotentialTerm}
    (x : ChargeSpectrum ğ“©) (h : âˆƒ a b c, x = allowsTermForm a b c T) :
    x.AllowsTerm T := by
  obtain âŸ¨a, b, c, rflâŸ© := h
  exact allowsTermForm_allowsTerm

/-!

### B.2. Subset relations for `allowsTermForm`

For any potential term `T` except for `WÂ¹áµ¢â±¼â‚–â‚— 10â± 10Ê² 10áµ 5Ì„MË¡` or `WÂ²áµ¢â±¼â‚– 10â± 10Ê² 10áµ 5Ì„Hd`,
a charge spectrum `allowsTermForm a b c T` is a subset of another charge spectrum
`allowsTermForm a' b' c' T` if they are equal.

The reason this does not work for `W1` an `W2` is due to the presence of three
charges in the 10d representation.

-/

open PotentialTerm in
lemma allowsTermForm_eq_of_subset {a b c a' b' c' : ğ“©} {T : PotentialTerm}
    (h : allowsTermForm a b c T âŠ† allowsTermForm a' b' c' T) (hT : T â‰  W1 âˆ§ T â‰  W2) :
    allowsTermForm a b c T = allowsTermForm a' b' c' T := by
  cases T
  case' W1 | W2 => simp at hT
  all_goals
    rw [subset_def] at h
    simp [allowsTermForm] at h âŠ¢
  case' Î¼ =>
    subst h
    rfl
  case' Î² =>
    subst h
    rfl
  case' K2 =>
    obtain âŸ¨rfl, rfl, h2âŸ© := h
    simp
  case' W4 =>
    obtain âŸ¨h2, rfl, rflâŸ© := h
    simp
  case' bottomYukawa =>
    obtain âŸ¨rfl, rfl, h2âŸ© := h
    simp
  case' Î› => obtain âŸ¨h2, h1âŸ© := h
  case' K1 => obtain âŸ¨rfl, h2âŸ© := h
  case' topYukawa => obtain âŸ¨rfl, h2âŸ© := h
  case' W3 => obtain âŸ¨rfl, h2âŸ© := h
  case' topYukawa | W3 | K1 | Î› =>
    rw [Finset.insert_subset_iff] at h2
    simp at h2
    obtain âŸ¨rfl | rfl, h1 | h2âŸ© := h2
    all_goals simp_all [Finset.pair_comm]

/-!

### B.3. Card of `allowsTermForm`

The cardinality of the charge spectrum `allowsTermForm a b c T` is always
less than or equal to the degree of the potential term `T`.

-/

lemma allowsTermForm_card_le_degree {a b c : ğ“©} {T : PotentialTerm} :
    (allowsTermForm a b c T).card â‰¤ T.degree := by
  cases T
  all_goals
    simp [allowsTermForm, PotentialTerm.toFieldLabel, card, PotentialTerm.degree]
  case' Î› =>
    have h1 : Finset.card {a, b} â‰¤ 2 := Finset.card_le_two
    omega
  case' W3 =>
    have h1 : Finset.card {b, -b - 2 â€¢ a} â‰¤ 2 := Finset.card_le_two
    omega
  case' K1 =>
    have h1 : Finset.card {b, -a - b} â‰¤ 2 := Finset.card_le_two
    omega
  case' topYukawa =>
    have h1 : Finset.card {b, -a - b} â‰¤ 2 := Finset.card_le_two
    omega
  all_goals
    have h1 : Finset.card {a, b, c} â‰¤ 3 := Finset.card_le_three
    omega

/-!

### B.4. If `AllowsTerm` then subset equal to `allowsTermForm a b c T`

We now show one of the more important properties of `allowsTermForm`.
Namely that if a charge spectrum `x`
allows a potential term `T`, then there exists charges `a`, `b`, and `c` such that
`allowsTermForm a b c T âŠ† x`.

The proof of this result is rather long, relying on case-by-case analysis of each
of the potential terms of interest.

-/

lemma allowsTermForm_subset_allowsTerm_of_allowsTerm {T : PotentialTerm} {x : ChargeSpectrum ğ“©}
    (h : x.AllowsTerm T) :
    âˆƒ a b c, allowsTermForm a b c T âŠ† x âˆ§ (allowsTermForm a b c T).AllowsTerm T := by
  simp [AllowsTerm, ofPotentialTerm] at h
  cases T
  all_goals
    simp [PotentialTerm.toFieldLabel, -existsAndEq] at h
    obtain âŸ¨f1, f2, âŸ¨âŸ¨f3, f4, âŸ¨h3, f4_memâŸ©, rflâŸ©, f2_memâŸ©, f1_add_f2_eq_zeroâŸ© := h
  case' Î¼ | Î² => obtain âŸ¨rflâŸ© := h3
  case' Î› | W1 | W2 | W3 | W4 | K1 | K2 | topYukawa | bottomYukawa =>
    obtain âŸ¨f5, f6, âŸ¨h4, f6_memâŸ©, rflâŸ© := h3
  case' Î› | K1 | K2 | topYukawa | bottomYukawa => obtain âŸ¨rflâŸ© := h4
  case' W1 | W2 | W3 | W4 => obtain âŸ¨f7, f8, âŸ¨rfl, f8_memâŸ©, rflâŸ© := h4
  -- The cases which are present
  case' Î¼ => use f4, (f2), 0
  case' Î² => use (- f4), f2, 0
  case' Î› => use f4, f6, f2
  case' W1 => use f4, f6, f8
  case' W2 => use f4, f6, f8
  case' W3 => use (f2), f6, f8
  case' W4 => use f6, (f2), f8
  case' K1 => use f2, f4, f6
  case' K2 => use f4, f6, f2
  case' topYukawa => use (f2), f4, f6
  case' bottomYukawa => use f2, f4, f6
  all_goals
    rw [subset_def]
    simp_all [ofFieldLabel, Finset.insert_subset_iff, allowsTermForm]
  all_goals
    simp [AllowsTerm, ofPotentialTerm, PotentialTerm.toFieldLabel, ofFieldLabel]
  -- Replacements of equalities
  case' W1 | W2 =>
    have hf2 : f2 = -f4 - f6 - f8 := by
      rw [â† sub_zero f2, â† f1_add_f2_eq_zero]
      abel
    subst hf2
    simp_all
  case Î² =>
    have hf2 : f4 = - f2 := by
      rw [â† sub_zero f2, â† f1_add_f2_eq_zero]
      abel
    subst hf2
    simp_all
  case K2 =>
    have hf2 : f2 = - f4 - f6 := by
      rw [â† sub_zero f2, â† f1_add_f2_eq_zero]
      abel
    subst hf2
    simp_all
  case' Î› =>
    have hf2 : f2 = -f4 - f6 := by
      rw [â† sub_zero f2, â† f1_add_f2_eq_zero]
      abel
    subst hf2
    simp_all
  case' W3 =>
    subst f4_mem
    have hf8 : f8 = - f6 - 2 â€¢ f4 := by
      rw [â† sub_zero f8, â† f1_add_f2_eq_zero]
      abel
    subst hf8
    simp_all
  case' bottomYukawa =>
    have hf6 : f6 = - f2 - f4 := by
      rw [â† sub_zero f2, â† f1_add_f2_eq_zero]
      abel
    subst hf6
    simp_all
  -- AllowsTerm
  case W3 =>
    use (- f6 - 2 â€¢ f4), f6
    simpa using f1_add_f2_eq_zero
  case W1 | W2 =>
    use f4, f6, f8
    simp only [true_or, or_true, and_self, true_and]
    abel
  case K1 =>
    have hf6 : f6 = - f2 - f4 := by
      rw [â† sub_zero f2, â† f1_add_f2_eq_zero]
      abel
    subst hf6
    simp_all
    use (-f2 - f4), f4
    simp
  case' topYukawa =>
    have hf2 : f2 = - f4 - f6 := by
      rw [â† sub_zero f2, â† f1_add_f2_eq_zero]
      abel
    subst hf2
    simp_all
  case topYukawa | Î› =>
    use f6, f4
    simp only [or_true, true_or, and_self, true_and]
    abel
  case W4 =>
    apply And.intro
    Â· rw [â† sub_zero f8, â† f1_add_f2_eq_zero]
      abel
    Â· abel
  case Î¼ =>
    rw [â† sub_zero f2, â† f1_add_f2_eq_zero]
    abel

/-!

### B.5. `AllowsTerm` if and only if subset equal to `allowsTermForm a b c T`

We now lift the previous result to show that a charge spectrum `x`
allows a potential term `T` if and only if there exists charges `a`, `b`, and `c` such that
`allowsTermForm a b c T âŠ† x`.

Given what has already been shown, this result is now trivial.

-/
lemma allowsTerm_iff_subset_allowsTermForm {T : PotentialTerm} {x : ChargeSpectrum ğ“©} :
    x.AllowsTerm T â†” âˆƒ a b c, allowsTermForm a b c T âŠ† x := by
  constructor
  Â· intro h
    obtain âŸ¨a, b, c, h1, h2âŸ© := allowsTermForm_subset_allowsTerm_of_allowsTerm h
    use a, b, c
  Â· intro h
    obtain âŸ¨a, b, c, h1âŸ© := h
    apply allowsTerm_mono h1 allowsTermForm_allowsTerm

/-!

### B.6. Cardinality of subset allowing potential term

We show that if a charge spectrum `x` allows a potential term `T`,
then there exists a subset of `x` which allows `T` and whose cardinality is less than or equal
to the degree of `T`.

This follows from the fact that `allowsTermForm a b c T` always has cardinality
less than or equal to the degree of `T`.

-/

lemma subset_card_le_degree_allowsTerm_of_allowsTerm {T : PotentialTerm} {x : ChargeSpectrum ğ“©}
    (h : x.AllowsTerm T) : âˆƒ y âˆˆ x.powerset, y.card â‰¤ T.degree âˆ§ y.AllowsTerm T := by
  obtain âŸ¨a, b, c, h1, h2âŸ© := allowsTermForm_subset_allowsTerm_of_allowsTerm h
  use allowsTermForm a b c T
  simp_all
  exact allowsTermForm_card_le_degree

/-!

## C. Allowing a potential term by insertion of a `Q5` charge

We now study what happens when we add a charge `q5` to the `Q5` charges of a charge spectrum `x`.
We define the proposition `AllowsTermQ5 x q5 T` which is true if adding the charge `q5`
to the `Q5` charges of `x` allows the potential term `T` due to the addition of that charge.

We prove a number of properties of this proposition, including its relation
to `AllowsTerm` and its decidability.

-/

/-- The proposition for which says, given a charge `x` adding a charge `q5` permits the
  existence of a potential term `T` due to the addition of that charge. -/
def AllowsTermQ5 [DecidableEq ğ“©] (x : ChargeSpectrum ğ“©) (q5 : ğ“©) (T : PotentialTerm) : Prop :=
  match T with
  | .Î¼ => false
  | .Î² =>
    match x with
    | âŸ¨_, some qHu, _, _âŸ© => q5 = qHu
    | _ => false
  | .Î› => (0 : ğ“©) âˆˆ ((insert q5 x.Q5).product x.Q10).val.map (fun (q1, q2) => (q1 + q5 + q2))
  | .W4 =>
    match x with
    | âŸ¨some qHd, some qHu, _, _âŸ© => q5 + qHd - qHu - qHu = 0
    | _ => false
  | .K1 => (0 : ğ“©) âˆˆ (x.Q10.product x.Q10).val.map (fun (y, z) => -q5 + y + z)
  | .W1 => (0 : ğ“©) âˆˆ (x.Q10.product (x.Q10.product x.Q10)).val.map
    (fun (q1, q2, q3) => q5 + q1 + q2 + q3)
  | .W2 => false
  | .bottomYukawa =>
    match x with
    | âŸ¨none, _, _, _âŸ© => false
    | âŸ¨some qHd, _, _, _âŸ© => (0 : ğ“©) âˆˆ x.Q10.val.map (fun y => y + q5 + qHd)
  | .topYukawa => false
  | .K2 => false
  | .W3 =>
    match x with
    | âŸ¨_, some qHu, _, _âŸ© =>
      (0 : ğ“©) âˆˆ (insert q5 x.Q5).val.map (fun y => y + q5 - qHu - qHu)
    | _ => false

/-!

### C.1. Decidability of `AllowsTermQ5`

We show that if the type `ğ“©` has decidable equality, then the proposition
`AllowsTermQ5 x q5 T` is decidable for any charge spectrum `x`, charge `q5`, and
potential term `T`.

-/
instance [DecidableEq ğ“©] (x : ChargeSpectrum ğ“©) (q5 : ğ“©) (T : PotentialTerm) :
    Decidable (AllowsTermQ5 x q5 T) :=
  match T with
  | .Î¼ => isFalse fun h => by simp [AllowsTermQ5] at h
  | .Î² =>
    match x with
    | âŸ¨_, some qHu, _, _âŸ© => decidable_of_iff (q5 = qHu) (by simp [AllowsTermQ5])
    | âŸ¨_, none, _, _âŸ© => isFalse fun h => by simp [AllowsTermQ5] at h
  | .Î› =>
    decidable_of_iff ((0 : ğ“©) âˆˆ ((insert q5 x.Q5).product x.Q10).val.map
      (fun (q1, q2) => (q1 + q5 + q2))) (by simp [AllowsTermQ5])
  | .W4 =>
    match x with
    | âŸ¨some qHd, some qHu, _, _âŸ© => decidable_of_iff (q5 + qHd - qHu - qHu = 0)
      (by simp [AllowsTermQ5])
    | âŸ¨some qHd, none, _, _âŸ© => isFalse fun h => by simp [AllowsTermQ5] at h
    | âŸ¨none, _, _, _âŸ© => isFalse fun h => by simp [AllowsTermQ5] at h
  | .K1 =>
    decidable_of_iff ((0 : ğ“©) âˆˆ (x.Q10.product x.Q10).val.map (fun (y, z) => -q5 + y + z))
      (by simp [AllowsTermQ5])
  | .W1 =>
    decidable_of_iff ((0 : ğ“©) âˆˆ (x.Q10.product (x.Q10.product x.Q10)).val.map
    (fun (q1, q2, q3) => q5 + q1 + q2 + q3)) (by rfl)
  | .W2 => isFalse fun h => by simp [AllowsTermQ5] at h
  | .bottomYukawa =>
    match x with
    | âŸ¨none, _, _, _âŸ© => isFalse fun h => by simp [AllowsTermQ5] at h
    | âŸ¨some qHd, _, _, Q10âŸ© => decidable_of_iff ((0 : ğ“©) âˆˆ Q10.val.map (fun y => y + q5 + qHd))
      (by simp [AllowsTermQ5])
  | .topYukawa => isFalse fun h => by simp [AllowsTermQ5] at h
  | .K2 => isFalse fun h => by simp [AllowsTermQ5] at h
  | .W3 =>
    match x with
    | âŸ¨_, some qHu, Q5, _âŸ© => decidable_of_iff
      ((0 : ğ“©) âˆˆ (insert q5 Q5).val.map (fun y => y + q5 - qHu - qHu))
      (by simp [AllowsTermQ5])
    | âŸ¨_, none, _, _âŸ© => isFalse fun h => by simp [AllowsTermQ5] at h

/-!

### C.2. AllowsTermQ5 or AllowsTerm from AllowsTerm with inserted of `Q5` charge

We show that if a charge spectrum `x` with an inserted charge `q5`
allows a potential term `T`, then either the charge spectrum `x`
allows that potential term `T` *due to* the addition of that charge,
or the charge spectrum `x` already allows that potential term `T`.

-/

lemma allowsTermQ5_or_allowsTerm_of_allowsTerm_insertQ5 {qHd qHu : Option ğ“©}
    {Q5 Q10: Finset ğ“©} {q5 : ğ“©} (T : PotentialTerm)
    (h : AllowsTerm âŸ¨qHd, qHu, insert q5 Q5, Q10âŸ© T) :
    AllowsTermQ5 âŸ¨qHd, qHu, Q5, Q10âŸ© q5 T âˆ¨
    AllowsTerm âŸ¨qHd, qHu, Q5, Q10âŸ© T := by
  rcases T
  all_goals
    simp [allowsTerm_iff_zero_mem_ofPotentialTerm', ofPotentialTerm', AllowsTermQ5] at h âŠ¢
  Â· exact h
  Â· match qHu with
    | some qHu =>
      simp at h
      simp only [Multiset.mem_map, Finset.mem_val]
      convert h using 1
      rw [neg_add_eq_zero, eq_comm]
    | none => simp at h
  Â· simp only [SProd.sprod, Multiset.mem_product] at h âŠ¢
    obtain âŸ¨a1, a2, a3, âŸ¨h1, h2, h3âŸ©, hsumâŸ© := h
    simp at h1 h2
    rcases h1 with h1 | h1
    Â· subst h1
      left
      rcases h2 with h2 | h2
      Â· use a2, a3
        simp_all
      Â· use a2, a3
        simp_all
        rw [â† hsum]
        abel
    Â· rcases h2 with h2 | h2
      Â· left
        use a1, a3
        simp_all
      Â· right
        use a1, a2, a3
        simp_all
  Â· simp only [SProd.sprod, Multiset.mem_product] at h âŠ¢
    obtain âŸ¨a1, a2, a3, a4, âŸ¨h1, h2, h3, h4âŸ©, hsumâŸ© := h
    simp at h1
    rcases h1 with h1 | h1
    Â· left
      use a2, a3, a4
      simp_all
    Â· right
      use a1, a2, a3, a4
      simp_all
  Â· simp_all
  Â· match qHu with
    | some qHu =>
      simp at h
      simp only [SProd.sprod, Multiset.mem_product] at h âŠ¢
      obtain âŸ¨a1, a2, âŸ¨h1, h2âŸ©, hsumâŸ© := h
      simp at h1 h2
      rcases h1 with h1 | h1
      Â· subst h1
        left
        rcases h2 with h2 | h2
        Â· left
          subst h2
          rw [â† hsum]
          abel
        Â· right
          use a2
          simp_all
          rw [â† hsum]
          abel
      Â· rcases h2 with h2 | h2
        Â· left
          right
          use a1
          simp_all
          rw [â† hsum]
          abel
        Â· right
          rw [@Multiset.mem_map]
          simp only [Multiset.mem_product, Finset.mem_val, Prod.exists]
          use a1, a2
    | none => simp at h
  Â· match qHd, qHu with
    | some qHd, some qHu =>
      simp_all
      convert h using 1
      constructor
      all_goals
      Â· intro h
        rw [â† h]
        abel
    | none, _ => simp at h
    | some x, none => simp at h
  Â· simp only [SProd.sprod, Multiset.mem_product] at h âŠ¢
    obtain âŸ¨a1, a2, a3, âŸ¨h1, h2, h3âŸ©, hsumâŸ© := h
    simp at h1
    rcases h1 with h1 | h1
    Â· left
      use a2, a3
      simp_all
    Â· right
      use a1, a2, a3
      simp_all
  Â· simp_all
  Â· simp_all
  Â· match qHd with
    | none => simp at h
    | some qHd =>
      simp_all
      simp only [SProd.sprod, Multiset.mem_product] at h âŠ¢
      obtain âŸ¨a1, a2, âŸ¨h1, h2âŸ©, hsumâŸ© := h
      simp at h1
      rcases h1 with h1 | h1
      Â· subst h1
        left
        use a2
        simp_all
        rw [â† hsum]
        abel
      Â· right
        use a1, a2
        simp_all

/-!

### C.3. AllowsTerm with inserted of `Q5` charge from AllowsTermQ5

We show that if a charge spectrum `x` allows a potential term `T`
*due to* the addition of a charge `q5`, then the charge spectrum `x` with that charge inserted
allows that potential term `T`.

-/
lemma allowsTerm_insertQ5_of_allowsTermQ5 {qHd qHu : Option ğ“©}
    {Q5 Q10: Finset ğ“©} {q5 : ğ“©} (T : PotentialTerm)
    (h : AllowsTermQ5 âŸ¨qHd, qHu, Q5, Q10âŸ© q5 T) :
    AllowsTerm âŸ¨qHd, qHu, insert q5 Q5, Q10âŸ© T := by
  rcases T
  all_goals
    simp [AllowsTermQ5] at h
  all_goals
    simp [allowsTerm_iff_zero_mem_ofPotentialTerm', ofPotentialTerm']
    try simp only [SProd.sprod, Multiset.mem_product] at h âŠ¢
  Â· match qHu with
    | some qHu =>
      simp at h
      subst h
      simp
    | none => simp at h
  Â· obtain âŸ¨q1, q2, âŸ¨h1, h2âŸ©, hsumâŸ© := h
    simp at h1
    use q1, q5, q2
    simp_all
  Â· obtain âŸ¨q1, q2, q3, h3, hsumâŸ© := h
    use q5, q1, q2, q3
    simp_all
  Â· match qHu with
    | some qHu =>
      simp at h
      simp only [Multiset.mem_map, Multiset.mem_product, Multiset.mem_ndinsert, Finset.mem_val,
        Prod.exists]
      by_cases h' : q5 + q5 - qHu - qHu = 0
      Â· use q5, q5
        simp only [true_or, and_self, true_and]
        rw [â† h']
        abel
      Â· simp_all
        obtain âŸ¨q1, hsumâŸ© := h
        use q1, q5
        simp_all
        rw [â† hsum.2]
        abel
    | none => simp at h
  Â· match qHd, qHu with
    | some qHd, some qHu =>
      simp_all
      left
      rw [â† h]
      abel
    | none, _ => simp at h
    | some x, none => simp at h
  Â· obtain âŸ¨q1, q2, âŸ¨h1, h2âŸ©, hsumâŸ© := h
    use q5, q1, q2
    simp_all
  Â· match qHd with
    | none => simp at h
    | some qHd =>
      simp at h
      obtain âŸ¨q1, h1, hsumâŸ© := h
      simp only [Multiset.mem_map, Multiset.mem_product, Multiset.mem_ndinsert, Finset.mem_val,
        Prod.exists]
      use q5, q1
      simp_all
      rw [â† hsum]
      abel

/-!

### C.4. AllowsTerm with inserted of `Q5` charge iff AllowsTermQ5 or AllowsTerm

We show that the charge spectrum `x` with that charge inserted
allows that potential term `T` if and only if either the charge spectrum `x`
allows that potential term `T` *due to* the addition of that charge,
or the charge spectrum `x` already allows that potential term `T`.

-/

lemma allowsTerm_insertQ5_iff_allowsTermQ5 {qHd qHu : Option ğ“©}
    {Q5 Q10: Finset ğ“©} {q5 : ğ“©} (T : PotentialTerm) :
    AllowsTerm âŸ¨qHd, qHu, insert q5 Q5, Q10âŸ© T â†”
    AllowsTermQ5 âŸ¨qHd, qHu, Q5, Q10âŸ© q5 T âˆ¨
    AllowsTerm âŸ¨qHd, qHu, Q5, Q10âŸ© T := by
  constructor
  Â· exact allowsTermQ5_or_allowsTerm_of_allowsTerm_insertQ5 T
  Â· intro h
    rcases h with h | h
    Â· exact allowsTerm_insertQ5_of_allowsTermQ5 T h
    Â· apply allowsTerm_mono _ h
      simp [subset_def]

/-!

## D. Allowing a potential term by insertion of a `Q10` charge

We now replicate the previous section, but for the insertion of a `Q10` charge, rather
than a `Q5` charge.

We study what happens when we add a charge `q10` to the `Q10` charges of a charge spectrum `x`.
We define the proposition `AllowsTermQ10 x q10 T` which is true if adding the charge `q10`
to the `Q10` charges of `x` allows the potential term `T` due to the addition of that charge.

We prove a number of properties of this proposition, including its relation
to `AllowsTerm` and its decidability.

-/

/-- The proposition for which says, given a charge `x` adding a charge `q5` permits the
  existence of a potential term `T` due to the addition of that charge. -/
def AllowsTermQ10 [DecidableEq ğ“©] (x : ChargeSpectrum ğ“©) (q10 : ğ“©) (T : PotentialTerm) : Prop :=
  match T with
  | .Î¼ => false
  | .Î² => false
  | .Î› => (0 : ğ“©) âˆˆ (x.Q5.product x.Q5).val.map (fun (y, z) => y + z + q10)
  | .W4 => false
  | .K1 => (0 : ğ“©) âˆˆ (x.Q5.product (insert q10 x.Q10)).val.map (fun (q5, q2) => -q5 + q2+ q10)
  | .W1 => (0 : ğ“©) âˆˆ (x.Q5.product ((insert q10 x.Q10).product (insert q10 x.Q10))).val.map
    (fun (q5, q2, q3) => q5 + q2 + q3 + q10)
  | .W2 =>
    match x with
    | âŸ¨some qHd, _, _, _âŸ© => (0 : ğ“©) âˆˆ
      (((insert q10 x.Q10).product (insert q10 x.Q10))).val.map
      (fun (q2, q3) => qHd + q2 + q3 + q10)
    | _ => false
  | .bottomYukawa =>
    match x with
    | âŸ¨none, _, _, _âŸ© => false
    | âŸ¨some qHd, _, _, _âŸ© => (0 : ğ“©) âˆˆ x.Q5.val.map (fun y => q10 + y + qHd)
  | .topYukawa =>
    match x with
    | âŸ¨_, some qHu, _, _âŸ© => (0 : ğ“©) âˆˆ (insert q10 x.Q10).val.map (fun y => q10 + y - qHu)
    | _ => false
  | .K2 =>
    match x with
    | âŸ¨some qHd, some qHu, _, _âŸ© => qHd + qHu + q10 = 0
    | _ => false
  | .W3 => false

/-!

### D.1. Decidability of `AllowsTermQ5`

We show that if the type `ğ“©` has decidable equality, then the proposition
`AllowsTermQ10 x q10 T` is decidable for any charge spectrum `x`, charge `q10`, and
potential term `T`.

-/

instance [DecidableEq ğ“©] (x : ChargeSpectrum ğ“©) (q10 : ğ“©) (T : PotentialTerm) :
    Decidable (AllowsTermQ10 x q10 T) :=
  match T with
  | .Î¼ => isFalse fun h => by simp [AllowsTermQ10] at h
  | .Î² => isFalse fun h => by simp [AllowsTermQ10] at h
  | .Î› =>
    decidable_of_iff ((0 : ğ“©) âˆˆ (x.Q5.product x.Q5).val.map (fun (y, z) => y + z + q10))
      (by simp [AllowsTermQ10])
  | .W4 => isFalse fun h => by simp [AllowsTermQ10] at h
  | .K1 =>
    decidable_of_iff ((0 : ğ“©) âˆˆ
      (x.Q5.product (insert q10 x.Q10)).val.map (fun (q5, q2) => -q5 + q2 + q10))
      (by simp [AllowsTermQ10])
  | .W1 =>
    decidable_of_iff ((0 : ğ“©) âˆˆ
    (x.Q5.product ((insert q10 x.Q10).product (insert q10 x.Q10))).val.map
    (fun (q5, q2, q3) => q5 + q2 + q3 + q10)) (by rfl)
  | .W2 =>
    match x with
    | âŸ¨some qHd, _, _, Q10âŸ© => decidable_of_iff ((0 : ğ“©) âˆˆ
      (((insert q10 Q10).product (insert q10 Q10))).val.map
      (fun (q2, q3) => qHd + q2 + q3 + q10)) (by simp [AllowsTermQ10])
    | âŸ¨none, _, _, _âŸ© => isFalse fun h => by simp [AllowsTermQ10] at h
  | .bottomYukawa =>
    match x with
    | âŸ¨none, _, _, _âŸ© => isFalse fun h => by simp [AllowsTermQ10] at h
    | âŸ¨some qHd, _, Q5, _âŸ© => decidable_of_iff ((0 : ğ“©) âˆˆ Q5.val.map (fun y => q10 + y + qHd))
      (by simp [AllowsTermQ10])
  | .topYukawa =>
    match x with
    | âŸ¨_, some qHu, _, Q10âŸ© => decidable_of_iff
      ((0 : ğ“©) âˆˆ (insert q10 Q10).val.map (fun y => q10 + y - qHu))
      (by simp [AllowsTermQ10])
    | âŸ¨_, none, _, _âŸ© => isFalse fun h => by simp [AllowsTermQ10] at h
  | .K2 =>
    match x with
    | âŸ¨some qHd, some qHu, _, _âŸ© => decidable_of_iff (qHd + qHu + q10 = 0) (by simp [AllowsTermQ10])
    | âŸ¨some qHd, none, _, _âŸ© => isFalse fun h => by simp [AllowsTermQ10] at h
    | âŸ¨none, _, _, _âŸ© => isFalse fun h => by simp [AllowsTermQ10] at h
  | .W3 => isFalse fun h => by simp [AllowsTermQ10] at h

/-!

### D.2. AllowsTermQ10 or AllowsTerm from AllowsTerm with inserted of `Q10` charge

We show that if a charge spectrum `x` with an inserted charge `q10`
allows a potential term `T`, then either the charge spectrum `x`
allows that potential term `T` *due to* the addition of that charge,
or the charge spectrum `x` already allows that potential term `T`.

-/

lemma allowsTermQ10_or_allowsTerm_of_allowsTerm_insertQ10 {qHd qHu : Option ğ“©}
    {Q5 Q10: Finset ğ“©} {q10 : ğ“©} (T : PotentialTerm)
    (h : AllowsTerm âŸ¨qHd, qHu, Q5, insert q10 Q10âŸ© T) :
    AllowsTermQ10 âŸ¨qHd, qHu, Q5, Q10âŸ© q10 T âˆ¨
    AllowsTerm âŸ¨qHd, qHu, Q5, Q10âŸ© T := by
  rcases T
  all_goals
    simp [allowsTerm_iff_zero_mem_ofPotentialTerm', ofPotentialTerm', AllowsTermQ10] at h âŠ¢
  Â· simp_all
  Â· simp_all
  Â· simp only [SProd.sprod, Multiset.mem_product] at h âŠ¢
    obtain âŸ¨a1, a2, a3, âŸ¨h1, h2, h3âŸ©, hsumâŸ© := h
    simp at h3
    rcases h3 with h3 | h3
    Â· subst h3
      left
      use a1, a2
    Â· right
      use a1, a2, a3
      simp_all
  Â· simp only [SProd.sprod, Multiset.mem_product] at h âŠ¢
    obtain âŸ¨a1, a2, a3, a4, âŸ¨h1, h2, h3, h4âŸ©, hsumâŸ© := h
    simp at h2
    rcases h2 with h2 | h2
    Â· subst h2
      left
      use a1, a3, a4
      simp_all
      rw [â† hsum]
      abel
    simp at h3
    rcases h3 with h3 | h3
    Â· subst h3
      left
      use a1, a2, a4
      simp_all
      rw [â† hsum]
      abel
    simp at h4
    rcases h4 with h4 | h4
    Â· subst h4
      left
      use a1, a2, a3
      simp_all
    right
    use a1, a2, a3, a4
    simp_all
  Â· match qHd with
    | none => simp at h
    | some qHd =>
    simp_all
    simp only [SProd.sprod, Multiset.mem_product] at h âŠ¢
    obtain âŸ¨a1, a2, a3, âŸ¨h1, h2, h3âŸ©, hsumâŸ© := h
    simp at h1
    rcases h1 with h1 | h1
    Â· subst h1
      left
      use a2, a3
      simp_all
      rw [â† hsum]
      abel
    simp at h2
    rcases h2 with h2 | h2
    Â· subst h2
      left
      use a1, a3
      simp_all
      rw [â† hsum]
      abel
    simp at h3
    rcases h3 with h3 | h3
    Â· subst h3
      left
      use a1, a2
      simp_all
    right
    use a1, a2, a3
    simp_all
  Â· match qHu with
    | none => simp at h
    | some qHu => simp_all
  Â· match qHd, qHu with
    | none, _ => simp at h
    | some x, none => simp at h
    | some qHd, some qHu => simp_all
  Â· simp only [SProd.sprod, Multiset.mem_product] at h âŠ¢
    obtain âŸ¨a1, a2, a3, âŸ¨h1, h2, h3âŸ©, hsumâŸ© := h
    simp at h2
    rcases h2 with h2 | h2
    Â· left
      use a1, a3
      simp_all
      rw [â† hsum]
      abel
    simp at h3
    rcases h3 with h3 | h3
    Â· left
      use a1, a2
      simp_all
    right
    use a1, a2, a3
    simp_all
  Â· match qHd, qHu with
    | none, _ => simp at h
    | some x, none => simp at h
    | some qHd, some qHu => simp_all
  Â· match qHu with
    | none => simp at h
    | some qHu =>
    simp at h
    simp only [SProd.sprod, Multiset.mem_product] at h âŠ¢
    obtain âŸ¨a1, a2, âŸ¨h1, h2âŸ©, hsumâŸ© := h
    simp at h1
    rcases h1 with h1 | h1
    Â· subst h1
      left
      simp at h2
      rcases h2 with h2 | h2
      Â· subst h2
        left
        rw [â† hsum]
        abel
      Â· right
        use a2
        simp_all
        rw [â† hsum]
        abel
    simp at h2
    rcases h2 with h2 | h2
    Â· subst h2
      left; right
      use a1
      simp_all
      rw [â† hsum]
      abel
    Â· right
      simp only [Multiset.mem_map, Multiset.mem_product, Finset.mem_val, Prod.exists]
      use a1, a2
  Â· match qHd with
    | none => simp at h
    | some qHd =>
    simp_all
    simp only [SProd.sprod, Multiset.mem_product] at h âŠ¢
    obtain âŸ¨a1, a2, âŸ¨h1, h2âŸ©, hsumâŸ© := h
    simp at h2
    rcases h2 with h2 | h2
    Â· subst h2
      left
      use a1
      simp_all
      rw [â† hsum]
      abel
    right
    use a1, a2
    simp_all

/-!

### D.3. AllowsTerm with inserted of `Q10` charge from AllowsTermQ5

We show that if a charge spectrum `x` allows a potential term `T`
*due to* the addition of a charge `q10`, then the charge spectrum `x` with that charge inserted
allows that potential term `T`.

-/

lemma allowsTerm_insertQ10_of_allowsTermQ10 {qHd qHu : Option ğ“©}
    {Q5 Q10: Finset ğ“©} {q10 : ğ“©} (T : PotentialTerm)
    (h : AllowsTermQ10 âŸ¨qHd, qHu, Q5, Q10âŸ© q10 T) :
    AllowsTerm âŸ¨qHd, qHu, Q5, insert q10 Q10âŸ© T := by
  rcases T
  all_goals
    simp [AllowsTermQ10] at h
  all_goals
    simp [allowsTerm_iff_zero_mem_ofPotentialTerm', ofPotentialTerm']
    try simp only [SProd.sprod, Multiset.mem_product] at h âŠ¢
  Â· obtain âŸ¨a1, a2, âŸ¨h1, h2âŸ©, hsumâŸ© := h
    use a1, a2, q10
    simp_all
  Â· obtain âŸ¨a1, a2, a3, âŸ¨h1, h2, h3âŸ©, hsumâŸ© := h
    use a1, a2, a3, q10
    simp_all
  Â· match qHd with
    | none => simp at h
    | some qHd =>
      simp at h âŠ¢
      obtain âŸ¨a1, a2, âŸ¨h1, h2âŸ©, hsumâŸ© := h
      use a1, a2, q10
      simp_all
  Â· obtain âŸ¨a1, a2, âŸ¨h1, h2âŸ©, hsumâŸ© := h
    use a1, a2, q10
    simp_all
  Â· match qHd, qHu with
    | none, _ => simp at h
    | some x, none => simp at h
    | some qHd, some qHu => simp_all
  Â· match qHu with
    | none => simp at h
    | some qHu =>
      simp at h âŠ¢
      rcases h with h | h
      Â· use q10, q10
        rw [â† h]
        simp only [true_or, and_self, true_and]
        abel
      Â· obtain âŸ¨a1, h1, hsumâŸ© := h
        use a1, q10
        simp_all
        rw [â† hsum]
        abel
  Â· match qHd with
    | none => simp at h
    | some qHd =>
      simp at h âŠ¢
      obtain âŸ¨a1, h1, hsumâŸ© := h
      use a1, q10
      simp_all
      rw [â† hsum]
      abel
/-!

### D.4. AllowsTerm with inserted of `Q10` charge iff AllowsTermQ10 or AllowsTerm

We show that the charge spectrum `x` with that charge inserted
allows that potential term `T` if and only if either the charge spectrum `x`
allows that potential term `T` *due to* the addition of that charge,
or the charge spectrum `x` already allows that potential term `T`.

-/

lemma allowsTerm_insertQ10_iff_allowsTermQ10 {qHd qHu : Option ğ“©}
    {Q5 Q10: Finset ğ“©} {q10 : ğ“©} (T : PotentialTerm) :
    AllowsTerm âŸ¨qHd, qHu, Q5, insert q10 Q10âŸ© T â†”
    AllowsTermQ10 âŸ¨qHd, qHu, Q5, Q10âŸ© q10 T âˆ¨
    AllowsTerm âŸ¨qHd, qHu, Q5, Q10âŸ© T := by
  constructor
  Â· exact allowsTermQ10_or_allowsTerm_of_allowsTerm_insertQ10 T
  Â· intro h
    rcases h with h | h
    Â· exact allowsTerm_insertQ10_of_allowsTermQ10 T h
    Â· apply allowsTerm_mono _ h
      simp [subset_def]

end ChargeSpectrum

end SU5
end SuperSymmetry
