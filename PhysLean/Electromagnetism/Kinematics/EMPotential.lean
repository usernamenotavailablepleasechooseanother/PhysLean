/-
Copyright (c) 2025 Joseph Tooby-Smith. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Joseph Tooby-Smith
-/
import PhysLean.Electromagnetism.Basic
import PhysLean.SpaceAndTime.SpaceTime.TimeSlice
import PhysLean.Relativity.Tensors.RealTensor.CoVector.Basic
import PhysLean.Mathematics.VariationalCalculus.HasVarGradient
/-!

# The Electromagnetic Potential

## i. Overview

The electromagnetic potential `A^Î¼` is the fundamental objects in
electromagnetism. Mathematically it is related to a connection
on a `U(1)`-bundle.

We define the electromagnetic potential as a function from
spacetime to contravariant Lorentz vectors.

## ii. Key results

- `ElectromagneticPotential`: is the type of electromagnetic potentials.
- `ElectromagneticPotential.deriv`: the derivative tensor `âˆ‚_Î¼ A^Î½`.

## iii. Table of contents

- A. The electromagnetic potential
  - A.1. The action on the space-time derivatives
  - A.2. Differentiability
  - A.3. Variational adjoint derivative of component
  - A.4. Variational adjoint derivative of derivatives of the potential
- B. The derivative tensor of the electromagnetic potential
  - B.1. Equivariance of the derivative tensor
  - B.2. The elements of the derivative tensor in terms of the basis

## iv. References

- https://quantummechanics.ucsd.edu/ph130a/130_notes/node452.html
- https://ph.qmul.ac.uk/sites/default/files/EMT10new.pdf

-/

namespace Electromagnetism
open Module realLorentzTensor
open IndexNotation
open TensorSpecies
open Tensor

/-!

## A. The electromagnetic potential

We define the electromagnetic potential as a function from spacetime to
contravariant Lorentz vectors, and prove some simple results about it.

-/
/-- The electromagnetic potential is a tensor `A^Î¼`. -/
noncomputable abbrev ElectromagneticPotential (d : â„• := 3) :=
  SpaceTime d â†’ Lorentz.Vector d

namespace ElectromagneticPotential

open TensorSpecies
open Tensor
open SpaceTime
open TensorProduct
open minkowskiMatrix
attribute [-simp] Fintype.sum_sum_type
attribute [-simp] Nat.succ_eq_add_one
/-!

### A.1. The action on the space-time derivatives

Given a ElectromagneticPotential `A^Î¼`, we can consider its derivative `âˆ‚_Î¼ A^Î½`.
Under a Lorentz transformation `Î›`, this transforms as
`âˆ‚_ Î¼ (fun x => Î› â€¢ A (Î›â»Â¹ â€¢ x))`, we write an expression for this in terms of the tensor.
`âˆ‚_ Ï A (Î›â»Â¹ â€¢ x) Îº`.

-/

lemma spaceTime_deriv_action_eq_sum {d} {Î¼ Î½ : Fin 1 âŠ• Fin d} {x : SpaceTime d}
    (Î› : LorentzGroup d) (A : ElectromagneticPotential d) (hA : Differentiable â„ A) :
    âˆ‚_ Î¼ (fun x => Î› â€¢ A (Î›â»Â¹ â€¢ x)) x Î½ =
    âˆ‘ Îº, âˆ‘ Ï, (Î›.1 Î½ Îº * Î›â»Â¹.1 Ï Î¼) * âˆ‚_ Ï A (Î›â»Â¹ â€¢ x) Îº := by
  calc _
    _ = ((Î› â€¢ (âˆ‚_ Î¼ (fun x => A (Î›â»Â¹ â€¢ x)) x)) Î½) := by
      have hdif : âˆ€ i, DifferentiableAt â„ (fun x => A (Î›â»Â¹ â€¢ x) i) x := by
          intro i
          apply Differentiable.differentiableAt
          revert i
          rw [SpaceTime.differentiable_vector]
          conv =>
            enter [2, x]; rw [â† Lorentz.Vector.actionCLM_apply]
          apply Differentiable.fun_comp hA
          exact ContinuousLinearMap.differentiable (Lorentz.Vector.actionCLM Î›â»Â¹)
      trans âˆ‚_ Î¼ (fun x => (Î› â€¢ A (Î›â»Â¹ â€¢ x)) Î½) x
      Â· rw [SpaceTime.deriv_eq, SpaceTime.deriv_eq, SpaceTime.fderiv_vector]
        intro Î½
        conv =>
          enter [2, x]; rw [â† Lorentz.Vector.actionCLM_apply, â† Lorentz.Vector.actionCLM_apply]
        apply Differentiable.comp
        Â· exact ContinuousLinearMap.differentiable (Lorentz.Vector.actionCLM Î›)
        Â· apply Differentiable.comp
          Â· exact hA
          Â· exact ContinuousLinearMap.differentiable (Lorentz.Vector.actionCLM Î›â»Â¹)
      conv_lhs =>
        enter [2, x]
        rw [Lorentz.Vector.smul_eq_sum]
      rw [SpaceTime.deriv_eq]
      rw [fderiv_fun_sum (ð•œ := â„)]
      conv_lhs =>
        enter [1, 2, i]
        rw [fderiv_const_mul (hdif i)]
      simp only [Nat.reduceSucc, ContinuousLinearMap.coe_sum', ContinuousLinearMap.coe_smul',
        Finset.sum_apply, Pi.smul_apply, smul_eq_mul]
      rw [Lorentz.Vector.smul_eq_sum]
      congr
      funext Îº
      congr
      rw [SpaceTime.deriv_eq, SpaceTime.fderiv_vector]
      Â· exact hA.comp (Lorentz.Vector.actionCLM Î›â»Â¹).differentiable
      Â· intro i _
        apply DifferentiableAt.const_mul
        exact hdif i
    _ = (((Î› â€¢ (âˆ‘ Ï, Î›â»Â¹.1 Ï Î¼ â€¢ âˆ‚_ Ï A (Î›â»Â¹ â€¢ x)))) Î½) := by
      rw [SpaceTime.deriv_comp_lorentz_action]
      Â· exact hA
    _ = (âˆ‘ Îº, Î›.1 Î½ Îº * (âˆ‘ Ï, Î›â»Â¹.1 Ï Î¼ â€¢ âˆ‚_ Ï A (Î›â»Â¹ â€¢ x) Îº)) := by
      rw [Lorentz.Vector.smul_eq_sum]
      congr
      funext j
      congr
      rw [Lorentz.Vector.apply_sum]
      rfl
  apply Finset.sum_congr rfl (fun Îº _ => ?_)
  rw [Finset.mul_sum]
  apply Finset.sum_congr rfl (fun Ï _ => ?_)
  simp only [Nat.reduceSucc, smul_eq_mul]
  ring

/-!

### A.2. Differentiability

We show that the components of field strength tensor are differentiable if the potential is.
-/

lemma differentiable_component {d : â„•}
    (A : ElectromagneticPotential d) (hA : Differentiable â„ A) (Î¼ : Fin 1 âŠ• Fin d) :
    Differentiable â„ (fun x => A x Î¼) := by
  revert Î¼
  rw [SpaceTime.differentiable_vector]
  exact hA

/-!

### A.3. Variational adjoint derivative of component

We find the variational adjoint derivative of the components of the potential.
This will be used to find e.g. the variational derivative of the kinetic term,
and derive the equations of motion.

-/

open ContDiff
lemma hasVarAdjDerivAt_component {d : â„•} (Î¼ : Fin 1 âŠ• Fin d) (A : SpaceTime d â†’ Lorentz.Vector d)
    (hA : ContDiff â„ âˆž A) :
        HasVarAdjDerivAt (fun (A' : SpaceTime d â†’ Lorentz.Vector d) x => A' x Î¼)
          (fun (A' : SpaceTime d â†’ â„) x => A' x â€¢ Lorentz.Vector.basis Î¼) A := by
  let f : SpaceTime d â†’ Lorentz.Vector d â†’ â„ := fun x v => v Î¼
  let f' : SpaceTime d â†’ Lorentz.Vector d â†’ â„ â†’ Lorentz.Vector d := fun x _ c =>
    c â€¢ Lorentz.Vector.basis Î¼
  change HasVarAdjDerivAt (fun A' x => f x (A' x)) (fun Ïˆ x => f' x (A x) (Ïˆ x)) A
  apply HasVarAdjDerivAt.fmap
  Â· fun_prop
  Â· fun_prop
  intro x A
  refine { differentiableAt := ?_, hasAdjoint_fderiv := ?_ }
  Â· fun_prop
  refine { adjoint_inner_left := ?_ }
  intro u v
  simp [f,f']
  simp [inner_smul_left, Lorentz.Vector.basis_inner]
  ring_nf
  rfl

/-!

### A.4. Variational adjoint derivative of derivatives of the potential

We find the variational adjoint derivative of the derivatives of the components of the potential.
This will again be used to find the variational derivative of the kinetic term,
and derive the equations of motion (Maxwell's equations).

-/

lemma deriv_hasVarAdjDerivAt {d} (Î¼ Î½ : Fin 1 âŠ• Fin d) (A : SpaceTime d â†’ Lorentz.Vector d)
    (hA : ContDiff â„ âˆž A) :
    HasVarAdjDerivAt (fun (A : SpaceTime d â†’ Lorentz.Vector d) x => âˆ‚_ Î¼ A x Î½)
      (fun Ïˆ x => - (fderiv â„ Ïˆ x) (Lorentz.Vector.basis Î¼) â€¢ Lorentz.Vector.basis Î½) A := by
  have h0' := HasVarAdjDerivAt.fderiv' _ _
        (hF := hasVarAdjDerivAt_component Î½ A hA) A (Lorentz.Vector.basis Î¼)
  refine HasVarAdjDerivAt.congr (G := (fun (A : SpaceTime d â†’
    Lorentz.Vector d) x => âˆ‚_ Î¼ A x Î½)) h0' ?_
  intro Ï† hÏ†
  funext x
  simp only
  rw [deriv_apply_eq Î¼ Î½ Ï†]
  exact hÏ†.differentiable (by simp)

/-!

## B. The derivative tensor of the electromagnetic potential

We define the derivative as a tensor in `Lorentz.CoVector âŠ—[â„] Lorentz.Vector` for the
electromagnetic potential `A^Î¼`. We then prove that this tensor transforms correctly
under Lorentz transformations.

-/

/-- The derivative of the electric potential, `âˆ‚_Î¼ A^Î½`. -/
noncomputable def deriv {d} (A : ElectromagneticPotential d) :
    SpaceTime d â†’ Lorentz.CoVector d âŠ—[â„] Lorentz.Vector d := fun x =>
  âˆ‘ Î¼, âˆ‘ Î½, (âˆ‚_ Î¼ A x Î½) â€¢ Lorentz.CoVector.basis Î¼ âŠ—â‚œ[â„] Lorentz.Vector.basis Î½

/-!

### B.1. Equivariance of the derivative tensor

We show that the derivative tensor is equivariant under the action of the Lorentz group.
That is, `âˆ‚_Î¼ (fun x => Î› â€¢ A (Î›â»Â¹ â€¢ x)) = Î› â€¢ (âˆ‚_Î¼ A (Î›â»Â¹ â€¢ x))`, or in words:
applying the Lorentz transformation to the potential and then taking the derivative is the same
as taking the derivative and then applying the Lorentz transformation to the resulting tensor.

-/
lemma deriv_equivariant {d} {x : SpaceTime d} (A : ElectromagneticPotential d)
    (Î› : LorentzGroup d)
    (hf : Differentiable â„ A) : deriv (fun x => Î› â€¢ A (Î›â»Â¹ â€¢ x)) x = Î› â€¢ (deriv A (Î›â»Â¹ â€¢ x)) := by
    calc _
      _ = âˆ‘ Î¼, âˆ‘ Î½, âˆ‘ Îº, âˆ‘ Ï, (Î›.1 Î½ Îº * (Î›â»Â¹.1 Ï Î¼ â€¢ âˆ‚_ Ï A (Î›â»Â¹ â€¢ x) Îº)) â€¢
          (Lorentz.CoVector.basis Î¼) âŠ—â‚œ[â„]
          Lorentz.Vector.basis Î½ := by
        refine Finset.sum_congr rfl (fun Î¼ _ => ?_)
        refine Finset.sum_congr rfl (fun Î½ _ => ?_)
        rw [spaceTime_deriv_action_eq_sum Î› A hf]
        rw [Finset.sum_smul]
        apply Finset.sum_congr rfl (fun Îº _ => ?_)
        rw [Finset.sum_smul]
        apply Finset.sum_congr rfl (fun Ï _ => ?_)
        congr 1
        simp only [Nat.succ_eq_add_one, Nat.reduceAdd, smul_eq_mul]
        ring
      _ = âˆ‘ Î¼, âˆ‘ Î½, âˆ‘ Îº, âˆ‘ Ï, (âˆ‚_ Ï A (Î›â»Â¹ â€¢ x) Îº) â€¢
          (Î›â»Â¹.1 Ï Î¼ â€¢ (Lorentz.CoVector.basis Î¼)) âŠ—â‚œ[â„]
          (Î›.1 Î½ Îº â€¢ Lorentz.Vector.basis Î½) := by
        refine Finset.sum_congr rfl (fun Î¼ _ => ?_)
        refine Finset.sum_congr rfl (fun Î½ _ => ?_)
        refine Finset.sum_congr rfl (fun Îº _ => ?_)
        refine Finset.sum_congr rfl (fun Ï _ => ?_)
        rw [smul_tmul, tmul_smul, tmul_smul, smul_smul, smul_smul]
        congr 1
        simp only [Nat.reduceSucc, smul_eq_mul]
        ring
      _ = âˆ‘ Îº, âˆ‘ Ï, âˆ‘ Î¼, âˆ‘ Î½, (âˆ‚_ Ï A (Î›â»Â¹ â€¢ x) Îº) â€¢
          (Î›â»Â¹.1 Ï Î¼ â€¢ (Lorentz.CoVector.basis Î¼)) âŠ—â‚œ[â„]
          (Î›.1 Î½ Îº â€¢ Lorentz.Vector.basis Î½) := by
        conv_lhs => enter [2, Î¼]; rw [Finset.sum_comm]
        conv_lhs => rw [Finset.sum_comm]
        conv_lhs => enter [2, Îº, 2, Î¼]; rw [Finset.sum_comm]
        conv_lhs => enter [2, Îº]; rw [Finset.sum_comm]
      _ = âˆ‘ Îº, âˆ‘ Ï, (âˆ‚_ Ï A (Î›â»Â¹ â€¢ x) Îº) â€¢ (âˆ‘ Î¼, Î›â»Â¹.1 Ï Î¼ â€¢ (Lorentz.CoVector.basis Î¼)) âŠ—â‚œ[â„]
          (âˆ‘ Î½, Î›.1 Î½ Îº â€¢ Lorentz.Vector.basis Î½) := by
        conv_rhs =>
          enter [2, Îº,2, Ï]; rw [sum_tmul, Finset.smul_sum]
          enter [2, Î¼]; rw [tmul_sum, Finset.smul_sum];
      _ = âˆ‘ Îº, âˆ‘ Ï, (âˆ‚_ Ï A (Î›â»Â¹ â€¢ x) Îº) â€¢ (Î› â€¢ (Lorentz.CoVector.basis Ï)) âŠ—â‚œ[â„]
          (Î› â€¢ Lorentz.Vector.basis Îº) := by
        apply Finset.sum_congr rfl (fun Îº _ => ?_)
        apply Finset.sum_congr rfl (fun Ï _ => ?_)
        congr 2
        Â· rw [Lorentz.CoVector.smul_basis]
        Â· rw [Lorentz.Vector.smul_basis]
      _ = âˆ‘ Îº, âˆ‘ Ï, (âˆ‚_ Ï A (Î›â»Â¹ â€¢ x) Îº) â€¢ Î› â€¢ ((Lorentz.CoVector.basis Ï) âŠ—â‚œ[â„]
        Lorentz.Vector.basis Îº) := by
        apply Finset.sum_congr rfl (fun Îº _ => ?_)
        apply Finset.sum_congr rfl (fun Ï _ => ?_)
        rw [Tensorial.smul_prod]
    rw [deriv]
    conv_rhs => rw [â† Tensorial.smulLinearMap_apply]
    rw [Finset.sum_comm]
    simp
    rfl

/-!

### B.2. The elements of the derivative tensor in terms of the basis

We show that in the standard basis, the elements of the derivative tensor
are just equal to `âˆ‚_ Î¼ A x Î½`.

-/

@[simp]
lemma deriv_basis_repr_apply {d} {Î¼Î½ : (Fin 1 âŠ• Fin d) Ã— (Fin 1 âŠ• Fin d)}
    (A : ElectromagneticPotential d)
    (x : SpaceTime d) :
    (Lorentz.CoVector.basis.tensorProduct Lorentz.Vector.basis).repr (deriv A x) Î¼Î½ =
    âˆ‚_ Î¼Î½.1 A x Î¼Î½.2 := by
  match Î¼Î½ with
  | (Î¼, Î½) =>
  rw [deriv]
  simp only [map_sum, map_smul, Finsupp.coe_finset_sum, Finsupp.coe_smul, Finset.sum_apply,
    Pi.smul_apply, Basis.tensorProduct_repr_tmul_apply, Basis.repr_self, smul_eq_mul]
  rw [Finset.sum_eq_single Î¼, Finset.sum_eq_single Î½]
  Â· simp
  Â· intro Î¼' _ h
    simp [h]
  Â· simp
  Â· intro Î½' _ h
    simp [h]
  Â· simp

lemma toTensor_deriv_basis_repr_apply {d} (A : ElectromagneticPotential d)
    (x : SpaceTime d) (b : ComponentIdx (S := realLorentzTensor d)
      (Fin.append ![Color.down] ![Color.up])) :
    (Tensor.basis _).repr (Tensorial.toTensor (deriv A x)) b =
    âˆ‚_ (finSumFinEquiv.symm (b 0)) A x (finSumFinEquiv.symm (b 1)) := by
  rw [Tensorial.basis_toTensor_apply]
  rw [Tensorial.basis_map_prod]
  simp only [Nat.reduceSucc, Nat.reduceAdd, Basis.repr_reindex, Finsupp.mapDomain_equiv_apply,
    Equiv.symm_symm, Fin.isValue]
  rw [Lorentz.Vector.tensor_basis_map_eq_basis_reindex,
    Lorentz.CoVector.tensor_basis_map_eq_basis_reindex]
  have hb : (((Lorentz.CoVector.basis (d := d)).reindex
      Lorentz.CoVector.indexEquiv.symm).tensorProduct
      (Lorentz.Vector.basis.reindex Lorentz.Vector.indexEquiv.symm)) =
      ((Lorentz.CoVector.basis (d := d)).tensorProduct (Lorentz.Vector.basis (d := d))).reindex
      (Lorentz.CoVector.indexEquiv.symm.prodCongr Lorentz.Vector.indexEquiv.symm) := by
    ext b
    match b with
    | âŸ¨i, jâŸ© =>
    simp
  rw [hb]
  rw [Module.Basis.repr_reindex_apply, deriv_basis_repr_apply]
  rfl

end ElectromagneticPotential

end Electromagnetism
