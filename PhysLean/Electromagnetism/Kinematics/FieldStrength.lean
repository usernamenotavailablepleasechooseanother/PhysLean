/-
Copyright (c) 2025 Joseph Tooby-Smith. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Joseph Tooby-Smith
-/
import PhysLean.Electromagnetism.Kinematics.EMPotential
/-!

# The Field Strength Tensor

## i. Overview

In this module we define the field strength tensor in terms of the electromagnetic potential.

We define a tensor version and a matrix version and prover various properties of these.

## ii. Key results

- `toFieldStrength` : The field strength tensor from an electromagnetic potential.
- `fieldStrengthMatrix` : The field strength matrix from an electromagnetic potential
  (matrix representation of the field strength tensor in the standard basis).
- `DistElectromagneticPotential.fieldStrength` : The field strength for
  electromagnetic potentials which are distributions.

## iii. Table of contents

- A. The field strength tensor
  - A.1. Basic equalities
  - A.2. Elements of the field strength tensor in terms of basis
  - A.3. The field strength matrix
    - A.3.1. Differentiability of the field strength matrix
  - A.4. The antisymmetry of the field strength tensor
  - A.5. Equivariance of the field strength tensor
  - A.6. Linearity of the field strength tensor
- B. Field strength for distributions
  - B.1. Auxiliary definition of field strength for distributions, with no linearity
  - B.2. The definition of the field strength
  - B.3. Field strength written in terms of a basis

## iv. References

-/
namespace Electromagnetism
open Module realLorentzTensor
open IndexNotation
open TensorSpecies
open Tensor

namespace ElectromagneticPotential

open TensorSpecies
open Tensor
open SpaceTime
open TensorProduct
open minkowskiMatrix
attribute [-simp] Fintype.sum_sum_type
attribute [-simp] Nat.succ_eq_add_one

/-!

## A. The field strength tensor

We define the field strength tensor `F_Œº^ŒΩ` in terms of the derivative of the
electromagnetic potential `A^Œº`. We then prove that this tensor transforms correctly
under Lorentz transformations.

-/
/-- The field strength from an electromagnetic potential, as a tensor `F_Œº^ŒΩ`. -/
noncomputable def toFieldStrength {d} (A : ElectromagneticPotential d) :
    SpaceTime d ‚Üí Lorentz.Vector d ‚äó[‚Ñù] Lorentz.Vector d := fun x =>
  Tensorial.toTensor.symm
  (permT id (PermCond.auto) {(Œ∑ d | Œº Œº' ‚äó A.deriv x | Œº' ŒΩ) + - (Œ∑ d | ŒΩ ŒΩ' ‚äó A.deriv x | ŒΩ' Œº)}·µÄ)

/-!

### A.1. Basic equalities

-/

lemma toFieldStrength_eq_add {d} (A : ElectromagneticPotential d) (x : SpaceTime d) :
    toFieldStrength A x =
    Tensorial.toTensor.symm (permT id (PermCond.auto) {(Œ∑ d | Œº Œº' ‚äó A.deriv x | Œº' ŒΩ)}·µÄ)
    - Tensorial.toTensor.symm (permT ![1, 0] (PermCond.auto)
      {(Œ∑ d | Œº Œº' ‚äó A.deriv x | Œº' ŒΩ)}·µÄ) := by
  rw [toFieldStrength]
  simp only [map_add, map_neg]
  rw [sub_eq_add_neg]
  apply congrArg‚ÇÇ
  ¬∑ rfl
  ¬∑ rw [permT_permT]
    rfl

lemma toTensor_toFieldStrength {d} (A : ElectromagneticPotential d) (x : SpaceTime d) :
    Tensorial.toTensor (toFieldStrength A x) =
    (permT id (PermCond.auto) {(Œ∑ d | Œº Œº' ‚äó A.deriv x | Œº' ŒΩ)}·µÄ)
    - (permT ![1, 0] (PermCond.auto) {(Œ∑ d | Œº Œº' ‚äó A.deriv x | Œº' ŒΩ)}·µÄ) := by
  rw [toFieldStrength_eq_add]
  simp

/-!

### A.2. Elements of the field strength tensor in terms of basis

-/

lemma toTensor_toFieldStrength_basis_repr {d} (A : ElectromagneticPotential d) (x : SpaceTime d)
    (b : ComponentIdx (S := realLorentzTensor d) (Fin.append ![Color.up] ![Color.up])) :
    (Tensor.basis _).repr (Tensorial.toTensor (toFieldStrength A x)) b =
    ‚àë Œ∫, (Œ∑ (finSumFinEquiv.symm (b 0)) Œ∫ * ‚àÇ_ Œ∫ A x (finSumFinEquiv.symm (b 1)) -
      Œ∑ (finSumFinEquiv.symm (b 1)) Œ∫ * ‚àÇ_ Œ∫ A x (finSumFinEquiv.symm (b 0))) := by
  rw [toTensor_toFieldStrength]
  simp only [Tensorial.self_toTensor_apply, map_sub,
    Finsupp.coe_sub, Pi.sub_apply]
  rw [Tensor.permT_basis_repr_symm_apply, contrT_basis_repr_apply_eq_fin]
  conv_lhs =>
    enter [1, 2, n]
    rw [Tensor.prodT_basis_repr_apply, contrMetric_repr_apply_eq_minkowskiMatrix]
    enter [1]
    change Œ∑ (finSumFinEquiv.symm (b 0)) (finSumFinEquiv.symm n)
  conv_lhs =>
    enter [1, 2, n, 2]
    rw [toTensor_deriv_basis_repr_apply]
    change ‚àÇ_ (finSumFinEquiv.symm n) A x (finSumFinEquiv.symm (b 1))
  rw [Tensor.permT_basis_repr_symm_apply, contrT_basis_repr_apply_eq_fin]
  conv_lhs =>
    enter [2, 2, n]
    rw [Tensor.prodT_basis_repr_apply, contrMetric_repr_apply_eq_minkowskiMatrix]
    enter [1]
    change Œ∑ (finSumFinEquiv.symm (b 1)) (finSumFinEquiv.symm n)
  conv_lhs =>
    enter [2, 2, n, 2]
    rw [toTensor_deriv_basis_repr_apply]
    change ‚àÇ_ (finSumFinEquiv.symm n) A x (finSumFinEquiv.symm (b 0))
  rw [‚Üê Finset.sum_sub_distrib]
  rw [‚Üê finSumFinEquiv.sum_comp]
  simp only [Fin.isValue, Equiv.symm_apply_apply]

lemma toFieldStrength_tensor_basis_eq_basis {d} (A : ElectromagneticPotential d) (x : SpaceTime d)
    (b : ComponentIdx (S := realLorentzTensor d) (Fin.append ![Color.up] ![Color.up])) :
    (Tensor.basis _).repr (Tensorial.toTensor (toFieldStrength A x)) b =
    (Lorentz.Vector.basis.tensorProduct Lorentz.Vector.basis).repr (toFieldStrength A x)
      (finSumFinEquiv.symm (b 0), finSumFinEquiv.symm (b 1)) := by
  rw [Tensorial.basis_toTensor_apply]
  rw [Tensorial.basis_map_prod]
  simp only [Nat.reduceSucc, Nat.reduceAdd, Basis.repr_reindex, Finsupp.mapDomain_equiv_apply,
    Equiv.symm_symm, Fin.isValue]
  rw [Lorentz.Vector.tensor_basis_map_eq_basis_reindex]
  have hb : (((Lorentz.Vector.basis (d := d)).reindex Lorentz.Vector.indexEquiv.symm).tensorProduct
          (Lorentz.Vector.basis.reindex Lorentz.Vector.indexEquiv.symm)) =
          ((Lorentz.Vector.basis (d := d)).tensorProduct (Lorentz.Vector.basis (d := d))).reindex
          (Lorentz.Vector.indexEquiv.symm.prodCongr Lorentz.Vector.indexEquiv.symm) := by
        ext b
        match b with
        | ‚ü®i, j‚ü© =>
        simp
  rw [hb]
  rw [Module.Basis.repr_reindex_apply]
  congr 1

lemma toFieldStrength_basis_repr_apply {d} {ŒºŒΩ : (Fin 1 ‚äï Fin d) √ó (Fin 1 ‚äï Fin d)}
    (A : ElectromagneticPotential d) (x : SpaceTime d) :
    (Lorentz.CoVector.basis.tensorProduct Lorentz.Vector.basis).repr (A.toFieldStrength x) ŒºŒΩ =
    ‚àë Œ∫, ((Œ∑ ŒºŒΩ.1 Œ∫ * ‚àÇ_ Œ∫ A x ŒºŒΩ.2) - Œ∑ ŒºŒΩ.2 Œ∫ * ‚àÇ_ Œ∫ A x ŒºŒΩ.1) := by
  match ŒºŒΩ with
  | (Œº, ŒΩ) =>
  trans (Tensor.basis _).repr (Tensorial.toTensor (toFieldStrength A x))
    (fun | 0 => finSumFinEquiv Œº | 1 => finSumFinEquiv ŒΩ); swap
  ¬∑ rw [toTensor_toFieldStrength_basis_repr]
    simp
  rw [toFieldStrength_tensor_basis_eq_basis]
  congr 1
  change _ = (finSumFinEquiv.symm (finSumFinEquiv Œº), finSumFinEquiv.symm (finSumFinEquiv ŒΩ))
  simp

lemma toFieldStrength_basis_repr_apply_eq_single {d} {ŒºŒΩ : (Fin 1 ‚äï Fin d) √ó (Fin 1 ‚äï Fin d)}
    (A : ElectromagneticPotential d) (x : SpaceTime d) :
    (Lorentz.CoVector.basis.tensorProduct Lorentz.Vector.basis).repr (A.toFieldStrength x) ŒºŒΩ =
    ((Œ∑ ŒºŒΩ.1 ŒºŒΩ.1 * ‚àÇ_ ŒºŒΩ.1 A x ŒºŒΩ.2) - Œ∑ ŒºŒΩ.2 ŒºŒΩ.2 * ‚àÇ_ ŒºŒΩ.2 A x ŒºŒΩ.1) := by
  rw [toFieldStrength_basis_repr_apply]
  simp only [Finset.sum_sub_distrib]
  rw [Finset.sum_eq_single ŒºŒΩ.1, Finset.sum_eq_single ŒºŒΩ.2]
  ¬∑ intro b _ hb
    rw [minkowskiMatrix.off_diag_zero]
    simp only [zero_mul]
    exact id (Ne.symm hb)
  ¬∑ simp
  ¬∑ intro b _ hb
    rw [minkowskiMatrix.off_diag_zero]
    simp only [zero_mul]
    exact id (Ne.symm hb)
  ¬∑ simp

/-!

### A.3. The field strength matrix

We define the field strength matrix to be the matrix representation of the field strength tensor
in the standard basis.

This is currently not used as much as it could be.
-/
open ContDiff

/-- The matrix corresponding to the field strength in the standard basis. -/
noncomputable abbrev fieldStrengthMatrix {d} (A : ElectromagneticPotential d) (x : SpaceTime d) :=
    (Lorentz.CoVector.basis.tensorProduct Lorentz.Vector.basis).repr (A.toFieldStrength x)

lemma fieldStrengthMatrix_eq_tensor_basis_repr {d} (A : ElectromagneticPotential d)
    (x : SpaceTime d) (Œº ŒΩ : (Fin 1 ‚äï Fin d)) :
    A.fieldStrengthMatrix x (Œº, ŒΩ) =
    (Tensor.basis _).repr (Tensorial.toTensor (toFieldStrength A x))
    (fun | 0 => finSumFinEquiv Œº | 1 => finSumFinEquiv ŒΩ) := by
  rw [toFieldStrength_tensor_basis_eq_basis]
  simp only [Equiv.symm_apply_apply]
  rfl

/-!

#### A.3.1. Differentiability of the field strength matrix

-/

lemma fieldStrengthMatrix_differentiable {d} {A : ElectromagneticPotential d}
    {ŒºŒΩ} (hA : ContDiff ‚Ñù 2 A) :
    Differentiable ‚Ñù (A.fieldStrengthMatrix ¬∑ ŒºŒΩ) := by
  have diff_partial (Œº) :
      ‚àÄ ŒΩ, Differentiable ‚Ñù fun x => (fderiv ‚Ñù A x) (Lorentz.Vector.basis Œº) ŒΩ := by
    rw [SpaceTime.differentiable_vector]
    refine Differentiable.clm_apply ?_ ?_
    ¬∑ exact ((contDiff_succ_iff_fderiv (n := 1)).mp hA).2.2.differentiable
        (Preorder.le_refl 1)
    ¬∑ fun_prop
  conv => enter [2, x]; rw [toFieldStrength_basis_repr_apply_eq_single,
    SpaceTime.deriv_eq, SpaceTime.deriv_eq]
  apply Differentiable.sub
  apply Differentiable.const_mul
  ¬∑ exact diff_partial _ _
  apply Differentiable.const_mul
  ¬∑ exact diff_partial _ _

lemma fieldStrengthMatrix_differentiable_space {d} {A : ElectromagneticPotential d}
    {ŒºŒΩ} (hA : ContDiff ‚Ñù 2 A) (t : Time) {c : SpeedOfLight} :
    Differentiable ‚Ñù (fun x => A.fieldStrengthMatrix ((toTimeAndSpace c).symm (t, x)) ŒºŒΩ) := by
  change Differentiable ‚Ñù ((A.fieldStrengthMatrix ¬∑ ŒºŒΩ) ‚àò fun x => (toTimeAndSpace c).symm (t, x))
  refine Differentiable.comp ?_ ?_
  ¬∑ exact fieldStrengthMatrix_differentiable hA
  ¬∑ fun_prop

lemma fieldStrengthMatrix_differentiable_time {d} {A : ElectromagneticPotential d}
    {ŒºŒΩ} (hA : ContDiff ‚Ñù 2 A) (x : Space d) {c : SpeedOfLight} :
    Differentiable ‚Ñù (fun t => A.fieldStrengthMatrix ((toTimeAndSpace c).symm (t, x)) ŒºŒΩ) := by
  change Differentiable ‚Ñù ((A.fieldStrengthMatrix ¬∑ ŒºŒΩ) ‚àò fun t => (toTimeAndSpace c).symm (t, x))
  refine Differentiable.comp ?_ ?_
  ¬∑ exact fieldStrengthMatrix_differentiable hA
  ¬∑ fun_prop

lemma fieldStrengthMatrix_contDiff {d} {n : WithTop ‚Ñï‚àû} {A : ElectromagneticPotential d}
    {ŒºŒΩ} (hA : ContDiff ‚Ñù (n + 1) A) :
    ContDiff ‚Ñù n (A.fieldStrengthMatrix ¬∑ ŒºŒΩ) := by
  conv => enter [3, x]; rw [toFieldStrength_basis_repr_apply_eq_single,
    SpaceTime.deriv_eq, SpaceTime.deriv_eq]
  apply ContDiff.sub
  apply ContDiff.mul
  ¬∑ fun_prop
  ¬∑ match ŒºŒΩ with
    | (Œº, ŒΩ) =>
    simp only
    revert ŒΩ
    rw [SpaceTime.contDiff_vector]
    apply ContDiff.clm_apply
    ¬∑ exact ContDiff.fderiv_right (m := n) hA (by rfl)
    ¬∑ fun_prop
  apply ContDiff.mul
  ¬∑ fun_prop
  ¬∑ match ŒºŒΩ with
    | (Œº, ŒΩ) =>
    simp only
    revert Œº
    rw [SpaceTime.contDiff_vector]
    apply ContDiff.clm_apply
    ¬∑ exact ContDiff.fderiv_right (m := n) hA (by rfl)
    ¬∑ fun_prop

lemma fieldStrengthMatrix_smooth {d} {A : ElectromagneticPotential d}
    (hA : ContDiff ‚Ñù ‚àû A) (ŒºŒΩ) :
    ContDiff ‚Ñù ‚àû (A.fieldStrengthMatrix ¬∑ ŒºŒΩ) := by
  apply fieldStrengthMatrix_contDiff
  simpa using hA

/-!

### A.4. The antisymmetry of the field strength tensor

We show that the field strength tensor is antisymmetric.

-/

lemma toFieldStrength_antisymmetric {d} (A : ElectromagneticPotential d) (x : SpaceTime d) :
    {A.toFieldStrength x | Œº ŒΩ = - (A.toFieldStrength x | ŒΩ Œº)}·µÄ := by
  apply (Tensor.basis _).repr.injective
  ext b
  rw [toTensor_toFieldStrength_basis_repr]
  rw [permT_basis_repr_symm_apply, map_neg]
  simp only [Nat.reduceAdd, Fin.isValue, Nat.reduceSucc, Finsupp.coe_neg, Pi.neg_apply]
  rw [toTensor_toFieldStrength_basis_repr]
  rw [‚Üê Finset.sum_neg_distrib]
  apply Finset.sum_congr rfl (fun Œ∫ _ => ?_)
  simp only [Fin.isValue, Fin.cast_eq_self, neg_sub]
  rfl

lemma fieldStrengthMatrix_antisymm {d} (A : ElectromagneticPotential d) (x : SpaceTime d)
    (Œº ŒΩ : Fin 1 ‚äï Fin d) :
    A.fieldStrengthMatrix x (Œº, ŒΩ) = - A.fieldStrengthMatrix x (ŒΩ, Œº) := by
  rw [toFieldStrength_basis_repr_apply, toFieldStrength_basis_repr_apply]
  rw [‚Üê Finset.sum_neg_distrib]
  apply Finset.sum_congr rfl (fun Œ∫ _ => ?_)
  simp

@[simp]
lemma fieldStrengthMatrix_diag_eq_zero {d} (A : ElectromagneticPotential d) (x : SpaceTime d)
    (Œº : Fin 1 ‚äï Fin d) :
    A.fieldStrengthMatrix x (Œº, Œº) = 0 := by
  rw [toFieldStrength_basis_repr_apply_eq_single]
  simp

/-!

### A.5. Equivariance of the field strength tensor

We show that the field strength tensor is equivariant under the action of the Lorentz group.
That is transforming the potential and then taking the field strength is the same
as taking the field strength and then transforming the resulting tensor.

-/

lemma toFieldStrength_equivariant {d} (A : ElectromagneticPotential d) (Œõ : LorentzGroup d)
    (hf : Differentiable ‚Ñù A) (x : SpaceTime d) :
    toFieldStrength (fun x => Œõ ‚Ä¢ A (Œõ‚Åª¬π ‚Ä¢ x)) x =
      Œõ ‚Ä¢ toFieldStrength A (Œõ‚Åª¬π ‚Ä¢ x) := by
  rw [toFieldStrength, deriv_equivariant A Œõ hf, ‚Üê actionT_contrMetric Œõ, toFieldStrength]
  simp only [Tensorial.toTensor_smul, prodT_equivariant, contrT_equivariant, map_neg,
    permT_equivariant, map_add, ‚Üê Tensorial.smul_toTensor_symm, smul_add, smul_neg]

lemma fieldStrengthMatrix_equivariant {d} (A : ElectromagneticPotential d)
    (Œõ : LorentzGroup d) (hf : Differentiable ‚Ñù A) (x : SpaceTime d)
    (Œº : (Fin 1 ‚äï Fin d)) (ŒΩ : Fin 1 ‚äï Fin d) :
    fieldStrengthMatrix (fun x => Œõ ‚Ä¢ A (Œõ‚Åª¬π ‚Ä¢ x)) x (Œº, ŒΩ) =
    ‚àë Œ∫, ‚àë œÅ, (Œõ.1 Œº Œ∫ * Œõ.1 ŒΩ œÅ) * A.fieldStrengthMatrix (Œõ‚Åª¬π ‚Ä¢ x) (Œ∫, œÅ) := by
  rw [fieldStrengthMatrix, toFieldStrength_equivariant A Œõ hf x]
  conv_rhs =>
    enter [2, Œ∫, 2, œÅ]
    rw [fieldStrengthMatrix]
  generalize A.toFieldStrength (Œõ‚Åª¬π ‚Ä¢ x) = F
  let P (F : Lorentz.Vector d ‚äó[‚Ñù] Lorentz.Vector d) : Prop :=
    ((Lorentz.CoVector.basis.tensorProduct Lorentz.Vector.basis).repr (Œõ ‚Ä¢ F)) (Œº, ŒΩ) =
    ‚àë Œ∫, ‚àë œÅ, Œõ.1 Œº Œ∫ * Œõ.1 ŒΩ œÅ *
    ((Lorentz.CoVector.basis.tensorProduct Lorentz.Vector.basis).repr F) (Œ∫, œÅ)
  change P F
  apply TensorProduct.induction_on
  ¬∑ simp [P]
  ¬∑ intro x y
    dsimp [P]
    rw [Tensorial.smul_prod]
    simp only [Basis.tensorProduct_repr_tmul_apply, Lorentz.Vector.basis_repr_apply,
      Lorentz.CoVector.basis_repr_apply, smul_eq_mul]
    rw [Lorentz.Vector.smul_eq_sum, Finset.sum_mul]
    conv_rhs => rw [Finset.sum_comm]
    apply Finset.sum_congr rfl (fun Œ∫ _ => ?_)
    rw [Lorentz.Vector.smul_eq_sum, Finset.mul_sum]
    apply Finset.sum_congr rfl (fun œÅ _ => ?_)
    ring
  ¬∑ intro F1 F2 h1 h2
    simp [P, h1, h2]
    rw [‚Üê Finset.sum_add_distrib]
    apply Finset.sum_congr rfl (fun Œ∫ _ => ?_)
    rw [‚Üê Finset.sum_add_distrib]
    apply Finset.sum_congr rfl (fun œÅ _ => ?_)
    ring

/-!

### A.6. Linearity of the field strength tensor

We show that the field strength tensor is linear in the potential.

-/

lemma toFieldStrength_add {d} (A1 A2 : ElectromagneticPotential d)
    (x : SpaceTime d) (hA1 : Differentiable ‚Ñù A1) (hA2 : Differentiable ‚Ñù A2) :
    toFieldStrength (A1 + A2) x = toFieldStrength A1 x + toFieldStrength A2 x := by
  apply (Lorentz.CoVector.basis.tensorProduct Lorentz.Vector.basis).repr.injective
  ext ŒºŒΩ
  simp only [map_add, Finsupp.coe_add, Pi.add_apply]
  repeat rw [toFieldStrength_basis_repr_apply]
  rw [‚Üê Finset.sum_add_distrib]
  apply Finset.sum_congr rfl (fun Œ∫ _ => ?_)
  repeat rw [SpaceTime.deriv_eq]
  rw [fderiv_add]
  simp only [ContinuousLinearMap.add_apply, Lorentz.Vector.apply_add]
  ring
  ¬∑ exact hA1.differentiableAt
  ¬∑ exact hA2.differentiableAt

lemma fieldStrengthMatrix_add {d} (A1 A2 : ElectromagneticPotential d)
    (x : SpaceTime d) (hA1 : Differentiable ‚Ñù A1) (hA2 : Differentiable ‚Ñù A2) :
    (A1 + A2).fieldStrengthMatrix x =
    A1.fieldStrengthMatrix x + A2.fieldStrengthMatrix x := by
  rw [fieldStrengthMatrix, toFieldStrength_add A1 A2 x hA1 hA2]
  conv_rhs => rw [fieldStrengthMatrix, fieldStrengthMatrix]
  simp

lemma toFieldStrength_smul {d} (c : ‚Ñù) (A : ElectromagneticPotential d)
    (x : SpaceTime d) (hA : Differentiable ‚Ñù A) :
    toFieldStrength (c ‚Ä¢ A) x = c ‚Ä¢ toFieldStrength A x := by
  apply (Lorentz.CoVector.basis.tensorProduct Lorentz.Vector.basis).repr.injective
  ext ŒºŒΩ
  simp only [map_smul, Finsupp.coe_smul, Pi.smul_apply, smul_eq_mul]
  repeat rw [toFieldStrength_basis_repr_apply]
  rw [Finset.mul_sum]
  apply Finset.sum_congr rfl (fun Œ∫ _ => ?_)
  repeat rw [SpaceTime.deriv_eq]
  rw [fderiv_const_smul]
  simp only [ContinuousLinearMap.coe_smul', Pi.smul_apply, Lorentz.Vector.apply_smul]
  ring
  exact hA.differentiableAt

lemma fieldStrengthMatrix_smul {d} (c : ‚Ñù) (A : ElectromagneticPotential d)
    (x : SpaceTime d) (hA : Differentiable ‚Ñù A) :
    (c ‚Ä¢ A).fieldStrengthMatrix x = c ‚Ä¢ A.fieldStrengthMatrix x := by
  rw [fieldStrengthMatrix, toFieldStrength_smul c A x hA]
  conv_rhs => rw [fieldStrengthMatrix]
  simp

end ElectromagneticPotential

/-!

## B. Field strength for distributions

-/

namespace DistElectromagneticPotential
open TensorSpecies
open Tensor
open SpaceTime
open TensorProduct
open minkowskiMatrix SchwartzMap
attribute [-simp] Fintype.sum_sum_type
attribute [-simp] Nat.succ_eq_add_one

/-!

### B.1. Auxiliary definition of field strength for distributions, with no linearity

-/

/-- An auxillary definition for the field strength of an electromagnetic potential
  based on a distribution. On Schwartz maps this has the same value as the field strength
  tensor, but no linearity or continous properties built in. -/
noncomputable def fieldStrengthAux {d} (A : DistElectromagneticPotential d)
    (Œµ : ùì¢(SpaceTime d, ‚Ñù)) : Lorentz.Vector d ‚äó[‚Ñù] Lorentz.Vector d :=
  Tensorial.toTensor.symm
      (permT id (PermCond.auto) {(Œ∑ d | Œº Œº' ‚äó A.deriv Œµ | Œº' ŒΩ) + -
      (Œ∑ d | ŒΩ ŒΩ' ‚äó A.deriv Œµ | ŒΩ' Œº)}·µÄ)

lemma fieldStrengthAux_eq_add {d} (A : DistElectromagneticPotential d) (Œµ : ùì¢(SpaceTime d, ‚Ñù)) :
    fieldStrengthAux A Œµ =
    Tensorial.toTensor.symm (permT id (PermCond.auto) {(Œ∑ d | Œº Œº' ‚äó A.deriv Œµ | Œº' ŒΩ)}·µÄ)
    - Tensorial.toTensor.symm (permT ![1, 0] (PermCond.auto)
      {(Œ∑ d | Œº Œº' ‚äó A.deriv Œµ | Œº' ŒΩ)}·µÄ) := by
  rw [fieldStrengthAux]
  simp only [map_add, map_neg]
  rw [sub_eq_add_neg]
  apply congrArg‚ÇÇ
  ¬∑ rfl
  ¬∑ rw [permT_permT]
    rfl

lemma toTensor_fieldStrengthAux {d} (A : DistElectromagneticPotential d)
    (Œµ : ùì¢(SpaceTime d, ‚Ñù)) :
    Tensorial.toTensor (fieldStrengthAux A Œµ) =
    (permT id (PermCond.auto) {(Œ∑ d | Œº Œº' ‚äó A.deriv Œµ | Œº' ŒΩ)}·µÄ)
    - (permT ![1, 0] (PermCond.auto) {(Œ∑ d | Œº Œº' ‚äó A.deriv Œµ | Œº' ŒΩ)}·µÄ) := by
  rw [fieldStrengthAux_eq_add]
  simp

lemma toTensor_fieldStrengthAux_basis_repr {d} (A : DistElectromagneticPotential d)
    (Œµ : ùì¢(SpaceTime d, ‚Ñù))
    (b : ComponentIdx (S := realLorentzTensor d) (Fin.append ![Color.up] ![Color.up])) :
    (Tensor.basis _).repr (Tensorial.toTensor (fieldStrengthAux A Œµ)) b =
    ‚àë Œ∫, (Œ∑ (finSumFinEquiv.symm (b 0)) Œ∫ * SpaceTime.distDeriv Œ∫ A Œµ (finSumFinEquiv.symm (b 1)) -
      Œ∑ (finSumFinEquiv.symm (b 1)) Œ∫ * SpaceTime.distDeriv Œ∫ A Œµ (finSumFinEquiv.symm (b 0))) := by
  rw [toTensor_fieldStrengthAux]
  simp only [Tensorial.self_toTensor_apply, map_sub,
    Finsupp.coe_sub, Pi.sub_apply]
  rw [Tensor.permT_basis_repr_symm_apply, contrT_basis_repr_apply_eq_fin]
  conv_lhs =>
    enter [1, 2, n]
    rw [Tensor.prodT_basis_repr_apply, contrMetric_repr_apply_eq_minkowskiMatrix]
    enter [1]
    change Œ∑ (finSumFinEquiv.symm (b 0)) (finSumFinEquiv.symm n)
  conv_lhs =>
    enter [1, 2, n, 2]
    rw [toTensor_deriv_basis_repr_apply]
    change distDeriv (finSumFinEquiv.symm n) A Œµ (finSumFinEquiv.symm (b 1))
  rw [Tensor.permT_basis_repr_symm_apply, contrT_basis_repr_apply_eq_fin]
  conv_lhs =>
    enter [2, 2, n]
    rw [Tensor.prodT_basis_repr_apply, contrMetric_repr_apply_eq_minkowskiMatrix]
    enter [1]
    change Œ∑ (finSumFinEquiv.symm (b 1)) (finSumFinEquiv.symm n)
  conv_lhs =>
    enter [2, 2, n, 2]
    rw [toTensor_deriv_basis_repr_apply]
    change distDeriv (finSumFinEquiv.symm n) A Œµ (finSumFinEquiv.symm (b 0))
  rw [‚Üê Finset.sum_sub_distrib]
  rw [‚Üê finSumFinEquiv.sum_comp]
  simp only [Fin.isValue, Equiv.symm_apply_apply]

lemma fieldStrengthAux_tensor_basis_eq_basis {d} (A : DistElectromagneticPotential d)
    (Œµ : ùì¢(SpaceTime d, ‚Ñù))
    (b : ComponentIdx (S := realLorentzTensor d) (Fin.append ![Color.up] ![Color.up])) :
    (Tensor.basis _).repr (Tensorial.toTensor (A.fieldStrengthAux Œµ)) b =
    (Lorentz.Vector.basis.tensorProduct Lorentz.Vector.basis).repr (A.fieldStrengthAux Œµ)
      (finSumFinEquiv.symm (b 0), finSumFinEquiv.symm (b 1)) := by
  rw [Tensorial.basis_toTensor_apply]
  rw [Tensorial.basis_map_prod]
  simp only [Nat.reduceSucc, Nat.reduceAdd, Basis.repr_reindex, Finsupp.mapDomain_equiv_apply,
    Equiv.symm_symm, Fin.isValue]
  rw [Lorentz.Vector.tensor_basis_map_eq_basis_reindex]
  have hb : (((Lorentz.Vector.basis (d := d)).reindex Lorentz.Vector.indexEquiv.symm).tensorProduct
          (Lorentz.Vector.basis.reindex Lorentz.Vector.indexEquiv.symm)) =
          ((Lorentz.Vector.basis (d := d)).tensorProduct (Lorentz.Vector.basis (d := d))).reindex
          (Lorentz.Vector.indexEquiv.symm.prodCongr Lorentz.Vector.indexEquiv.symm) := by
        ext b
        match b with
        | ‚ü®i, j‚ü© =>
        simp
  rw [hb]
  rw [Module.Basis.repr_reindex_apply]
  congr 1

lemma fieldStrengthAux_basis_repr_apply {d} {ŒºŒΩ : (Fin 1 ‚äï Fin d) √ó (Fin 1 ‚äï Fin d)}
    (A : DistElectromagneticPotential d) (Œµ : ùì¢(SpaceTime d, ‚Ñù)) :
    (Lorentz.Vector.basis.tensorProduct Lorentz.Vector.basis).repr (A.fieldStrengthAux Œµ) ŒºŒΩ =
    ‚àë Œ∫, ((Œ∑ ŒºŒΩ.1 Œ∫ * distDeriv Œ∫ A Œµ ŒºŒΩ.2) - Œ∑ ŒºŒΩ.2 Œ∫ * distDeriv Œ∫ A Œµ ŒºŒΩ.1) := by
  match ŒºŒΩ with
  | (Œº, ŒΩ) =>
  trans (Tensor.basis _).repr (Tensorial.toTensor (A.fieldStrengthAux Œµ))
    (fun | 0 => finSumFinEquiv Œº | 1 => finSumFinEquiv ŒΩ); swap
  ¬∑ rw [toTensor_fieldStrengthAux_basis_repr]
    simp
  rw [fieldStrengthAux_tensor_basis_eq_basis]
  congr 1
  change _ = (finSumFinEquiv.symm (finSumFinEquiv Œº), finSumFinEquiv.symm (finSumFinEquiv ŒΩ))
  simp

lemma fieldStrengthAux_basis_repr_apply_eq_single {d} {ŒºŒΩ : (Fin 1 ‚äï Fin d) √ó (Fin 1 ‚äï Fin d)}
    (A : DistElectromagneticPotential d) (Œµ : ùì¢(SpaceTime d, ‚Ñù)) :
    (Lorentz.Vector.basis.tensorProduct Lorentz.Vector.basis).repr (A.fieldStrengthAux Œµ) ŒºŒΩ =
    ((Œ∑ ŒºŒΩ.1 ŒºŒΩ.1 * distDeriv ŒºŒΩ.1 A Œµ ŒºŒΩ.2) - Œ∑ ŒºŒΩ.2 ŒºŒΩ.2 * distDeriv ŒºŒΩ.2 A Œµ ŒºŒΩ.1) := by
  rw [fieldStrengthAux_basis_repr_apply]
  simp only [Finset.sum_sub_distrib]
  rw [Finset.sum_eq_single ŒºŒΩ.1, Finset.sum_eq_single ŒºŒΩ.2]
  ¬∑ intro b _ hb
    rw [minkowskiMatrix.off_diag_zero]
    simp only [zero_mul]
    exact id (Ne.symm hb)
  ¬∑ simp
  ¬∑ intro b _ hb
    rw [minkowskiMatrix.off_diag_zero]
    simp only [zero_mul]
    exact id (Ne.symm hb)
  ¬∑ simp

lemma fieldStrengthAux_eq_basis {d} (A : DistElectromagneticPotential d)
    (Œµ : ùì¢(SpaceTime d, ‚Ñù)) :
    (A.fieldStrengthAux Œµ) = ‚àë Œº, ‚àë ŒΩ,
      ((Œ∑ Œº Œº * distDeriv Œº A Œµ ŒΩ) - Œ∑ ŒΩ ŒΩ * distDeriv ŒΩ A Œµ Œº)
      ‚Ä¢ Lorentz.Vector.basis Œº ‚äó‚Çú[‚Ñù] Lorentz.Vector.basis ŒΩ := by
  apply (Lorentz.Vector.basis.tensorProduct Lorentz.Vector.basis).repr.injective
  ext b
  match b with
  | (Œº, ŒΩ) =>
  simp [map_sum, map_smul, Finsupp.coe_finset_sum, Finsupp.coe_smul, Finset.sum_apply,
    Pi.smul_apply, Basis.tensorProduct_repr_tmul_apply, Basis.repr_self, smul_eq_mul]
  simp [Finsupp.single_apply]
  rw [fieldStrengthAux_basis_repr_apply_eq_single]

/-!

### B.2. The definition of the field strength

-/

/-- The field strength of an electromagnetic potential which is a distribution. -/
noncomputable def fieldStrength {d} :
    DistElectromagneticPotential d ‚Üí‚Çó[‚Ñù]
    (SpaceTime d) ‚Üíd[‚Ñù] Lorentz.Vector d ‚äó[‚Ñù] Lorentz.Vector d where
  toFun A := {
    toFun Œµ := A.fieldStrengthAux Œµ
    map_add' Œµ1 Œµ2 := by
      apply (Lorentz.Vector.basis.tensorProduct Lorentz.Vector.basis).repr.injective
      ext ŒºŒΩ
      simp [fieldStrengthAux_basis_repr_apply_eq_single]
      ring
    map_smul' c Œµ := by
      apply (Lorentz.Vector.basis.tensorProduct Lorentz.Vector.basis).repr.injective
      ext ŒºŒΩ
      simp [fieldStrengthAux_basis_repr_apply_eq_single]
      ring
    cont := by
      simp [fieldStrengthAux_eq_basis]
      fun_prop}
  map_add' A1 A2 := by
    ext Œµ
    apply (Lorentz.Vector.basis.tensorProduct Lorentz.Vector.basis).repr.injective
    ext ŒºŒΩ
    simp only [ContinuousLinearMap.coe_mk', LinearMap.coe_mk, AddHom.coe_mk,
      fieldStrengthAux_basis_repr_apply_eq_single, map_add, ContinuousLinearMap.add_apply,
      Lorentz.Vector.apply_add, Finsupp.coe_add, Pi.add_apply]
    ring
  map_smul' c A := by
    ext Œµ
    apply (Lorentz.Vector.basis.tensorProduct Lorentz.Vector.basis).repr.injective
    ext ŒºŒΩ
    simp only [ContinuousLinearMap.coe_mk', LinearMap.coe_mk, AddHom.coe_mk,
      fieldStrengthAux_basis_repr_apply_eq_single, map_smul, ContinuousLinearMap.coe_smul',
      Pi.smul_apply, Lorentz.Vector.apply_smul, Real.ringHom_apply, Finsupp.coe_smul, smul_eq_mul]
    ring

/-!

### B.3. Field strength written in terms of a basis

-/

lemma fieldStrength_eq_basis {d} (A : DistElectromagneticPotential d)
    (Œµ : ùì¢(SpaceTime d, ‚Ñù)) :
    A.fieldStrength Œµ = ‚àë Œº, ‚àë ŒΩ,
      ((Œ∑ Œº Œº * distDeriv Œº A Œµ ŒΩ) - Œ∑ ŒΩ ŒΩ * distDeriv ŒΩ A Œµ Œº)
      ‚Ä¢ Lorentz.Vector.basis Œº ‚äó‚Çú[‚Ñù] Lorentz.Vector.basis ŒΩ := by
  rw [fieldStrength]
  exact fieldStrengthAux_eq_basis A Œµ

end DistElectromagneticPotential

end Electromagnetism
